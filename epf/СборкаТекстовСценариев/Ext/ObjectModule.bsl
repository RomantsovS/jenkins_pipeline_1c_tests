#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

# Область ПрограммныйИнтерфейс

//Возвращает имя профиля для подключения TestClient
//
// Параметры:
//  Роль - Тип - СправочникСсылка.ПрофилиПользователей.
//  НастройкиБДДляЗапускаТестовИзСценария - Тип - Структура
//  ЭкранироватьСпецСимволы - Тип - Булево
//
// Возвращаемое значение:
//  Тип - Строка.
//
Функция ИмяПрофиляTestClient(Роль,НастройкиБДДляЗапускаТестовИзСценария,ЭкранироватьСпецСимволы) Экспорт
	ИмяПрофиля = Роль.Наименование;
	Если Не ЗначениеЗаполнено(ИмяПрофиля) Тогда
		ИмяПрофиля = "РольНеУказана";
	КонецЕсли;	 
	Стр = СокрЛП(ИмяПрофиля) + ". База: " + СокрЛП(НастройкиБДДляЗапускаТестовИзСценария.СтрокаИБ);
	
	Если ЭкранироватьСпецСимволы Тогда
		ЭкранироватьОсновныеСпецСимволыGherkin(Стр);
	КонецЕсли;	 
	
	Возврат Стр;
КонецФункции	

//Возвращает соответствие по данным переданной базы
//
//
// Параметры:
//  Параметры - ЭталоннаяБаза - Справочник.ЭталонныеБазыТестирования.
//
// Возвращаемое значение:
//  Тип - Структура. Набор настроек
//
Функция ДанныеПользователейЭталоннойБазы(ЭталоннаяБазаТестирования) Экспорт
	ДанныеЭталоннойБазы = ПользователиЭталоннойБазы(ЭталоннаяБазаТестирования);
	
	Результат = Новый Соответствие;
	Для Каждого Пользователь Из ДанныеЭталоннойБазы Цикл
		Результат.Вставить(Пользователь.ПрофильПользователя,
		    Новый Структура("Логин,Пароль",Пользователь.Логин,Пользователь.Пароль));
	КонецЦикла;	
	
	Возврат Результат;
КонецФункции	

//Возвращает настройки БД для запуска тестов
//
// Возвращаемое значение:
//  Тип - Структура. Набор настроек
//
Функция НастройкиБДДляЗапускаТестовИзСценария() Экспорт
	СохраненныеНастройки = Неопределено;
	
	Если НЕ РаботаВРежимеВнешнейОбработки Тогда
		//сохраненные настройки используются только для режима, когда обработка находится внутри СППР
		СохраненныеНастройки = Вычислить(
		   "ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(""Тестирование"", ""НастройкиДляЗапускаТестовИзСценария"")");
	КонецЕсли;	 
	
	Если СохраненныеНастройки <> Неопределено Тогда
		Если СохраненныеНастройки.Свойство("ЭталоннаяБаза") Тогда
			ДанныеПользователейЭталоннойБазы = ДанныеПользователейЭталоннойБазы(СохраненныеНастройки.ЭталоннаяБаза);
		Иначе	
			ДанныеПользователейЭталоннойБазы = Новый Соответствие;
		КонецЕсли;
		
		СохраненныеНастройки.Вставить("ДанныеПользователейЭталоннойБазы",ДанныеПользователейЭталоннойБазы);
		
		Если НЕ СохраненныеНастройки.Свойство("КлючиЗапускаTestClient") Тогда
			СохраненныеНастройки.Вставить("КлючиЗапускаTestClient","");
		КонецЕсли;	 
		Если НЕ СохраненныеНастройки.Свойство("КлючиЗапускаTestManager") Тогда
			СохраненныеНастройки.Вставить("КлючиЗапускаTestManager","");
		КонецЕсли;	 
	КонецЕсли;	 
	
	Возврат СохраненныеНастройки;
КонецФункции	

// Заполняет таблицу параметров из дерева значений
//
// Параметры:
//  Дерево - Тип - ДеревоЗначений.
//  ТаблицаПараметров - Тип - ТаблицаЗначений.
//
Процедура ПараметрыВходящиеИзДерева(Дерево,ТаблицаПараметров) Экспорт
	ИДОбщий = 0;
	ЗаполнитьПараметрыВходящиеИзДереваРекурсивно(Дерево,ТаблицаПараметров,ИДОбщий);
КонецПроцедуры

//Возвращает данные для подключения TestClient
//
// Параметры:
//  ПрофильПользователя - Тип - СправочникСсылка.ПрофилиПользователей.
//  БазаДанных - Тип - СправочникСсылка.ЭталонныеБазы.
//
// Возвращаемое значение:
//  Тип - Структура или Неопределено.
//
Функция ДанныеДляПодключенияКБДTestClient(ПрофильПользователя,БазаДанных,НастройкаСценария) Экспорт
	Возврат Обработки.СборкаТекстовСценариев.ДанныеДляПодключенияКБДTestClient(ПрофильПользователя,БазаДанных,НастройкаСценария);
КонецФункции	

//Возвращает текст бизнес-процесса по переданным параметрам
//
// Параметры:
//  Параметры - Тип - Струтктура.
//
// Возвращаемое значение:
//  Тип - Строка.
//
Функция ТекстБизнесПроцесса(Параметры) Экспорт
	ТекстыСценариев                     = Параметры.ОбъектТекстыСценариев.ТекстыСценариев;
	ЗаголовокЗначенияПараметровСценария = Параметры.ОбъектТекстыСценариев.ЗаголовокЗначенияПараметровСценария;
	ФОИмеющиеОдноЗначение               = Параметры.ОбъектТекстыСценариев.ФОИмеющиеОдноЗначение;
	ФОИмеющиеНесколькоЗначений          = Параметры.ОбъектТекстыСценариев.ФОИмеющиеНесколькоЗначений;
	ШагиДоТеста                         = Параметры.ШагиДоТеста;
	ШагиПроверка                        = Параметры.ШагиПроверка;
	ШагиПослеТеста                      = Параметры.ШагиПослеТеста;
	ЭтоБизнесПроцесс                    = Параметры.ЭтоБизнесПроцесс;
	
	ВставитьВКаждыйСценарийПроверкуЧтоФОСНесколькимиЗначениямиИмеютНужноеЗначение(ТекстыСценариев,
	                                                                    ФОИмеющиеНесколькоЗначений);
	
	ТекстЗаголовок = ТекстЗаголовок(Параметры,ТекстыСценариев);
	
	Если ЭтоБизнесПроцесс Тогда
		Если ТипЗнч(Параметры.Процесс) = Тип("Строка") Тогда
			ТекстЗаголовок = ТекстЗаголовок + Символы.ПС + Символы.ПС + "@КодНастройкиПроцесса=" + СокрЛП(Параметры.КодНастройки);
			ТекстЗаголовок = ТекстЗаголовок + Символы.ПС + "@ПолныйКодПроцесса=" + СокрЛП(Параметры.ПолныйКодПроцесса);
			ТекстЗаголовок = ТекстЗаголовок + Символы.ПС + "Сценарий: " + СокрЛП(Параметры.Процесс);
		Иначе	
			ТекстЗаголовок = ТекстЗаголовок + Символы.ПС + Символы.ПС + "Сценарий: " + СокрЛП(Параметры.Процесс.Наименование);
		КонецЕсли;	 
	КонецЕсли;	 
	
	Если Не ЭтоБизнесПроцесс Тогда
		Для Каждого ЭлементТекстыСценариев Из ТекстыСценариев Цикл
			ТекстОбщийКонтекст = ТекстОбщийКонтекст(ФОИмеющиеОдноЗначение,ШагиДоТеста,
			            ДанныеДляПодключенияКБДTestClient(ЭлементТекстыСценариев.Сценарий.ПрофильПользователя,Параметры.БазаДанных,Неопределено));
		КонецЦикла;	
	КонецЕсли;	 
	
	ВставитьТекстОкончаниеВСценарии(ШагиПроверка,ШагиПослеТеста,ТекстыСценариев);
	
	Если Параметры.Свойство("ДанныеНомеровСтрок") Тогда
		ДанныеНомеровСтрок = Параметры.ДанныеНомеровСтрок;
	Иначе	
		ДанныеНомеровСтрок = Новый Массив;
	КонецЕсли;	 
	Текст = ОсновнойТекст(ТекстЗаголовок,ТекстОбщийКонтекст,ТекстыСценариев,
	                       ЗаголовокЗначенияПараметровСценария,ДанныеНомеровСтрок);
	
	Возврат Текст;
КонецФункции

//Возвращает текст сценария по переданным параметрам
//
// Параметры:
//  Параметры - Тип - Струтктура.
//
// Возвращаемое значение:
//  Тип - Строка.
//
Функция ТекстСценария(Параметры) Экспорт
	ТекстыСценариев                     = Параметры.ОбъектТекстыСценариев.ТекстыСценариев;
	ЗаголовокЗначенияПараметровСценария = Параметры.ОбъектТекстыСценариев.ЗаголовокЗначенияПараметровСценария;
	ФОИмеющиеОдноЗначение               = Параметры.ОбъектТекстыСценариев.ФОИмеющиеОдноЗначение;
	ФОИмеющиеНесколькоЗначений          = Параметры.ОбъектТекстыСценариев.ФОИмеющиеНесколькоЗначений;
	ШагиДоТеста                         = Параметры.ШагиДоТеста;
	ШагиПроверка                        = Параметры.ШагиПроверка;
	ШагиПослеТеста                      = Параметры.ШагиПослеТеста;
	НастройкаСценария                   = Неопределено;
	Если Параметры.ОбъектТекстыСценариев.Свойство("НастройкаСценария") Тогда
		НастройкаСценария = Параметры.ОбъектТекстыСценариев.НастройкаСценария;
	КонецЕсли;	 
	
	ВставитьВКаждыйСценарийПроверкуЧтоФОСНесколькимиЗначениямиИмеютНужноеЗначение(ТекстыСценариев,
	                                                                   ФОИмеющиеНесколькоЗначений);
	
	ТекстЗаголовок = ТекстЗаголовок(Параметры,ТекстыСценариев);
	
	Если Параметры.ЧтениеИзТекста Тогда
		ДанныеДляПодключенияКБДTestClient = ДанныеДляПодключенияКБДTestClientТекст(Параметры);
	Иначе	
		ДанныеДляПодключенияКБДTestClient = ДанныеДляПодключенияКБДTestClient(Параметры.Процесс.ПрофильПользователя
		    ,Параметры.БазаДанных,НастройкаСценария);
	КонецЕсли;	 
	
	ТекстОбщийКонтекст = ТекстОбщийКонтекст(ФОИмеющиеОдноЗначение,ШагиДоТеста,ДанныеДляПодключенияКБДTestClient);
	
	ВставитьТекстОкончаниеВСценарии(ШагиПроверка,ШагиПослеТеста,ТекстыСценариев);
	
	ДанныеНомеровСтрок = Неопределено;
	Параметры.Свойство("ДанныеНомеровСтрок",ДанныеНомеровСтрок);
	
	Текст = ОсновнойТекст(ТекстЗаголовок,ТекстОбщийКонтекст,ТекстыСценариев,
	    ЗаголовокЗначенияПараметровСценария,ДанныеНомеровСтрок);
													 
	Если Параметры.ИсключитьСлужебныеСловаИзТекстаСценария Тогда
		ИсключитьСлужебныеСловаИзТекстаСценария(Текст);
	КонецЕсли;	 												 
	
	Если Параметры.ИсключитьСлужебныеСловаИзТекстаСценария Тогда
		МногоязыковаяПоддержка(Текст,Неопределено);
	КонецЕсли;	 
	
	Возврат Текст;
КонецФункции

// Обновляет из БД таблицу ВерсииШаблонов
//
// Параметры:
//  ПараметрыВерсийСценариев - Тип - Струтктура - со свойствами:
//  	* Дерево - Тип - ДеревоЗначений
//  	* ВерсииШаблонов - Тип - ТаблицаЗначений
//  	* Проект - Тип - Справочник.Проекты
//  	* Сценарий - Тип - Справочник.СценарииРаботыПользователей
//
Процедура ВерсииШаблонов(ПараметрыВерсийСценариев) Экспорт
	
	Дерево = ПараметрыВерсийСценариев.Дерево;
	ВерсииШаблонов = ПараметрыВерсийСценариев.ВерсииШаблонов;
	Проект = ПараметрыВерсийСценариев.Проект;
	Сценарий = ПараметрыВерсийСценариев.Сценарий;
	
	ТаблицаШаблоновСценариев = ТаблицаШаблоновСценариев(ПараметрыВерсийСценариев);
	МассивПервыхСловНРег     = МассивПервыхСловGherkinНРег();
	ВерсииШаблонов.Очистить();
	ЗаполнитьВерсииШаблоновРекурсивно(Дерево,ВерсииШаблонов,ТаблицаШаблоновСценариев,МассивПервыхСловНРег,1);
КонецПроцедуры

// Преобразует текст перед установкой в поле форматированного документа
//
// Параметры:
//  ТекстДокумента - Тип - Строка.
//
Процедура СделатьСтандартнуюПодготовкуТекстаHTML(ТекстДокумента) Экспорт
	ТекстДокумента = СтрЗаменить(ТекстДокумента,Символы.ПС,"<br>");
КонецПроцедуры

// Преобразует схему во внутреннем формате в виде дерева значений в форматированный документ
//
// Параметры:
//  ДеревоСхемы - Тип - ДеревоЗначений.
//  ФорматированныйДокумент - Тип - ФорматированныйДокумент.
//  ШаблонHTML - Тип - ТекстовыйДокумент.
//  ДополнительныеПараметры - Тип - Структура.
//
Процедура ФорматированныйДокументПоДеревуСхемы(ДеревоСхемы,ФорматированныйДокумент,
	       ШаблонHTML,ДополнительныеПараметры) Экспорт
	ТекстHTML = ШаблонHTML.ПолучитьТекст();
	
	ТекстДокумента = СоздатьТекстФорматированногоДокументаПоДеревуСхемы(ДеревоСхемы,ДополнительныеПараметры);
	
	Если ДополнительныеПараметры.ДелатьРаскраску Тогда
		СделатьСтандартнуюПодготовкуТекстаHTML(ТекстДокумента);
		ТекстHTML      = СтрЗаменить(ТекстHTML,"<ПроизвольныйТекст>",ТекстДокумента);
		ФорматированныйДокумент.УстановитьHTML(ТекстHTML,Новый Структура);
	Иначе	
		ДополнительныеПараметры.Вставить("ТекстДокумента",ТекстДокумента);
	КонецЕсли;	 
КонецПроцедуры

// Преобразует обычный текст в форматированный текст сценария
//
// Параметры:
//  Текст - Тип - Строка.
//  ФД - Тип - ФорматированныйДокумент.
//  Проект - Тип - Справочники.Проекты.
//  Сценарий - Тип - Справочники.Сценарии.
//  ДополнительныеПараметры - Тип - Структура.
// 
Процедура ТекстФорматированныйТекстСценарияИзОбычногоТекста(Текст,ФД,Проект,Сценарий,ДополнительныеПараметры) Экспорт
	ДеревоСхемы = СоздатьДеревоСхемы();
	
	ДеревоСценарияИзТекста(Текст,ДеревоСхемы,Проект,Сценарий,ДополнительныеПараметры);
	
	МногоязыковаяПоддержка(Текст,ДеревоСхемы);
	
	ШаблонHTML   = Справочники.СценарииРаботыПользователей.ПолучитьМакет("ШаблонHTML");
	ФорматированныйДокументПоДеревуСхемы(ДеревоСхемы,ФД,ШаблонHTML,ДополнительныеПараметры);
КонецПроцедуры

// Преобразует обычный текст во внутренний формат в виде дерева значений
//
// Параметры:
//  Текст - Тип - Строка.
//  ДеревоСхемы - Тип - ДеревоЗначений.
//  Проект - Тип - Справочники.Проекты.
//  Сценарий - Тип - Справочники.Сценарии.
// 
// Возвращаемое значение:
//  СтруктураВозврата - Тип - Структура
//
Процедура ДеревоСценарияИзТекста(Текст,ДеревоСхемы,Проект,Сценарий,ДополнительныеПараметры) Экспорт
	Попытка
		ПараметрыВерсийСценариев = Новый Структура;
		ПараметрыВерсийСценариев.Вставить("Проект",Проект);
		ПараметрыВерсийСценариев.Вставить("Сценарий",Сценарий);
		Если НЕ ДополнительныеПараметры.ЧтениеИзТекста Тогда
			Если Сценарий = Неопределено Тогда
				ПараметрыВерсийСценариев.Вставить("ФункцияСистемы",Неопределено);
			Иначе	
				ПараметрыВерсийСценариев.Вставить("ФункцияСистемы",Сценарий.ФункцияСистемы);
			КонецЕсли;	 
		Иначе	
			//значит это вариант сборки сценария из файла
			ПараметрыВерсийСценариев.Вставить("ФункцияСистемы",Неопределено);
		КонецЕсли;	 
		
		ТаблицаШаблоновСценариев = ТаблицаШаблоновСценариев(ПараметрыВерсийСценариев,ДополнительныеПараметры);
		ДеревоСценарияИзТекстаСлужебный(Текст,ДеревоСхемы,ТаблицаШаблоновСценариев,Сценарий,ДополнительныеПараметры);
	Исключение
		НоваяОшибка = Ошибки.Добавить();
		НоваяОшибка.ОписаниеОшибки =  ОписаниеОшибки();
		НоваяОшибка.ПодробноеПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Если ДополнительныеПараметры.Свойство("КодСценария") Тогда
			НоваяОшибка.КодСценария = ДополнительныеПараметры.КодСценария;
		КонецЕсли;	 
		Если ДополнительныеПараметры.Свойство("Сценарий") Тогда
			НоваяОшибка.ИмяСценария = ДополнительныеПараметры.Сценарий;
		КонецЕсли;	 

		Если ДополнительныеПараметры.Свойство("СтруктураПараметров") Тогда
			ДополнительныеПараметры.СтруктураПараметров.Вставить("ОшибкаСобрана",Истина);
		КонецЕсли;	 
		
		ВызватьИсключение ОписаниеОшибки();
	КонецПопытки;
КонецПроцедуры

// Возвращает флаг получения из строки снипета
//
// Параметры:
//  Стр - Тип - Строка.
//  МассивПервыхСловНРег - Тип - Массив.
//  КешПараметровСтроки - Тип - ТаблицаЗначений.
// 
// Возвращаемое значение:
//  Тип - Булево
//
Функция СнипетПоСтроке(Стр,МассивПервыхСловНРег = Неопределено,КешПараметровСтроки = Неопределено) Экспорт
	
	Если МассивПервыхСловНРег = Неопределено Тогда
		МассивПервыхСловНРег = МассивПервыхСловGherkinНРег();
	КонецЕсли;	 
	
	МассивСлов  = СтрРазделить(Стр," ",Истина);
	
	ПервоеСлово   = МассивСлов[0];
	
	Если ПервоеСлово = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;	 
	
	НРегСтр = НРег(Стр);
	Для Каждого КлючевоеСлово Из МассивПервыхСловНРег Цикл
		Позиция = Найти(НРегСтр, КлючевоеСлово);	
		Если Позиция = 1 и Сред(НРегСтр,СтрДлина(КлючевоеСлово)+1,1) = " " Тогда
			Стр = СнипетИзСтроки(СокрЛП(Сред(Стр,СтрДлина(КлючевоеСлово)+1)),КешПараметровСтроки,Стр);
			Возврат Истина; 
		КонецЕсли;	 
	КонецЦикла;	
	
	Возврат Ложь;
КонецФункции	

// Возвращает снипет по переданной строке
//
// Параметры:
//  Стр - Тип - Строка.
//  КешПараметровСтроки - Тип - ТаблицаЗначений.
// 
// Возвращаемое значение:
//  Тип - Булево
//
Функция СнипетИзСтроки(Знач Стр,КешПараметровСтроки = Неопределено,СтрокаВКеш = Неопределено) Экспорт
	Параметры = ПараметрыGherkinИзСтрокиИПреобразоватьСтрокуДляРаботыСПараметрами(Стр,,КешПараметровСтроки,СтрокаВКеш);
	УбратьПараметрыИзСтроки(Стр,Параметры);
	
	МассивСтрок = СтрРазделить(Стр," ",Ложь);
	Для Ккк = 0 По МассивСтрок.Количество()-1 Цикл
		МассивСтрок[Ккк] = СокрЛП(МассивСтрок[Ккк]);
		МассивСтрок[Ккк] = ВРег(Лев(МассивСтрок[Ккк],1)) + Сред(МассивСтрок[Ккк],2);
	КонецЦикла;	
	
	Стр = СтрСоединить(МассивСтрок,"");
	
	Возврат УбратьЗапрещенныеСимволыИзСнипет(Стр);
КонецФункции	

// Возвращает полное дерево сценария, считывая его из БД
//
// Параметры:
//  Сценарий - Тип - Справочник.СценарииРаботыПользователей.
// 
// Возвращаемое значение:
//  Статус операции - Тип - ДеревоЗначений
//
Функция ДеревоСценарияИзБазыДанных(Сценарий,СтруктураПараметров) Экспорт
	Если Не ЗначениеЗаполнено(Сценарий) Тогда
		Возврат Неопределено;
	КонецЕсли;	 
	
	ДеревоСхемы = ПолучитьДеревоСценария(Сценарий,СтруктураПараметров);
	
	Если ТипЗнч(ДеревоСхемы) = Тип("ДеревоЗначений") Тогда
		Если ДеревоСхемы.Колонки.Найти("ДополнительныеСвойства") = Неопределено Тогда
			ДеревоСхемы.Колонки.Добавить("ДополнительныеСвойства");
		КонецЕсли;	 
	КонецЕсли;	 
	
	Возврат ДеревоСхемы;
КонецФункции	

// Возвращает ветки условия
Функция ВеткиУсловия(Дерево) Экспорт
	Массив = Новый Массив;
	
	Для Каждого СтрокаДерева Из Дерево.Строки Цикл
		Если СтрокаДерева.ТипЭлемента = ТипЭлементаКонецЕслиПрепроцессор()
  			 или СтрокаДерева.ТипЭлемента = ТипЭлементаКонецЕсли()
  			 или СтрокаДерева.ТипЭлемента = ТипЭлементаКонецЕслиEng()
  			 или СтрокаДерева.ТипЭлемента = ТипЭлементаКонецЦикла()
  			 или СтрокаДерева.ТипЭлемента = ТипЭлементаКонецЦиклаEng()
 			Тогда
			Продолжить;
		КонецЕсли;	 
		
		Массив.Добавить(СтрокаДерева);
	КонецЦикла;	
	
	Возврат Массив;
КонецФункции	

// Делает экранирование спецсимволов < и >
//
// Параметры:
//  Стр - Тип - Строка
// 
Процедура ЭкранироватьУгловыеСкобки(Стр) Экспорт
	Стр = СтрЗаменить(Стр,"<","&lt;");
	Стр = СтрЗаменить(Стр,">","&gt;");
КонецПроцедуры

// Делает разэкранирование спецсимволов < и >
//
// Параметры:
//  Стр - Тип - Строка
// 
Процедура ВернутьУгловыеСкобки(Стр) Экспорт
	Стр = СтрЗаменить(Стр,"&lt;","<");
	Стр = СтрЗаменить(Стр,"&gt;",">");
КонецПроцедуры

// Делает разэкранирование спецсимволов \[ и \]
//
// Параметры:
//  Стр - Тип - Строка
// 
Процедура РазэкранироватьУгловыеСкобки(Стр) Экспорт
	Стр = СтрЗаменить(Стр,"\[","[");
	Стр = СтрЗаменить(Стр,"\]","]");
КонецПроцедуры

// Делает экранирование спецсимволов |, ", '
//
// Параметры:
//  Стр - Тип - Строка
// 
Процедура ЭкранироватьСпецСимволы(Стр) Экспорт
	Стр = СтрЗаменить(Стр,"\""","""");
	Стр = СтрЗаменить(Стр,"\'","'");
	Стр = СтрЗаменить(Стр,"\\","~ПредставлениеЭкранированныйСлеш~");
	Стр = СтрЗаменить(Стр,"\[","~ПредставлениеЛевойКвадратнойСкобки~");
	Стр = СтрЗаменить(Стр,"\]","~ПредставлениеПравойКвадратнойСкобки~");
	Стр = СтрЗаменить(Стр,"\|","~ПредставлениеВертикальнойЧерты~");
	Стр = СтрЗаменить(Стр,"""","\""");
	Стр = СтрЗаменить(Стр,"'","\'");
	Стр = СтрЗаменить(Стр,"~ПредставлениеВертикальнойЧерты~","\|");
	Стр = СтрЗаменить(Стр,"~ПредставлениеЛевойКвадратнойСкобки~","\[");
	Стр = СтрЗаменить(Стр,"~ПредставлениеПравойКвадратнойСкобки~","\]");
	Стр = СтрЗаменить(Стр,"~ПредставлениеЭкранированныйСлеш~","\\");
	ЭкранироватьУгловыеСкобки(Стр);
КонецПроцедуры

// Обрабатывает текст сценария чтобы исключить из него служебные конструкции
//
// Параметры:
//  ТекстСценария - Тип - Строка.
//
Процедура ИсключитьСлужебныеСловаИзТекстаСценария(ТекстСценария) Экспорт
	Если ТекстСценария = Неопределено Тогда
		Возврат;
	КонецЕсли;	 
	
	МассивСтрок = СтрРазделить(ТекстСценария,Символы.ПС);
	
	//исключим лишние служебные слова
	НовыйМассив = Новый Массив;
	Для Ккк = 0 По МассивСтрок.Количество()-1 Цикл
		Стр = МассивСтрок[Ккк];
		Если СокрЛП(НРег(Стр)) = "конецесли" Тогда
			НовыйМассив.Добавить("");
			Продолжить;
		ИначеЕсли СокрЛП(НРег(Стр)) = "endif" Тогда
			НовыйМассив.Добавить("");
			Продолжить;
		ИначеЕсли СокрЛП(НРег(Стр)) = "конеццикла" Тогда
			НовыйМассив.Добавить("");
			Продолжить;
		ИначеЕсли СокрЛП(НРег(Стр)) = "enddo" Тогда
			НовыйМассив.Добавить("");
			Продолжить;
		КонецЕсли;	 
		
		Если НРег(Лев(СокрЛП(Стр),4)) = "цикл" Тогда
			Позиция = Найти(НРег(Стр),"цикл");
			Стр = ПолучитьСтрокуЦикла(Стр,Позиция,СтрДлина("цикл"),"И");
		ИначеЕсли НРег(Лев(СокрЛП(Стр),2)) = "do" Тогда
			Позиция = Найти(НРег(Стр),"do");
			Стр = ПолучитьСтрокуЦикла(Стр,Позиция,СтрДлина("do"),"And");
		КонецЕсли;	 
		
		НовыйМассив.Добавить(Стр);
	КонецЦикла;	
	
	ТекстСценария = СтрСоединить(НовыйМассив,Символы.ПС);
КонецПроцедуры

// Делает экранирование спецсимволов \, ", '
//
// Параметры:
//  Стр - Тип - Строка
// 
Процедура ЭкранироватьОсновныеСпецСимволыGherkin(Стр) Экспорт
	Стр = СтрЗаменить(Стр,"\","\\");
	Стр = СтрЗаменить(Стр,"""","\""");
	Стр = СтрЗаменить(Стр,"'","\'");
КонецПроцедуры

// Возвращает данные для получения текста бизнес-процесса
//
// Параметры:
//  ШагиБизнесПроцесса - Тип - ТаблицаЗначений.
//  ПараметрыШагов - Тип - СправочникТабличнаяЧасть.БизнесПроцессы.ПараметрыШагов.
//  ПараметрыБизнесПроцесса - Тип - ТаблицаЗначений.
//  ПараметрыФормированияТекстаСценария - Тип - Структура.
// 
// Возвращаемое значение:
//  Статус операции - Тип - Структура
//
Функция ДанныеДляТекстаБизнесПроцесса(ШагиБизнесПроцесса,ПараметрыШагов,ПараметрыБизнесПроцесса,
	                                                      ПараметрыФормированияТекстаСценария) Экспорт
	
	СценарииБизнесПроцесса = СценарииИзДереваБизнесПроцесса(ШагиБизнесПроцесса);
	
	ДанныеВложенныхСценариев = ДанныеВложенныхСценариев();
	
	ПараметрыБизнесПроцесса.Колонки.Добавить("ЗначенияПараметра");
	
	МассивСценариев = Новый Массив;
	Для Каждого СтрокаСценария Из ШагиБизнесПроцесса Цикл
		Сценарий = СтрокаСценария.Сценарий;
		
		СтрокаДанныеВложенныхСценариев = ДанныеВложенныхСценариев.Найти(Сценарий,"Сценарий");
		   
		Если СтрокаДанныеВложенныхСценариев = Неопределено Тогда
			СтрокаДанныеВложенныхСценариев = ДанныеВложенныхСценариев.Добавить();
			СтрокаДанныеВложенныхСценариев.Сценарий = Сценарий;
			Если ПараметрыФормированияТекстаСценария.ЧтениеИзТекста Тогда
				СтрокаДанныеВложенныхСценариев.ПараметрыВходящие = ПустаяТаблицаПараметров();
			Иначе	
				СтрокаДанныеВложенныхСценариев.ПараметрыВходящие = ПараметрыВходящиеСценария(Сценарий);
			КонецЕсли;	 
		КонецЕсли;	    
		
		
		СхемаДерево = Сценарий.ХранилищеСтруктурыСхемы.Получить();
		Если ТипЗнч(СхемаДерево) <> Тип("ДеревоЗначений") Тогда
			ВызватьИсключение СтрШаблон(НСтр("ru = 'Не найдена схема сценария: %1'"), Сценарий.Наименование);
		КонецЕсли;	
		
		КоличествоВариантовСценария = 1;
		ОбработатьЗначенияПараметровСценария(ПараметрыБизнесПроцесса,КоличествоВариантовСценария);
		МаксимальноеКоличествоВариантов = 100;
		Если КоличествоВариантовСценария > МаксимальноеКоличествоВариантов Тогда
			ВызватьИсключение СтрШаблон(НСтр("ru = 'Слишком много вариантов сценария: Получено вариантов <%1>, максимальное количество <%2>'"),
			                            КоличествоВариантовСценария,МаксимальноеКоличествоВариантов);
		КонецЕсли;	 
		
		//надо передать параметры используя значения из дерева вида: ОбщийПараметр.Клиент  и  Заказ1.Парам01
		ЗначенияВходящихПараметров = ПараметрыСценарияБизнесПроцесса(СтрокаСценария,ПараметрыШагов,
		     ПараметрыБизнесПроцесса,ДанныеВложенныхСценариев,ШагиБизнесПроцесса);
		
		ДоТеста       = Новый ТаблицаЗначений;
		ПроверкаТеста = Новый ТаблицаЗначений;
		ПослеТеста    = Новый ТаблицаЗначений;
		
		НаборПараметров = Новый Структура;
		НаборПараметров.Вставить("ПараметрыСНесколькимиЗначениями",Новый Массив);
		НаборПараметров.Вставить("ТаблицаПараметров",ЗначенияВходящихПараметров);
		
		
		СтруктураПараметров = Новый Структура;
		СтруктураПараметров.Вставить("ЧтениеИзТекста",Ложь);
		
		СхемаДерево = ДеревоСценарияИзБазыДанных(Сценарий,СтруктураПараметров);
		
		ПараметрыДляПолученияТекстаСценария = Новый Структура;
		ПараметрыДляПолученияТекстаСценария.Вставить("Сценарий",Сценарий);
		ПараметрыДляПолученияТекстаСценария.Вставить("НаборПараметров",НаборПараметров);
		ПараметрыДляПолученияТекстаСценария.Вставить("СхемаДерево",СхемаДерево);
		ПараметрыДляПолученияТекстаСценария.Вставить("ДанныеВложенныхСценариев",ДанныеВложенныхСценариев);
		ПараметрыДляПолученияТекстаСценария.Вставить("ЧтениеИзТекста",Ложь);
		ПараметрыДляПолученияТекстаСценария.Вставить("ДоТеста",ДоТеста);
		ПараметрыДляПолученияТекстаСценария.Вставить("ПроверкаТеста",ПроверкаТеста);
		ПараметрыДляПолученияТекстаСценария.Вставить("ПослеТеста",ПослеТеста);
		ПараметрыДляПолученияТекстаСценария.Вставить("НомерНабора",-1);
		ПараметрыДляПолученияТекстаСценария.Вставить("РазмерНабора",0);
		ПараметрыДляПолученияТекстаСценария.Вставить("ИмяСценария",СтрокаСценария.Описание);
		ПараметрыДляПолученияТекстаСценария.Вставить("ЭтоБизнесПроцесс",Истина);
		ПараметрыДляПолученияТекстаСценария.Вставить("ПросмотрСценария",ПараметрыФормированияТекстаСценария.ПросмотрСценария);
		ПараметрыДляПолученияТекстаСценария.Вставить("УровеньВложенности",0);
		ПараметрыДляПолученияТекстаСценария.Вставить("ИсключитьСлужебныеСловаИзТекстаСценария",
		        ПараметрыФормированияТекстаСценария.ИсключитьСлужебныеСловаИзТекстаСценария);
				
		ПараметрыДляПолученияТекстаСценария.Вставить("ФормироватьДанныеПоНомерамСтрок",Ложь);
						
				
		ОбъектСценария = ТекстСценарияПоНаборуПараметров(ПараметрыДляПолученияТекстаСценария);
		
		МассивСценариев.Добавить(ОбъектСценария);
	КонецЦикла;	
	
	ЗаголовокЗначенияПараметровСценария = ЗаголовокЗначенияПараметровСценария(ПараметрыБизнесПроцесса);
	                        
	ОбъектТекстыСценариев = Новый Структура;
	ОбъектТекстыСценариев.Вставить("ТекстыСценариев",МассивСценариев);
	ОбъектТекстыСценариев.Вставить("ЗаголовокЗначенияПараметровСценария",ЗаголовокЗначенияПараметровСценария);
	ОбъектТекстыСценариев.Вставить("ФОИмеющиеОдноЗначение",Новый Массив);
	ОбъектТекстыСценариев.Вставить("ФОИмеющиеНесколькоЗначений",Новый Массив);
	
	Возврат ОбъектТекстыСценариев;
КонецФункции	

// Возвращает данные для получения текста бизнес-процесса
//
// Параметры:
//  ШагиБизнесПроцесса - Тип - ТаблицаЗначений.
//  ПараметрыШагов - Тип - СправочникТабличнаяЧасть.БизнесПроцессы.ПараметрыШагов.
//  ПараметрыБизнесПроцесса - Тип - ТаблицаЗначений.
//  ПараметрыФормированияТекстаСценария - Тип - Структура.
// 
// Возвращаемое значение:
//  Статус операции - Тип - Структура
//
Функция ДанныеДляТекстаСценарияПроцесса(Процесс,ПараметрыФормированияТекстаСценария) Экспорт
	
	Если ПараметрыФормированияТекстаСценария.ЧтениеИзТекста	Тогда
		UIDПроцесса = ПараметрыФормированияТекстаСценария.UIDПроцесса;
		СтрокаДанныеПроцессы = ПараметрыФормированияТекстаСценария.ДанныеПроцессов.Найти(UIDПроцесса,"UID");
		
		СценарииБизнесПроцесса = Новый ТаблицаЗначений;
		СценарииБизнесПроцесса.Колонки.Добавить("Сценарий");
		СценарииБизнесПроцесса.Колонки.Добавить("ШагПроцесса");
		СценарииБизнесПроцесса.Колонки.Добавить("ШагПроцессаUID");
		СценарииБизнесПроцесса.Колонки.Добавить("Наименование");
		СценарииБизнесПроцесса.Колонки.Добавить("СценарийUID");
		СценарииБизнесПроцесса.Колонки.Добавить("СценарийНаименование");
		СценарииБизнесПроцесса.Колонки.Добавить("КодСценария");
		СценарииБизнесПроцесса.Колонки.Добавить("Исполнитель");
		СценарииБизнесПроцесса.Колонки.Добавить("ТипШага");
		
		Для Каждого СтрокаШагиПроцесса Из СтрокаДанныеПроцессы.ШагиПроцесса Цикл
			СтрокаСценарииБизнесПроцесса = СценарииБизнесПроцесса.Добавить();
			СтрокаСценарииБизнесПроцесса.Сценарий = СтрокаШагиПроцесса.Сценарий;
			СтрокаСценарииБизнесПроцесса.ШагПроцесса = СтрокаШагиПроцесса.ШагПроцесса;
			СтрокаСценарииБизнесПроцесса.ШагПроцессаUID = СтрокаШагиПроцесса.UID;
			СтрокаСценарииБизнесПроцесса.Наименование = СтрокаШагиПроцесса.Наименование;
			СтрокаСценарииБизнесПроцесса.СценарийUID = СтрокаШагиПроцесса.СценарийUID;
			СтрокаСценарииБизнесПроцесса.СценарийНаименование = СтрокаШагиПроцесса.СценарийНаименование;
			СтрокаСценарииБизнесПроцесса.КодСценария = СтрокаШагиПроцесса.СценарийКод;
			СтрокаСценарииБизнесПроцесса.Исполнитель = СтрокаШагиПроцесса.ИсполнительНаименование;
			СтрокаСценарииБизнесПроцесса.ТипШага = СтрокаШагиПроцесса.ТипШага;
		КонецЦикла;	 
	Иначе	
		СценарииБизнесПроцесса = СценарииПроцесса(Процесс);
	КонецЕсли;	 
	
	ДанныеВложенныхСценариев = ДанныеВложенныхСценариев();
	
	Если ПараметрыФормированияТекстаСценария.ЧтениеИзТекста	Тогда
		ПараметрыБизнесПроцесса = Новый ТаблицаЗначений;
		ПараметрыБизнесПроцесса.Колонки.Добавить("Имя");
		ПараметрыБизнесПроцесса.Колонки.Добавить("Значение");
		ПараметрыБизнесПроцесса.Колонки.Добавить("ТипПараметра");
		Для Каждого СтрокаТаблицаПараметров Из ПараметрыФормированияТекстаСценария.ТаблицаПараметров Цикл
			СтрокаПараметрыБизнесПроцесса = ПараметрыБизнесПроцесса.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаПараметрыБизнесПроцесса,СтрокаТаблицаПараметров);
		КонецЦикла;	 
	Иначе	
		ПараметрыБизнесПроцесса = ПараметрыПроцесса(Процесс);
	КонецЕсли;	 
	ПараметрыБизнесПроцесса.Колонки.Добавить("ЗначенияПараметра");
	
	МассивСценариев = Новый Массив;
	Для Каждого СтрокаСценария Из СценарииБизнесПроцесса Цикл
		Сценарий = СтрокаСценария.Сценарий;
		
		СтрокаДанныеВложенныхСценариев = ДанныеВложенныхСценариев.Найти(Сценарий,"Сценарий");
		
		
		Если ПараметрыФормированияТекстаСценария.ЧтениеИзТекста Тогда
			Если ПустаяСтрока(СтрокаСценария.СценарийНаименование) Тогда
				Продолжить;
			КонецЕсли;	 
			
			ПараметрыФормированияТекстаСценария.Вставить("UIDСценария",СтрокаСценария.СценарийUID);
			ПараметрыФормированияТекстаСценария.Вставить("ИмяСценария",СтрокаСценария.СценарийНаименование);
			ПараметрыФормированияТекстаСценария.Вставить("КодСценария",СтрокаСценария.КодСценария);
			ДанныеСценариев = ПараметрыФормированияТекстаСценария.ДанныеСценариев;
			СтрокаДанныеСценариев = ДанныеСценариев.Найти(СтрокаСценария.СценарийUID,"UID");
			Если СтрокаДанныеСценариев = Неопределено Тогда
				ВызватьИсключение СтрШаблон(НСтр("ru = 'Не найдены данные сценария: %1. Процесс: %2'"), Сценарий,Процесс);
			КонецЕсли;	 
			ПараметрыФормированияТекстаСценария.Вставить("UIDФункцияСистемы",СтрокаДанныеСценариев.UIDФункцияСистемы);
			ПараметрыФормированияТекстаСценария.Вставить("ПараметрыСценария",СтрокаДанныеСценариев.ПараметрыСценария);
		КонецЕсли;	 
		
		Если СтрокаДанныеВложенныхСценариев = Неопределено Тогда
			СтрокаДанныеВложенныхСценариев = ДанныеВложенныхСценариев.Добавить();
			СтрокаДанныеВложенныхСценариев.Сценарий = Сценарий;
			Если ПараметрыФормированияТекстаСценария.ЧтениеИзТекста Тогда
				СтрокаДанныеВложенныхСценариев.ПараметрыВходящие = ПустаяТаблицаПараметров();
			Иначе	
				СтрокаДанныеВложенныхСценариев.ПараметрыВходящие = ПараметрыВходящиеСценария(Сценарий);
			КонецЕсли;	 
			
			СтрокаДанныеВложенныхСценариев.ДеревоСхемы 
			    = ПолучитьДеревоСценария(СтрокаДанныеВложенныхСценариев.Сценарий,ПараметрыФормированияТекстаСценария);
		КонецЕсли;
		
		СтрокаДанныеСценариев = Неопределено;
		Если ПараметрыФормированияТекстаСценария.ЧтениеИзТекста Тогда
			ПараметрыФормированияТекстаСценария.Вставить("UIDСценария",СтрокаСценария.СценарийUID);
			ПараметрыФормированияТекстаСценария.Вставить("ИмяСценария",СтрокаСценария.СценарийНаименование);
			ПараметрыФормированияТекстаСценария.Вставить("КодСценария",СтрокаСценария.КодСценария);
			ДанныеСценариев = ПараметрыФормированияТекстаСценария.ДанныеСценариев;
			СтрокаДанныеСценариев = ДанныеСценариев.Найти(СтрокаСценария.СценарийUID,"UID");
			Если СтрокаДанныеСценариев = Неопределено Тогда
				ВызватьИсключение СтрШаблон(НСтр("ru = 'Не найдены данные сценария: %1. Процесс: %2'"), Сценарий,Процесс);
			КонецЕсли;	 
			ПараметрыФормированияТекстаСценария.Вставить("UIDФункцияСистемы",СтрокаДанныеСценариев.UIDФункцияСистемы);
			ПараметрыФормированияТекстаСценария.Вставить("ПараметрыСценария",СтрокаДанныеСценариев.ПараметрыСценария);
		КонецЕсли;	 
		
		
		Если СтрокаСценария.ТипШага = 1 Тогда
			СхемаДерево = ПолучитьДеревоВложенногоПроцесса(СтрокаСценария,ПараметрыФормированияТекстаСценария,
			ПараметрыБизнесПроцесса,ПараметрыФормированияТекстаСценария);
		Иначе	
			ПараметрыФормированияТекстаСценария.Вставить("КомпилироватьСценарий",Истина);
			СхемаДерево = ПолучитьДеревоСценария(Сценарий,ПараметрыФормированияТекстаСценария);
		КонецЕсли;	 
		
		
		Если ТипЗнч(СхемаДерево) <> Тип("ДеревоЗначений") Тогда
			ВызватьИсключение СтрШаблон(НСтр("ru = 'Не найдена схема сценария: %1'"), СтрокаСценария.Наименование);
		КонецЕсли;	
		
		КоличествоВариантовСценария = 1;
		ОбработатьЗначенияПараметровСценария(ПараметрыБизнесПроцесса,КоличествоВариантовСценария);
		Если КоличествоВариантовСценария > 100 Тогда
			ВызватьИсключение СтрШаблон(НСтр("ru = 'Слишком много вариантов сценария: %1'"),
			                            КоличествоВариантовСценария);
		КонецЕсли;	 
		
		Если ПараметрыФормированияТекстаСценария.ЧтениеИзТекста Тогда
			ЗначенияВходящихПараметров = ПараметрыШагаПроцессаИзТекста(СтрокаСценария,ПараметрыБизнесПроцесса,
			   ПараметрыФормированияТекстаСценария);
			ДополнитьПередаваемыеПараметрыПараметрамиПоУмолчанию(ЗначенияВходящихПараметров,
				СтрокаДанныеСценариев.ПараметрыСценария);
		Иначе	
			ЗначенияВходящихПараметров = ПараметрыШагаПроцесса(СтрокаСценария.ШагПроцесса,ПараметрыБизнесПроцесса,
			   СтрокаСценария,СценарииБизнесПроцесса,ПараметрыФормированияТекстаСценария);
			ДополнитьПередаваемыеПараметрыПараметрамиПоУмолчанию(ЗначенияВходящихПараметров,
				СтрокаДанныеВложенныхСценариев.ПараметрыВходящие);
		КонецЕсли;
		
		ДоТеста       = Новый ТаблицаЗначений;
		ПроверкаТеста = Новый ТаблицаЗначений;
		ПослеТеста    = Новый ТаблицаЗначений;
		
		НаборПараметров = Новый Структура;
		НаборПараметров.Вставить("ПараметрыСНесколькимиЗначениями",Новый Массив);
		НаборПараметров.Вставить("ТаблицаПараметров",ЗначенияВходящихПараметров);
		
		
		СтруктураПараметров = Новый Структура;
		СтруктураПараметров.Вставить("ЧтениеИзТекста",Ложь);
		Если ПараметрыФормированияТекстаСценария.Свойство("ЧтениеИзТекста") Тогда
			СтруктураПараметров.Вставить("ЧтениеИзТекста",ПараметрыФормированияТекстаСценария.ЧтениеИзТекста);
		КонецЕсли;	 
		
		//СхемаДерево = ДеревоСценарияИзБазыДанных(Сценарий,СтруктураПараметров);
		
		ПараметрыДляПолученияТекстаСценария = Новый Структура;
		ПараметрыДляПолученияТекстаСценария.Вставить("Сценарий",Сценарий);
		ПараметрыДляПолученияТекстаСценария.Вставить("НаборПараметров",НаборПараметров);
		ПараметрыДляПолученияТекстаСценария.Вставить("СхемаДерево",СхемаДерево);
		ПараметрыДляПолученияТекстаСценария.Вставить("ДанныеВложенныхСценариев",ДанныеВложенныхСценариев);
		ПараметрыДляПолученияТекстаСценария.Вставить("ЧтениеИзТекста",СтруктураПараметров.ЧтениеИзТекста);
		ПараметрыДляПолученияТекстаСценария.Вставить("СтруктураПараметров",ПараметрыФормированияТекстаСценария);
		ПараметрыДляПолученияТекстаСценария.Вставить("ДоТеста",ДоТеста);
		ПараметрыДляПолученияТекстаСценария.Вставить("ПроверкаТеста",ПроверкаТеста);
		ПараметрыДляПолученияТекстаСценария.Вставить("ПослеТеста",ПослеТеста);
		ПараметрыДляПолученияТекстаСценария.Вставить("НомерНабора",-1);
		ПараметрыДляПолученияТекстаСценария.Вставить("РазмерНабора",0);
		ПараметрыДляПолученияТекстаСценария.Вставить("ЭтоБизнесПроцесс",Истина);
		ПараметрыДляПолученияТекстаСценария.Вставить("ПросмотрСценария",ПараметрыФормированияТекстаСценария.ПросмотрСценария);
		ПараметрыДляПолученияТекстаСценария.Вставить("УровеньВложенности",0);
		ПараметрыДляПолученияТекстаСценария.Вставить("ИсключитьСлужебныеСловаИзТекстаСценария",
		        ПараметрыФормированияТекстаСценария.ИсключитьСлужебныеСловаИзТекстаСценария);
				
		ПараметрыДляПолученияТекстаСценария.Вставить("ФормироватьДанныеПоНомерамСтрок",Ложь);
						
		Если СтрокаДанныеСценариев <> Неопределено Тогда
			ПараметрыДляПолученияТекстаСценария.Вставить("ПараметрыСценария",СтрокаДанныеСценариев.ПараметрыСценария);
		КонецЕсли;	 
		
		ИспользоватьИмяСценария = Ложь;
		Если СтруктураПараметров.Свойство("ИспользоватьИмяСценария") Тогда
			ИспользоватьИмяСценария = СтруктураПараметров.ИспользоватьИмяСценария;
		КонецЕсли;	 
		ПараметрыДляПолученияТекстаСценария.Вставить("ИспользоватьИмяСценария",ИспользоватьИмяСценария);
		
		
		ПараметрыДляПолученияТекстаСценария.Вставить("ФормироватьДанныеПоНомерамСтрок",Ложь);
		Если СтруктураПараметров.ЧтениеИзТекста Тогда
			ПараметрыДляПолученияТекстаСценария.Вставить("ФормироватьДанныеПоНомерамСтрок",Истина);
			ПараметрыДляПолученияТекстаСценария.Вставить("ИмяСценария",СтрокаСценария.Наименование);
		Иначе	
			ПараметрыДляПолученияТекстаСценария.Вставить("ИмяСценария",СтрокаСценария.Наименование);
		КонецЕсли;	 
		
		ПараметрыДляПолученияТекстаСценария.Вставить("ИсполнительШагаПроцесса",СтрокаСценария.Исполнитель);
		
		ОбъектСценария = ТекстСценарияПоНаборуПараметров(ПараметрыДляПолученияТекстаСценария);
		
		МассивСценариев.Добавить(ОбъектСценария);
	КонецЦикла;	
	
	ЗаголовокЗначенияПараметровСценария = ЗаголовокЗначенияПараметровСценария(ПараметрыБизнесПроцесса);
	                        
	ОбъектТекстыСценариев = Новый Структура;
	ОбъектТекстыСценариев.Вставить("ТекстыСценариев",МассивСценариев);
	ОбъектТекстыСценариев.Вставить("ЗаголовокЗначенияПараметровСценария",ЗаголовокЗначенияПараметровСценария);
	ОбъектТекстыСценариев.Вставить("ФОИмеющиеОдноЗначение",Новый Массив);
	ОбъектТекстыСценариев.Вставить("ФОИмеющиеНесколькоЗначений",Новый Массив);
	
	Возврат ОбъектТекстыСценариев;
КонецФункции	

// Делает сборку текстов сценариев по данным, прочитаным из текстовых файлов
//
// Параметры:
//  ДанныеКаталогаСценариев - Тип - Массив.
//  ДанныеЭталонныхБД - Тип - Массив.
//  ДанныеСценариев - Тип - ТаблицаЗначений.
// 
Процедура ТекстыСценариевИзТекстовыхДанных(ДанныеКаталогаСценариев,ДанныеЭталонныхБД,ДанныеСценариев) Экспорт
	ДанныеСценариев.Колонки.Добавить("ИмяФайла");
	ДанныеСценариев.Колонки.Добавить("Имя",Новый ОписаниеТипов("Строка"));
	ДанныеСценариев.Колонки.Добавить("ВерсияСценария",Новый ОписаниеТипов("Строка"));
	ДанныеСценариев.Колонки.Добавить("ТекстСценария");
	ДанныеСценариев.Колонки.Добавить("Проект");
	ДанныеСценариев.Колонки.Добавить("ПараметрыСценария");
	ДанныеСценариев.Колонки.Добавить("ПрофильПользователя");
	ДанныеСценариев.Колонки.Добавить("UID",Новый ОписаниеТипов("Строка"));
	ДанныеСценариев.Колонки.Добавить("ФункцияСистемы");
	ДанныеСценариев.Колонки.Добавить("UIDФункцияСистемы");
	ДанныеСценариев.Колонки.Добавить("Код");
	ДанныеСценариев.Колонки.Добавить("ВложенныеСценарии");
	ДанныеСценариев.Колонки.Добавить("РазрешеноИспользоватьВДругихФункциях");
	ДанныеСценариев.Колонки.Добавить("ДеревоСхемы");
	ДанныеСценариев.Колонки.Добавить("ДанныеВложенныхСценариев");
	ДанныеСценариев.Колонки.Добавить("Ответственный");
	ДанныеСценариев.Колонки.Добавить("УровеньОтчета1");
	ДанныеСценариев.Колонки.Добавить("УровеньОтчета2");
	ДанныеСценариев.Индексы.Добавить("ИмяФайла");
	ДанныеСценариев.Индексы.Добавить("UID");
	
	
	ДанныеНастроек = Новый ТаблицаЗначений;
	ДанныеНастроек.Колонки.Добавить("ИмяФайла");
	ДанныеНастроек.Колонки.Добавить("Имя");
	ДанныеНастроек.Колонки.Добавить("Код");
	ДанныеНастроек.Колонки.Добавить("ИмяСценария");
	ДанныеНастроек.Колонки.Добавить("UIDСценария");
	ДанныеНастроек.Колонки.Добавить("ПараметрыТеста");
	ДанныеНастроек.Колонки.Добавить("ИдМассива");
	ДанныеНастроек.Колонки.Добавить("ИдентификаторБазы");
	ДанныеНастроек.Колонки.Добавить("ПрофильПользователя");
	ДанныеНастроек.Колонки.Добавить("ПараметрыЗапуска");
	
	
	
	Ном = -1;
	Для Каждого Элем Из ДанныеКаталогаСценариев Цикл
		Ном = Ном + 1;
		Если НРег(Элем.ДанныеФайлаYaml["ТипФайла"]) = "настройкатеста" Тогда
			СтрокаДанныеНастроек                     = ДанныеНастроек.Добавить();
			СтрокаДанныеНастроек.ИмяФайла            = Элем.ИмяФайла;
			СтрокаДанныеНастроек.Имя                 = Элем.ДанныеФайлаYaml["ДанныеТеста"]["Имя"];
			СтрокаДанныеНастроек.Код                 = Элем.ДанныеФайлаYaml["ДанныеТеста"]["Код"];
			СтрокаДанныеНастроек.ИмяСценария         = Элем.ДанныеФайлаYaml["ДанныеТеста"]["СценарийНаименование"];
			СтрокаДанныеНастроек.UIDСценария         = Элем.ДанныеФайлаYaml["ДанныеТеста"]["UIDСценария"];
			СтрокаДанныеНастроек.ИдентификаторБазы   = Элем.ДанныеФайлаYaml["ДанныеТеста"]["ИдентификаторБазы"];
			СтрокаДанныеНастроек.ПараметрыТеста      = Элем.ДанныеФайлаYaml["ПараметрыТеста"];
			СтрокаДанныеНастроек.ПрофильПользователя = Элем.ДанныеФайлаYaml["ДанныеТеста"]["ПрофильПользователя"];
			СтрокаДанныеНастроек.ПараметрыЗапуска    = Элем.ДанныеФайлаYaml["ДанныеТеста"]["ПараметрыЗапуска"];
			СтрокаДанныеНастроек.ИдМассива           = Ном;
			Продолжить;
		КонецЕсли;	 
		
		Если Элем.ДанныеФайлаYaml["ДанныеСценария"] = Неопределено Тогда
			//значит этот yaml файл не содержит данных о сценариях
			Продолжить;
		КонецЕсли;	 
		
		СтрокаДанныеСценариев                     = ДанныеСценариев.Добавить();
		СтрокаДанныеСценариев.ИмяФайла            = Элем.ИмяФайла;
		СтрокаДанныеСценариев.Имя                 = Элем.ДанныеФайлаYaml["ДанныеСценария"]["Имя"];
		СтрокаДанныеСценариев.ВерсияСценария      = Элем.ДанныеФайлаYaml["ДанныеСценария"]["ВерсияСценария"];
		СтрокаДанныеСценариев.UID                 = Элем.ДанныеФайлаYaml["ДанныеСценария"]["UID"];
		СтрокаДанныеСценариев.ФункцияСистемы      = Элем.ДанныеФайлаYaml["ДанныеСценария"]["ФункцияСистемы"];
		СтрокаДанныеСценариев.UIDФункцияСистемы   = Элем.ДанныеФайлаYaml["ДанныеСценария"]["UIDФункцияСистемы"];
		СтрокаДанныеСценариев.Код                 = Элем.ДанныеФайлаYaml["ДанныеСценария"]["Код"];
		СтрокаДанныеСценариев.Проект              = Элем.ДанныеФайлаYaml["ДанныеСценария"]["Проект"];
		СтрокаДанныеСценариев.ПрофильПользователя = Элем.ДанныеФайлаYaml["ДанныеСценария"]["ПрофильПользователя"];
		СтрокаДанныеСценариев.Ответственный       = Элем.ДанныеФайлаYaml["ДанныеСценария"]["Ответственный"];
		СтрокаДанныеСценариев.УровеньОтчета1      = Элем.ДанныеФайлаYaml["ДанныеСценария"]["УровеньОтчета1"];
		СтрокаДанныеСценариев.УровеньОтчета2      = Элем.ДанныеФайлаYaml["ДанныеСценария"]["УровеньОтчета2"];
		СтрокаДанныеСценариев.ТекстСценария       = Элем.ДанныеФайлаYaml["ТекстСценария"];
		СтрокаДанныеСценариев.ПараметрыСценария   = 
		   ПараметрыСценарияИзТекстовыхДанных(Элем.ДанныеФайлаYaml["ПараметрыСценария"]);
		СтрокаДанныеСценариев.ВложенныеСценарии   = 
		   ВложенныеСценарииИзТекстовыхДанных(Элем.ДанныеФайлаYaml["ВложенныеСценарии"]);
		   
		СтрокаДанныеСценариев.РазрешеноИспользоватьВДругихФункциях =
		      Элем.ДанныеФайлаYaml["ДанныеСценария"]["РазрешеноИспользоватьВДругихФункциях"];
	
	КонецЦикла;
		 
	Если НЕ ВыполнятьСборкуСценариев И РаботаВРежимеВнешнейОбработки Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого СтрокаДанныеНастроек Из ДанныеНастроек Цикл
		
		Попытка
			
			Если ИдБазыСборки.Количество() > 0 Тогда
				//значит идет компиляция процессов по определенным базам
				Если ИдБазыСборки.НайтиПоЗначению(НРег(СтрокаДанныеНастроек.ИдентификаторБазы)) = Неопределено Тогда
					Продолжить;
				КонецЕсли;
			КонецЕсли;
			
			Если ЕстьОтборПоНастройкам Тогда
				// значит установлен отбор по настройкам запуска
				ПропуститьНастройку = ОтборПоНастройкамСценариев.НайтиПоЗначению(СтрокаДанныеНастроек.Код) = Неопределено;
			КонецЕсли;
			
			Если ЕстьОтборПоТестам Тогда
				// значит установлен отбор по сценариям
				
				ПропуститьСценарий = Истина;
				СписокПодсценариев = Новый Массив;
				Подсценарии(СтрокаДанныеНастроек.UIDСценария, ДанныеСценариев, СписокПодсценариев);
				
				Для Каждого Сценарий Из ОтборПоСценариям Цикл
					Если СписокПодсценариев.Найти(Сценарий.Значение) <> Неопределено Тогда
						ПропуститьСценарий = Ложь;
						Прервать;
					КонецЕсли;
				КонецЦикла;
				
			КонецЕсли;
			
			Если ЕстьОтборПоНастройкам И ЕстьОтборПоТестам Тогда
				// отбор по сценариям и настройкам
				Если ПропуститьСценарий И ПропуститьНастройку Тогда
					Продолжить;
				КонецЕсли;
			ИначеЕсли ЕстьОтборПоНастройкам Тогда
				// отбор только по настройкам
				Если ПропуститьНастройку Тогда
					Продолжить;
				КонецЕсли;
			ИначеЕсли ЕстьОтборПоТестам Тогда
				// отбор только по сценариям или процессам
				Если ПропуститьСценарий Тогда
					Продолжить;
				КонецЕсли;
			КонецЕсли;
			
			СтруктураПараметров = Новый Структура;
			СтруктураПараметров.Вставить("ЧтениеИзТекста",Истина);
			СтруктураПараметров.Вставить("ИмяНастройки",СтрокаДанныеНастроек.Имя);
			СтруктураПараметров.Вставить("ТаблицаПараметров",
			     ПараметрыСценарияИзТекстовыхДанных(СтрокаДанныеНастроек.ПараметрыТеста));
			СтруктураПараметров.Вставить("ДоТеста",Новый ТаблицаЗначений);
			СтруктураПараметров.Вставить("ПроверкаТеста",Новый ТаблицаЗначений);
			СтруктураПараметров.Вставить("ПослеТеста",Новый ТаблицаЗначений);
			СтруктураПараметров.Вставить("UIDСценария",СтрокаДанныеНастроек.UIDСценария);
			СтруктураПараметров.Вставить("ДанныеСценариев",ДанныеСценариев);
			
			//добавим данные сценария
			СтрокаДанныеСценариев = ДанныеСценариев.Найти(СтрокаДанныеНастроек.UIDСценария,"UID");
			
			Если СтрокаДанныеСценариев = Неопределено Тогда
				ВызватьИсключение СтрШаблон(НСтр("ru = 'Не найден сценарий с UID: %1 для настройки сценария: %2'"), 
											СтрокаДанныеНастроек.UIDСценария,
											СтрокаДанныеНастроек.Имя);
			КонецЕсли;
			
			СтруктураПараметров.Вставить("Проект",СтрокаДанныеСценариев.Проект);
			СтруктураПараметров.Вставить("ПараметрыСценария",СтрокаДанныеСценариев.ПараметрыСценария);
			СтруктураПараметров.Вставить("ВложенныеСценарии",СтрокаДанныеСценариев.ВложенныеСценарии);
			
			СтруктураПараметров.Вставить("ИсключитьСлужебныеСловаИзТекстаСценария",Истина);
			СтруктураПараметров.Вставить("ДелатьРаскраску",Ложь);
			СтруктураПараметров.Вставить("ФункцияСистемы",СтрокаДанныеСценариев.ФункцияСистемы);
			СтруктураПараметров.Вставить("UIDФункцияСистемы",СтрокаДанныеСценариев.UIDФункцияСистемы);
			СтруктураПараметров.Вставить("ИмяСценария",СтрокаДанныеСценариев.Имя);
			СтруктураПараметров.Вставить("КодСценария",СтрокаДанныеСценариев.Код);
			СтруктураПараметров.Вставить("УровеньОтчета1",СтрокаДанныеСценариев.УровеньОтчета1);
			СтруктураПараметров.Вставить("УровеньОтчета2",СтрокаДанныеСценариев.УровеньОтчета2);
			СтруктураПараметров.Вставить("ДанныеНастройкиСценария",СтрокаДанныеНастроек);
			
			СтруктураПараметров.Вставить("ОшибкаСобрана",Ложь);
			ОбъектТекстыСценариев = ТекстыСценариев(СтрокаДанныеНастроек.ИмяСценария,СтруктураПараметров);
			
			ЭталонныеБД       = ДанныеЭталонныхБД[0]["ДанныеФайлаYaml"]["ЭталонныеБД"];
			ПараметрыСценария = Новый Структура;
			ПараметрыСценария.Вставить("ОбъектТекстыСценариев",ОбъектТекстыСценариев);
			ПараметрыСценария.Вставить("ИмяНастройки",СтрокаДанныеНастроек.Имя);
			ПараметрыСценария.Вставить("Процесс",СтрокаДанныеНастроек.ИмяСценария);
			ПараметрыСценария.Вставить("ШагиДоТеста",Новый ТаблицаЗначений);
			ПараметрыСценария.Вставить("ШагиПроверка",Новый ТаблицаЗначений);
			ПараметрыСценария.Вставить("ШагиПослеТеста",Новый ТаблицаЗначений);
			ПараметрыСценария.Вставить("ИсключитьСлужебныеСловаИзТекстаСценария",Истина);
			ПараметрыСценария.Вставить("ЧтениеИзТекста",Истина);
			ПараметрыСценария.Вставить("УровеньОтчета1",СтрокаДанныеСценариев.УровеньОтчета1);
			ПараметрыСценария.Вставить("УровеньОтчета2",СтрокаДанныеСценариев.УровеньОтчета2);
			
			Если ЗначениеЗаполнено(СтрокаДанныеНастроек.ПрофильПользователя) Тогда
				//тогда берем профиль пользователя из настройки
				ПараметрыСценария.Вставить("ПрофильПользователя",СтрокаДанныеНастроек.ПрофильПользователя);
			ИначеЕсли ЗначениеЗаполнено(СтрокаДанныеСценариев.ПрофильПользователя) Тогда
				//тогда берем профиль пользователя из сценария
				ПараметрыСценария.Вставить("ПрофильПользователя",СтрокаДанныеСценариев.ПрофильПользователя);
			Иначе
				ВызватьИсключение СтрШаблон(НСтр("ru = 'Не найден пользователь для запуска сценария %1. сценарий <%1> код <%2>'"), 
											СтрокаДанныеСценариев.Имя,
											СтрокаДанныеСценариев.Код);
			КонецЕсли;
			 
			ПараметрыСценария.Вставить("ИдентификаторБазы",СтрокаДанныеНастроек.ИдентификаторБазы);
			ПараметрыСценария.Вставить("ДанныеЭталонныхБД",ЭталонныеБД);
			
			ДанныеНомеровСтрок = Новый Массив;
			ПараметрыСценария.Вставить("ДанныеНомеровСтрок",ДанныеНомеровСтрок);
			
			ТекстСценария = ТекстСценария(ПараметрыСценария);
			
			
			ДанныеКаталогаСценариев[СтрокаДанныеНастроек.ИдМассива].Вставить("ЛинейныйСценарий",ТекстСценария);
			ДанныеКаталогаСценариев[СтрокаДанныеНастроек.ИдМассива].Вставить("ДанныеНомеровСтрок",ДанныеНомеровСтрок);
			ДанныеКаталогаСценариев[СтрокаДанныеНастроек.ИдМассива].Вставить("КодНастройкиСценария",СтрокаДанныеНастроек.Код);
			ДанныеКаталогаСценариев[СтрокаДанныеНастроек.ИдМассива].Вставить("ИмяНастройкиСценария",СтрокаДанныеНастроек.Имя);
			ДанныеКаталогаСценариев[СтрокаДанныеНастроек.ИдМассива].Вставить(
			               "Ответственный",СтрокаДанныеСценариев.Ответственный);
			ДанныеКаталогаСценариев[СтрокаДанныеНастроек.ИдМассива].Вставить(
			        "ИдентификаторБазы",СтрокаДанныеНастроек.ИдентификаторБазы);
			
		Исключение
			
			Если НЕ СтруктураПараметров.ОшибкаСобрана Тогда
				СтрокаДанныеСценариев = ДанныеСценариев.Найти(СтрокаДанныеНастроек.UIDСценария,"UID");
				
				НоваяОшибка = Ошибки.Добавить();
				Если ЗначениеЗаполнено(СтрокаДанныеСценариев) Тогда
					НоваяОшибка.КодСценария = СтрокаДанныеСценариев.Код;
					НоваяОшибка.ИмяСценария = СтрокаДанныеСценариев.Имя;
				КонецЕсли;
				НоваяОшибка.ОписаниеОшибки =  ОписаниеОшибки();
				НоваяОшибка.ПодробноеПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
				
				ДополнительнаяИнформацияОбОшибке = СтрШаблон(НСтр("ru = '%1 UIDСценария: %2;%1 Имя настройки: %3;%1 Код настройки: %4;%1 Имя сценария: %5;%1 Имя файла: %6;'"),
				Символы.ПС,
				СтрокаДанныеНастроек.UIDСценария,
				СтрокаДанныеНастроек.Имя,
				СтрокаДанныеНастроек.Код,
				СтрокаДанныеНастроек.ИмяСценария,
				СтрокаДанныеНастроек.ИмяФайла);
				НоваяОшибка.ПодробноеПредставлениеОшибки = НоваяОшибка.ПодробноеПредставлениеОшибки + ДополнительнаяИнформацияОбОшибке;
			КонецЕсли;	 
			
		КонецПопытки;
	
	КонецЦикла;
	
КонецПроцедуры

// Делает сборку текстов процессов по данным, прочитаным из текстовых файлов
//
// Параметры:
//  ДанныеКаталогаПроцессов - Тип - Массив.
//  ДанныеСценариев - Тип - ТаблицаЗначений.
//  ДанныеЭталонныхБД - Тип - Массив.
// 
Процедура ТекстыПроцессовИзТекстовыхДанных(ДанныеКаталогаПроцессов,ДанныеСценариев,ДанныеЭталонныхБД) Экспорт
	ДанныеПроцессов = Новый ТаблицаЗначений;
	ДанныеПроцессов.Колонки.Добавить("ИмяФайла");
	ДанныеПроцессов.Колонки.Добавить("Имя",Новый ОписаниеТипов("Строка"));
	ДанныеПроцессов.Колонки.Добавить("ВерсияСценария",Новый ОписаниеТипов("Строка"));
	ДанныеПроцессов.Колонки.Добавить("Проект");
	ДанныеПроцессов.Колонки.Добавить("ПараметрыСценария");
	ДанныеПроцессов.Колонки.Добавить("ПрофильПользователя");
	ДанныеПроцессов.Колонки.Добавить("UID",Новый ОписаниеТипов("Строка"));
	ДанныеПроцессов.Колонки.Добавить("Код");
	ДанныеПроцессов.Колонки.Добавить("ПолныйКод");
	ДанныеПроцессов.Колонки.Добавить("Ответственный");
	ДанныеПроцессов.Колонки.Добавить("УровеньОтчета1");
	ДанныеПроцессов.Колонки.Добавить("УровеньОтчета2");
	ДанныеПроцессов.Колонки.Добавить("ШагиПроцесса");
	ДанныеПроцессов.Индексы.Добавить("ИмяФайла");
	ДанныеПроцессов.Индексы.Добавить("UID");
	
	ДанныеНастроек = Новый ТаблицаЗначений;
	ДанныеНастроек.Колонки.Добавить("ИмяФайла");
	ДанныеНастроек.Колонки.Добавить("Имя");
	ДанныеНастроек.Колонки.Добавить("Код");
	ДанныеНастроек.Колонки.Добавить("ИмяПроцесса");
	ДанныеНастроек.Колонки.Добавить("UIDПроцесса");
	ДанныеНастроек.Колонки.Добавить("ПараметрыТеста");
	ДанныеНастроек.Колонки.Добавить("ИдМассива");
	ДанныеНастроек.Колонки.Добавить("ИдентификаторБазы");
	ДанныеНастроек.Колонки.Добавить("ПрофильПользователя");
	ДанныеНастроек.Колонки.Добавить("ПараметрыЗапуска");
	
	
	Ном = -1;
	Для Каждого Элем Из ДанныеКаталогаПроцессов Цикл
		Ном = Ном + 1;
		Если НРег(Элем.ДанныеФайлаYaml["ТипФайла"]) = "настройкапроцесса" Тогда
			СтрокаДанныеНастроек                     = ДанныеНастроек.Добавить();
			СтрокаДанныеНастроек.ИмяФайла            = Элем.ИмяФайла;
			СтрокаДанныеНастроек.Имя                 = Элем.ДанныеФайлаYaml["ДанныеТеста"]["Имя"];
			СтрокаДанныеНастроек.Код                 = Элем.ДанныеФайлаYaml["ДанныеТеста"]["Код"];
			СтрокаДанныеНастроек.ИмяПроцесса         = Элем.ДанныеФайлаYaml["ДанныеТеста"]["ПроцессНаименование"];
			СтрокаДанныеНастроек.UIDПроцесса         = Элем.ДанныеФайлаYaml["ДанныеТеста"]["UIDПроцесса"];
			СтрокаДанныеНастроек.ИдентификаторБазы   = Элем.ДанныеФайлаYaml["ДанныеТеста"]["ИдентификаторБазы"];
			СтрокаДанныеНастроек.ПараметрыТеста      = Элем.ДанныеФайлаYaml["ПараметрыТеста"];
			СтрокаДанныеНастроек.ПрофильПользователя = Элем.ДанныеФайлаYaml["ДанныеТеста"]["ПрофильПользователя"];
			СтрокаДанныеНастроек.ПараметрыЗапуска    = Элем.ДанныеФайлаYaml["ДанныеТеста"]["ПараметрыЗапуска"];
			СтрокаДанныеНастроек.ИдМассива           = Ном;
			Продолжить;
		КонецЕсли;	 
		
		Если Элем.ДанныеФайлаYaml["ДанныеПроцесса"] = Неопределено Тогда
			//значит этот yaml файл не содержит данных о сценариях
			Продолжить;
		КонецЕсли;	 
		
		СтрокаДанныеПроцессов                     = ДанныеПроцессов.Добавить();
		СтрокаДанныеПроцессов.ИмяФайла            = Элем.ИмяФайла;
		СтрокаДанныеПроцессов.Имя                 = Элем.ДанныеФайлаYaml["ДанныеПроцесса"]["Имя"];
		СтрокаДанныеПроцессов.ВерсияСценария      = Элем.ДанныеФайлаYaml["ДанныеПроцесса"]["ВерсияСценария"];
		СтрокаДанныеПроцессов.UID                 = Элем.ДанныеФайлаYaml["ДанныеПроцесса"]["UID"];
		СтрокаДанныеПроцессов.Код                 = Элем.ДанныеФайлаYaml["ДанныеПроцесса"]["Код"];
		СтрокаДанныеПроцессов.ПолныйКод           = Элем.ДанныеФайлаYaml["ДанныеПроцесса"]["ПолныйКод"];
		СтрокаДанныеПроцессов.Проект              = Элем.ДанныеФайлаYaml["ДанныеПроцесса"]["Проект"];
		СтрокаДанныеПроцессов.ПрофильПользователя = Элем.ДанныеФайлаYaml["ДанныеПроцесса"]["ПрофильПользователя"];
		СтрокаДанныеПроцессов.Ответственный       = Элем.ДанныеФайлаYaml["ДанныеПроцесса"]["Ответственный"];
		СтрокаДанныеПроцессов.УровеньОтчета1      = Элем.ДанныеФайлаYaml["ДанныеПроцесса"]["УровеньОтчета1"];
		СтрокаДанныеПроцессов.УровеньОтчета2      = Элем.ДанныеФайлаYaml["ДанныеПроцесса"]["УровеньОтчета2"];
		СтрокаДанныеПроцессов.ШагиПроцесса   = 
		   ШагиПроцессаИзТекстовыхДанных(Элем.ДанныеФайлаYaml["ШагиПроцесса"]);
		
	КонецЦикла;
	
	Если НЕ ВыполнятьСборкуПроцессов И РаботаВРежимеВнешнейОбработки Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого СтрокаДанныеНастроек Из ДанныеНастроек Цикл
		
		Попытка
		
			Если ИдБазыСборки.Количество() > 0 Тогда
				//значит идет компиляция сценариев по определенным базам
				Если ИдБазыСборки.НайтиПоЗначению(НРег(СтрокаДанныеНастроек.ИдентификаторБазы)) = Неопределено Тогда
					Продолжить;
				КонецЕсли;
			КонецЕсли;
			
			Если ЕстьОтборПоНастройкам Тогда
				// установлен отбор по настройкам
				ПропуститьНастройку = ОтборПоНастройкамПроцессов.НайтиПоЗначению(СтрокаДанныеНастроек.Код) = Неопределено;
			КонецЕсли;
			
			Если ЕстьОтборПоТестам  Тогда
				// значит установлен отбор по процессам или сценариям
				
				ПропуститьПроцесс = Истина;
				СписокПодпроцессов = Новый Массив;
				СписокПодсценариев = Новый Массив;
				Подпроцессы(СтрокаДанныеНастроек.UIDПроцесса, ДанныеПроцессов, ДанныеСценариев, 
									СписокПодпроцессов, СписокПодсценариев);
				
				Для Каждого Сценарий Из ОтборПоСценариям Цикл
					Если СписокПодсценариев.Найти(Сценарий.Значение) <> Неопределено Тогда
						ПропуститьПроцесс = Ложь;
						Прервать;
					КонецЕсли;
				КонецЦикла;
				
				Для Каждого Процесс Из ОтборПоПроцессам Цикл
					Если СписокПодпроцессов.Найти(Процесс.Значение) <> Неопределено Тогда
						ПропуститьПроцесс = Ложь;
						Прервать;
					КонецЕсли;
				КонецЦикла;
				
			КонецЕсли;
			
			Если ЕстьОтборПоНастройкам И ЕстьОтборПоТестам Тогда
				// отбор по процессам или сценариям и по настройкам
				Если ПропуститьПроцесс И ПропуститьНастройку Тогда
					Продолжить;
				КонецЕсли;
			ИначеЕсли ЕстьОтборПоНастройкам Тогда
				// отбор только по настройкам
				Если ПропуститьНастройку Тогда
					Продолжить;
				КонецЕсли;
			ИначеЕсли ЕстьОтборПоТестам Тогда
				// отбор только по процессам или сценариям
				Если ПропуститьПроцесс Тогда
					Продолжить;
				КонецЕсли;
			КонецЕсли;
			
			СтруктураПараметров = Новый Структура;
			СтруктураПараметров.Вставить("ЧтениеИзТекста",Истина);
			СтруктураПараметров.Вставить("ИмяНастройки",СтрокаДанныеНастроек.Имя);
			СтруктураПараметров.Вставить("ТаблицаПараметров",
			     ПараметрыСценарияИзТекстовыхДанных(СтрокаДанныеНастроек.ПараметрыТеста));
			СтруктураПараметров.Вставить("ДоТеста",Новый ТаблицаЗначений);
			СтруктураПараметров.Вставить("ПроверкаТеста",Новый ТаблицаЗначений);
			СтруктураПараметров.Вставить("ПослеТеста",Новый ТаблицаЗначений);
			СтруктураПараметров.Вставить("UIDПроцесса",СтрокаДанныеНастроек.UIDПроцесса);
			СтруктураПараметров.Вставить("ДанныеПроцессов",ДанныеПроцессов);
			
			//добавим данные сценария
			СтрокаДанныеПроцессов = ДанныеПроцессов.Найти(СтрокаДанныеНастроек.UIDПроцесса,"UID");
			СтруктураПараметров.Вставить("Проект",СтрокаДанныеПроцессов.Проект);
			СтруктураПараметров.Вставить("ПараметрыСценария",СтрокаДанныеПроцессов.ПараметрыСценария);
			
			СтруктураПараметров.Вставить("ИсключитьСлужебныеСловаИзТекстаСценария",Истина);
			СтруктураПараметров.Вставить("ДелатьРаскраску",Ложь);
			СтруктураПараметров.Вставить("УровеньОтчета1",СтрокаДанныеПроцессов.УровеньОтчета1);
			СтруктураПараметров.Вставить("УровеньОтчета2",СтрокаДанныеПроцессов.УровеньОтчета2);
			
			СтруктураПараметров.Вставить("ДанныеСценариев",ДанныеСценариев);
			СтруктураПараметров.Вставить("ПросмотрСценария",Ложь);
			СтруктураПараметров.Вставить("ДанныеЭталонныхБД",ДанныеЭталонныхБД);
			СтруктураПараметров.Вставить("ТекущаяНастройкаПроцесса",СтрокаДанныеНастроек);
			
			ОбъектТекстыСценариев = ДанныеДляТекстаСценарияПроцесса(СтрокаДанныеНастроек.ИмяПроцесса,СтруктураПараметров);
			
			ЭталонныеБД       = ДанныеЭталонныхБД[0]["ДанныеФайлаYaml"]["ЭталонныеБД"];
			ПараметрыПроцесса = Новый Структура;
			ПараметрыПроцесса.Вставить("ОбъектТекстыСценариев",ОбъектТекстыСценариев);
			ПараметрыПроцесса.Вставить("ИмяНастройки",СтрокаДанныеНастроек.Имя);
			ПараметрыПроцесса.Вставить("КодНастройки",СтрокаДанныеНастроек.Код);
			ПараметрыПроцесса.Вставить("Процесс",СтрокаДанныеНастроек.ИмяПроцесса);
			ПараметрыПроцесса.Вставить("ПолныйКодПроцесса",СтрокаДанныеПроцессов.ПолныйКод);
			ПараметрыПроцесса.Вставить("ШагиДоТеста",Новый ТаблицаЗначений);
			ПараметрыПроцесса.Вставить("ШагиПроверка",Новый ТаблицаЗначений);
			ПараметрыПроцесса.Вставить("ШагиПослеТеста",Новый ТаблицаЗначений);
			ПараметрыПроцесса.Вставить("ИсключитьСлужебныеСловаИзТекстаСценария",Истина);
			ПараметрыПроцесса.Вставить("ЧтениеИзТекста",Истина);
			ПараметрыПроцесса.Вставить("УровеньОтчета1",СтрокаДанныеПроцессов.УровеньОтчета1);
			ПараметрыПроцесса.Вставить("УровеньОтчета2",СтрокаДанныеПроцессов.УровеньОтчета2);
			
			Если ЗначениеЗаполнено(СтрокаДанныеНастроек.ПрофильПользователя) Тогда
				//тогда берем профиль пользователя из настройки
				ПараметрыПроцесса.Вставить("ПрофильПользователя",СтрокаДанныеНастроек.ПрофильПользователя);
			Иначе	
				//тогда берем профиль пользователя из сценария
				ПараметрыПроцесса.Вставить("ПрофильПользователя",СтрокаДанныеПроцессов.ПрофильПользователя);
			КонецЕсли;	 
			ПараметрыПроцесса.Вставить("ИдентификаторБазы",СтрокаДанныеНастроек.ИдентификаторБазы);
			ПараметрыПроцесса.Вставить("ДанныеЭталонныхБД",ЭталонныеБД);
			
			ДанныеНомеровСтрок = Новый Массив;
			ПараметрыПроцесса.Вставить("ДанныеНомеровСтрок",ДанныеНомеровСтрок);
			ПараметрыПроцесса.Вставить("ЭтоБизнесПроцесс",Истина);
			
			
			ТекстСценария = ТекстБизнесПроцесса(ПараметрыПроцесса);
			
			
			ДанныеКаталогаПроцессов[СтрокаДанныеНастроек.ИдМассива].Вставить("ЛинейныйСценарий",ТекстСценария);
			ДанныеКаталогаПроцессов[СтрокаДанныеНастроек.ИдМассива].Вставить("ДанныеНомеровСтрок",ДанныеНомеровСтрок);
			ДанныеКаталогаПроцессов[СтрокаДанныеНастроек.ИдМассива].Вставить("КодНастройкиСценария",СтрокаДанныеНастроек.Код);
			ДанныеКаталогаПроцессов[СтрокаДанныеНастроек.ИдМассива].Вставить("ИмяНастройкиСценария",СтрокаДанныеНастроек.Имя);
			ДанныеКаталогаПроцессов[СтрокаДанныеНастроек.ИдМассива].Вставить(
			               "Ответственный",СтрокаДанныеПроцессов.Ответственный);
			ДанныеКаталогаПроцессов[СтрокаДанныеНастроек.ИдМассива].Вставить(
			        "ИдентификаторБазы",СтрокаДанныеНастроек.ИдентификаторБазы);
			
		Исключение
			
			НоваяОшибка = Ошибки.Добавить();
			НоваяОшибка.ОписаниеОшибки =  ОписаниеОшибки();
			НоваяОшибка.ПодробноеПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			
			ДополнительнаяИнформацияОбОшибке = СтрШаблон(НСтр("ru = '%1 UIDПроцесса: %2;%1 Имя настройки: %3;%1 Код настройки: %4;%1 Имя процесса: %5;%1 Имя файла: %6;'"),
											Символы.ПС,
											СтрокаДанныеНастроек.UIDПроцесса,
											СтрокаДанныеНастроек.Имя,
											СтрокаДанныеНастроек.Код,
											СтрокаДанныеНастроек.ИмяПроцесса,
											СтрокаДанныеНастроек.ИмяФайла);
			НоваяОшибка.ПодробноеПредставлениеОшибки = НоваяОшибка.ПодробноеПредставлениеОшибки + ДополнительнаяИнформацияОбОшибке;
			
		КонецПопытки;
			
	КонецЦикла;	
КонецПроцедуры 

// Формирует тексты сценариев по переданным параметрам
//
// Параметры:
//  ДанныеКаталогаСценариев - Тип - Справочник.СценарииРаботыПользователей.
//  СтруктураПараметров - Тип - Структура.
// 
Функция ТекстыСценариев(Сценарий,СтруктураПараметров) Экспорт
	ЧтениеИзТекста = СтруктураПараметров.ЧтениеИзТекста;
	ПараметрыТеста = СтруктураПараметров.ТаблицаПараметров;
	ДоТеста        = СтруктураПараметров.ДоТеста;
	ПроверкаТеста  = СтруктураПараметров.ПроверкаТеста;
	ПослеТеста     = СтруктураПараметров.ПослеТеста;
	
	ДанныеВложенныхСценариев = ДанныеВложенныхСценариев();
	
	Если СтруктураПараметров.ЧтениеИзТекста Тогда
		UIDСценария = СтруктураПараметров.UIDСценария;
		СтрокаДанныеСценариев = СтруктураПараметров.ДанныеСценариев.Найти(UIDСценария,"UID");
		СхемаДерево  = СтрокаДанныеСценариев.ДеревоСхемы;
	Иначе
		СхемаДерево  = СтруктураПараметров.ДеревоСхемы;
	КонецЕсли;	 
	
	Если СхемаДерево = Неопределено Тогда
		СхемаДерево = ДеревоСценарияИзБазыДанных(Сценарий,СтруктураПараметров);
	КонецЕсли;	 
	
	Если ТипЗнч(СхемаДерево) <> Тип("ДеревоЗначений") Тогда
		ВызватьИсключение СтрШаблон(НСтр("ru = 'Не найдена схема сценария: %1'"), Сценарий);
	КонецЕсли;	 
	
	ПараметрыТеста.Колонки.Добавить("ЗначенияПараметра");
	Если ЧтениеИзТекста Тогда
		Для Каждого ПараметрСценария Из СтрокаДанныеСценариев.ПараметрыСценария Цикл
			ПараметрТеста = ПараметрыТеста.Найти(ПараметрСценария.Имя,"Имя");
			Если ПараметрТеста <> Неопределено Тогда
				Продолжить;
			КонецЕсли;	 
			
			ПараметрТеста = ПараметрыТеста.Добавить();
			ЗаполнитьЗначенияСвойств(ПараметрТеста,ПараметрСценария);
			
			МассивЗначений = СтрРазделить(ПараметрТеста.Значение,";");
			Если МассивЗначений.Количество() <= 1 Тогда
				ПараметрТеста.НесколькоЗначений = Ложь;
			Иначе	
				ПараметрТеста.НесколькоЗначений = Истина;
			КонецЕсли;	 
			
		КонецЦикла;	 
	КонецЕсли;	 
	
	КоличествоВариантовСценария = 1;
	ОбработатьЗначенияПараметровСценария(ПараметрыТеста,КоличествоВариантовСценария);
	Если КоличествоВариантовСценария > 100 Тогда
		ВызватьИсключение СтрШаблон(НСтр("ru = 'Слишком много вариантов сценария: %1'"),
																				  КоличествоВариантовСценария);
	КонецЕсли;	 
	
	НаборыПараметровСценария = НаборыПараметровСценария(ПараметрыТеста);
	
	МассивСценариев = Новый Массив;
	
	НомерНабора = 0;
	Для Каждого НаборПараметров Из НаборыПараметровСценария Цикл
		НомерНабора = НомерНабора + 1;
		
		ПараметрыДляПолученияТекстаСценария = Новый Структура;
		ПараметрыДляПолученияТекстаСценария.Вставить("Сценарий",Сценарий);
		ПараметрыДляПолученияТекстаСценария.Вставить("ДанныеВложенныхСценариев",ДанныеВложенныхСценариев);
		ПараметрыДляПолученияТекстаСценария.Вставить("ЧтениеИзТекста",ЧтениеИзТекста);
		ПараметрыДляПолученияТекстаСценария.Вставить("СтруктураПараметров",СтруктураПараметров);
		ПараметрыДляПолученияТекстаСценария.Вставить("НаборПараметров",НаборПараметров);
		ПараметрыДляПолученияТекстаСценария.Вставить("СхемаДерево",СхемаДерево);
		ПараметрыДляПолученияТекстаСценария.Вставить("ДоТеста",ДоТеста);
		ПараметрыДляПолученияТекстаСценария.Вставить("ПроверкаТеста",ПроверкаТеста);
		ПараметрыДляПолученияТекстаСценария.Вставить("ПослеТеста",ПослеТеста);
		ПараметрыДляПолученияТекстаСценария.Вставить("НомерНабора",НомерНабора);
		ПараметрыДляПолученияТекстаСценария.Вставить("РазмерНабора",НаборыПараметровСценария.Количество());
		ПараметрыДляПолученияТекстаСценария.Вставить("ЭтоБизнесПроцесс",Ложь);
		ПараметрыДляПолученияТекстаСценария.Вставить("УровеньВложенности",0);
		ПараметрыДляПолученияТекстаСценария.Вставить("ИсключитьСлужебныеСловаИзТекстаСценария",
		        СтруктураПараметров.ИсключитьСлужебныеСловаИзТекстаСценария);
				
		Если СтруктураПараметров.Свойство("СТекущегоШага") Тогда
			ПараметрыДляПолученияТекстаСценария.Вставить("СТекущегоШага",СтруктураПараметров.СТекущегоШага);
			ПараметрыДляПолученияТекстаСценария.Вставить("ТекущийНомерСтрокиСценария",СтруктураПараметров.ТекущийНомерСтрокиСценария);
		КонецЕсли;	 
		
		Если СтруктураПараметров.Свойство("ПараметрыСценария") Тогда
			ПараметрыДляПолученияТекстаСценария.Вставить("ПараметрыСценария",СтруктураПараметров.ПараметрыСценария);
		КонецЕсли;	 
		
		ИспользоватьИмяСценария = Ложь;
		Если СтруктураПараметров.Свойство("ИспользоватьИмяСценария") Тогда
			ИспользоватьИмяСценария = СтруктураПараметров.ИспользоватьИмяСценария;
		КонецЕсли;	 
		ПараметрыДляПолученияТекстаСценария.Вставить("ИспользоватьИмяСценария",ИспользоватьИмяСценария);
		
		
		ПараметрыДляПолученияТекстаСценария.Вставить("ФормироватьДанныеПоНомерамСтрок",Ложь);
		Если ЧтениеИзТекста Тогда
			ПараметрыДляПолученияТекстаСценария.Вставить("ФормироватьДанныеПоНомерамСтрок",Истина);
		КонецЕсли;	 
		
		ОбъектСценария = ТекстСценарияПоНаборуПараметров(ПараметрыДляПолученияТекстаСценария);
		МассивСценариев.Добавить(ОбъектСценария);
	КонецЦикла;	
	
	ЗаголовокЗначенияПараметровСценария = ЗаголовокЗначенияПараметровСценария(ПараметрыТеста);
	ФОИмеющиеОдноЗначение               = ФОИмеющиеОдноЗначение(ПараметрыТеста);
	ФОИмеющиеНесколькоЗначений          = ФОИмеющиеНесколькоЗначений(ПараметрыТеста);
	
	ОбъектТекстыСценариев = Новый Структура;
	ОбъектТекстыСценариев.Вставить("ТекстыСценариев",МассивСценариев);
	ОбъектТекстыСценариев.Вставить("ЗаголовокЗначенияПараметровСценария",ЗаголовокЗначенияПараметровСценария);
	ОбъектТекстыСценариев.Вставить("ФОИмеющиеОдноЗначение",ФОИмеющиеОдноЗначение);
	ОбъектТекстыСценариев.Вставить("ФОИмеющиеНесколькоЗначений",ФОИмеющиеНесколькоЗначений);
	
	Возврат ОбъектТекстыСценариев;
КонецФункции	 

// Возвращает массив слов Gherkin, преобразованных к нижнему регистру
//
Функция МассивПервыхСловGherkinНРег() Экспорт
	МассивПервыхСлов     = МассивПервыхСловGherkin();
	МассивПервыхСловНРег = Новый Массив;
	Для Каждого Стр Из МассивПервыхСлов Цикл
		МассивПервыхСловНРег.Добавить(НРег(Стр));
	КонецЦикла;	
	
	Возврат МассивПервыхСловНРег;
КонецФункции	

// Делает рекурсиный обход дерева значений, чтобы заполнить нужную колонку
//
// Параметры:
//  Дерево - Тип - ДеревоЗначений.
//  ИдСтрокиДерева - Тип - Число.
// 
Процедура ПроставитьИдСтрокиИЗависимыеПараметрыДереваРекурсивно(Дерево,ИдСтрокиДерева) Экспорт
	Для Каждого СтрокаДерева Из Дерево.Строки Цикл
		ИдСтрокиДерева              = ИдСтрокиДерева + 1;
		СтрокаДерева.ИдСтрокиДерева = ИдСтрокиДерева;
		
		СтрокаДерева.ИмяЭлемента          = ИмяЭлемента(СтрокаДерева.ТипЭлемента,ИдСтрокиДерева);
		
		Если СтрокаДерева.Строки.Количество() > 0 Тогда
			ПроставитьИдСтрокиИЗависимыеПараметрыДереваРекурсивно(СтрокаДерева,ИдСтрокиДерева);
		КонецЕсли;	 
	КонецЦикла;	
КонецПроцедуры

//Возвращает тип элемента, соответствующий шаблону сценария
Функция ТипЭлементаШаблонСценария() Экспорт
	Возврат "ШаблонСценария";
КонецФункции	 

//Возвращает дерево сценария 
Функция СоздатьДеревоСхемы() Экспорт
	ДеревоСхемы = Новый ДеревоЗначений;
	ДеревоСхемы.Колонки.Добавить("ИмяЭлемента");
	ДеревоСхемы.Колонки.Добавить("ТипЭлемента");
	ДеревоСхемы.Колонки.Добавить("ЗначениеУсловия");
	ДеревоСхемы.Колонки.Добавить("ИмяМетки");
	ДеревоСхемы.Колонки.Добавить("ИдСтрокиТаблицаСтрок");
	ДеревоСхемы.Колонки.Добавить("Параметры");
	ДеревоСхемы.Колонки.Добавить("ОбработаннаяСтрокаПараметров");
	ДеревоСхемы.Колонки.Добавить("ПодчиненнаяСхема");
	ДеревоСхемы.Колонки.Добавить("ОписаниеЭлемента");
	ДеревоСхемы.Колонки.Добавить("ИдСтрокиДерева");
	ДеревоСхемы.Колонки.Добавить("ПерейтиКМетке");
	ДеревоСхемы.Колонки.Добавить("Комментарии");
	ДеревоСхемы.Колонки.Добавить("Теги");
	ДеревоСхемы.Колонки.Добавить("НомерСтрокиТекстаСценария");
	ДеревоСхемы.Колонки.Добавить("ДополнительныеСвойства");
	ДеревоСхемы.Колонки.Добавить("ЭтаСтрокаВложенногоСценария",Новый ОписаниеТипов("Булево"));
	
	Возврат ДеревоСхемы;
КонецФункции	

#КонецОбласти

# Область СлужебныеПроцедурыИФункции

Функция ДанныеСценарияДляПолученияСхемыСценария(Сценарий)
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СценарииРаботыПользователей.Ссылка КАК Ссылка,
		|	СценарииРаботыПользователей.Владелец КАК Владелец,
		|	СценарииРаботыПользователей.ОбычныйТекст КАК ОбычныйТекст,
		|	СценарииРаботыПользователей.ПараметрыВходящие.(
		|		Ссылка КАК Ссылка,
		|		НомерСтроки КАК НомерСтроки,
		|		Имя КАК Имя,
		|		Значение КАК Значение,
		|		ФО КАК ФО,
		|		ТипПараметра КАК ТипПараметра,
		|		ИсходящийПараметр КАК ИсходящийПараметр
		|	) КАК ПараметрыВходящие
		|ИЗ
		|	Справочник.СценарииРаботыПользователей КАК СценарииРаботыПользователей
		|ГДЕ
		|	СценарииРаботыПользователей.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Сценарий);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Результат = Новый Структура;
	Результат.Вставить("Проект",Неопределено);
	Результат.Вставить("Текст",Неопределено);
	Результат.Вставить("ПараметрыВходящие",Неопределено);
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Результат.Вставить("Проект",ВыборкаДетальныеЗаписи.Владелец);
		Результат.Вставить("Текст",ВыборкаДетальныеЗаписи.ОбычныйТекст);
		Результат.Вставить("ПараметрыВходящие",ВыборкаДетальныеЗаписи.ПараметрыВходящие.Выгрузить());
	КонецЦикла;

	Возврат Результат;
КонецФункции

// Дополняет массив МассивПриемник значениями из массива МассивИсточник.
//
// Параметры:
//  МассивПриемник - Массив - массив, в который необходимо добавить значения.
//  МассивИсточник - Массив - массив значений для заполнения.
//  ТолькоУникальныеЗначения - Булево - если истина, то в массив будут включены только уникальные значения.
//
Функция Подсценарии(UIDСценария, ДанныеСценариев, МассивПодцсценариев)
	
	СтрокаДанныеСценариев = ДанныеСценариев.Найти(UIDСценария, "UID");
	Если СтрокаДанныеСценариев <> Неопределено Тогда
		Если ЗначениеЗаполнено(СтрокаДанныеСценариев) Тогда
			МассивПодцсценариев.Добавить(СтрокаДанныеСценариев.Код);
			Для Каждого ВложенныйСценарий Из СтрокаДанныеСценариев.ВложенныеСценарии Цикл
				 Подсценарии(ВложенныйСценарий.UID, ДанныеСценариев, МассивПодцсценариев);
			 КонецЦикла;
		КонецЕсли;  
	КонецЕсли;

КонецФункции

Функция Подпроцессы(UIDПроцесса, ДанныеПроцесса, ДанныеСценариев, МассивПодпроцессов, МассивПодцсценариев)
	
	СтрокаДанныеПроцесса = ДанныеПроцесса.Найти(UIDПроцесса, "UID");
	Если СтрокаДанныеПроцесса <> Неопределено Тогда
	
		МассивПодпроцессов.Добавить(СтрокаДанныеПроцесса.ПолныйКод);
		
		Для Каждого ШагПроцесса Из СтрокаДанныеПроцесса.ШагиПроцесса Цикл
			РезультатШага = Новый Массив;
			Если ШагПроцесса.ТипШага = 0 Тогда
				Подсценарии(ШагПроцесса.СценарийUID, ДанныеСценариев, МассивПодцсценариев);

			ИначеЕсли ШагПроцесса.ТипШага = 1 Тогда
				Подпроцессы(ШагПроцесса.ПроцессUID, ДанныеПроцесса, ДанныеСценариев, МассивПодпроцессов, МассивПодцсценариев);
				
			Иначе
				ВызватьИсключение СтрШаблон(НСтр("ru = 'Неизвестный тип шага %1 в процессе %1'"), 
											ШагПроцесса.UID, UIDПроцесса);
			КонецЕсли;
			
		КонецЦикла;
	КонецЕсли;
	
КонецФункции

Функция ПараметрыСценарияИзТекстовыхДанных(МассивДанных)
	Тзн = ПустаяТаблицаПараметров();
	Если МассивДанных = Неопределено Тогда
		Возврат Тзн;
	КонецЕсли;	 
	
	Для Каждого Элем Из МассивДанных Цикл
		СтрТзн                   = Тзн.Добавить();
		СтрТзн.Значение          = Элем["Значение"];
		СтрТзн.Имя               = Элем["Имя"];
		
		Если  НРег(Элем["ИсходящийПараметр"]) = "да" Тогда
			СтрТзн.ИсходящийПараметр = Истина;
		Иначе	
			СтрТзн.ИсходящийПараметр = Ложь;
		КонецЕсли;	 
		
		СтрТзн.ТипПараметра      = Элем["ТипПараметра"];
		
		Если  НРег(Элем["НесколькоЗначений"]) = "да" Тогда
			СтрТзн.НесколькоЗначений = Истина;
		Иначе	
			СтрТзн.НесколькоЗначений = Ложь;
		КонецЕсли;	 
	КонецЦикла;	
	
	Возврат Тзн;
КонецФункции	

Функция ПустаяТаблицаВложенныхСценариев()
	Тзн = Новый ТаблицаЗначений;
	Тзн.Колонки.Добавить("Версия");
	Тзн.Колонки.Добавить("UID");
	Тзн.Колонки.Добавить("ИмяСценария");
	
	Возврат Тзн;
КонецФункции	

Функция ПустаяТаблицаШаговПроцесса()
	Тзн = Новый ТаблицаЗначений;
	Тзн.Колонки.Добавить("ШагПроцесса");
	Тзн.Колонки.Добавить("UID");
	Тзн.Колонки.Добавить("Процесс");
	Тзн.Колонки.Добавить("Наименование");
	Тзн.Колонки.Добавить("Сценарий");
	Тзн.Колонки.Добавить("СценарийНаименование");
	Тзн.Колонки.Добавить("СценарийКод");
	Тзн.Колонки.Добавить("СценарийUID");
	Тзн.Колонки.Добавить("ПолныйКод");
	Тзн.Колонки.Добавить("ИсполнительНаименование");
	Тзн.Колонки.Добавить("ПараметрыШага");
	Тзн.Колонки.Добавить("ТипШага");
	
	Возврат Тзн;
КонецФункции	

Функция ВложенныеСценарииИзТекстовыхДанных(МассивДанных)
	Тзн = ПустаяТаблицаВложенныхСценариев();
	Если МассивДанных = Неопределено Тогда
		Возврат Тзн;
	КонецЕсли;	 
	
	Для Каждого Элем Из МассивДанных Цикл
		СтрТзн                   = Тзн.Добавить();
		СтрТзн.Версия            = Элем["Версия"];
		СтрТзн.UID               = Элем["UIDВложенныйСценарий"];
		СтрТзн.ИмяСценария       = Элем["ИмяСценария"];
	КонецЦикла;	
	
	Возврат Тзн;
КонецФункции	

Функция ПараметрыШагаПроцессаИзТекстовыхДанных(Массив)
	Тзн = Новый ТаблицаЗначений;
	Тзн.Колонки.Добавить("Имя");
	Тзн.Колонки.Добавить("ТипПараметра");
	Тзн.Колонки.Добавить("ШагПроцессаЗначениеПоСсылке");
	Тзн.Колонки.Добавить("ШагПроцессаЗначениеПоСсылкеUID");
	Тзн.Колонки.Добавить("ИсходящийПараметр");
	Тзн.Колонки.Добавить("ЗначениеПоСсылке");
	Тзн.Колонки.Добавить("ЗначениеПроизвольное");
	Тзн.Колонки.Добавить("ИмяПараметраПоСсылке");
	
	Для Каждого Элем Из Массив Цикл
		СтрокаТзн = Тзн.Добавить();
		СтрокаТзн.Имя = Элем["Имя"];
		СтрокаТзн.ТипПараметра = Элем["ТипПараметра"];
		СтрокаТзн.ШагПроцессаЗначениеПоСсылке = Элем["ШагПроцессаЗначениеПоСсылке"];
		СтрокаТзн.ШагПроцессаЗначениеПоСсылкеUID = Элем["ШагПроцессаЗначениеПоСсылкеUID"];
		СтрокаТзн.ИсходящийПараметр = Элем["ИсходящийПараметр"];
		СтрокаТзн.ЗначениеПоСсылке = Элем["ЗначениеПоСсылке"];
		СтрокаТзн.ЗначениеПроизвольное = Элем["ЗначениеПроизвольное"];
		СтрокаТзн.ИмяПараметраПоСсылке = Элем["ИмяПараметраПоСсылке"];
	КонецЦикла;	 
	
	Возврат Тзн;
КонецФункции	 

Функция ШагиПроцессаИзТекстовыхДанных(МассивДанных)
	Тзн = ПустаяТаблицаШаговПроцесса();
	Если МассивДанных = Неопределено Тогда
		Возврат Тзн;
	КонецЕсли;	 
	
	Для Каждого Элем Из МассивДанных Цикл
		СтрТзн                   = Тзн.Добавить();
		СтрТзн.ШагПроцесса       = Элем["ШагПроцесса"];
		СтрТзн.UID               = Элем["UID"];
		СтрТзн.Процесс           = Элем["Процесс"];
		СтрТзн.Наименование      = Элем["Наименование"];
		СтрТзн.Сценарий          = Элем["Сценарий"];
		СтрТзн.СценарийНаименование = Элем["СценарийНаименование"];
		СтрТзн.СценарийКод       = Элем["СценарийКод"];
		СтрТзн.СценарийUID       = Элем["СценарийUID"];
		СтрТзн.ПолныйКод         = Элем["ПолныйКод"];
		СтрТзн.ИсполнительНаименование = Элем["ИсполнительНаименование"];
		СтрТзн.ПараметрыШага    = ПараметрыШагаПроцессаИзТекстовыхДанных(Элем["ПараметрыШага"]);
		СтрТзн.ТипШага          = Число(Элем["ТипШага"]);
	КонецЦикла;	
	
	Возврат Тзн;
КонецФункции	

Функция ПустаяТаблицаПараметров()
	Тзн = Новый ТаблицаЗначений;
	Тзн.Колонки.Добавить("Имя");
	Тзн.Колонки.Добавить("Значение");
	Тзн.Колонки.Добавить("ФО");
	Тзн.Колонки.Добавить("ТипПараметра");
	Тзн.Колонки.Добавить("ИсходящийПараметр");
	Тзн.Колонки.Добавить("НесколькоЗначений");
	
	Возврат Тзн;
КонецФункции	

Функция ДанныеДляПодключенияКБДTestClientТекст(Параметры)
	Для Каждого СтрокаДанныеЭталонныхБД Из Параметры.ДанныеЭталонныхБД Цикл
		Если СтрокаДанныеЭталонныхБД["ИдентификаторБазы"] <> Параметры.ИдентификаторБазы Тогда
			Продолжить;
		КонецЕсли;	 
		
		ПрофилиПользователей = СтрокаДанныеЭталонныхБД["ПрофилиПользователей"];
		
		Для Каждого Профиль Из ПрофилиПользователей Цикл
			Если Профиль["ПрофильПользователя"] <> Параметры.ПрофильПользователя Тогда
				Продолжить;
			КонецЕсли;	 
			
			Возврат Новый Структура("Логин,Пароль,ИмяПрофиля",
			       Профиль["Логин"],Профиль["Пароль"],Профиль["ПрофильПользователя"]);
		КонецЦикла;	
		
	КонецЦикла;	
	
	Возврат Неопределено;
КонецФункции	

Процедура ОбработатьЗначенияПараметровСценария(ПараметрыТеста,КоличествоВариантовСценария)
	Для Каждого ПараметрТеста Из ПараметрыТеста Цикл
		ЗначенияПараметра = ПараметрТеста.Значение;
		
		ПараметрТеста.ЗначенияПараметра = МассивЗначенийПараметра(ЗначенияПараметра);
		
		КоличествоВариантовСценария = КоличествоВариантовСценария * ПараметрТеста.ЗначенияПараметра.Количество();
	КонецЦикла;	
КонецПроцедуры

Функция НаборыПараметровСценария(ПараметрыТеста)
	НаборыПараметров = Новый Массив;
	ТекущийНабор     = ПустойНаборПараметров();
	НаборыПараметров.Добавить(ТекущийНабор);
	
	РассчитатьНаборыПараметровРекурсивно(НаборыПараметров,ТекущийНабор,ПараметрыТеста,-1);
	
	Возврат НаборыПараметров;
КонецФункции	

Функция ТекстСценарияПоНаборуПараметров(ПараметрыДляПолученияТекстаСценария)
	Сценарий         = ПараметрыДляПолученияТекстаСценария.Сценарий;
	НаборПараметров  = ПараметрыДляПолученияТекстаСценария.НаборПараметров;
	СхемаДерево      = ПараметрыДляПолученияТекстаСценария.СхемаДерево;
	ДоТеста          = ПараметрыДляПолученияТекстаСценария.ДоТеста;
	ПроверкаТеста    = ПараметрыДляПолученияТекстаСценария.ПроверкаТеста;
	ПослеТеста       = ПараметрыДляПолученияТекстаСценария.ПослеТеста;
	НомерНабора      = ПараметрыДляПолученияТекстаСценария.НомерНабора;
	РазмерНабора     = ПараметрыДляПолученияТекстаСценария.РазмерНабора;
	ЭтоБизнесПроцесс = ПараметрыДляПолученияТекстаСценария.ЭтоБизнесПроцесс;
	
	МассивСтрок = Новый Массив;
	
	Если ПараметрыДляПолученияТекстаСценария.Свойство("ПараметрыСценария") Тогда
		ПараметрыВходящие = ПараметрыДляПолученияТекстаСценария.ПараметрыСценария;
	Иначе	
		ПараметрыВходящие = Сценарий.ПараметрыВходящие.Выгрузить();
	КонецЕсли;	 
	
	
	СтрокаПараметровСНесколькимиЗначениями = "";
	Для Каждого ПараметрСНесколькимиЗначениями Из НаборПараметров.ПараметрыСНесколькимиЗначениями Цикл
		СтрокаПараметровСНесколькимиЗначениями = СтрокаПараметровСНесколькимиЗначениями + ПараметрСНесколькимиЗначениями.ИмяПараметра
		                                         + "=" + ПараметрСНесколькимиЗначениями.ЗначениеПараметра + ";";
	КонецЦикла;	
	
	Если ЭтоБизнесПроцесс Тогда
		
		Если ПараметрыДляПолученияТекстаСценария.ЧтениеИзТекста Тогда
			МассивСтрок.Добавить("@КодСценария=" + ПараметрыДляПолученияТекстаСценария.СтруктураПараметров.КодСценария);
		Иначе	
			МассивСтрок.Добавить("@КодСценария=" + ПараметрыДляПолученияТекстаСценария.Сценарий.Код);
		КонецЕсли;	 
		
		МассивСтрок.Добавить("* Сценарий: " + ПараметрыДляПолученияТекстаСценария.ИмяСценария);
		
		Если ПараметрыДляПолученияТекстаСценария.ЧтениеИзТекста Тогда
			ДанныеСценариев = ПараметрыДляПолученияТекстаСценария.СтруктураПараметров.ДанныеСценариев;
			СтрокаДанныеСценариев = ДанныеСценариев.Найти(ПараметрыДляПолученияТекстаСценария.СтруктураПараметров.UIDСценария
			    ,"UID");
			ДанныеПроцессов = ПараметрыДляПолученияТекстаСценария.СтруктураПараметров.ДанныеПроцессов;
			СтрокаДанныеПроцессов = ДанныеПроцессов.Найти(ПараметрыДляПолученияТекстаСценария.СтруктураПараметров.UIDПроцесса,"UID");
			ШагиПроцесса = СтрокаДанныеПроцессов.ШагиПроцесса;
			СтрокаШагиПроцесса = ШагиПроцесса.Найти(ПараметрыДляПолученияТекстаСценария.СтруктураПараметров.UIDСценария,
			  "СценарийUID");
			
			ТекущаяНастройкаПроцесса = ПараметрыДляПолученияТекстаСценария.СтруктураПараметров.ТекущаяНастройкаПроцесса;
			ДанныеЭталонныхБД = ПараметрыДляПолученияТекстаСценария.СтруктураПараметров.ДанныеЭталонныхБД;
			Если ДанныеЭталонныхБД.Количество() > 0 Тогда
				ЭталонныеБД = ДанныеЭталонныхБД[0].ДанныеФайлаYaml["ЭталонныеБД"];
				ИдентификаторБазыИзнастройки = ТекущаяНастройкаПроцесса.ИдентификаторБазы;
				Для Каждого ДанныеЭталоннойБД Из ЭталонныеБД Цикл
					Если НРег(ДанныеЭталоннойБД["ИдентификаторБазы"]) = НРег(ИдентификаторБазыИзнастройки) Тогда
						ПрофилиПользователей = ДанныеЭталоннойБД["ПрофилиПользователей"];
						Для Каждого ПрофильПользователяЭталоннойБД Из ПрофилиПользователей Цикл
							Если ПрофильПользователяЭталоннойБД["ПрофильПользователя"] = СтрокаШагиПроцесса.ИсполнительНаименование Тогда
								МассивСтрок.Добавить("	И я подключаю TestClient """ + ПрофильПользователяЭталоннойБД["ПрофильПользователя"]
									+ """ логин """ +  ПрофильПользователяЭталоннойБД["Логин"]
									+ """ пароль """ + ПрофильПользователяЭталоннойБД["Пароль"] + """");
								МассивСтрок.Добавить("	И я закрыл все окна клиентского приложения");
								Прервать;
							КонецЕсли;	 
						КонецЦикла;	 
						
						Прервать;
					КонецЕсли;	 
				КонецЦикла;	 
			КонецЕсли;	 
			
		Иначе	
			НастройкиБДДляЗапускаТестовИзСценария = НастройкиБДДляЗапускаТестовИзСценария();
			Если ПараметрыДляПолученияТекстаСценария.Свойство("ИсполнительШагаПроцесса")
				и ЗначениеЗаполнено(ПараметрыДляПолученияТекстаСценария.ИсполнительШагаПроцесса) Тогда
				ПараметрыПодключения = ДанныеДляПодключенияКБДTestClient(
					ПараметрыДляПолученияТекстаСценария.ИсполнительШагаПроцесса,
					НастройкиБДДляЗапускаТестовИзСценария.ЭталоннаяБаза,Неопределено);
			Иначе	
				ПараметрыПодключения = ДанныеДляПодключенияКБДTestClient(
					Сценарий.ПрофильПользователя,
					НастройкиБДДляЗапускаТестовИзСценария.ЭталоннаяБаза,Неопределено);
			КонецЕсли;	 
			
			Если ПараметрыПодключения <> Неопределено Тогда
				
				Если ПараметрыДляПолученияТекстаСценария.ПросмотрСценария Тогда
					ИмяПрофиля = ПараметрыПодключения.ИмяПрофиля;
				Иначе
					ИмяПрофиля = ИмяПрофиляTestClient(Сценарий.ПрофильПользователя, НастройкиБДДляЗапускаТестовИзСценария, Истина);
				КонецЕсли;	 
				
				МассивСтрок.Добавить("	И я подключаю TestClient """ + ИмяПрофиля
				+ """ логин """ +  ПараметрыПодключения.Логин 
				+ """ пароль """ + ПараметрыПодключения.Пароль + """");
				МассивСтрок.Добавить("	И я закрыл все окна клиентского приложения");
			КонецЕсли;	 
		КонецЕсли;	 
		
	Иначе	
		Если ПараметрыДляПолученияТекстаСценария.ЧтениеИзТекста Тогда
			МассивСтрок.Добавить("@КодСценария=" + ПараметрыДляПолученияТекстаСценария.СтруктураПараметров.КодСценария);
			МассивСтрок.Добавить("@КодНастройкиСценария=" + ПараметрыДляПолученияТекстаСценария.СтруктураПараметров.ДанныеНастройкиСценария.Код);
		Иначе	
			МассивСтрок.Добавить("@КодСценария=" + ПараметрыДляПолученияТекстаСценария.Сценарий.Код);
			Если ПараметрыДляПолученияТекстаСценария.СтруктураПараметров.Свойство("НастройкаСценария") Тогда
				МассивСтрок.Добавить("@КодНастройкиСценария=" + ПараметрыДляПолученияТекстаСценария.СтруктураПараметров.НастройкаСценария.Код);
			КонецЕсли;	 
		КонецЕсли;	 
		
		Если ПараметрыДляПолученияТекстаСценария.ИспользоватьИмяСценария Тогда
			МассивСтрок.Добавить("Сценарий: " + Сценарий
		                 + ?(СтрокаПараметровСНесколькимиЗначениями = "",""," " + СтрокаПараметровСНесколькимиЗначениями));
		Иначе				 
			Если ПараметрыДляПолученияТекстаСценария.ЧтениеИзТекста Тогда
				МассивСтрок.Добавить("Сценарий: " + ПараметрыДляПолученияТекстаСценария.СтруктураПараметров.ИмяНастройки
		                 + ?(СтрокаПараметровСНесколькимиЗначениями = "",""," " + СтрокаПараметровСНесколькимиЗначениями));
			Иначе	
				Если РазмерНабора = 1 Тогда
					МассивСтрок.Добавить("Сценарий: " + СокрЛП(ПараметрыДляПолученияТекстаСценария.Сценарий)
		                 + ?(СтрокаПараметровСНесколькимиЗначениями = "",""," " + СтрокаПараметровСНесколькимиЗначениями));
				Иначе	
					МассивСтрок.Добавить("Сценарий: " + СокрЛП(ПараметрыДляПолученияТекстаСценария.Сценарий) + " " + XMLСтрока(НомерНабора)
		                 + ?(СтрокаПараметровСНесколькимиЗначениями = "",""," " + СтрокаПараметровСНесколькимиЗначениями));
				КонецЕсли;	 
			КонецЕсли;	 				 
		КонецЕсли;	 

	КонецЕсли;	 
	
	ПараметрыДляПолученияТекстаСценария.Вставить("ПараметрыВходящие",ПараметрыВходящие);
	ПараметрыДляПолученияТекстаСценария.Вставить("МассивСтрок",МассивСтрок);
	ПараметрыДляПолученияТекстаСценария.Вставить("НаборПараметров",НаборПараметров);
	ПараметрыДляПолученияТекстаСценария.Вставить("СхемаДерево",СхемаДерево);
	
	ДанныеНомеровСтрок = Новый Массив;
	ПараметрыДляПолученияТекстаСценария.Вставить("ДанныеНомеровСтрок",ДанныеНомеровСтрок);
	ПараметрыДляПолученияТекстаСценария.Вставить("ДанныеВложенногоСценария",Неопределено);
	
	МассивИерархииСценариев = Новый Массив;
	ПараметрыДляПолученияТекстаСценария.Вставить("МассивИерархииСценариев",МассивИерархииСценариев);
	
	ТекстЛинейногоСценарияПоСхемеРекурсивно(ПараметрыДляПолученияТекстаСценария,0);
	
	ОбъектСценария = Новый Структура;
	ОбъектСценария.Вставить("ТекстСценария",СтрСоединить(МассивСтрок,Символы.ПС));
	ОбъектСценария.Вставить("ПараметрыСНесколькимиЗначениями",НаборПараметров.ПараметрыСНесколькимиЗначениями);
	ОбъектСценария.Вставить("Сценарий",Сценарий);
	ОбъектСценария.Вставить("ДанныеНомеровСтрок",ДанныеНомеровСтрок);
	
	Если ПараметрыДляПолученияТекстаСценария.ИсключитьСлужебныеСловаИзТекстаСценария Тогда
		ИсключитьСлужебныеСловаИзТекстаСценария(ОбъектСценария.ТекстСценария);
	КонецЕсли;	 
	
	Возврат ОбъектСценария;
КонецФункции	

// Получает часть заголовка сценария, в котором содержатся значения параметров
Функция ЗаголовокЗначенияПараметровСценария(ПараметрыТеста)
	КопияПараметрыТеста = ПараметрыТеста.Скопировать();
	КопияПараметрыТеста.Сортировать("Имя");
	
	Массив = Новый Массив;
	Для Каждого ПараметрТеста Из КопияПараметрыТеста Цикл
		Массив.Добавить("#" + ПараметрТеста.Имя + "=" + ПерваяСтрока(ПараметрТеста.Значение));
	КонецЦикла;	
	
	Возврат СтрСоединить(Массив,Символы.ПС);
КонецФункции	

// Возвращает массив ФО, которые имеют только одно значение
Функция ФОИмеющиеОдноЗначение(ПараметрыТеста)
	КопияПараметрыТеста = ПараметрыТеста.Скопировать();
	КопияПараметрыТеста.Сортировать("Имя");
	
	Массив = Новый Массив;
	Для Каждого ПараметрТеста Из КопияПараметрыТеста Цикл
		Если ПараметрТеста.НесколькоЗначений Тогда
			Продолжить;
		КонецЕсли;	 
		
		Если Не ЗначениеЗаполнено(ПараметрТеста.ФО) Тогда
			Продолжить;
		КонецЕсли;	 
		
		Массив.Добавить(Новый Структура("Имя,Значение",ПараметрТеста.Имя,ПараметрТеста.Значение));
	КонецЦикла;	
	
	Возврат Массив;
КонецФункции	

// Возвращает массив ФО, которые имеют только несколько значений
Функция ФОИмеющиеНесколькоЗначений(ПараметрыТеста)
	КопияПараметрыТеста = ПараметрыТеста.Скопировать();
	КопияПараметрыТеста.Сортировать("Имя");
	
	Массив = Новый Массив;
	Для Каждого ПараметрТеста Из КопияПараметрыТеста Цикл
		Если НЕ ПараметрТеста.НесколькоЗначений Тогда
			Продолжить;
		КонецЕсли;	 
		
		Если Не ЗначениеЗаполнено(ПараметрТеста.ФО) Тогда
			Продолжить;
		КонецЕсли;	 
		
		Массив.Добавить(Новый Структура("Имя,Значение",ПараметрТеста.Имя,ПараметрТеста.Значение));
	КонецЦикла;	
	
	Возврат Массив;
КонецФункции	

Функция МассивЗначенийПараметра(Знач Стр)
	Стр = СтрЗаменить(Стр,"\;","~ПредставлениеТочкиСЗапятой~");
	
	Массив = СтрРазделить(Стр,";");
	
	Для Ккк = 0 По Массив.Количество()-1 Цикл
		Массив[Ккк] = СтрЗаменить(Массив[Ккк],"~ПредставлениеТочкиСЗапятой~",";");
	КонецЦикла;	
	
	Возврат Массив;
КонецФункции	

Функция ПустойНаборПараметров()
	СтруктураНабора = Новый Структура;
	СтруктураНабора.Вставить("ПараметрыСНесколькимиЗначениями",Новый Массив);
	
	Тзн = Новый ТаблицаЗначений;
	Тзн.Колонки.Добавить("Имя");
	Тзн.Колонки.Добавить("Значение");
	Тзн.Колонки.Добавить("ТипПараметра");
	
	СтруктураНабора.Вставить("ТаблицаПараметров",Тзн);
	
	Возврат СтруктураНабора;
КонецФункции	 

Процедура РассчитатьНаборыПараметровРекурсивно(НаборыПараметров,ТекущийНабор,ПараметрыТеста,Знач ИдПараметра)
	ИдПараметра = ИдПараметра + 1;
	
	Если ИдПараметра > (ПараметрыТеста.Количество()-1) Тогда
		Возврат;
	КонецЕсли;	 
	
	ЗначенияПараметра = ПараметрыТеста[ИдПараметра].ЗначенияПараметра;
	
	Если ЗначенияПараметра.Количество() = 1 Тогда
		СтрТекущийНабор              = ТекущийНабор.ТаблицаПараметров.Добавить();
		СтрТекущийНабор.Значение     = ЗначенияПараметра[0];
		СтрТекущийНабор.Имя          = ПараметрыТеста[ИдПараметра].Имя;
		СтрТекущийНабор.ТипПараметра = ПараметрыТеста[ИдПараметра].ТипПараметра;
		
		РассчитатьНаборыПараметровРекурсивно(НаборыПараметров,ТекущийНабор,ПараметрыТеста,ИдПараметра);
		Возврат;
	КонецЕсли;	 
	
	КопияТекущегоНабора = СкопироватьНаборПараметров(ТекущийНабор);
	
	НомерПараметра = 0;
	Для Каждого ЗначениеПараметра Из ЗначенияПараметра Цикл
		НомерПараметра = НомерПараметра + 1;
		
		
		Если НомерПараметра > 1 Тогда
			КопияНабора = СкопироватьНаборПараметров(КопияТекущегоНабора);
			НаборыПараметров.Добавить(КопияНабора);
		Иначе
			КопияНабора = ТекущийНабор;
		КонецЕсли;	 
		
		КопияНабора.ПараметрыСНесколькимиЗначениями.Добавить(Новый Структура("ИмяПараметра,ЗначениеПараметра",
																		  	 ПараметрыТеста[ИдПараметра].Имя,ЗначениеПараметра));
		
		СтрКопияНабора              = КопияНабора.ТаблицаПараметров.Добавить();
		СтрКопияНабора.Значение     = ЗначениеПараметра;
		СтрКопияНабора.Имя          = ПараметрыТеста[ИдПараметра].Имя;
		СтрКопияНабора.ТипПараметра = ПараметрыТеста[ИдПараметра].ТипПараметра;
		
		РассчитатьНаборыПараметровРекурсивно(НаборыПараметров,КопияНабора,ПараметрыТеста,ИдПараметра)
	КонецЦикла;	
	
КонецПроцедуры

Функция СтрокаСтекаВызоваСценариев(ПараметрыПолученияТекстаСценария)
	СтрокаИерархии = "";
	Оступ = "";
	Для Каждого ДанныеСценария Из ПараметрыПолученияТекстаСценария.МассивИерархииСценариев Цикл
		СтрокаИерархии = СтрокаИерархии + Оступ 
		  + СтрШаблон(Нстр("ru = 'Строка №%1. '"),ДанныеСценария.СхемаДерево.НомерСтрокиТекстаСценария) 
		  +  ДанныеСценария.СхемаДерево.ОписаниеЭлемента + Символы.ПС;
		Оступ = Оступ + "    ";
	КонецЦикла;	
	
	Если ПараметрыПолученияТекстаСценария.Свойство("ДанныеВложенногоСценария") Тогда
		Если ПараметрыПолученияТекстаСценария.ДанныеВложенногоСценария <> Неопределено Тогда
			ОписаниеЭлементаОшибки = ПараметрыПолученияТекстаСценария.СтрокаДерева.ОписаниеЭлемента;
			
			Если ПараметрыПолученияТекстаСценария.Свойство("ОписаниеЭлементаОшибки") Тогда
				ОписаниеЭлементаОшибки = ПараметрыПолученияТекстаСценария.ОписаниеЭлементаОшибки;
			КонецЕсли;	 
			
			СтрокаИерархии = СтрокаИерархии + Оступ 
			  + СтрШаблон(Нстр("ru = 'Строка №%1. <%2>'"),ПараметрыПолученияТекстаСценария.СтрокаДерева.НомерСтрокиТекстаСценария,
			  ОписаниеЭлементаОшибки) + Символы.ПС;
			Оступ = Оступ + "    ";
		КонецЕсли;	 
	КонецЕсли;	 
	
	Возврат СтрокаИерархии; 
КонецФункции	 

Процедура ТекстЛинейногоСценарияПоСхемеРекурсивноПоУсловиюПрепроцессор(ПараметрыПолученияТекстаСценария,Отступ)
	ВеткиУсловия = ВеткиУсловия(ПараметрыПолученияТекстаСценария.СтрокаДерева);
	
	НужнаяВетка = Неопределено;
	
	ПараметрыВходящие = ПараметрыПолученияТекстаСценария.ПараметрыВходящие;
	НаборПараметров   = ПараметрыПолученияТекстаСценария.НаборПараметров;
	
	Для Каждого ВеткаУсловия Из ВеткиУсловия Цикл
		
		Если ВеткаУсловия.ОписаниеЭлемента = ОписаниеЭлементаИначеПрепроцессор() Тогда
			НужнаяВетка = ВеткаУсловия;
			Прервать;
		КонецЕсли;	 
		
		ЗначениеУсловия = ВеткаУсловия.ОбработаннаяСтрокаПараметров;
		УстановитьВШагеЗначениеПараметра(ПараметрыВходящие,ЗначениеУсловия,ВеткаУсловия,ПараметрыПолученияТекстаСценария);
			 
		ВернутьУгловыеСкобки(ЗначениеУсловия);
		ЗаменитьСлужебныеСимволыВСтроке(ЗначениеУсловия);
		
		Попытка
			ВыражениеУсловия = "?(" + ЗначениеУсловия + ",Истина,Ложь)";
			РезультатУсловия = Вычислить(ВыражениеУсловия);
		Исключение
			ПараметрыПолученияТекстаСценария.Вставить("ОписаниеЭлементаОшибки",ВеткаУсловия.ОписаниеЭлемента);
			СтрокаСтекаВызова = СтрокаСтекаВызоваСценариев(ПараметрыПолученияТекстаСценария);
			ПараметрыПолученияТекстаСценария.Удалить("ОписаниеЭлементаОшибки");
			ВызватьИсключение СтрокаСтекаВызова + СтрШаблон(НСтр("ru = 'Не смог вычислить условие для ветки: %1'"),
			                            ВеткаУсловия.ЗначениеУсловия) + Символы.ПС + ОписаниеОшибки();
		КонецПопытки;
		
		Если РезультатУсловия = Истина Тогда
			НужнаяВетка = ВеткаУсловия;
			Прервать;
		ИначеЕсли РезультатУсловия = Ложь Тогда
		Иначе	
			СтрокаСтекаВызова = СтрокаСтекаВызоваСценариев(ПараметрыПолученияТекстаСценария);
			ВызватьИсключение СтрокаСтекаВызова + СтрШаблон(НСтр("ru = 'Не смог вычислить условие для ветки: %1'"),
			                            ВеткаУсловия.ЗначениеУсловия) + Символы.ПС + ОписаниеОшибки();
		КонецЕсли;	 
	КонецЦикла;	
	
	Если НужнаяВетка = Неопределено Тогда
		Возврат;
	КонецЕсли;	 
	
	ПараметрыПолученияТекстаСценария.Вставить("СхемаДерево",НужнаяВетка);
	
	ТекстЛинейногоСценарияПоСхемеРекурсивно(ПараметрыПолученияТекстаСценария,Отступ-2);
	//уменьшаем отступ, т.к. в дереве Условие создаёт ветку, а в неё ещё вложена данная ветка шагов
КонецПроцедуры

Процедура ТекстЛинейногоСценарияПоСхемеРекурсивноПоУсловию(ПараметрыПолученияТекстаСценария,Отступ)
	ВеткиУсловия = ВеткиУсловия(ПараметрыПолученияТекстаСценария.СтрокаДерева);
	
	Для Каждого НужнаяВетка Из ВеткиУсловия Цикл
		Если НужнаяВетка.ТипЭлемента = ТипЭлементаНачалоУсловия() Тогда
			Стр = "Если " + НужнаяВетка.ОбработаннаяСтрокаПараметров + " Тогда";
			КлючевоеСловоОкончаниеУсловия = КлючевоеСловоКонецЕсли();
		ИначеЕсли НужнаяВетка.ТипЭлемента = ТипЭлементаНачалоУсловияEng() Тогда	
			Стр = "If " + НужнаяВетка.ОбработаннаяСтрокаПараметров + " Then";
			КлючевоеСловоОкончаниеУсловия = КлючевоеСловоКонецЕслиEng();
		ИначеЕсли НужнаяВетка.ТипЭлемента = ТипЭлементаИначеЕсли() Тогда
			Стр = "ИначеЕсли " + НужнаяВетка.ОбработаннаяСтрокаПараметров + " Тогда";
			КлючевоеСловоОкончаниеУсловия = КлючевоеСловоКонецЕсли();
		ИначеЕсли НужнаяВетка.ТипЭлемента = ТипЭлементаИначеЕслиEng() Тогда	
			Стр = "ElseIf " + НужнаяВетка.ОбработаннаяСтрокаПараметров + " Then";
			КлючевоеСловоОкончаниеУсловия = КлючевоеСловоКонецЕслиEng();
		ИначеЕсли НужнаяВетка.ТипЭлемента = ТипЭлементаИначе() Тогда
			Стр = "Иначе";
			КлючевоеСловоОкончаниеУсловия = КлючевоеСловоКонецЕсли();
		ИначеЕсли НужнаяВетка.ТипЭлемента = ТипЭлементаИначеEng() Тогда	
			Стр = "Else";
			КлючевоеСловоОкончаниеУсловия = КлючевоеСловоКонецЕслиEng();
		Иначе
			ВызватьИсключение СтрШаблон(НСтр("ru = 'Не известный тип условия в схеме: %1. Строка %2'"),
			НужнаяВетка.ТипЭлемента,НужнаяВетка.ОписаниеЭлемента);
		КонецЕсли;	 
		
		Если НужнаяВетка.Параметры <> Неопределено Тогда
			УстановитьВШагеЗначениеПараметра(ПараметрыПолученияТекстаСценария.ПараметрыВходящие,
			Стр,НужнаяВетка,ПараметрыПолученияТекстаСценария);
		КонецЕсли;	
		
		Если ПараметрыПолученияТекстаСценария.НадоДобавитьОтметкуОЗапускеСЭтойСтроки Тогда
			Стр = Стр + НСтр("ru = '//~ОтметкаЗапускСценарияСЭтойСтроки~'");
		КонецЕсли;	 	
		
		ПараметрыПолученияТекстаСценария.МассивСтрок.Добавить(
		ПолучитьСтрокуОтступПоЧислуПробелов((НужнаяВетка.Уровень() + Отступ),Истина) + Стр);
		ЗаполнитьДанныеНомеровСтрок(ПараметрыПолученияТекстаСценария.МассивСтрок,
		ПараметрыПолученияТекстаСценария,НужнаяВетка);														  
		
		ВставкаПараметраСТипомТаблицы(НужнаяВетка,ПараметрыПолученияТекстаСценария,
		    ПараметрыПолученияТекстаСценария.ПараметрыВходящие,ПараметрыПолученияТекстаСценария.МассивСтрок,Отступ);
		
		
		ПараметрыПолученияТекстаСценария.Вставить("СхемаДерево",НужнаяВетка);
		
		ТекстЛинейногоСценарияПоСхемеРекурсивно(ПараметрыПолученияТекстаСценария,Отступ);
	КонецЦикла;	 
	ПараметрыПолученияТекстаСценария.МассивСтрок.Добавить(ПолучитьСтрокуОтступПоЧислуПробелов(
	(НужнаяВетка.Уровень() + Отступ),Истина) + КлючевоеСловоОкончаниеУсловия);
	
КонецПроцедуры

Процедура ТекстЛинейногоСценарияПоСхемеРекурсивноПоЦиклу(ПараметрыПолученияТекстаСценария,Отступ)
	ВеткиУсловия = ВеткиУсловия(ПараметрыПолученияТекстаСценария.СтрокаДерева);
	
	НужнаяВетка = ВеткиУсловия[0];
	
	
	Если НужнаяВетка.ТипЭлемента = ТипЭлементаНачалоЦикла() Тогда
		Стр = "Цикл " + НужнаяВетка.ОбработаннаяСтрокаПараметров;
		КлючевоеСловоОкончаниеЦикла = КлючевоеСловоКонецЦикла();
	ИначеЕсли НужнаяВетка.ТипЭлемента = ТипЭлементаНачалоЦиклаEng() Тогда
		Стр = "Do " + НужнаяВетка.ОбработаннаяСтрокаПараметров;
		КлючевоеСловоОкончаниеЦикла = КлючевоеСловоКонецЦиклаEng();
	Иначе
		ВызватьИсключение СтрШаблон(НСтр("ru = 'Не известный тип цикла в схеме: %1. Строка %2'"),
							НужнаяВетка.ТипЭлемента,НужнаяВетка.ОписаниеЭлемента);
	КонецЕсли;	 
	
	Если ПараметрыПолученияТекстаСценария.НадоДобавитьОтметкуОЗапускеСЭтойСтроки Тогда
		Стр = Стр + НСтр("ru = '//~ОтметкаЗапускСценарияСЭтойСтроки~'");
	КонецЕсли;	 	
	
	Если НужнаяВетка.Параметры <> Неопределено Тогда
		УстановитьВШагеЗначениеПараметра(ПараметрыПолученияТекстаСценария.ПараметрыВходящие,
		    Стр,НужнаяВетка,ПараметрыПолученияТекстаСценария);
	КонецЕсли;	 
	ПараметрыПолученияТекстаСценария.МассивСтрок.Добавить(ПолучитьСтрокуОтступПоЧислуПробелов((НужнаяВетка.Уровень() + Отступ),Истина) + Стр);
	ЗаполнитьДанныеНомеровСтрок(ПараметрыПолученияТекстаСценария.МассивСтрок,ПараметрыПолученияТекстаСценария,НужнаяВетка);														  
	
	ВставкаПараметраСТипомТаблицы(НужнаяВетка,ПараметрыПолученияТекстаСценария,
		    ПараметрыПолученияТекстаСценария.ПараметрыВходящие,ПараметрыПолученияТекстаСценария.МассивСтрок,Отступ);

	
	ПараметрыПолученияТекстаСценария.Вставить("СхемаДерево",НужнаяВетка);
	
	ТекстЛинейногоСценарияПоСхемеРекурсивно(ПараметрыПолученияТекстаСценария,Отступ);
	ПараметрыПолученияТекстаСценария.МассивСтрок.Добавить(ПолучитьСтрокуОтступПоЧислуПробелов((НужнаяВетка.Уровень() + Отступ),Истина) + КлючевоеСловоОкончаниеЦикла);
КонецПроцедуры

Процедура ЗаполнитьДанныеНомеровСтрок(МассивСтрок,ПараметрыПолученияТекстаСценария,СтрокаДерева)
	Если Не ПараметрыПолученияТекстаСценария.ФормироватьДанныеПоНомерамСтрок Тогда
		Возврат;
	КонецЕсли;	
	
	UIDСценария = "";
	ИмяСценария = "";
	КодСценария = "";
	Если ПараметрыПолученияТекстаСценария.УровеньВложенности > 0 Тогда
		UIDСценария = ПараметрыПолученияТекстаСценария.ДанныеВложенногоСценария.UID;
		ИмяСценария = ПараметрыПолученияТекстаСценария.ДанныеВложенногоСценария.Шаблон;
		КодСценария = ПараметрыПолученияТекстаСценария.ДанныеВложенногоСценария.Код;
		Основной    = Ложь;
	Иначе
		UIDСценария = ПараметрыПолученияТекстаСценария.СтруктураПараметров.UIDСценария;
		ИмяСценария = ПараметрыПолученияТекстаСценария.СтруктураПараметров.ИмяСценария;
		КодСценария = ПараметрыПолученияТекстаСценария.СтруктураПараметров.КодСценария;
		Основной    = Истина;
	КонецЕсли;	 
	
	ДанныеСтрокиЛинейногоСценария = Новый Структура;
	ДанныеСтрокиЛинейногоСценария.Вставить("UIDСценария",UIDСценария);
	ДанныеСтрокиЛинейногоСценария.Вставить("ИмяСценария",ИмяСценария);
	ДанныеСтрокиЛинейногоСценария.Вставить("КодСценария",КодСценария);
	ДанныеСтрокиЛинейногоСценария.Вставить("НомерСтрокиТекстаСценария",СтрокаДерева.НомерСтрокиТекстаСценария);
	ДанныеСтрокиЛинейногоСценария.Вставить("НомерСтрокиЛинейногоСценария",МассивСтрок.Количество());
	ДанныеСтрокиЛинейногоСценария.Вставить("ТекстШага",СтрокаДерева.ОписаниеЭлемента);
	ДанныеСтрокиЛинейногоСценария.Вставить("УровеньВложенности",ПараметрыПолученияТекстаСценария.УровеньВложенности);
	ДанныеСтрокиЛинейногоСценария.Вставить("Основной",Основной);
	
	ПараметрыПолученияТекстаСценария.ДанныеНомеровСтрок.Добавить(ДанныеСтрокиЛинейногоСценария);
КонецПроцедуры

Функция ПараметрыВходящиеСценария(Сценарий)
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СценарииРаботыПользователейПараметрыВходящие.Ссылка КАК Ссылка,
		|	СценарииРаботыПользователейПараметрыВходящие.НомерСтроки КАК НомерСтроки,
		|	СценарииРаботыПользователейПараметрыВходящие.Имя КАК Имя,
		|	СценарииРаботыПользователейПараметрыВходящие.Значение КАК Значение,
		|	СценарииРаботыПользователейПараметрыВходящие.ФО КАК ФО,
		|	СценарииРаботыПользователейПараметрыВходящие.ТипПараметра КАК ТипПараметра,
		|	СценарииРаботыПользователейПараметрыВходящие.ИсходящийПараметр КАК ИсходящийПараметр
		|ИЗ
		|	Справочник.СценарииРаботыПользователей.ПараметрыВходящие КАК СценарииРаботыПользователейПараметрыВходящие
		|ГДЕ
		|	СценарииРаботыПользователейПараметрыВходящие.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка",Сценарий);
	Возврат Запрос.Выполнить().Выгрузить();
КонецФункции	 

Функция ПолучитьПараметрыВходящиеИзФайла(Сценарий,СтруктураПараметров)
	UIDСценария     = СтруктураПараметров.UIDСценария;
	ДанныеСценариев = СтруктураПараметров.ДанныеСценариев;
	СтрокаДанныеСценариев = ДанныеСценариев.Найти(UIDСценария,"UID");
	Если СтрокаДанныеСценариев = Неопределено Тогда
		ВызватьИсключение СтрШаблон(
		    НСтр("ru = 'Не найден сценарий: %1  по настройке %2'"),СтруктураПараметров.ИмяСценария,СтруктураПараметров.Имя);
	КонецЕсли;
	
	Возврат СтрокаДанныеСценариев.ПараметрыСценария;
КонецФункции	 

Процедура ТекстЛинейногоСценарияИзВложенногоСценария(ПараметрыПолученияТекстаСценария,Отступ)
	ДанныеВложенныхСценариев = ПараметрыПолученияТекстаСценария.ДанныеВложенныхСценариев;
	
	Если НЕ ПараметрыПолученияТекстаСценария.ЧтениеИзТекста Тогда
		СтрокаДанныеВложенныхСценариев = ДанныеВложенныхСценариев.Найти(
		   ПараметрыПолученияТекстаСценария.ДанныеВложенногоСценария.Шаблон,"Сценарий");
		   
		Если СтрокаДанныеВложенныхСценариев = Неопределено Тогда
			СтрокаДанныеВложенныхСценариев = ДанныеВложенныхСценариев.Добавить();
			СтрокаДанныеВложенныхСценариев.Сценарий = ПараметрыПолученияТекстаСценария.ДанныеВложенногоСценария.Шаблон;
			СтрокаДанныеВложенныхСценариев.ДеревоСхемы 
			    = ПолучитьДеревоСценария(СтрокаДанныеВложенныхСценариев.Сценарий,ПараметрыПолученияТекстаСценария);
			СтрокаДанныеВложенныхСценариев.ПараметрыВходящие = ПараметрыВходящиеСценария(СтрокаДанныеВложенныхСценариев.Сценарий);
		КонецЕсли;	    
			
		ДеревоСхемы = СтрокаДанныеВложенныхСценариев.ДеревоСхемы;
	Иначе
		
		СтрокаДанныеВложенныхСценариев = ДанныеВложенныхСценариев.Найти(
		   ПараметрыПолученияТекстаСценария.ДанныеВложенногоСценария.UID,"UID");
		   
		Если СтрокаДанныеВложенныхСценариев = Неопределено Тогда
			СтрокаДанныеВложенныхСценариев = ДанныеВложенныхСценариев.Добавить();
			СтрокаДанныеВложенныхСценариев.UID 
			    = ПараметрыПолученияТекстаСценария.ДанныеВложенногоСценария.UID;
				
				
			СтруктураПараметровДляПолученияВложенногоСценария = 
				СтруктураПараметровДляПолученияВложенногоСценария(ПараметрыПолученияТекстаСценария,
			    	СтрокаДанныеВложенныхСценариев.UID,ПараметрыПолученияТекстаСценария.ДанныеВложенногоСценария.Шаблон);
				
			СтрокаДанныеВложенныхСценариев.ДеревоСхемы = ПолучитьДеревоСценария(
			  ПараметрыПолученияТекстаСценария.ДанныеВложенногоСценария.Шаблон,СтруктураПараметровДляПолученияВложенногоСценария);
			 
			  
			СтрокаДанныеВложенныхСценариев.ПараметрыВходящие = ПолучитьПараметрыВходящиеИзФайла(
			  ПараметрыПолученияТекстаСценария.ДанныеВложенногоСценария.Шаблон,СтруктураПараметровДляПолученияВложенногоСценария);
		КонецЕсли;	    
			
		ДеревоСхемы = СтрокаДанныеВложенныхСценариев.ДеревоСхемы;
	КонецЕсли;	 
	
	
	ДанныеРодительскогоСценария = Новый Структура;
	ДанныеРодительскогоСценария.Вставить("СхемаДерево",ПараметрыПолученияТекстаСценария.СхемаДерево);
	ПараметрыПолученияТекстаСценария.МассивИерархииСценариев.Добавить(ДанныеРодительскогоСценария);
	
	ПараметрыПолученияТекстаСценария.Вставить("СхемаДерево",ДеревоСхемы);
	ТекстЛинейногоСценарияПоСхемеРекурсивно(ПараметрыПолученияТекстаСценария,Отступ);
	
	ПараметрыПолученияТекстаСценария.МассивИерархииСценариев.Удалить(
	     ПараметрыПолученияТекстаСценария.МассивИерархииСценариев.Количество()-1);
КонецПроцедуры

Процедура ВыровнятьМассивСтрокПоПервомуВхождениюСимвола(МассивСтрок,Символ)
	
	Для Ккк = 0 По МассивСтрок.Количество()-1 Цикл
		Стр = МассивСтрок[Ккк];
		Поз = Найти(Стр,Символ);
		
		МассивСтрок[Ккк] = СокрП(Лев(Стр,Поз-1)) + " " + Символ + " " + СокрЛП(Сред(Стр,Поз+1));
	КонецЦикла;	
	
	МаксПоз = 0;
	Для Каждого Стр Из МассивСтрок Цикл
		Поз = Найти(Стр,Символ);
		Если МаксПоз < Поз Тогда
			МаксПоз = Поз;
		КонецЕсли;	 
	КонецЦикла;	
	
	Если МаксПоз = 0 Тогда
		Возврат;
	КонецЕсли;	
	
	Для Ккк = 0 По МассивСтрок.Количество()-1 Цикл
		Стр = МассивСтрок[Ккк];
		
		Поз = Найти(Стр,Символ);
		Если Поз > 0 Тогда
			Разница = МаксПоз - Поз; 
			СтрокаПробелов = "";
			Для Сч = 1 По Разница Цикл
				СтрокаПробелов = СтрокаПробелов + " ";
			КонецЦикла;	
			
			МассивСтрок[Ккк] = Лев(Стр,Поз-1) + СтрокаПробелов + Сред(Стр,Поз);
		КонецЕсли;	 
	КонецЦикла;	
	
КонецПроцедуры

Процедура ВставкаПараметраСТипомТаблицы(СтрокаДерева,ПараметрыПолученияТекстаСценария,ПараметрыВходящие,МассивСтрок,Отступ)
	Если ТипЗнч(СтрокаДерева.ДополнительныеСвойства) = Тип("Структура") Тогда
		Если СтрокаДерева.ДополнительныеСвойства.Свойство("ПараметрыСТипомТаблицы") Тогда
			ПараметрыСТипомТаблицы = СтрокаДерева.ДополнительныеСвойства.ПараметрыСТипомТаблицы;
			Для Каждого Элем Из ПараметрыСТипомТаблицы Цикл
				Параметр = Новый Структура;
				Параметр.Вставить("Имя",Элем);
				Параметр.Вставить("НовоеИмяПараметра",Элем);
				Параметр.Вставить("Значение",Неопределено);
				Параметр.Вставить("НомерПараметраВСценарии",-1);
				Параметр.Вставить("Тип",ТипПараметра("Таблица"));
				
				УстановкаОдногоПараметра(Параметр,ПараметрыПолученияТекстаСценария,СтрокаДерева,
				ПараметрыВходящие,ПараметрыПолученияТекстаСценария.НаборПараметров);
				
				Если ТипЗнч(Параметр.ТекущееЗначениеПараметра) = Тип("Массив")  Тогда
					Для Каждого Стр Из Параметр.ТекущееЗначениеПараметра Цикл
						МассивСтрок.Добавить(ПолучитьСтрокуОтступПоЧислуПробелов((СтрокаДерева.Уровень()+2 + Отступ),Истина)
						+ Стр);
					КонецЦикла;	 
				Иначе
					
					МассивСтрокПараметра = СтрРазделить(Параметр.ТекущееЗначениеПараметра,Символы.ПС);
					Для Каждого Стр Из МассивСтрокПараметра Цикл
						МассивСтрок.Добавить(ПолучитьСтрокуОтступПоЧислуПробелов((СтрокаДерева.Уровень()+2 + Отступ),Истина)
						+ Стр);
					КонецЦикла;	 
					
				КонецЕсли;	 	
			КонецЦикла;	 
		КонецЕсли;	 
	КонецЕсли;	 
КонецПроцедуры 

Процедура ТекстЛинейногоСценарияПоСхемеРекурсивно(ПараметрыПолученияТекстаСценария,Отступ)
			   
	СхемаДерево       = ПараметрыПолученияТекстаСценария.СхемаДерево;
	ПараметрыВходящие = ПараметрыПолученияТекстаСценария.ПараметрыВходящие;
	НаборПараметров   = ПараметрыПолученияТекстаСценария.НаборПараметров;
	МассивСтрок       = ПараметрыПолученияТекстаСценария.МассивСтрок;
	
	Для Каждого СтрокаДерева Из СхемаДерево.Строки Цикл
		
		Если СтрокаДерева.Теги <> Неопределено Тогда
			Для Каждого Тег Из СтрокаДерева.Теги Цикл
				МассивСтрок.Добавить(ПолучитьСтрокуОтступПоЧислуПробелов((СтрокаДерева.Уровень()+1 + Отступ),Истина) + Тег);
			КонецЦикла;	 
		КонецЕсли;	 
		
		НадоДобавитьОтметкуОЗапускеСЭтойСтроки = Ложь;
		Если ПараметрыПолученияТекстаСценария <> Неопределено Тогда
			Если ПараметрыПолученияТекстаСценария.Свойство("СТекущегоШага") Тогда
				Если ПараметрыПолученияТекстаСценария.СТекущегоШага Тогда
					Если СтрокаДерева.НомерСтрокиТекстаСценария = ПараметрыПолученияТекстаСценария.ТекущийНомерСтрокиСценария Тогда
						НадоДобавитьОтметкуОЗапускеСЭтойСтроки = Истина;
					КонецЕсли;	 
				КонецЕсли;	 
			КонецЕсли;	 
		КонецЕсли;	 
		
		Если СтрокаДерева.ТипЭлемента = ТипЭлементаДействие() Тогда
			Стр = СтрокаДерева.ОбработаннаяСтрокаПараметров;
			Если СтрокаДерева.Параметры <> Неопределено Тогда
				УстановитьВШагеЗначениеПараметра(ПараметрыВходящие,Стр,СтрокаДерева,ПараметрыПолученияТекстаСценария);
			КонецЕсли;	 
			
			Если НадоДобавитьОтметкуОЗапускеСЭтойСтроки Тогда
				Стр = Стр + НСтр("ru = '//~ОтметкаЗапускСценарияСЭтойСтроки~'");
			КонецЕсли;	 
			
			МассивСтрок.Добавить(ПолучитьСтрокуОтступПоЧислуПробелов((СтрокаДерева.Уровень()+1 + Отступ),Истина) + Стр);
			ЗаполнитьДанныеНомеровСтрок(МассивСтрок,ПараметрыПолученияТекстаСценария,СтрокаДерева);
			
			ВставкаПараметраСТипомТаблицы(СтрокаДерева,ПараметрыПолученияТекстаСценария,ПараметрыВходящие,МассивСтрок,Отступ);
			
			ПараметрыПолученияТекстаСценария.Вставить("СхемаДерево",СтрокаДерева);
			
			ТекстЛинейногоСценарияПоСхемеРекурсивно(ПараметрыПолученияТекстаСценария,Отступ);
		ИначеЕсли СтрокаДерева.ТипЭлемента = ТипЭлементаВложенныйПроцесс() Тогда
			Стр = "*" + СтрокаДерева.ИмяЭлемента;
			
			МассивСтрок.Добавить(ПолучитьСтрокуОтступПоЧислуПробелов((СтрокаДерева.Уровень()+1 + Отступ),Истина) + Стр);
			ЗаполнитьДанныеНомеровСтрок(МассивСтрок,ПараметрыПолученияТекстаСценария,СтрокаДерева);
			
			ПараметрыПолученияТекстаСценария.Вставить("СхемаДерево",СтрокаДерева);
			
			ТекстЛинейногоСценарияПоСхемеРекурсивно(ПараметрыПолученияТекстаСценария,Отступ);
		ИначеЕсли СтрокаДерева.ТипЭлемента = ТипЭлементаШагВложенногоПроцесса() Тогда
			Стр = "*" + СтрокаДерева.ИмяЭлемента;
			
			МассивСтрок.Добавить(ПолучитьСтрокуОтступПоЧислуПробелов((СтрокаДерева.Уровень()+1 + Отступ),Истина) + Стр);
			ЗаполнитьДанныеНомеровСтрок(МассивСтрок,ПараметрыПолученияТекстаСценария,СтрокаДерева);
			
			ПараметрыПолученияТекстаСценария.Вставить("СхемаДерево",СтрокаДерева);
			
			ПараметрыВходящиеКопия = ПараметрыПолученияТекстаСценария.ПараметрыВходящие;
			ТаблицаПараметровКопия = ПараметрыПолученияТекстаСценария.НаборПараметров.ТаблицаПараметров;
			
			ПараметрыПолученияТекстаСценария.ПараметрыВходящие = СтрокаДерева.ДополнительныеСвойства.ПараметрыШагаВложенногоПроцесса;
			ПараметрыПолученияТекстаСценария.НаборПараметров.ТаблицаПараметров = СтрокаДерева.ДополнительныеСвойства.ПараметрыШагаВложенногоПроцесса;
			ТекстЛинейногоСценарияПоСхемеРекурсивно(ПараметрыПолученияТекстаСценария,Отступ);
			
			ПараметрыПолученияТекстаСценария.ПараметрыВходящие = ПараметрыВходящиеКопия;
			ПараметрыПолученияТекстаСценария.НаборПараметров.ТаблицаПараметров = ТаблицаПараметровКопия;
		ИначеЕсли СтрокаДерева.ТипЭлемента = ТипЭлементаУсловие() или СтрокаДерева.ТипЭлемента = ТипЭлементаУсловиеEng() Тогда
			ПараметрыПолученияТекстаСценария.Вставить("СхемаДерево",СтрокаДерева);
			ПараметрыПолученияТекстаСценария.Вставить("СтрокаДерева",СтрокаДерева);
			ПараметрыПолученияТекстаСценария.Вставить("НадоДобавитьОтметкуОЗапускеСЭтойСтроки",
			      НадоДобавитьОтметкуОЗапускеСЭтойСтроки);
			
			ТекстЛинейногоСценарияПоСхемеРекурсивноПоУсловию(ПараметрыПолученияТекстаСценария,Отступ);                                            
			Продолжить;
		ИначеЕсли (СтрокаДерева.ТипЭлемента = ТипЭлементаГруппаШагов()) Тогда
			Если ВГруппеЕстьИсполняемыеШаги(СтрокаДерева) Тогда
				МассивСтрок.Добавить(ПолучитьСтрокуОтступПоЧислуПробелов((СтрокаДерева.Уровень()+1 + Отступ),Истина) 
			                                                          + СтрокаДерева.ОписаниеЭлемента);
																	  
				ЗаполнитьДанныеНомеровСтрок(МассивСтрок,ПараметрыПолученияТекстаСценария,СтрокаДерева);																	  
				ПараметрыПолученияТекстаСценария.Вставить("СхемаДерево",СтрокаДерева);
																	  
				ТекстЛинейногоСценарияПоСхемеРекурсивно(ПараметрыПолученияТекстаСценария,Отступ);
			КонецЕсли;	 
		ИначеЕсли (СтрокаДерева.ТипЭлемента = ТипЭлементаШаблонСценария()) Тогда
			СтрокаСценария = "*" + СтрокаДерева.ОписаниеЭлемента;
			Если НадоДобавитьОтметкуОЗапускеСЭтойСтроки Тогда
				СтрокаСценария = СтрокаСценария + НСтр("ru = '//~ОтметкаЗапускСценарияСЭтойСтроки~'");
			КонецЕсли;	 
			
			МассивСтрок.Добавить(ПолучитьСтрокуОтступПоЧислуПробелов((СтрокаДерева.Уровень()+1 + Отступ),Истина)
															          + СтрокаСценария);
																	  
			КоличествоСтрокДоПолученияВложенногоСценария = МассивСтрок.Количество();
																	  
			ПараметрыПереданыСнизу = Ложь;
			Если СтрокаДерева.Параметры.Количество() > 0 Тогда
				Если СтрокаДерева.Параметры[0].Свойство("ПереданСнизу") и СтрокаДерева.Параметры[0].ПереданСнизу Тогда
					ПараметрыПереданыСнизу = Истина;
				КонецЕсли;	 
			КонецЕсли;	 																	  
			
			ЗаполнитьДанныеНомеровСтрок(МассивСтрок,ПараметрыПолученияТекстаСценария,СтрокаДерева);														  
			
			Если ПараметрыПереданыСнизу Тогда
				МассивСтрокПараметров = Новый Массив;
				Для Каждого ПараметрСценария Из СтрокаДерева.Параметры Цикл
					Если ТипЗнч(ПараметрСценария.ЗначениеПараметра) = Тип("Массив") Тогда
						ЗначениеПараметра = "";
						Если ПараметрСценария.ЗначениеПараметра.Количество() > 0 Тогда
							ЗначениеПараметра = ПараметрСценария.ЗначениеПараметра[0];
						КонецЕсли;
					Иначе	
						ЗначениеПараметра = ПараметрСценария.ЗначениеПараметра;
					КонецЕсли;	 
				  МассивСтрокПараметров.Добавить(ПолучитьСтрокуОтступПоЧислуПробелов((СтрокаДерева.Уровень()+3 + Отступ),Истина)
		              + "#" + ПараметрСценария.ИмяПараметра + " = " + ЗначениеПараметра);
				КонецЦикла;	 
				
				ВыровнятьМассивСтрокПоПервомуВхождениюСимвола(МассивСтрокПараметров,"=");
				
				Для Каждого Стр Из МассивСтрокПараметров Цикл
					МассивСтрок.Добавить(Стр);
				КонецЦикла;	  
			КонецЕсли;	 
																	  
			ПараметрыПолученияТекстаСценария.Вставить("СхемаДерево",СтрокаДерева);
			
			//запомним текущие данные вложенного сценария
			ТекДанныеВложенногоСценария = ПараметрыПолученияТекстаСценария.ДанныеВложенногоСценария;
			ПараметрыПолученияТекстаСценария.Вставить("ДанныеВложенногоСценария",СтрокаДерева.ДополнительныеСвойства);
			ПараметрыПолученияТекстаСценария.УровеньВложенности = ПараметрыПолученияТекстаСценария.УровеньВложенности + 1;
			
			//проверим корректность передачи параметров
			Если ПараметрыПереданыСнизу Тогда
				Если ПараметрыПолученияТекстаСценария.ЧтениеИзТекста Тогда
					ШаблонUID = ПараметрыПолученияТекстаСценария.ДанныеВложенногоСценария.UID;
					СтрокаДанныеВложенныхСценариев = ПараметрыПолученияТекстаСценария.СтруктураПараметров.ДанныеСценариев.Найти(
						ПараметрыПолученияТекстаСценария.ДанныеВложенногоСценария.UID,"UID");
						
					Если СтрокаДанныеВложенныхСценариев = Неопределено Тогда
						ВызватьИсключение СтрШаблон(НСтр("ru = 'Не найдены данные вложенного с ценария <%1>, <%2>'"),
							 ПараметрыПолученияТекстаСценария.ДанныеВложенногоСценария.Шаблон
							 ,ПараметрыПолученияТекстаСценария.ДанныеВложенногоСценария.Код);
					КонецЕсли;	 	
						
					ПараметрыВложенногоСценария = СтрокаДанныеВложенныхСценариев.ПараметрыСценария;
					
					ИменаПараметров = Новый Массив;
					Для Каждого Параметр Из ПараметрыВложенногоСценария Цикл
						ИменаПараметров.Добавить(НРег(Параметр.Имя));
					КонецЦикла;	
				Иначе	
					ИменаПараметров = Новый Массив;
					Для Каждого Параметр Из ПараметрыПолученияТекстаСценария.ДанныеВложенногоСценария.Шаблон.ПараметрыВходящие Цикл
						ИменаПараметров.Добавить(НРег(Параметр.Имя));
					КонецЦикла;	
				КонецЕсли;	 
				
				
				Для Каждого ПараметрСценария Из СтрокаДерева.Параметры Цикл
					СтрокаИменаПараметров = ИменаПараметров.Найти(НРег(ПараметрСценария.ИмяПараметра));
					Если СтрокаИменаПараметров = Неопределено Тогда
						ВызватьИсключение СтрШаблон(НСтр("ru = 'В сценарии <%1> нет параметра <%2>'"),
							 ПараметрыПолученияТекстаСценария.ДанныеВложенногоСценария.Шаблон.Наименование,ПараметрСценария.ИмяПараметра);
					КонецЕсли;	 
				КонецЦикла;	 
			КонецЕсли;	 
			
			Если СтрокаДерева.Параметры <> Неопределено Тогда
				Стр = СтрокаДерева.ОбработаннаяСтрокаПараметров;
				УстановитьВШагеЗначениеПараметра(ПараметрыВходящие,Стр,СтрокаДерева,ПараметрыПолученияТекстаСценария);
			КонецЕсли;	 
			
			ТекстЛинейногоСценарияИзВложенногоСценария(ПараметрыПолученияТекстаСценария,Отступ + СтрокаДерева.Уровень()+1);
			
			//вернем текущие данные вложенного сценария
			ПараметрыПолученияТекстаСценария.УровеньВложенности = ПараметрыПолученияТекстаСценария.УровеньВложенности - 1;
			ПараметрыПолученияТекстаСценария.Вставить("ДанныеВложенногоСценария",ТекДанныеВложенногоСценария);
			
			Если НЕ ВШагахПодсценарияЕстьИсполняемыеШаги(МассивСтрок,КоличествоСтрокДоПолученияВложенногоСценария) Тогда
				КолСтрокДляУдаления = МассивСтрок.Количество() - КоличествоСтрокДоПолученияВложенногоСценария;
				Для Счетчик = 1 По КолСтрокДляУдаления Цикл
					МассивСтрок.Удалить(МассивСтрок.Количество()-1);
				КонецЦикла;	
				//комментируем строку подсценария
				СтрокаСценария = МассивСтрок[МассивСтрок.Количество()-1];
				СтрокаСценария = "#" + СтрокаСценария;
				МассивСтрок[МассивСтрок.Количество()-1] = СтрокаСценария;
			КонецЕсли;	 
			
		ИначеЕсли СтрокаДерева.ТипЭлемента = ТипЭлементаУсловиеПрепроцессор() Тогда
			ПараметрыПолученияТекстаСценария.Вставить("СхемаДерево",СтрокаДерева);
			ПараметрыПолученияТекстаСценария.Вставить("СтрокаДерева",СтрокаДерева);
			
			ТекстЛинейногоСценарияПоСхемеРекурсивноПоУсловиюПрепроцессор(ПараметрыПолученияТекстаСценария,Отступ);
			Продолжить;
		ИначеЕсли СтрокаДерева.ТипЭлемента = ТипЭлементаЦикл() или СтрокаДерева.ТипЭлемента = ТипЭлементаЦиклEng() Тогда
			ПараметрыПолученияТекстаСценария.Вставить("СхемаДерево",СтрокаДерева);
			ПараметрыПолученияТекстаСценария.Вставить("СтрокаДерева",СтрокаДерева);
			ПараметрыПолученияТекстаСценария.Вставить("НадоДобавитьОтметкуОЗапускеСЭтойСтроки",
			      НадоДобавитьОтметкуОЗапускеСЭтойСтроки);
			ТекстЛинейногоСценарияПоСхемеРекурсивноПоЦиклу(ПараметрыПолученияТекстаСценария,Отступ);
			Продолжить
		ИначеЕсли СтрокаДерева.ТипЭлемента = ТипЭлементаНачалоСхемы() Тогда
			Продолжить;
		ИначеЕсли СтрокаДерева.ТипЭлемента = ТипЭлементаСтоп() Тогда
			Продолжить;
		ИначеЕсли СтрокаДерева.ТипЭлемента = ТипЭлементаОкончаниеСхемы() Тогда
			Продолжить;
		ИначеЕсли СтрокаДерева.ТипЭлемента = ТипЭлементаПустаяСтрока() Тогда
			Продолжить;
		ИначеЕсли СтрокаДерева.ТипЭлемента = ТипЭлементаБлочныйКомментарийНачало() Тогда
			Продолжить;
		ИначеЕсли СтрокаДерева.ТипЭлемента = ТипЭлементаБлочныйКомментарийОкончание() Тогда
			Продолжить;
		ИначеЕсли СтрокаДерева.ТипЭлемента = ТипЭлементаТаблица() Тогда
			Стр = СтрокаДерева.ОбработаннаяСтрокаПараметров;
			Если СтрокаДерева.Параметры <> Неопределено Тогда
				УстановитьВШагеЗначениеПараметра(ПараметрыВходящие,Стр,СтрокаДерева,ПараметрыПолученияТекстаСценария);
			КонецЕсли;	 
			
			МассивСтрок.Добавить(ПолучитьСтрокуОтступПоЧислуПробелов((СтрокаДерева.Уровень()+1 + Отступ),Истина) 
			                                                          + Стр);
			ЗаполнитьДанныеНомеровСтрок(МассивСтрок,ПараметрыПолученияТекстаСценария,СтрокаДерева);
		ИначеЕсли СтрокаДерева.ТипЭлемента = ТипЭлементаКомментарий() Тогда
			Стр = СтрокаДерева.ОписаниеЭлемента;
			Если Лев(СокрЛ(Стр),1) <> "#" и Лев(СокрЛ(Стр),2) <> "//" Тогда
				Стр = "#" + Стр;
			КонецЕсли;	 
			
			МассивСтрок.Добавить(ПолучитьСтрокуОтступПоЧислуПробелов((СтрокаДерева.Уровень()+1 + Отступ),Истина) + Стр);
			ЗаполнитьДанныеНомеровСтрок(МассивСтрок,ПараметрыПолученияТекстаСценария,СтрокаДерева);
			
		ИначеЕсли СтрокаДерева.ТипЭлемента = ТипЭлементаБлочныйКомментарий() Тогда
			Стр = СтрокаДерева.ОписаниеЭлемента;
			Стр = "//" + Стр;
			
			МассивСтрок.Добавить(ПолучитьСтрокуОтступПоЧислуПробелов((СтрокаДерева.Уровень()+1 + Отступ),Истина) + Стр);
			ЗаполнитьДанныеНомеровСтрок(МассивСтрок,ПараметрыПолученияТекстаСценария,СтрокаДерева);
		Иначе
			ВызватьИсключение СтрШаблон(НСтр("ru = 'Не известный тип элемента в схеме: %1'"),
																					  СтрокаДерева.ТипЭлемента);
		КонецЕсли;	 
	КонецЦикла;	
КонецПроцедуры

Функция ВШагахПодсценарияЕстьИсполняемыеШаги(МассивСтрок,НомерСтроки)
	Для СчетчикСтрок = НомерСтроки+1 По МассивСтрок.Количество() Цикл
		Стр = СокрЛП(МассивСтрок[СчетчикСтрок-1]);
		Если НЕ ЗначениеЗаполнено(Стр) Тогда
			Продолжить;
		ИначеЕсли Лев(Стр,1) = "*" Тогда
			Продолжить;
		ИначеЕсли Лев(Стр,1) = "#" Тогда
			Продолжить;
		ИначеЕсли Лев(Стр,2) = "//" Тогда
			Продолжить;
		ИначеЕсли Лев(Стр,1) = "@" Тогда
			Продолжить;
		КонецЕсли;	 
		
		Возврат Истина;
	КонецЦикла;	 
	
	Возврат Ложь;
КонецФункции	 

Функция СкопироватьНаборПараметров(Набор)
	КопияНабора = ПустойНаборПараметров();
	КопияНабора.Вставить("ТаблицаПараметров",Набор.ТаблицаПараметров.Скопировать());
	
	ПараметрыСНесколькимиЗначениями = Новый Массив;
	Для Каждого Элем Из Набор.ПараметрыСНесколькимиЗначениями Цикл
		ПараметрыСНесколькимиЗначениями.Добавить(Элем);
	КонецЦикла;	
	КопияНабора.Вставить("ПараметрыСНесколькимиЗначениями",ПараметрыСНесколькимиЗначениями);
	
	Возврат КопияНабора;
КонецФункции	

Процедура УстановкаОдногоПараметра(Параметр,ПараметрыПолученияТекстаСценария,СтрокаДерева,
	   ПараметрыВходящие,НаборПараметров)
	   
	Если ПараметрыПолученияТекстаСценария.МассивИерархииСценариев.Количество() > 0 Тогда
		//это вложенный сценарий
		ТекущееЗначениеПараметра = Неопределено;
		НужныйТип                = Неопределено;
		СтрокаДереваСПараметрами = НайтиСтрокуДереваСПараметрамиДляТекущейСтроки(СтрокаДерева,ПараметрыПолученияТекстаСценария);
		
		ЗначениеИТипПараметраИзВеткиРодителя(СтрокаДерева,ТекущееЗначениеПараметра,НужныйТип,Параметр,
		  ПараметрыВходящие,НаборПараметров,СтрокаДереваСПараметрами,ПараметрыПолученияТекстаСценария);
		
		Если ТекущееЗначениеПараметра = Неопределено Тогда
			ВызватьИсключение СтрШаблон(
			НСтр("ru = 'Не найдено значение для подстановки в шаблон сценария: %1'"),
			   СтрокаДерева.ОписаниеЭлемента);
		КонецЕсли;	 
		
		Параметр.Вставить("ТекущееЗначениеПараметра",ТекущееЗначениеПараметра);
		Параметр.Вставить("НужныйТип",НужныйТип);
	Иначе	
		СтрокаНаборПараметров = НайтиСтрокуНабора(ПараметрыВходящие,НаборПараметров,Параметр);
		Если СтрокаНаборПараметров = Неопределено Тогда
			Если ПараметрыПолученияТекстаСценария.СтруктураПараметров.Свойство("ДанныеНастройкиСценария") Тогда
				ВызватьИсключение СтрШаблон(
				НСтр("ru = 'Не найдено значение для параметра с именем: <%1>. Сценарий <%2>. Настройка <%3>.'"),
				Параметр.НовоеИмяПараметра,ПараметрыПолученияТекстаСценария.Сценарий,
				ПараметрыПолученияТекстаСценария.СтруктураПараметров.ДанныеНастройкиСценария.Код);
			Иначе	
				ВызватьИсключение СтрШаблон(
				НСтр("ru = 'Не найдено значение для параметра с именем: <%1>. Сценарий <%2>.'"),
				Параметр.НовоеИмяПараметра,ПараметрыПолученияТекстаСценария.Сценарий);
			КонецЕсли;	 
		Иначе	
			Параметр.Вставить("ТекущееЗначениеПараметра",СтрокаНаборПараметров.Значение);
			Параметр.Вставить("НужныйТип",СтрокаНаборПараметров.ТипПараметра);
		КонецЕсли;	 
	КонецЕсли;	 
КонецПроцедуры

Процедура УстановитьВШагеЗначениеПараметра(ПараметрыВходящие,Стр,СтрокаДерева,ПараметрыПолученияТекстаСценария, Параметры = Неопределено)
	
	Если Параметры = Неопределено Тогда
		Параметры       = СтрокаДерева.Параметры;
	КонецЕсли;	 
	НаборПараметров = ПараметрыПолученияТекстаСценария.НаборПараметров;
	
	Если Параметры = Неопределено Тогда
		Возврат;
	КонецЕсли;	 
	
	Для Каждого Параметр Из Параметры Цикл
		Если Параметр.ИзменяемыйПараметр Тогда
			УстановкаОдногоПараметра(Параметр,ПараметрыПолученияТекстаСценария,СтрокаДерева,ПараметрыВходящие,НаборПараметров);
		ИначеЕсли Параметр.Свойство("ВложенныеПараметры") и Параметр.ВложенныеПараметры <> Неопределено Тогда
			Для Каждого ВложенныйПараметр Из Параметр.ВложенныеПараметры Цикл
				УстановкаОдногоПараметра(ВложенныйПараметр,ПараметрыПолученияТекстаСценария,СтрокаДерева,ПараметрыВходящие,НаборПараметров);
			КонецЦикла;	
			Параметр.Вставить("НужныйТип",Параметр.Тип);
		Иначе	
			Параметр.Вставить("ТекущееЗначениеПараметра",Параметр.ЗначениеПараметра);
			Параметр.Вставить("НужныйТип",Параметр.Тип);
			Если Параметр.Тип = ТипПараметра("Таблица") Тогда
				//Параметр.Вставить("ЗначениеПараметраПодстановкаЗначений",Параметр.ЗначениеПараметраПодстановкаЗначений);
				Параметр.Вставить("ТекущееЗначениеПараметра",
				ЗначениеПараметраТаблицаИзПараметров(Параметр,ПараметрыВходящие,ПараметрыПолученияТекстаСценария,СтрокаДерева));
			КонецЕсли;	 
		КонецЕсли;	 
	КонецЦикла;	
	
	УстановитьПараметрыВСтроку(Стр,Параметры);
КонецПроцедуры

Процедура УстановитьПараметрыВСтроку(Стр,Параметры)
	ПодстановкаЗначенийПараметровВСтрокуИзменяемыеПараметры(Стр,Параметры);
	ПодстановкаЗначенийПараметровВСтроку(Стр,Параметры,ТипПараметра("Строка"),"Кавычки");
	ПодстановкаЗначенийПараметровВСтроку(Стр,Параметры,ТипПараметра("Строка"),"Апострофы");
	ПодстановкаЗначенийПараметровВСтроку(Стр,Параметры,ТипПараметра("Дата"));
	ПодстановкаЗначенийПараметровВСтроку(Стр,Параметры,ТипПараметра("Число"));
КонецПроцедуры 

Процедура ЗаменитьСлужебныеСимволыВСтроке(Стр)
	Стр = СтрЗаменить(Стр,"\|","~ПредставлениеЭкранированнойВертикальнойЧерты~");
	Стр = СтрЗаменить(Стр,"\\","~ПредставлениеДвойнойСлеш~");
	Стр = СтрЗаменить(Стр,"\""","~ПредставлениеКавычки~");
	Стр = СтрЗаменить(Стр,"\'","~ПредставлениеАпострофа~");
	Стр = СтрЗаменить(Стр,"\<","~ПредставлениеЛеваяУгловаяСкобка~");
	Стр = СтрЗаменить(Стр,"\>","~ПредставлениеПраваяУгловаяСкобка~");
	Стр = СтрЗаменить(Стр,"\[","~ПредставлениеЛеваяКвадратнаяСкобка~");
	Стр = СтрЗаменить(Стр,"\]","~ПредставлениеПраваяКвадратнаяСкобка~");
КонецПроцедуры

Функция УбратьСлужебныеСимволы(Знач Стр)
	Стр = СтрЗаменить(Стр,"~ПредставлениеЭкранированнойВертикальнойЧерты~","\|");
	Стр = СтрЗаменить(Стр,"~ПредставлениеДвойнойСлеш~","\\");
	Стр = СтрЗаменить(Стр,"~ПредставлениеКавычки~","""");
	Стр = СтрЗаменить(Стр,"~ПредставлениеАпострофа~","'");
	Стр = СтрЗаменить(Стр,"~ПредставлениеЛеваяУгловаяСкобка~","<");
	Стр = СтрЗаменить(Стр,"~ПредставлениеПраваяУгловаяСкобка~",">");
	Стр = СтрЗаменить(Стр,"~ПредставлениеЛеваяКвадратнаяСкобка~","\[");
	Стр = СтрЗаменить(Стр,"~ПредставлениеПраваяКвадратнаяСкобка~","\]");
	Возврат Стр;
КонецФункции	

Функция ПолучитьСтрокуОтступПоЧислуПробелов(Количество,СимволТаб = Ложь)
	Отступ = БыстрыйРасчетСтрокиОтступа(Количество,СимволТаб);
	Если Отступ <> Неопределено Тогда
		Возврат Отступ;
	КонецЕсли;	 
	
	Стр = "";
	Для Ккк = 1 По Количество Цикл
		Если СимволТаб Тогда
			Стр = Стр + Символы.Таб;
		Иначе	
			Стр = Стр + " ";
		КонецЕсли;	 
	КонецЦикла;	
	
	Возврат Стр;
КонецФункции	

Процедура ВГруппеЕстьИсполняемыеШагиРекурсивно(Дерево,ЕстьИсполняемыеШаги)
	Если ЕстьИсполняемыеШаги Тогда
		Возврат;
	КонецЕсли;	 
	
	Для Каждого СтрокаДерева Из Дерево.Строки Цикл
		Если  СтрокаДерева.ТипЭлемента <> ТипЭлементаГруппаШагов()
			И СтрокаДерева.ТипЭлемента <> ТипЭлементаНачалоСхемы()
			И СтрокаДерева.ТипЭлемента <> ТипЭлементаОкончаниеСхемы()
			И СтрокаДерева.ТипЭлемента <> ТипЭлементаПустаяСтрока()
			И СтрокаДерева.ТипЭлемента <> ТипЭлементаКомментарий()
			И СтрокаДерева.ТипЭлемента <> ТипЭлементаБлочныйКомментарийНачало()
			И СтрокаДерева.ТипЭлемента <> ТипЭлементаБлочныйКомментарийОкончание()
			И СтрокаДерева.ТипЭлемента <> ТипЭлементаБлочныйКомментарий()
			Тогда
			ЕстьИсполняемыеШаги = Истина;
			Прервать;
		КонецЕсли;	 
		
		ВГруппеЕстьИсполняемыеШагиРекурсивно(СтрокаДерева,ЕстьИсполняемыеШаги);
	КонецЦикла;	
КонецПроцедуры

Функция ВГруппеЕстьИсполняемыеШаги(Дерево)
	ЕстьИсполняемыеШаги = Ложь;
	
	ВГруппеЕстьИсполняемыеШагиРекурсивно(Дерево,ЕстьИсполняемыеШаги);
	
	Возврат ЕстьИсполняемыеШаги;
КонецФункции	

Функция НайтиСтрокуДереваСПараметрамиДляТекущейСтроки(СтрокаДерева,ПараметрыПолученияТекстаСценария)
	Родитель = СтрокаДерева.Родитель;
	
	Если Родитель <> Неопределено Тогда
		Пока Родитель.ТипЭлемента <> ТипЭлементаШаблонСценария() Цикл
			Родитель = Родитель.Родитель;
			Если Родитель = Неопределено Тогда
				Прервать;
			КонецЕсли;	 
		КонецЦикла;	
	КонецЕсли;	 
	
	Если Родитель = Неопределено Тогда
		//Значит вышли из вложенного сценария
		ИД = ПараметрыПолученияТекстаСценария.МассивИерархииСценариев.Количество()-1;
		Возврат ПараметрыПолученияТекстаСценария.МассивИерархииСценариев[ИД].СхемаДерево;
	КонецЕсли;	 
	
	Возврат Родитель;
КонецФункции	

Процедура ЗначениеИТипПараметраИзВеткиРодителя(СтрокаДерева,ТекущееЗначениеПараметра,НужныйТип,Параметр,
	               ПараметрыВходящие,НаборПараметров,СтрокаДереваСПараметрами,ПараметрыПолученияТекстаСценария)
	Родитель = СтрокаДереваСПараметрами;
	
	Если Родитель.Параметры = Неопределено Тогда
		ВызватьИсключение СтрШаблон(НСтр("ru = 'Не переданы параметры в строке : %1'"),
																				  Родитель.ОписаниеЭлемента);
	КонецЕсли;	 
	
	//если хотя бы один параметр переда снизу, значит так переданы все,
	//т.к. смешанная передача параметров не поддерживается
	ПараметрыПереданыСнизу = Ложь;
	Если Родитель.Параметры.Количество() > 0 Тогда
		Если Родитель.Параметры[0].Свойство("ПереданСнизу") Тогда
			ПараметрыПереданыСнизу = Родитель.Параметры[0].ПереданСнизу;
		КонецЕсли;	 
	Иначе	
		ПараметрыПереданыСнизу = Истина;
		//значит все параметры будут заданы по умолчанию
	КонецЕсли;	 
	
	ИдПараметраВСтроке = Параметр.НомерПараметраВСценарии;
	
	Если НЕ ПараметрыПереданыСнизу И Родитель.Параметры.Количество() < (ИдПараметраВСтроке+1) Тогда
		Если ПараметрыПолученияТекстаСценария.МассивИерархииСценариев.Количество() > 0 Тогда
			//это вложенный сценарий
			ОписаниеОшибки = ОписаниеОшибкиВыполненияВложенныйСценарияНеПереданПараметр(СтрокаДерева,Параметр,ПараметрыПолученияТекстаСценария);
		Иначе	
			ОписаниеОшибки = ОписаниеОшибкиВыполненияСценарияНеПереданПараметр(СтрокаДерева,Параметр);
		КонецЕсли;	 
		
		ОписаниеОшибки = ОписаниеОшибки + Символы.ПС + НСтр("ru = 'Сценарий:'") + ПараметрыПолученияТекстаСценария.Сценарий;
		
		ВызватьИсключение СтрШаблон(НСтр("ru = 'Ошибка передачи параметра: %1'"),ОписаниеОшибки);
	КонецЕсли;	 
	
	
	Если ПараметрыПереданыСнизу Тогда
		НашлиПередачуПараметра = Ложь;
		Для Каждого ПараметрРодителя Из Родитель.Параметры Цикл
			Если Нрег(ПараметрРодителя.ИмяПараметра) = Нрег(Параметр.НовоеИмяПараметра) Тогда
				НашлиПередачуПараметра = Истина;
				Прервать;
			КонецЕсли;	 
		КонецЦикла;	
		
		Если Не НашлиПередачуПараметра Тогда
			//значит значение параметра не было передано и берём значение по умолчанию
			
			Шаблон = Родитель.ДополнительныеСвойства.Шаблон;
			ДанныеВложенныхСценариев = ПараметрыПолученияТекстаСценария.ДанныеВложенныхСценариев;
			Если НЕ ПараметрыПолученияТекстаСценария.ЧтениеИзТекста Тогда
				СтрокаДанныеВложенныхСценариев = ДанныеВложенныхСценариев.Найти(Шаблон,"Сценарий");
			Иначе	
				СтрокаДанныеВложенныхСценариев = ДанныеВложенныхСценариев.Найти(
					Родитель.ДополнительныеСвойства.UID,"UID");
			КонецЕсли;	  
			
			Если СтрокаДанныеВложенныхСценариев = Неопределено Тогда
				ВызватьИсключение СтрШаблон(НСтр("ru = 'Не найдены данные вложенного сценария: %1. Основной сценарий: %2'"),
					Шаблон,ПараметрыПолученияТекстаСценария.Сценарий);
			КонецЕсли;	 
			
			ПараметрыВходящие = СтрокаДанныеВложенныхСценариев.ПараметрыВходящие;
			СтрокаПараметрыВходящие = ПараметрыВходящие.Найти(Параметр.НовоеИмяПараметра,"Имя");
			Если СтрокаПараметрыВходящие = Неопределено Тогда
				ВызватьИсключение СтрШаблон(НСтр("ru = 'У вложенного сценария: %1 не найден параметр %2'"),
					Шаблон,Параметр.НовоеИмяПараметра);
			КонецЕсли;	 
			
			ТекущееЗначениеПараметра = СтрокаПараметрыВходящие.Значение;
			НужныйТип                = Параметр.Тип;
			Возврат;
		КонецЕсли;	 
	Иначе	
		ПараметрРодителя = Родитель.Параметры[ИдПараметраВСтроке];
	КонецЕсли;	 
	
	Если ПараметрРодителя.ИзменяемыйПараметр Тогда
		Если ПараметрыПолученияТекстаСценария.МассивИерархииСценариев.Количество() > 1 Тогда
			//тут именно > 1, т.к. это означает, что надо выйти на один уровень вложенности вверх
			
			//это вложенный сценарий
			ИД = ПараметрыПолученияТекстаСценария.МассивИерархииСценариев.Количество()-1;
			ПоследнееЗначениеМассивИерархииСценариев = ПараметрыПолученияТекстаСценария.МассивИерархииСценариев[ИД];
			ПараметрыПолученияТекстаСценария.МассивИерархииСценариев.Удалить(ИД);
			
			СтрокаДереваСПараметрами = НайтиСтрокуДереваСПараметрамиДляТекущейСтроки(Родитель,ПараметрыПолученияТекстаСценария);
			ЗначениеИТипПараметраИзВеткиРодителя(Родитель,ТекущееЗначениеПараметра,НужныйТип,ПараметрРодителя,
			ПараметрыВходящие,НаборПараметров,СтрокаДереваСПараметрами,ПараметрыПолученияТекстаСценария);
			
			ПараметрыПолученияТекстаСценария.МассивИерархииСценариев.Добавить(ПоследнееЗначениеМассивИерархииСценариев);
		Иначе
			СтрокаНаборПараметров = НайтиСтрокуНабора(ПараметрыВходящие,НаборПараметров,ПараметрРодителя);
			Если СтрокаНаборПараметров = Неопределено Тогда
				ВызватьИсключение СтрШаблон(НСтр("ru = 'Не найдено значение для параметра с именем: %1.
				|Сценарий: %2'"),
				   ПараметрРодителя.НовоеИмяПараметра,ПараметрыПолученияТекстаСценария.Сценарий);
			Иначе	
				ТекущееЗначениеПараметра = СтрокаНаборПараметров.Значение;
				НужныйТип                = СтрокаНаборПараметров.ТипПараметра;
			КонецЕсли;	 
		КонецЕсли;	 
	Иначе
		ТекущееЗначениеПараметра = ПараметрРодителя.ЗначениеПараметра;
		НужныйТип                = ПараметрРодителя.Тип;
		Если ПараметрРодителя.Тип = ТипПараметра("Таблица") Тогда
			ТекущееЗначениеПараметра = ПараметрРодителя.ТекущееЗначениеПараметра;
		КонецЕсли;	 
	КонецЕсли;	 
	
КонецПроцедуры

Функция ЗначениеПараметраТаблицаИзПараметров(Параметр,ПараметрыВходящие,ПараметрыПолученияТекстаСценария,СтрокаДерева)
	Массив = Новый Массив;
	Для Каждого ЗначениеПараметраПодстановкаЗначений Из Параметр.ЗначениеПараметраПодстановкаЗначений Цикл
		ОбработаннаяСтрокаПараметров = ЗначениеПараметраПодстановкаЗначений.ОбработаннаяСтрокаПараметров;
		УстановитьВШагеЗначениеПараметра(ПараметрыВходящие,ОбработаннаяСтрокаПараметров,СтрокаДерева,
		  ПараметрыПолученияТекстаСценария,ЗначениеПараметраПодстановкаЗначений.Параметры);
		Массив.Добавить(ОбработаннаяСтрокаПараметров);  
	КонецЦикла;	 
	
	Возврат Массив;
КонецФункции	 

Функция НайтиСтрокуНабора(ПараметрыВходящие,НаборПараметров,Параметр)
	СтрокаНаборПараметров = НаборПараметров.ТаблицаПараметров.Найти(Параметр.НовоеИмяПараметра,"Имя");
	
	Если СтрокаНаборПараметров = Неопределено Тогда
		СтрПараметрыВходящие = ПараметрыВходящие.Найти(Параметр.ЗначениеПараметра,"Значение");
		Если СтрПараметрыВходящие <> Неопределено Тогда
			СтрокаНаборПараметров = НаборПараметров.ТаблицаПараметров.Найти(СтрПараметрыВходящие.Имя,"Имя");
		КонецЕсли;	 
	КонецЕсли;	 
	
	Возврат СтрокаНаборПараметров;
КонецФункции	

Процедура ПодстановкаЗначенийПараметровВСтрокуИзменяемыеПараметры(Стр,Параметры,ДобавлятьСимвол = Истина)
	Вид           = "КвадратныеСкобки";
	Тип           = ТипПараметра("Строка");
	КолПараметров = 0;
	
	Для Каждого Параметр Из Параметры Цикл
		Если Параметр.Символ <> "[" Тогда
			Продолжить;
		КонецЕсли;	 
		
		ЗначениеПараметра = Параметр.ТекущееЗначениеПараметра;
		
		Если ТипЗнч(ЗначениеПараметра) = Тип("Массив") Тогда
			ЗначениеПараметра = СтрСоединить(ЗначениеПараметра,Символы.ПС);
		КонецЕсли;	 
		
		КолПараметров = КолПараметров + 1;
		ЭкранироватьСпецСимволы(ЗначениеПараметра);
		
		Если Параметр.НужныйТип = ТипПараметра("Строка") ИЛИ Параметр.НужныйТип = ТипПараметра("Таблица") Тогда
			Если ДобавлятьСимвол Тогда
				СтрокаЗаментыПараметра = """" + ЗначениеПараметра + """";
			Иначе	
				СтрокаЗаментыПараметра = ЗначениеПараметра;
			КонецЕсли;	 
			Стр = СтрЗаменить(Стр,"~ПараметрСтрока" + Вид + XMLСтрока(КолПараметров) + "~",СтрокаЗаментыПараметра);
			
		Иначе	
			Стр = СтрЗаменить(Стр,"~ПараметрСтрока" + Вид + XMLСтрока(КолПараметров) + "~",ЗначениеПараметра);
		КонецЕсли;	 
		
		Если Параметр.Свойство("СвязанСФО") Тогда
			Если Параметр.СвязанСФО Тогда
				Стр = Сред(Стр,3); //т.к. первые символы равны ФО
			КонецЕсли;	 
		КонецЕсли;	 
		
	КонецЦикла;	
КонецПроцедуры

Процедура ПодстановкаЗначенийПараметровВСтрокуОдногоПараметра(Стр,Параметр,Тип,Вид,ЗначениеПараметра,КолПараметров)
	ЭкранироватьСпецСимволы(ЗначениеПараметра);
	
	Если Тип = ТипПараметра("Строка") Тогда
		Если Параметр.НужныйТип = ТипПараметра("Строка") Тогда
			СтрокаЗаментыПараметра = Параметр.Символ + ЗначениеПараметра + Параметр.Символ;
		Иначе	
			СтрокаЗаментыПараметра = ЗначениеПараметра;
		КонецЕсли;	 
		Стр = СтрЗаменить(Стр,"~Параметр" + Тип + Вид + XMLСтрока(КолПараметров) + "~",СтрокаЗаментыПараметра);
		
		Если Параметр.Свойство("СвязанСФО") Тогда
			Если Параметр.СвязанСФО Тогда
				Стр = Сред(Стр,3); //т.к. первые символы равны ФО
				Стр = СокрЛП(СтрЗаменить(Стр, Параметр.Символ,""));
			КонецЕсли;	 
		КонецЕсли;	 
		
	Иначе	
		Стр = СтрЗаменить(Стр,"~Параметр" + Тип + Вид + XMLСтрока(КолПараметров) + "~",ЗначениеПараметра);
	КонецЕсли;	 
КонецПроцедуры

Процедура ПодстановкаЗначенийПараметровВСтроку(Стр,Параметры,Тип,Вид = "")
	КолПараметров = 0;
	Для Каждого Параметр Из Параметры Цикл
		Если Параметр.Тип <> Тип Тогда
			Продолжить;
		КонецЕсли;	 
		
		Если Вид <> "" Тогда
			Если Параметр.Вид <> Вид Тогда
				Продолжить;
			КонецЕсли;	 
		КонецЕсли;	 
		
		КолПараметров = КолПараметров + 1;
		
		Если НЕ Параметр.Свойство("ВложенныеПараметры") Или Параметр.ВложенныеПараметры = Неопределено Тогда
			ЗначениеПараметра = Параметр.ТекущееЗначениеПараметра;
		Иначе
					 
			СтрокаДляУстановкиЗначения = Параметр.ЗначениеПараметраВложенное;
			ПодстановкаЗначенийПараметровВСтрокуИзменяемыеПараметры(СтрокаДляУстановкиЗначения,Параметр.ВложенныеПараметры,Ложь);
			ЗначениеПараметра = СтрокаДляУстановкиЗначения;
			Параметр.Вставить("ЗначениеПараметра",ЗначениеПараметра);
		КонецЕсли;	 
		
		ПодстановкаЗначенийПараметровВСтрокуОдногоПараметра(Стр,
				     Параметр,Тип,Вид,ЗначениеПараметра,КолПараметров);
	КонецЦикла;	
КонецПроцедуры

Функция БыстрыйРасчетСтрокиОтступа(Количество,СимволТаб)
	Если Не СимволТаб Тогда
		Если Количество = 0 Тогда
			Возврат "";
		ИначеЕсли Количество = 8 Тогда
			Возврат "        ";
		ИначеЕсли Количество = 16 Тогда
			Возврат "                ";
		ИначеЕсли Количество = 24 Тогда
			Возврат "                        ";
		ИначеЕсли Количество = 32 Тогда
			Возврат "                                ";
		ИначеЕсли Количество = 40 Тогда
			Возврат "                                        ";
		ИначеЕсли Количество = 48 Тогда
			Возврат "                                                ";
		ИначеЕсли Количество = 56 Тогда
			Возврат "                                                        ";
		ИначеЕсли Количество = 64 Тогда
			Возврат "                                                                ";
		ИначеЕсли Количество = 72 Тогда
			Возврат "                                                                        ";
		ИначеЕсли Количество = 80 Тогда
			Возврат "                                                                                ";
		ИначеЕсли Количество = 80 Тогда
			Возврат "                                                                                        ";
		КонецЕсли;	 
	КонецЕсли;	 
	
	Возврат Неопределено;
КонецФункции	

Функция ОписаниеОшибкиВыполненияВложенныйСценарияНеПереданПараметр(СтрокаДерева,Параметр,
	                                                    ПараметрыПолученияТекстаСценария)
														
	Если СтрокаДерева = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;	 
	
	ИДМассивИерархииСценариев = ПараметрыПолученияТекстаСценария.МассивИерархииСценариев.Количество()-1;
	
	МассивВызовов = Новый Массив;
	ТекСтрокаДерева = СтрокаДерева;
	Пока Истина Цикл
		МассивВызовов.Добавить(ТекСтрокаДерева);
		
		ТекСтрокаДерева = ТекСтрокаДерева.Родитель;
		Если ТекСтрокаДерева = Неопределено Тогда
			Если ИДМассивИерархииСценариев >= 0 Тогда
				ТекСтрокаДерева = ПараметрыПолученияТекстаСценария.МассивИерархииСценариев[ИДМассивИерархииСценариев].СхемаДерево;
				ИДМассивИерархииСценариев = ИДМассивИерархииСценариев - 1;
				Продолжить;
			КонецЕсли;	 
			
			Прервать;
		КонецЕсли;	 
	КонецЦикла;	
	
	Стр = Символы.ПС;
	Для Ккк = 0 По МассивВызовов.Количество()-1 Цикл
		Отступ = "";
		Для Сч = 1 По Ккк Цикл
			Отступ = Отступ + "---";
		КонецЦикла;	
		
		ТекИД = МассивВызовов.Количество()-1 - Ккк;
		Стр   = Стр + Отступ + НСтр("ru = 'Строка№'") + МассивВызовов[ТекИД].НомерСтрокиТекстаСценария
		                           + ". " + МассивВызовов[ТекИД].ОписаниеЭлемента;
		Стр = Стр + Символы.ПС;
	КонецЦикла;	
	
	
	Стр = Стр + НСтр("ru = 'параметр: '") + Параметр.НовоеИмяПараметра;
	
	Возврат Стр;
КонецФункции	

Функция ОписаниеОшибкиВыполненияСценарияНеПереданПараметр(СтрокаДерева,Параметр)
	Если СтрокаДерева = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;	 
	
	Если СтрокаДерева.ДополнительныеСвойства = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;	 
	
	Если Не СтрокаДерева.ДополнительныеСвойства.Свойство("Шаблон") Тогда
		Возврат Неопределено;
	КонецЕсли;	 
	
	Возврат СтрШаблон(НСтр("ru = '%1, строка №%2, параметр: %3'"),
		             СтрокаДерева.ДополнительныеСвойства.Шаблон.Наименование,
					 СтрокаДерева.НомерСтрокиТекстаСценария,
					 Параметр.НовоеИмяПараметра);
КонецФункции	

Функция СценарииИзДереваБизнесПроцесса(ШагиБизнесПроцесса)
	Массив = Новый Массив;
	
	Для Каждого СтрокаДерева Из ШагиБизнесПроцесса Цикл
		Массив.Добавить(СтрокаДерева.Сценарий);
	КонецЦикла;	
	
	Возврат Массив;
КонецФункции	

Функция ПараметрыСценарияБизнесПроцесса(СтрокаСценария,ПараметрыШагов,ПараметрыБизнесПроцесса,
	 ДанныеВложенныхСценариев,ШагиБизнесПроцесса)
	 
	ТаблицаПараметров = Новый ТаблицаЗначений;
	ТаблицаПараметров.Колонки.Добавить("Имя");
	ТаблицаПараметров.Колонки.Добавить("ТипПараметра");
	ТаблицаПараметров.Колонки.Добавить("Значение");
	
	Для Каждого СтрокаПараметра Из ПараметрыШагов Цикл
		Если СтрокаПараметра.КлючШага <> СтрокаСценария.КлючШага Тогда
			Продолжить;
		КонецЕсли;	 
		
		СтрокаТаблицаПараметров                   = ТаблицаПараметров.Добавить();
		СтрокаТаблицаПараметров.ТипПараметра      = СтрокаПараметра.ТипПараметра;
		СтрокаТаблицаПараметров.Значение          = ЗначениеПараметраБизнесПроцесса(СтрокаПараметра.Значение,
			ПараметрыБизнесПроцесса,СтрокаПараметра,СтрокаСценария,ШагиБизнесПроцесса,ПараметрыШагов);
			
		СтрокаТаблицаПараметров.Имя               = СтрокаПараметра.Имя;
		
		Если СтрокаТаблицаПараметров.Значение = "" Тогда
			//значит значение не передано и надо взять значение по умолчанию
			СтрокаДанныеВложенныхСценариев = ДанныеВложенныхСценариев.Найти(СтрокаСценария.Сценарий,"Сценарий");
			ПараметрыВходящие = СтрокаДанныеВложенныхСценариев.ПараметрыВходящие;
			СтрокаПараметраВСценарии = ПараметрыВходящие.Найти(СтрокаТаблицаПараметров.Имя,"Имя");
			Если СтрокаПараметраВСценарии <> Неопределено Тогда
				СтрокаТаблицаПараметров.Значение = СтрокаПараметраВСценарии.Значение;
			КонецЕсли;	 
			
		КонецЕсли;	 
	КонецЦикла;	
	
	Возврат ТаблицаПараметров;
КонецФункции	

Функция ЗначениеПараметраБизнесПроцесса(ЗначениеПараметра,ПараметрыБизнесПроцесса,СтрокаПараметра,
	  СтрокаСценария, ШагиБизнесПроцесса,ПараметрыШагов)
	  
	Если Лев(НРег(ЗначениеПараметра),14) = "общийпараметр." Тогда
		ИмяПараметра = Сред(ЗначениеПараметра,15);
		
		СтрокаПараметрыБизнесПроцесса = ПараметрыБизнесПроцесса.Найти(ИмяПараметра,"Имя");
		
		Если СтрокаПараметрыБизнесПроцесса = Неопределено Тогда
			ВызватьИсключение СтрШаблон(НСтр("ru = 'Ошибка в передаче параметров в шаг бизнес-процесса №%1.
			|Сценарий <%2>.
			|Не найден общий параметр бизнес-процесса: %3'"),
			  СтрокаСценария.НомерСтроки,СтрокаСценария.Описание,ЗначениеПараметра);
		КонецЕсли;	 
		
		Возврат СтрокаПараметрыБизнесПроцесса.Значение;
	КонецЕсли;	 
	
	Если ЗначениеЗаполнено(ЗначениеПараметра) Тогда
		Если СтрокаПараметра.КлючЗначенияПараметра > 0 Тогда
			//значит можно явно вычислить служеюную строку
			СтрокаПараметрыШагов = ПараметрыШагов.Найти(СтрокаПараметра.КлючЗначенияПараметра,"КлючПараметра");
			Если СтрокаПараметрыШагов <> Неопределено Тогда
				СтрокаШагиБизнесПроцесса = ШагиБизнесПроцесса.Найти(СтрокаПараметрыШагов.КлючШага,"КлючШага");
				Если СтрокаШагиБизнесПроцесса <> Неопределено Тогда
					Возврат "$" + СтрокаШагиБизнесПроцесса.Описание + "." + СтрокаПараметрыШагов.Имя + "$";
				КонецЕсли;	 
			КонецЕсли;	 
		КонецЕсли;	 
		
		Возврат "$" + ЗначениеПараметра + "$";
	ИначеЕсли ЗначениеЗаполнено(СтрокаПараметра.ЗначениеПроизвольное) Тогда
		Возврат СтрокаПараметра.ЗначениеПроизвольное; 
	КонецЕсли;	 
	
	Если СтрокаПараметра.ИсходящийПараметр Тогда
		Возврат "$" + СтрокаСценария.Описание + "." + СтрокаПараметра.Имя + "$";
	КонецЕсли;	 
	
	Возврат ЗначениеПараметра;
КонецФункции	

Функция ЗначениеПараметраПроцесса(ЗначениеПараметра,ПараметрыБизнесПроцесса,СтрокаПараметра,
	  СтрокаСценария, ШагиБизнесПроцесса, ПараметрыФормированияТекстаСценария)
	  
	Если Лев(НРег(ЗначениеПараметра),15) = "общий параметр." Тогда
		ИмяПараметра = Сред(ЗначениеПараметра,16);
		
		СтрокаПараметрыБизнесПроцесса = ПараметрыБизнесПроцесса.Найти(ИмяПараметра,"Имя");
		
		Если СтрокаПараметрыБизнесПроцесса = Неопределено Тогда
			ВызватьИсключение СтрШаблон(НСтр("ru = 'Ошибка в передаче параметров в шаг бизнес-процесса №%1.
			|Сценарий <%2>.
			|Не найден общий параметр бизнес-процесса: %3'"),
			  СтрокаСценария.НомерСтроки,СтрокаСценария.Наименование,ЗначениеПараметра);
		КонецЕсли;	 
		
		Возврат СтрокаПараметрыБизнесПроцесса.Значение;
	КонецЕсли;	 
	
	Если ЗначениеЗаполнено(ЗначениеПараметра) И ЗначениеЗаполнено(СтрокаПараметра.ШагПроцессаЗначениеПоСсылке) Тогда
		Если ПараметрыФормированияТекстаСценария.ЧтениеИзТекста Тогда
			UIDПроцесса = ПараметрыФормированияТекстаСценария.UIDПроцесса;
			СтрокаДанныеПроцессов = ПараметрыФормированияТекстаСценария.ДанныеПроцессов.Найти(UIDПроцесса,"UID");
			СтрокаШагиБизнесПроцесса = СтрокаДанныеПроцессов.ШагиПроцесса.Найти(СтрокаПараметра.ШагПроцессаЗначениеПоСсылкеUID,"UID");
		Иначе	
			СтрокаШагиБизнесПроцесса = ШагиБизнесПроцесса.Найти(СтрокаПараметра.ШагПроцессаЗначениеПоСсылке,"ШагПроцесса");
		КонецЕсли;	 
		Возврат "$" + СтрокаШагиБизнесПроцесса.Наименование + "." + СтрокаПараметра.ИмяПараметраПоСсылке + "$";
	ИначеЕсли ЗначениеЗаполнено(СтрокаПараметра.ЗначениеПроизвольное) Тогда
		Возврат СтрокаПараметра.ЗначениеПроизвольное; 
	КонецЕсли;	 
	
	Если СтрокаПараметра.ИсходящийПараметр Тогда
		Возврат "$" + СтрокаСценария.Наименование + "." + СтрокаПараметра.Имя + "$";
	КонецЕсли;	 
	
	Возврат ЗначениеПараметра;
КонецФункции	

Функция ИмяЭлемента(ТипЭлемента,ИдСтрокиДерева)
	Если ТипЭлемента = ТипЭлементаКомментарий() Тогда
		Возврат "";
	ИначеЕсли ТипЭлемента = ТипЭлементаДействие() Тогда
		Возврат ИмяЭлементаДействие() + XMLСтрока(ИдСтрокиДерева);
	ИначеЕсли ТипЭлемента = ТипЭлементаТаблица() Тогда
		Возврат "";
	ИначеЕсли ТипЭлемента = ТипЭлементаОкончаниеСхемы() Тогда
		Возврат ИмяЭлементаСтоп() + XMLСтрока(ИдСтрокиДерева);
	ИначеЕсли ТипЭлемента = ТипЭлементаНачалоСхемы() Тогда
		Возврат ИмяЭлементаСтарт() + XMLСтрока(ИдСтрокиДерева);
	ИначеЕсли ТипЭлемента = ТипЭлементаСтоп() Тогда
		Возврат ИмяЭлементаСтоп() + XMLСтрока(ИдСтрокиДерева);
	ИначеЕсли ТипЭлемента = ТипЭлементаГруппаШагов() Тогда
		Возврат ИмяЭлементаВложенныйПроцесс() + XMLСтрока(ИдСтрокиДерева);
	ИначеЕсли ТипЭлемента = ТипЭлементаШаблонСценария() Тогда
		Возврат ИмяЭлементаШаблонСценария() + XMLСтрока(ИдСтрокиДерева);
	ИначеЕсли ТипЭлемента = ТипЭлементаУсловиеПрепроцессор() Тогда
		Возврат "УсловиеПрепроцессор" + XMLСтрока(ИдСтрокиДерева);
	ИначеЕсли ТипЭлемента = ТипЭлементаУсловие() Тогда
		Возврат "Условие" + XMLСтрока(ИдСтрокиДерева);
	ИначеЕсли ТипЭлемента = ТипЭлементаУсловиеEng() Тогда
		Возврат "Condition" + XMLСтрока(ИдСтрокиДерева);
	ИначеЕсли ТипЭлемента = ТипЭлементаМетка() Тогда
		Возврат ИмяЭлементаМетка() + XMLСтрока(ИдСтрокиДерева);
	ИначеЕсли ТипЭлемента = ТипЭлементаЦикл() Тогда
		Возврат "Цикл";
	ИначеЕсли ТипЭлемента = ТипЭлементаЦиклEng() Тогда
		Возврат "Do";
	ИначеЕсли ТипЭлемента = ТипЭлементаПередачаПараметров() Тогда
		Возврат "ПередачаПараметра";
	Иначе
		Возврат ""; 
	КонецЕсли;	 
КонецФункции	

Функция ПолучитьДеревоСценарияИзФайла(Сценарий,СтруктураПараметров)
	UIDСценария     = СтруктураПараметров.UIDСценария;
	ДанныеСценариев = СтруктураПараметров.ДанныеСценариев;
	СтрокаДанныеСценариев = ДанныеСценариев.Найти(UIDСценария,"UID");
	Если СтрокаДанныеСценариев = Неопределено Тогда
		ВызватьИсключение СтрШаблон(
		    НСтр("ru = 'Не найден сценарий: %1  по настройке %2'"),СтруктураПараметров.ИмяСценария,СтруктураПараметров.Имя);
	КонецЕсли;
	
	Если СтрокаДанныеСценариев.ДеревоСхемы <> Неопределено Тогда
		Возврат СтрокаДанныеСценариев.ДеревоСхемы;
	КонецЕсли;	 
		
	ДанныеСценария = СтрокаДанныеСценариев;
	
	ТекстСценария = ДанныеСценария.ТекстСценария;
	
	Если ТипЗнч(ТекстСценария) = Тип("Массив") Тогда
		ТекстСценария = СтрСоединить(ТекстСценария,Символы.ПС);
	КонецЕсли;	 
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ДелатьРаскраску",Ложь);
	ДополнительныеПараметры.Вставить("ИсключитьСлужебныеСловаИзТекстаСценария",Истина);
	ДополнительныеПараметры.Вставить("ЧтениеИзТекста",Истина);
	ДополнительныеПараметры.Вставить("СтруктураПараметров",СтруктураПараметров);
	ДополнительныеПараметры.Вставить("Сценарий",Сценарий);
	ДополнительныеПараметры.Вставить("КодСценария",СтрокаДанныеСценариев.Код);
	
	ОбъектСервер       = Сценарий;
	ДеревоСхемы = СоздатьДеревоСхемы();
	
	ПроектСценария = СтруктураПараметров.Проект;
	
	ДеревоСценарияИзТекста(ТекстСценария,
	                         ДеревоСхемы,ПроектСценария,ОбъектСервер,ДополнительныеПараметры);
	
							 
	ПараметрыВходящие = СтруктураПараметров.ПараметрыСценария;
	
	
	ПараметрыВходящиеИзДерева(ДеревоСхемы,ПараметрыВходящие);
							 
							 
	СтрокаДанныеСценариев.ДеревоСхемы = ДеревоСхемы;
	Возврат ДеревоСхемы;							 
КонецФункции	

Функция ПустаяТаблицаШаблонов()
	Тзн = Новый ТаблицаЗначений;
	Тзн.Колонки.Добавить("Ссылка");
	Тзн.Колонки.Добавить("UID");
	Тзн.Колонки.Добавить("ПараметрыВходящие");
	Тзн.Колонки.Добавить("ВерсияСценария");
	Тзн.Колонки.Добавить("Наименование");
	Тзн.Колонки.Добавить("Код");
	Тзн.Колонки.Добавить("ДанныеШаблона");
	Тзн.Колонки.Добавить("Снипет");
	Тзн.Колонки.Добавить("СнипетНРег");
	Тзн.Колонки.Добавить("ЧтениеИзТекста");
	
	Возврат Тзн;
КонецФункции	

Функция ТаблицаШаблоновИзТекста(ПараметрыВерсийСценариев,ДополнительныеПараметры)
	ТаблицаШаблонов = ПустаяТаблицаШаблонов();
	
	КешПараметровСтроки = СоздатьТаблицуКешПараметровСтроки();
	
	UIDФункцияСистемы = ДополнительныеПараметры.СтруктураПараметров.UIDФункцияСистемы;
	ДанныеСценариев = ДополнительныеПараметры.СтруктураПараметров.ДанныеСценариев;
	Для Каждого ДанныеОдногоСценария Из ДанныеСценариев Цикл
		Если ДанныеОдногоСценария.UIDФункцияСистемы = UIDФункцияСистемы
		Или  ДанныеОдногоСценария.РазрешеноИспользоватьВДругихФункциях = "Да"	
		Тогда
			СтрокаТаблицаШаблонов                   = ТаблицаШаблонов.Добавить();
			СтрокаТаблицаШаблонов.Наименование      = ДанныеОдногоСценария.Имя;
			СтрокаТаблицаШаблонов.Код               = ДанныеОдногоСценария.Код;
			СтрокаТаблицаШаблонов.UID               = ДанныеОдногоСценария.UID;
			СтрокаТаблицаШаблонов.ПараметрыВходящие = ДанныеОдногоСценария.ПараметрыСценария.Скопировать();
			СтрокаТаблицаШаблонов.ВерсияСценария    = ДанныеОдногоСценария.ВерсияСценария;
			СтрокаТаблицаШаблонов.Снипет            = ПолучитьСнипетИзИмениСценария(ДанныеОдногоСценария.Имя,КешПараметровСтроки);
			СтрокаТаблицаШаблонов.СнипетНРег        = НРег(СтрокаТаблицаШаблонов.Снипет);
			СтрокаТаблицаШаблонов.ЧтениеИзТекста    = Истина;
		КонецЕсли;	 
	КонецЦикла;	
	
	Возврат ТаблицаШаблонов;
КонецФункции	

Функция ПолучитьСнипетИзИмениСценария(Знач Стр,КешПараметровСтроки)
	Если Стр = "" Тогда
		Возврат Стр;
	КонецЕсли;	 
	
	МассивСлов  = СтрРазделить(Стр," ",Истина);
	
	ПервоеСлово   = МассивСлов[0];
	
	Если ПервоеСлово = Неопределено Тогда
		Возврат Стр;
	КонецЕсли;	 
	
	Возврат УбратьЗапрещенныеСимволыИзСнипет(СнипетИзСтроки(Стр,КешПараметровСтроки));
КонецФункции	

Функция ТаблицаШаблоновСценариев(ПараметрыВерсийСценариев,ДополнительныеПараметры = Неопределено)
	
	Если ДополнительныеПараметры <> Неопределено Тогда
		Если ДополнительныеПараметры.Свойство("ЧтениеИзТекста") Тогда
			Если ДополнительныеПараметры.ЧтениеИзТекста Тогда
				Возврат ТаблицаШаблоновИзТекста(ПараметрыВерсийСценариев,ДополнительныеПараметры);
			КонецЕсли;	 
		КонецЕсли;	 
	КонецЕсли;	 
	
	Если Не ЗначениеЗаполнено(ПараметрыВерсийСценариев.Проект) Тогда
		Возврат ПустаяТаблицаШаблонов();
	КонецЕсли;	 
	
	КешПараметровСтроки = СоздатьТаблицуКешПараметровСтроки();
	
	ТаблицаСценариев = Обработки.СборкаТекстовСценариев.ТаблицаШаблоновСценариев(ПараметрыВерсийСценариев);
			
	ТаблицаСценариев.Колонки.Добавить("ДанныеШаблона");
	ТаблицаСценариев.Колонки.Добавить("Снипет");
	ТаблицаСценариев.Колонки.Добавить("СнипетНРег");
	ТаблицаСценариев.Колонки.Добавить("ЧтениеИзТекста");
	
	Для Каждого СтрТаблицаСтрок Из ТаблицаСценариев Цикл
		СтрТаблицаСтрок.Снипет = ПолучитьСнипетИзИмениСценария(СтрТаблицаСтрок.Наименование,КешПараметровСтроки);
		СтрТаблицаСтрок.СнипетНРег = НРег(СтрТаблицаСтрок.Снипет);
		СтрТаблицаСтрок.ЧтениеИзТекста = Ложь;
	КонецЦикла;	
	
	Возврат ТаблицаСценариев;
КонецФункции	

Процедура ДеревоСценарияИзТекстаСлужебный(Текст,ДеревоСхемы,ТаблицаШаблоновСценариев,Сценарий,ДополнительныеПараметры)
	ДеревоСхемы.Строки.Очистить();
	ТаблицаСтрок = ПреобразоватьТекстВТаблицуСтрок(Текст);
	
	МассивОшибок = Новый Массив;
	КешПараметровСтроки = СоздатьТаблицуКешПараметровСтроки();
	СнипетыШагов(ТаблицаСтрок,МассивОшибок,КешПараметровСтроки);
	Если МассивОшибок.Количество() > 0 Тогда
		КодСценария = "";
		Если ДополнительныеПараметры.ЧтениеИзТекста Тогда
			КодСценария = ДополнительныеПараметры.КодСценария;
		Иначе	
			СтрокаТаблицаШаблоновСценариев = ТаблицаШаблоновСценариев.Найти(Сценарий,"Ссылка");
			Если СтрокаТаблицаШаблоновСценариев <> Неопределено Тогда
				КодСценария = СтрокаТаблицаШаблоновСценариев.Код;
			КонецЕсли;	 
		КонецЕсли;	 
		
		Стр = СтрШаблон(НСтр("ru = 'Ошибка компиляции сценария <%1>. Код: <%2>'"),Сценарий,КодСценария);
		Для Каждого СтруктураОшибки Из МассивОшибок Цикл
			Стр = Стр + Символы.ПС + СтруктураОшибки.ТекстОшибки;
		КонецЦикла;	
		
		ВызватьИсключение Стр;
	КонецЕсли;	 
	
	ТаблицаСтрокВДеревоСхемы(ТаблицаСтрок,ДеревоСхемы,ТаблицаШаблоновСценариев,Сценарий
	    ,ДополнительныеПараметры,КешПараметровСтроки);
КонецПроцедуры

Функция СоздатьТаблицуКешПараметровСтроки()
	КешПараметровСтроки = Новый ТаблицаЗначений;
	КешПараметровСтроки.Колонки.Добавить("Строка");
	КешПараметровСтроки.Колонки.Добавить("Параметры");
	КешПараметровСтроки.Колонки.Добавить("ПреобразованнаяСтрока");
	
	КешПараметровСтроки.Индексы.Добавить("Строка");
	
	Возврат КешПараметровСтроки;
КонецФункции	

Функция УбратьЗапрещенныеСимволыИзСнипет(Снипет) 
	Снипет = СтрЗаменить(Снипет," ","");
	Снипет = СтрЗаменить(Снипет,"""","");
	Снипет = СтрЗаменить(Снипет,".","");
	Снипет = СтрЗаменить(Снипет,",","");
	Снипет = СтрЗаменить(Снипет,":","");
	Снипет = СтрЗаменить(Снипет,";","");
	Снипет = СтрЗаменить(Снипет,"-","_");
	Снипет = СтрЗаменить(Снипет,"+","");
	Снипет = СтрЗаменить(Снипет,"/","");
	Снипет = СтрЗаменить(Снипет,"\","");
	Снипет = СтрЗаменить(Снипет,"=","");
	Снипет = СтрЗаменить(Снипет,"!","");
	Снипет = СтрЗаменить(Снипет,"@","");
	Снипет = СтрЗаменить(Снипет,"#","");
	Снипет = СтрЗаменить(Снипет,"$","");
	Снипет = СтрЗаменить(Снипет,"%","");
	Снипет = СтрЗаменить(Снипет,"^","");
	Снипет = СтрЗаменить(Снипет,"&","");
	Снипет = СтрЗаменить(Снипет,"(","");
	Снипет = СтрЗаменить(Снипет,")","");
	Снипет = СтрЗаменить(Снипет,"№","");
	Снипет = СтрЗаменить(Снипет,"?","");
	Снипет = СтрЗаменить(Снипет,"`","");
	Снипет = СтрЗаменить(Снипет,"'","");
	Снипет = СтрЗаменить(Снипет,"~","");
	Снипет = СтрЗаменить(Снипет,Символы.НПП," ");
	Снипет = СтрЗаменить(Снипет,"*","");
	Снипет = СтрЗаменить(Снипет,"<","");
	Снипет = СтрЗаменить(Снипет,">","");
	
	Возврат Снипет;
КонецФункции

Функция СкопироватьПараметрыШага(Параметры)
	Копия = Новый Массив;
	
	Для Каждого ПараметрШага Из Параметры Цикл
		СтруктураПараметра = СтруктураПараметра();
		ЗаполнитьЗначенияСвойств(СтруктураПараметра,ПараметрШага);
		
		Если ПараметрШага.ВложенныеПараметры <> Неопределено Тогда
			СтруктураПараметра.ВложенныеПараметры = СкопироватьПараметрыШага(ПараметрШага.ВложенныеПараметры);
		КонецЕсли;	 
		
		Копия.Добавить(СтруктураПараметра);
	КонецЦикла;	
	
	Возврат Копия; 
КонецФункции	 

Функция ПараметрыGherkinИзСтрокиИПреобразоватьСтрокуДляРаботыСПараметрами(Стр,
	                                         ИзменяемыеПараметрыСценария = Неопределено,
											 КешПараметровСтроки = Неопределено,
											 СтрокаКеш = Неопределено,
											 ДопПараметры = Неопределено) Экспорт
											 
											 
	ИспользоватьКеш = Истина;
	Если ТипЗнч(ДопПараметры) = Тип("Структура") и ДопПараметры.Свойство("ИспользоватьКеш") Тогда
		ИспользоватьКеш = ДопПараметры.ИспользоватьКеш;
	КонецЕсли;	 
	
	Если КешПараметровСтроки <> Неопределено и ИспользоватьКеш Тогда
		
		Если СтрокаКеш = Неопределено Тогда
			СтрокаКешПараметровСтроки = КешПараметровСтроки.Найти(Стр,"Строка");
		Иначе	
			СтрокаКешПараметровСтроки = КешПараметровСтроки.Найти(СтрокаКеш,"Строка");
		КонецЕсли;	 
		
		Если СтрокаКешПараметровСтроки <> Неопределено Тогда
			Стр = СтрокаКешПараметровСтроки.ПреобразованнаяСтрока;
			Возврат СкопироватьПараметрыШага(СтрокаКешПараметровСтроки.Параметры);
		КонецЕсли;	 
	КонецЕсли;	 											 
	
	ОригинальнаяСтрока = Стр;
	Параметры          = Новый Массив;
	
	ЗаменитьСлужебныеСимволыВСтроке(Стр);
	
	НайтиСтроковыеПараметрыПоСимволу(Стр,Параметры,"'","Апострофы");
	НайтиСтроковыеПараметрыПоСимволу(Стр,Параметры,"""","Кавычки");
	НайтиИзменяемыеПараметры(Стр,Параметры,ИзменяемыеПараметрыСценария);
	НайтиПараметрыДаты(Стр,Параметры);
	НайтиЧисловыеПараметры(Стр,Параметры, Новый ОписаниеТипов("Число"));
	
	НайтиИзменяемыеПараметрыВПараметрах(Стр,Параметры,ИзменяемыеПараметрыСценария);
	
	УпорядочитьМассивПараметров(Стр,Параметры);
	
	ЭкранироватьУгловыеСкобки(Стр);
	
	Если КешПараметровСтроки <> Неопределено Тогда
		СтрокаКешПараметровСтроки                       = КешПараметровСтроки.Добавить();
		СтрокаКешПараметровСтроки.Строка                = ОригинальнаяСтрока;
		Если СтрокаКеш = Неопределено Тогда
			СтрокаКешПараметровСтроки.ПреобразованнаяСтрока = Стр;
		Иначе	
			СтрокаКешПараметровСтроки.ПреобразованнаяСтрока = СтрокаКеш;
		КонецЕсли;	 
		СтрокаКешПараметровСтроки.Параметры = Параметры;
	КонецЕсли;
	
	Возврат Параметры;
КонецФункции	

Процедура УбратьПараметрыИзСтроки(Стр,Параметры)
	Для Каждого Параметр Из Параметры Цикл
		Стр = СтрЗаменить(Стр,"~Параметр" + Параметр.Тип + Параметр.Вид + XMLСтрока(Параметр.НомерПараметра) + "~","");
	КонецЦикла;	
КонецПроцедуры

Функция ПреобразоватьТекстВТаблицуСтрок(Текст)
	ТаблицаСтрок = Новый ТаблицаЗначений;
	ТаблицаСтрок.Колонки.Добавить("СтрокаОригинал");
	ТаблицаСтрок.Колонки.Добавить("СтрокаСокр");
	ТаблицаСтрок.Колонки.Добавить("Отступ");
	ТаблицаСтрок.Колонки.Добавить("СтрокаРезультат");
	ТаблицаСтрок.Колонки.Добавить("ГруппаШаговНачалась");
	ТаблицаСтрок.Колонки.Добавить("ГруппаШаговЗакончилась");
	ТаблицаСтрок.Колонки.Добавить("НачатьГруппыСначала");
	ТаблицаСтрок.Колонки.Добавить("Оператор");
	ТаблицаСтрок.Колонки.Добавить("ПараметрыОператора");
	ТаблицаСтрок.Колонки.Добавить("Снипет");
	ТаблицаСтрок.Колонки.Добавить("СнипетНРег");
	ТаблицаСтрок.Колонки.Добавить("ИдРодительскойГруппы");
	ТаблицаСтрок.Колонки.Добавить("НомерСтрокиТекстаСценария");
	
	МассивСтрок = СтрРазделить(Текст,Символы.ПС);
	НомерСтроки = 0;
	Для Каждого Стр Из МассивСтрок Цикл
		НомерСтроки = НомерСтроки + 1;
		
		СтрТаблицаСтрок = ТаблицаСтрок.Добавить();
		СтрТаблицаСтрок.СтрокаОригинал            = Стр;
		СтрТаблицаСтрок.СтрокаСокр                = СокрЛП(Стр);
		СтрТаблицаСтрок.Отступ                    = СтрДлина(Стр) - СтрДлина(СокрЛ(Стр));
		СтрТаблицаСтрок.СтрокаРезультат           = СтрТаблицаСтрок.СтрокаСокр;
		СтрТаблицаСтрок.ГруппаШаговНачалась       = Ложь;
		СтрТаблицаСтрок.ГруппаШаговЗакончилась    = Ложь;
		СтрТаблицаСтрок.НачатьГруппыСначала       = Ложь;
		СтрТаблицаСтрок.Оператор                  = Неопределено;
		СтрТаблицаСтрок.ПараметрыОператора        = Неопределено;
		СтрТаблицаСтрок.НомерСтрокиТекстаСценария = НомерСтроки;
	КонецЦикла;	
	
	
	НомерСтроки          = 0;
	ТекОтступ            = 0;
	ПредыдущийОтступ     = 0;
	НашлиГруппу          = Ложь;
	ОтступГруппы         = 0;
	ИдРодительскойГруппы = -1;
	
	КешОператорКонтекст    = ОператорКонтекст();
	КешОператорСценарий    = ОператорСценарий();
	КешОператорКомментарий = ОператорКомментарий();
	КешОператорПараметрТаблица= ОператорПараметрТаблица();
	
	НайденоНачалоБлочногоКомментария = Ложь;
	Для Каждого СтрТаблицаСтрок Из ТаблицаСтрок Цикл
		НомерСтроки = НомерСтроки + 1;
		
		СтрТаблицаСтрок.ИдРодительскойГруппы = ИдРодительскойГруппы;
		
		
		Если СтрТаблицаСтрок.СтрокаСокр = "" Тогда
			Если НайденоНачалоБлочногоКомментария Тогда
				СтрТаблицаСтрок.Оператор = ОператорКомментарий();
			КонецЕсли;	 
			
			ПредыдущийОтступ = 0;
			Продолжить;
		КонецЕсли;	 
		
		ОператорыВСтроке(СтрТаблицаСтрок,НайденоНачалоБлочногоКомментария,НомерСтроки);
		Если СтрТаблицаСтрок.Оператор = ОператорБлочныйКомменатрийНачало() Тогда
			НайденоНачалоБлочногоКомментария = Истина;
			СтрТаблицаСтрок.ИдРодительскойГруппы = -1;
		ИначеЕсли СтрТаблицаСтрок.Оператор = ОператорБлочныйКомменатрийОкончание() Тогда
			НайденоНачалоБлочногоКомментария = Ложь;
			СтрТаблицаСтрок.ИдРодительскойГруппы = -1;
		ИначеЕсли СтрТаблицаСтрок.Оператор = ОператорКомментарий() Тогда
			СтрТаблицаСтрок.ИдРодительскойГруппы = -1;
		КонецЕсли;	 
		
		Если Не ЗначениеЗаполнено(СтрТаблицаСтрок.Оператор) Тогда
			Если НомерСтроки > 1 Тогда
				Если ТаблицаСтрок[НомерСтроки-1-1].Оператор = ОператорЗаголовок() Тогда
					СтрТаблицаСтрок.Оператор = ОператорЧастьЗаголовока();
				ИначеЕсли ТаблицаСтрок[НомерСтроки-1-1].Оператор = ОператорЧастьЗаголовока() Тогда
					СтрТаблицаСтрок.Оператор = ОператорЧастьЗаголовока();
				КонецЕсли;	 
			КонецЕсли;	 
		КонецЕсли;	 
		
		Если (СтрТаблицаСтрок.Оператор = КешОператорКонтекст) или (СтрТаблицаСтрок.Оператор = КешОператорСценарий) Тогда
			ИдРодительскойГруппы = НомерСтроки-1;
			Продолжить;
		КонецЕсли;	 
		
		Если НомерСтроки = 1 Тогда
			ПредыдущийОтступ = СтрТаблицаСтрок.Отступ;
		КонецЕсли;	 
		
		Если Лев(СтрТаблицаСтрок.СтрокаСокр,1) = "#" И СтрТаблицаСтрок.Оператор = КешОператорКомментарий Тогда
			СтрТаблицаСтрок.Отступ = ПредыдущийОтступ;
			Продолжить;
		ИначеЕсли Лев(СтрТаблицаСтрок.СтрокаСокр,1) = "@" Тогда
			СтрТаблицаСтрок.Отступ = ПредыдущийОтступ;
			Продолжить;
		КонецЕсли;	 
		
		ТекОтступ = СтрТаблицаСтрок.Отступ;
		
		Если (Лев(СтрТаблицаСтрок.СтрокаСокр,1) = "*")
			И (СтрТаблицаСтрок.Оператор <> ОператорБлочныйКомменатрийОкончание())
			И (СтрТаблицаСтрок.Оператор <> ОператорБлочныйКомментарий())
			Тогда
			СтрТаблицаСтрок.ГруппаШаговНачалась = Истина;
			ИдРодительскойГруппы(ТаблицаСтрок,СтрТаблицаСтрок,НомерСтроки-1-1,ТекОтступ);
			ОтступГруппы = ТекОтступ;
			ИдРодительскойГруппы = НомерСтроки-1;
		ИначеЕсли ОтступГруппы >= ТекОтступ И НЕ НайденоНачалоБлочногоКомментария
			И (СтрТаблицаСтрок.Оператор <> ОператорБлочныйКомменатрийОкончание())
			И (СтрТаблицаСтрок.Оператор <> ОператорКомментарий()) Тогда
			
			ИдРодительскойГруппы(ТаблицаСтрок,СтрТаблицаСтрок,НомерСтроки-1-1,ТекОтступ);
			ИдРодительскойГруппы = СтрТаблицаСтрок.ИдРодительскойГруппы;
			СтрТаблицаСтрок.ГруппаШаговЗакончилась = Истина;
		ИначеЕсли НайденоНачалоБлочногоКомментария  Тогда
			СтрТаблицаСтрок.ИдРодительскойГруппы = -1;
		КонецЕсли;	 
		
		ПредыдущийОтступ = СтрТаблицаСтрок.Отступ;
	КонецЦикла;	
	
	ОтформатироватьТаблицы(ТаблицаСтрок);
	
	Возврат ТаблицаСтрок;
КонецФункции	

Процедура СнипетыШагов(ТаблицаСтрок,МассивОшибок,КешПараметровСтроки)
	МассивПервыхСловНРег     = МассивПервыхСловGherkinНРег();
	ТаблицаКешСнипетов       = Новый ТаблицаЗначений;
	ТаблицаКешСнипетов.Колонки.Добавить("Строка");
	ТаблицаКешСнипетов.Колонки.Добавить("Снипет");
	
	ТаблицаКешСнипетов.Индексы.Добавить("Строка");
	
	Для Каждого СтрТаблицаСтрок Из ТаблицаСтрок Цикл
		Если ЗначениеЗаполнено(СтрТаблицаСтрок.Оператор) Тогда
			Продолжить;
		КонецЕсли;	 
		
		Стр = СтрТаблицаСтрок.СтрокаСокр;
		
		Если Стр = "" Тогда
			Продолжить;
		КонецЕсли;	 
		
		Если Лев(Стр,1) = "*" Тогда
			Продолжить;
		КонецЕсли;	 
		
		Если Лев(Стр,1) = "|" Тогда
			Продолжить;
		КонецЕсли;	 
		
		СтрокаТаблицаКешСнипетов = ТаблицаКешСнипетов.Найти(Стр,"Строка");
		Если СтрокаТаблицаКешСнипетов <> Неопределено Тогда
			СтрТаблицаСтрок.Снипет     = СтрокаТаблицаКешСнипетов.Снипет;
			СтрТаблицаСтрок.СнипетНРег = НРег(СтрТаблицаСтрок.Снипет);
			Продолжить;
		КонецЕсли;	 
		
		Если СнипетПоСтроке(Стр,МассивПервыхСловНРег,КешПараметровСтроки) Тогда
			СтрТаблицаСтрок.Снипет     = Стр;
			СтрТаблицаСтрок.СнипетНРег = НРег(Стр);
			
			СтрокаТаблицаКешСнипетов        = ТаблицаКешСнипетов.Добавить();
			СтрокаТаблицаКешСнипетов.Строка = СтрТаблицаСтрок.СтрокаСокр;
			СтрокаТаблицаКешСнипетов.Снипет = СтрТаблицаСтрок.Снипет;
		Иначе
			КопияСтроки = Стр;
			ПараметрыGherkinИзСтрокиИПреобразоватьСтрокуДляРаботыСПараметрами(КопияСтроки);
			
			Если Найти(КопияСтроки,"=") > 0 Тогда
				//значит это передача параметров во вложенный сценарий
				СтрТаблицаСтрок.Оператор = ОператорПередачаПараметров();
				Продолжить;
			КонецЕсли;	 
			
			СтруктураОшибки = Новый Структура;
			СтруктураОшибки.Вставить("НомерСтрокиТекстаСценария",СтрТаблицаСтрок.НомерСтрокиТекстаСценария);
			СтруктураОшибки.Вставить("СтрокаОригинал",СтрТаблицаСтрок.СтрокаОригинал);
			
			ТекстОшибки = СтрШаблон(НСтр(
			   "ru = 'Строка №%1. Не найдено ключевое слово в начале строки. <%2>'"),
			   СтрТаблицаСтрок.НомерСтрокиТекстаСценария,
			   СтрТаблицаСтрок.СтрокаОригинал);
																							  
			СтруктураОшибки.Вставить("ТекстОшибки",ТекстОшибки);
			
			МассивОшибок.Добавить(СтруктураОшибки);
		КонецЕсли;	 
	КонецЦикла;	
КонецПроцедуры

Функция ТаблицаСтрокВДеревоСхемы(ТаблицаСтрок,ДеревоСхемы,ТаблицаШаблоновСценариев,Сценарий,
	    СтруктураПараметров,КешПараметровСтроки)
	ТекРодитель = ДеревоСхемы;
	
	ТекЭлемент                      = ТекРодитель.Строки.Добавить();
	ТекЭлемент.ОписаниеЭлемента     = ОписаниеЭлементаСтарт();
	ТекЭлемент.ТипЭлемента          = ТипЭлементаНачалоСхемы();
	ТекЭлемент.ИдСтрокиТаблицаСтрок = -10;
	
	МассивУсловий = Новый Массив;
	
	Комментарии   = Новый Массив;
	Теги          = Новый Массив;
	ИдетЗаполнениеЗаголовка = Ложь;
	
	ИдСтрокиТаблицаСтрок = -1;
	
	Если СтруктураПараметров.ЧтениеИзТекста  Тогда
		ДанныеСценариев = СтруктураПараметров.СтруктураПараметров.ДанныеСценариев;
		СтрокаДанныеСценариев = ДанныеСценариев.Найти(СтруктураПараметров.СтруктураПараметров.UIDСценария,"UID");
		ИзменяемыеПараметрыСценария = СтрокаДанныеСценариев.ПараметрыСценария.ВыгрузитьКолонку("Имя");
	ИначеЕсли Сценарий = Неопределено Тогда
		ИзменяемыеПараметрыСценария = Новый Массив;
	Иначе	
		ИзменяемыеПараметрыСценария = Сценарий.ПараметрыВходящие.ВыгрузитьКолонку("Имя");
	КонецЕсли;	 
	
	
	ПараметрыОбработки = Новый Структура;
	ПараметрыОбработки.Вставить("ТаблицаСтрок",ТаблицаСтрок);
	ПараметрыОбработки.Вставить("Комментарии",Комментарии);
	ПараметрыОбработки.Вставить("Теги",Теги);
	ПараметрыОбработки.Вставить("МассивУсловий",МассивУсловий);
	
	ТекущаяСтрокаВложенногоСценария = Неопределено;
	ОбрабатываласьЯвнаяПередачаПараметров = Ложь;
	ПередчаПараметраВПодсценарийТаблица = Ложь;
	СтруктураПараметраПередчаПараметраВПодсценарийТаблица = Неопределено;
	
	ОператорНачалоУсловияПрепроцессор = Новый Массив;
	ОператорОкончанияУсловияПрепроцессор = Новый Массив;
	ОператорНачалоУсловия = Новый Массив;
	ОператорОкончанияУсловия = Новый Массив;
	ОператорНачалоЦикла = Новый Массив;
	ОператорОкончанияЦикла = Новый Массив;
	
	
	Для Каждого СтрТаблицаСтрок Из ТаблицаСтрок Цикл
		ИдСтрокиТаблицаСтрок = ИдСтрокиТаблицаСтрок + 1;
		
		Если СтрТаблицаСтрок.СтрокаСокр = "" И НЕ ЗначениеЗаполнено(СтрТаблицаСтрок.Оператор) Тогда
			ТекЭлемент                      = НовыйЭлементДерева(ТекРодитель,ДеревоСхемы,ИдСтрокиТаблицаСтрок,
																		СтрТаблицаСтрок,ПараметрыОбработки,Истина);
			ТекЭлемент.ОписаниеЭлемента     = СтрТаблицаСтрок.СтрокаСокр;
			ТекЭлемент.ТипЭлемента          = ТипЭлементаПустаяСтрока();
		ИначеЕсли СтрТаблицаСтрок.НачатьГруппыСначала Тогда
			ТекРодитель = ДеревоСхемы;
		ИначеЕсли СтрТаблицаСтрок.ГруппаШаговНачалась и (СокрЛП(СтрТаблицаСтрок.Оператор) = "") Тогда
			ТекРодитель = РодительПоИдИдРодительскойГруппы(ТекРодитель,ДеревоСхемы,СтрТаблицаСтрок,МассивУсловий);
			
			ТекЭлемент                      = НовыйЭлементДерева(ТекРодитель,ДеревоСхемы,ИдСтрокиТаблицаСтрок,
																		СтрТаблицаСтрок,ПараметрыОбработки,Истина);
			
			ТекЭлемент.ОписаниеЭлемента     = СтрТаблицаСтрок.СтрокаСокр;
			ТекЭлемент.ТипЭлемента          = ТипЭлементаГруппаШагов();
			
			ТекРодитель                     = ТекЭлемент; //создаём ещё один уровень дерева
			
		ИначеЕсли СтрТаблицаСтрок.Оператор = ОператорКомментарий() Тогда
			ТекЭлемент                      = НовыйЭлементДерева(ТекРодитель,ДеревоСхемы,ИдСтрокиТаблицаСтрок,
																		СтрТаблицаСтрок,ПараметрыОбработки);
			
			ТекЭлемент.ОписаниеЭлемента     = СтрТаблицаСтрок.СтрокаОригинал;
			ТекЭлемент.ТипЭлемента          = ТипЭлементаКомментарий();
			
		ИначеЕсли СтрТаблицаСтрок.Оператор = ОператорТаблица() Тогда
			Если ПередчаПараметраВПодсценарийТаблица Тогда
				ЗначениеПараметра = СтруктураПараметраПередчаПараметраВПодсценарийТаблица.ЗначениеПараметра;
				ЗначениеПараметра.Добавить(СтрТаблицаСтрок.СтрокаСокр);
				
				ЗаполнитьПараметрыДляДанногоЭлемента(СтрТаблицаСтрок.СтрокаСокр,ТекЭлемент,ИзменяемыеПараметрыСценария,Ложь,
			                                     КешПараметровСтроки);
				ДанныеСтроки = Новый Структура;
				ДанныеСтроки.Вставить("ОбработаннаяСтрокаПараметров",ТекЭлемент.ОбработаннаяСтрокаПараметров);
				ДанныеСтроки.Вставить("Параметры",ТекЭлемент.Параметры);
				СтруктураПараметраПередчаПараметраВПодсценарийТаблица.ЗначениеПараметраПодстановкаЗначений.Добавить(ДанныеСтроки);
				
			Иначе	
				ТекЭлемент                      = НовыйЭлементДерева(ТекРодитель,ДеревоСхемы,ИдСтрокиТаблицаСтрок,
				СтрТаблицаСтрок,ПараметрыОбработки);
				
				ТекЭлемент.ОписаниеЭлемента     = СтрТаблицаСтрок.СтрокаСокр;
				ТекЭлемент.ТипЭлемента          = ТипЭлементаТаблица();
			
				ЗаполнитьПараметрыДляДанногоЭлемента(СтрТаблицаСтрок.СтрокаСокр,ТекЭлемент,ИзменяемыеПараметрыСценария,Ложь,
			                                     КешПараметровСтроки);
			КонецЕсли;	 
												 
		ИначеЕсли СтрТаблицаСтрок.Оператор = ОператорЗаголовок() Тогда
			ИдетЗаполнениеЗаголовка = Истина;
			ТекРодитель = ДеревоСхемы;
			
			ТекЭлемент                      = НовыйЭлементДерева(ТекРодитель,ДеревоСхемы,ИдСтрокиТаблицаСтрок,
																		СтрТаблицаСтрок,ПараметрыОбработки);
			
			ТекЭлемент.ОписаниеЭлемента     = СтрТаблицаСтрок.СтрокаСокр;
			ТекЭлемент.ТипЭлемента          = ТипЭлементаЗаголовок();
			
			ТекРодитель                     = ТекЭлемент; //создаём ещё один уровень дерева
			
		ИначеЕсли СтрТаблицаСтрок.Оператор = ОператорКонтекст() Тогда
			ТекРодитель = ДеревоСхемы;
			ТекЭлемент                      = НовыйЭлементДерева(ТекРодитель,ДеревоСхемы,ИдСтрокиТаблицаСтрок,
																		СтрТаблицаСтрок,ПараметрыОбработки);
			
			ТекЭлемент.ОписаниеЭлемента     = СтрТаблицаСтрок.СтрокаСокр;
			ТекЭлемент.ТипЭлемента          = ТипЭлементаКонтекст();
			
			ТекРодитель                     = ТекЭлемент; //создаём ещё один уровень дерева
			
			ИдетЗаполнениеЗаголовка = Ложь;
		ИначеЕсли СтрТаблицаСтрок.Оператор = ОператорСценарий() Тогда
			МассивУсловий.Очистить();
			
			ТекРодитель = ДеревоСхемы;
			ТекЭлемент                      = НовыйЭлементДерева(ТекРодитель,ДеревоСхемы,ИдСтрокиТаблицаСтрок,
																		СтрТаблицаСтрок,ПараметрыОбработки);
			
			ТекЭлемент.ОписаниеЭлемента     = СтрТаблицаСтрок.СтрокаСокр;
			ТекЭлемент.ТипЭлемента          = ТипЭлементаСценарий();
			
			ТекРодитель                     = ТекЭлемент; //создаём ещё один уровень дерева
			
			ИдетЗаполнениеЗаголовка = Ложь;
		ИначеЕсли СтрТаблицаСтрок.Оператор = ОператорУсловиеПрепроцессор() 
			     или  СтрТаблицаСтрок.Оператор = ОператорУсловие() 
			     или  СтрТаблицаСтрок.Оператор = ОператорУсловиеEng() 
			     или  СтрТаблицаСтрок.Оператор = ОператорЦикл() 
			     или  СтрТаблицаСтрок.Оператор = ОператорЦиклEng() 
				 Тогда
			Если МассивУсловий.Количество() = 0 Тогда
				ТекЭлемент                      = НовыйЭлементДерева(ТекРодитель,ДеревоСхемы,ИдСтрокиТаблицаСтрок,
																			СтрТаблицаСтрок,ПараметрыОбработки);
			Иначе
				ТекУсловие                      = МассивУсловий[МассивУсловий.Количество()-1];
				НижняяСекцияУсловия             = ТекУсловие.Строки[ТекУсловие.Строки.Количество()-1];
				ТекущийРодительЭлемента         = РодительЭлемента(ТекЭлемент,ДеревоСхемы,СтрТаблицаСтрок,ТаблицаСтрок,МассивУсловий);
				
				Если ПервыйЭлементДереваПодчиненВторому(ТекущийРодительЭлемента,НижняяСекцияУсловия) Тогда
					ТекЭлемент                  = НовыйЭлементДерева(ТекущийРодительЭлемента,ДеревоСхемы,ИдСтрокиТаблицаСтрок,
																			СтрТаблицаСтрок,ПараметрыОбработки,Истина);
				Иначе	
					ТекЭлемент                  = НовыйЭлементДерева(НижняяСекцияУсловия,ДеревоСхемы,ИдСтрокиТаблицаСтрок,
																			СтрТаблицаСтрок,ПараметрыОбработки,Истина);
				КонецЕсли;	 
			КонецЕсли;	 
			
			Если СтрТаблицаСтрок.Оператор = ОператорУсловиеПрепроцессор() Тогда
				ТекЭлемент.ТипЭлемента          = ТипЭлементаУсловиеПрепроцессор();
				ТекЭлемент.ОписаниеЭлемента     = ОписаниеЭлементаУсловиеПрепроцессор();
				ОператорНачалоУсловияПрепроцессор.Добавить(ТекЭлемент);
			ИначеЕсли СтрТаблицаСтрок.Оператор = ОператорУсловие() Тогда
				ТекЭлемент.ТипЭлемента          = ТипЭлементаУсловие();
				ТекЭлемент.ОписаниеЭлемента     = ОписаниеЭлементаУсловие();
				ОператорНачалоУсловия.Добавить(ТекЭлемент);
			ИначеЕсли СтрТаблицаСтрок.Оператор = ОператорУсловиеEng() Тогда
				ТекЭлемент.ТипЭлемента          = ТипЭлементаУсловиеEng();
				ТекЭлемент.ОписаниеЭлемента     = ОписаниеЭлементаУсловиеEng();
				ОператорНачалоУсловия.Добавить(ТекЭлемент);
			ИначеЕсли СтрТаблицаСтрок.Оператор = ОператорЦикл() Тогда
				ТекЭлемент.ТипЭлемента          = ТипЭлементаЦикл();
				ТекЭлемент.ОписаниеЭлемента     = ОписаниеЭлементаЦикл();
				ОператорНачалоЦикла.Добавить(ТекЭлемент);
			ИначеЕсли СтрТаблицаСтрок.Оператор = ОператорЦиклEng() Тогда
				ТекЭлемент.ТипЭлемента          = ТипЭлементаЦиклEng();
				ТекЭлемент.ОписаниеЭлемента     = ОписаниеЭлементаЦиклEng();
				ОператорНачалоЦикла.Добавить(ТекЭлемент);
			КонецЕсли;	 
			
			ТекРодитель = ТекЭлемент; //создаём ещё один уровень дерева
			
			МассивУсловий.Добавить(ТекЭлемент);
			
			//добавляем специальный элемент, чтобы дерево выглядело логично и ветки условия были на одном уровне
			ТекЭлемент                      = ТекРодитель.Строки.Добавить(); 
			ТекЭлемент.ОписаниеЭлемента     = СтрТаблицаСтрок.СтрокаСокр;
			
			ТекЭлемент.НомерСтрокиТекстаСценария = СтрТаблицаСтрок.НомерСтрокиТекстаСценария;
			
			Если СтрТаблицаСтрок.Оператор = ОператорУсловиеПрепроцессор() Тогда
				ТекЭлемент.ТипЭлемента         = ТипЭлементаНачалоУсловияПрепроцессор();
				ТекЭлемент.ЗначениеУсловия      = СтрТаблицаСтрок.ПараметрыОператора.ЗначениеУсловия;
			ИначеЕсли СтрТаблицаСтрок.Оператор = ОператорУсловие() Тогда
				ТекЭлемент.ТипЭлемента         = ТипЭлементаНачалоУсловия();
				ТекЭлемент.ЗначениеУсловия      = СтрТаблицаСтрок.ПараметрыОператора.ЗначениеУсловия;
			ИначеЕсли СтрТаблицаСтрок.Оператор = ОператорУсловиеEng() Тогда
				ТекЭлемент.ТипЭлемента         = ТипЭлементаНачалоУсловияEng();
				ТекЭлемент.ЗначениеУсловия      = СтрТаблицаСтрок.ПараметрыОператора.ЗначениеУсловия;
			ИначеЕсли СтрТаблицаСтрок.Оператор = ОператорЦикл() Тогда
				ТекЭлемент.ТипЭлемента         = ТипЭлементаНачалоЦикла();
				ТекЭлемент.ЗначениеУсловия      = СтрТаблицаСтрок.ПараметрыОператора.Шаг;
			ИначеЕсли СтрТаблицаСтрок.Оператор = ОператорЦиклEng() Тогда
				ТекЭлемент.ТипЭлемента         = ТипЭлементаНачалоЦиклаEng();
				ТекЭлемент.ЗначениеУсловия      = СтрТаблицаСтрок.ПараметрыОператора.Шаг;
			КонецЕсли;	 
			
			ТекЭлемент.ИдСтрокиТаблицаСтрок = -1;
			ТекРодитель                     = ТекЭлемент; //создаём ещё один уровень дерева
			
			ЗаполнитьПараметрыДляДанногоЭлемента(ТекЭлемент.ЗначениеУсловия,ТекЭлемент,ИзменяемыеПараметрыСценария,Истина,
			                            КешПараметровСтроки);
			
			
		ИначеЕсли СтрТаблицаСтрок.Оператор = ОператорИначеЕслиПрепроцессор() Тогда
			Если МассивУсловий.Количество() = 0 Тогда
				ВызватьИсключение СтрШаблон(НСтр("ru = 'Неверно используется ИначеЕсли: %1'"),
																						  СтрТаблицаСтрок.СтрокаСокр);
			КонецЕсли;	 
			ТекРодитель                     = МассивУсловий[МассивУсловий.Количество()-1];
			
			ТекЭлемент                      = ТекРодитель.Строки.Добавить(); //считается, что перед этим ранее была строка Если...Тогда
			ТекЭлемент.ОписаниеЭлемента     = СтрТаблицаСтрок.СтрокаСокр;
			ТекЭлемент.ТипЭлемента          = ТипЭлементаИначеЕслиПрепроцессор();
			ТекЭлемент.ЗначениеУсловия      = СтрТаблицаСтрок.ПараметрыОператора.ЗначениеУсловия;
			ТекЭлемент.ИдСтрокиТаблицаСтрок = -1;
			ТекЭлемент.НомерСтрокиТекстаСценария = СтрТаблицаСтрок.НомерСтрокиТекстаСценария;
			
			ЗаполнитьПараметрыДляДанногоЭлемента(ТекЭлемент.ЗначениеУсловия,ТекЭлемент,ИзменяемыеПараметрыСценария,Истина,
			                             КешПараметровСтроки);
			
			ТекРодитель                     = ТекЭлемент; //создаём ещё один уровень дерева
		ИначеЕсли СтрТаблицаСтрок.Оператор = ОператорИначеПрепроцессор() Тогда
			Если МассивУсловий.Количество() = 0 Тогда
				ВызватьИсключение СтрШаблон(НСтр("ru = 'Неверно используется Иначе: %1'"),
																						  СтрТаблицаСтрок.СтрокаСокр);
			КонецЕсли;	 
			ТекРодитель                     = МассивУсловий[МассивУсловий.Количество()-1];
			
			ТекЭлемент                      = ТекРодитель.Строки.Добавить(); //считается, что перед этим ранее была строка Если...Тогда
			ТекЭлемент.ОписаниеЭлемента     = ОписаниеЭлементаИначеПрепроцессор();
			ТекЭлемент.ТипЭлемента          = ТипЭлементаИначеПрепроцессор();
			ТекЭлемент.ИдСтрокиТаблицаСтрок = -1;
			ТекЭлемент.НомерСтрокиТекстаСценария = СтрТаблицаСтрок.НомерСтрокиТекстаСценария;
			
			ТекРодитель                     = ТекЭлемент; //создаём ещё один уровень дерева
			
		ИначеЕсли СтрТаблицаСтрок.Оператор = ОператорИначеЕсли() или СтрТаблицаСтрок.Оператор = ОператорИначеЕслиEng() Тогда
			Если МассивУсловий.Количество() = 0 Тогда
				ВызватьИсключение СтрШаблон(НСтр("ru = 'Неверно используется ИначеЕсли: %1'"),
																						  СтрТаблицаСтрок.СтрокаСокр);
			КонецЕсли;	 
			ТекРодитель                     = МассивУсловий[МассивУсловий.Количество()-1];
			
			ТекЭлемент                      = ТекРодитель.Строки.Добавить(); //считается, что перед этим ранее была строка Если...Тогда
			ТекЭлемент.ОписаниеЭлемента     = СтрТаблицаСтрок.СтрокаСокр;
			Если СтрТаблицаСтрок.Оператор = ОператорИначеЕсли() Тогда
				ТекЭлемент.ТипЭлемента          = ТипЭлементаИначеЕсли();
			Иначе	
				ТекЭлемент.ТипЭлемента          = ТипЭлементаИначеЕслиEng();
			КонецЕсли;	 
			ТекЭлемент.ЗначениеУсловия      = СтрТаблицаСтрок.ПараметрыОператора.ЗначениеУсловия;
			ТекЭлемент.ИдСтрокиТаблицаСтрок = -1;
			ТекЭлемент.НомерСтрокиТекстаСценария = СтрТаблицаСтрок.НомерСтрокиТекстаСценария;
			
			ЗаполнитьПараметрыДляДанногоЭлемента(ТекЭлемент.ЗначениеУсловия,ТекЭлемент,ИзменяемыеПараметрыСценария,Истина,
			                             КешПараметровСтроки);
			
			ТекРодитель                     = ТекЭлемент; //создаём ещё один уровень дерева
		ИначеЕсли СтрТаблицаСтрок.Оператор = ОператорИначе() или СтрТаблицаСтрок.Оператор = ОператорИначеEng() Тогда
			Если МассивУсловий.Количество() = 0 Тогда
				ВызватьИсключение СтрШаблон(НСтр("ru = 'Неверно используется Иначе: %1'"),
																						  СтрТаблицаСтрок.СтрокаСокр);
			КонецЕсли;	 
			ТекРодитель                     = МассивУсловий[МассивУсловий.Количество()-1];
			
			ТекЭлемент                      = ТекРодитель.Строки.Добавить(); //считается, что перед этим ранее была строка Если...Тогда
			Если СтрТаблицаСтрок.Оператор = ОператорИначе() Тогда
				ТекЭлемент.ОписаниеЭлемента     = ОписаниеЭлементаИначе();
				ТекЭлемент.ТипЭлемента          = ТипЭлементаИначе();
			Иначе	
				ТекЭлемент.ОписаниеЭлемента     = ОписаниеЭлементаИначеEng();
				ТекЭлемент.ТипЭлемента          = ТипЭлементаИначеEng();
			КонецЕсли;	 
			ТекЭлемент.ИдСтрокиТаблицаСтрок = -1;
			ТекЭлемент.НомерСтрокиТекстаСценария = СтрТаблицаСтрок.НомерСтрокиТекстаСценария;
			
			ТекРодитель                     = ТекЭлемент; //создаём ещё один уровень дерева
			
		ИначеЕсли СтрТаблицаСтрок.Оператор = ОператорКонецЕслиПрепроцессор()
			      или СтрТаблицаСтрок.Оператор = ОператорКонецЕсли()
			      или СтрТаблицаСтрок.Оператор = ОператорКонецЕслиEng()
			      или СтрТаблицаСтрок.Оператор = ОператорКонецЦикла()
			      или СтрТаблицаСтрок.Оператор = ОператорКонецЦиклаEng()
				  Тогда
			Если МассивУсловий.Количество() = 0 Тогда
				ВызватьИсключение СтрШаблон(НСтр
				("ru = 'Ошибка в шаге: %1. Строка №%2. Не найдено условие.'"),
				 СтрТаблицаСтрок.СтрокаСокр,СтрТаблицаСтрок.НомерСтрокиТекстаСценария);
			КонецЕсли;	
			
			ПередчаПараметраВПодсценарийТаблица = Ложь;
			
			ТекРодитель                     = МассивУсловий[МассивУсловий.Количество()-1];
			
			ТекЭлемент                      = ТекРодитель.Строки.Добавить(); //считается, что перед этим ранее были операторные скобки
			Если СтрТаблицаСтрок.Оператор = ОператорКонецЕслиПрепроцессор() Тогда
				ТекЭлемент.ТипЭлемента          = ТипЭлементаКонецЕслиПрепроцессор();
				ТекЭлемент.ОписаниеЭлемента     = ОписаниеЭлементаКонецЕслиПрепроцессор();
				ОператорОкончанияУсловияПрепроцессор.Добавить(ТекЭлемент);
			ИначеЕсли СтрТаблицаСтрок.Оператор = ОператорКонецЕсли() Тогда
				ТекЭлемент.ТипЭлемента          = ТипЭлементаКонецЕсли();
				ТекЭлемент.ОписаниеЭлемента     = ОписаниеЭлементаКонецЕсли();
				ОператорОкончанияУсловия.Добавить(ТекЭлемент);
			ИначеЕсли СтрТаблицаСтрок.Оператор = ОператорКонецЕслиEng() Тогда
				ТекЭлемент.ТипЭлемента          = ТипЭлементаКонецЕслиEng();
				ТекЭлемент.ОписаниеЭлемента     = ОписаниеЭлементаКонецЕслиEng();
				ОператорОкончанияУсловия.Добавить(ТекЭлемент);
			ИначеЕсли СтрТаблицаСтрок.Оператор = ОператорКонецЦикла() Тогда
				ТекЭлемент.ТипЭлемента          = ТипЭлементаКонецЦикла();
				ТекЭлемент.ОписаниеЭлемента     = ОписаниеЭлементаКонецЦикла();
				ОператорОкончанияЦикла.Добавить(ТекЭлемент);
			ИначеЕсли СтрТаблицаСтрок.Оператор = ОператорКонецЦиклаEng() Тогда
				ТекЭлемент.ТипЭлемента          = ТипЭлементаКонецЦиклаEng();
				ТекЭлемент.ОписаниеЭлемента     = ОписаниеЭлементаКонецЦиклаEng();
				ОператорОкончанияЦикла.Добавить(ТекЭлемент);
			КонецЕсли;	 
			
			ТекЭлемент.ИдСтрокиТаблицаСтрок = -1;
			
			МассивУсловий.Удалить(МассивУсловий.Количество()-1); //удаляем условие из списка
			
			СтрокаУсловия = ТаблицаСтрок[ТекРодитель.ИдСтрокиТаблицаСтрок];
			ТекРодитель   = РодительЭлемента(ТекРодитель,ДеревоСхемы,СтрокаУсловия,ТаблицаСтрок,МассивУсловий); //возвращаемся на один уровень назад
			
			Если МассивУсловий.Количество() > 0 Тогда
				ТекУсловие  = МассивУсловий[МассивУсловий.Количество()-1];
				Если ТекУсловие.НомерСтрокиТекстаСценария >= ТекРодитель.НомерСтрокиТекстаСценария Тогда
					Если ТекУсловие.Строки.Количество() > 0 Тогда
						ТекРодитель = ТекУсловие.Строки[ТекУсловие.Строки.Количество()-1];
					Иначе	
						ТекРодитель = ТекУсловие;
					КонецЕсли;	 
				КонецЕсли;	 
			КонецЕсли;	 
			
		ИначеЕсли СтрТаблицаСтрок.Оператор = ОператорБлочныйКомментарий() Тогда
			ТекЭлемент                      = НовыйЭлементДерева(ТекРодитель,ДеревоСхемы,ИдСтрокиТаблицаСтрок,
																		СтрТаблицаСтрок,ПараметрыОбработки);
			
			ТекЭлемент.ОписаниеЭлемента     = СтрТаблицаСтрок.СтрокаОригинал;
			ТекЭлемент.ТипЭлемента          = ТипЭлементаБлочныйКомментарий();
			
		ИначеЕсли СтрТаблицаСтрок.Оператор = ОператорБлочныйКомменатрийНачало() Тогда
			ТекЭлемент                      = НовыйЭлементДерева(ТекРодитель,ДеревоСхемы,ИдСтрокиТаблицаСтрок,
																		СтрТаблицаСтрок,ПараметрыОбработки);
			
			ТекЭлемент.ОписаниеЭлемента     = СтрТаблицаСтрок.СтрокаСокр;
			ТекЭлемент.ТипЭлемента          = ТипЭлементаБлочныйКомментарийНачало();
			
		ИначеЕсли СтрТаблицаСтрок.Оператор = ОператорБлочныйКомменатрийОкончание() Тогда
			ТекЭлемент                      = НовыйЭлементДерева(ТекРодитель,ДеревоСхемы,ИдСтрокиТаблицаСтрок,
																		СтрТаблицаСтрок,ПараметрыОбработки);
			
			ТекЭлемент.ОписаниеЭлемента     = СтрТаблицаСтрок.СтрокаСокр;
			ТекЭлемент.ТипЭлемента          = ТипЭлементаБлочныйКомментарийОкончание();
			
		ИначеЕсли СтрТаблицаСтрок.Оператор = ОператорПараметрТаблица() Тогда
			ИмяПараметра = Сред(СтрТаблицаСтрок.СтрокаСокр,2);
			ИмяПараметра = СокрЛП(Лев(ИмяПараметра,СтрДлина(ИмяПараметра)-1));
			Если ТекЭлемент <> Неопределено Тогда
				
				Если ТекЭлемент.ТипЭлемента = ТипЭлементаШаблонСценария() Тогда
					ВызватьИсключение СтрШаблон(НСтр("ru = 'Неверно происходит передача таблицы в подцесценарий : %1.'"),
					    ТекЭлемент.ОписаниеЭлемента);
				КонецЕсли;	 
				
				Если ТекЭлемент.ДополнительныеСвойства = Неопределено Тогда
					ТекЭлемент.ДополнительныеСвойства = Новый Структура;
				КонецЕсли;	 
				
				Если НЕ ТекЭлемент.ДополнительныеСвойства.Свойство("ПараметрыСТипомТаблицы") Тогда
					ТекЭлемент.ДополнительныеСвойства.Вставить("ПараметрыСТипомТаблицы",Новый Массив);
				КонецЕсли;	 
				
				ТекЭлемент.ДополнительныеСвойства.ПараметрыСТипомТаблицы.Добавить(ИмяПараметра);
			КонецЕсли;	 
			
		ИначеЕсли СтрТаблицаСтрок.Оператор = ОператорПередачаПараметров() Тогда
			ПередчаПараметраВПодсценарийТаблица = Ложь;
			                                                       
			Если ТекущаяСтрокаВложенногоСценария = Неопределено Тогда
				ВызватьИсключение СтрШаблон(НСтр("ru = 'Сценарий <%1>.
				|Строка №%2
				|Неверно происходит передача параметра : %3.
				|Не найден вложенный сценарий, в который происходит передача параметра.'"),
				  Сценарий, СтрТаблицаСтрок.НомерСтрокиТекстаСценария,
				  СтрТаблицаСтрок.СтрокаСокр);
			КонецЕсли;	 
			
			СтрокаПараметра = СтрТаблицаСтрок.СтрокаСокр;
			
			Поз = Найти(СтрокаПараметра,"=");
			Если Поз = 0 Тогда
				ВызватьИсключение СтрШаблон(
			       НСтр("ru = 'Неверно происходит передача параметра : %1'"),СтрТаблицаСтрок.СтрокаСокр);
			КонецЕсли;	 
			
			ИмяПараметра            = СокрЛП(Лев(СтрокаПараметра,Поз-1));
			СтрокаЗначениеПараметра = СокрЛП(Сред(СтрокаПараметра,Поз+1));
			Если СтрокаЗначениеПараметра = "" Тогда
				//Значит идёт передача таблицы. Таблица идёт в следующих строках.
				СтруктураПараметра = СтруктураПараметра();
				СтруктураПараметра.Вставить("ЗначениеПараметра",Новый Массив);
				СтруктураПараметра.Вставить("ЗначениеПараметраПодстановкаЗначений",Новый Массив);
				СтруктураПараметра.Вставить("Символ",Неопределено);
				СтруктураПараметра.Вставить("Тип",ТипПараметра("Таблица"));
				СтруктураПараметра.Вставить("Вид","");
				СтруктураПараметра.Вставить("ПереданСнизу",Истина);
				СтруктураПараметра.Вставить("ИмяПараметра",ИмяПараметра);
				СтруктураПараметра.Вставить("ИзменяемыйПараметр",Ложь);
				СтруктураПараметра.Вставить("НомерПараметра",ТекущаяСтрокаВложенногоСценария.Параметры.Количество()+1);
				СтруктураПараметра.Вставить("НомерПараметраВСценарии",-1);
				ТекущаяСтрокаВложенногоСценария.Параметры.Добавить(СтруктураПараметра);
				
				ПередчаПараметраВПодсценарийТаблица = Истина;
				СтруктураПараметраПередчаПараметраВПодсценарийТаблица = СтруктураПараметра;
				
				ТекЭлемент                      = ТекущаяСтрокаВложенногоСценария.Строки.Добавить();
				ТекЭлемент.ОписаниеЭлемента     = СтрТаблицаСтрок.СтрокаСокр;
				ТекЭлемент.ТипЭлемента          = ТипЭлементаПередачаПараметров();
				
				ПараметрыДаннойСтроки = Новый Массив;
				ПараметрыДаннойСтроки.Добавить(СтруктураПараметра);
				ТекЭлемент.Параметры = ПараметрыДаннойСтроки;
				ТекЭлемент.ОбработаннаяСтрокаПараметров = ИмяПараметра + " = " + СтрокаЗначениеПараметра;
				Если ТекЭлемент.ДополнительныеСвойства = Неопределено Тогда
					ТекЭлемент.ДополнительныеСвойства = Новый Структура;
				КонецЕсли;	 
				
				ТекЭлемент.ДополнительныеСвойства.Вставить("ПередачаТаблицы",Истина);
				ТекЭлемент.ДополнительныеСвойства.Вставить("ИмяПараметра",ИмяПараметра);
				
				Продолжить;
			КонецЕсли;
			
			
			ПараметрыПодсценария = ТекущаяСтрокаВложенногоСценария.ДополнительныеСвойства.ПараметрыВходящие;
			Если ПараметрыПодсценария.Найти(НРег(ИмяПараметра)) = Неопределено Тогда
				ВызватьИсключение СтрШаблон(НСтр("ru = 'В подсценарии %1 нет параметра с именем ""%2"".'"),
				      ТекущаяСтрокаВложенногоСценария.ОписаниеЭлемента,ИмяПараметра);
			КонецЕсли;	 
			
			ПараметрыВСтроке = ПараметрыGherkinИзСтрокиИПреобразоватьСтрокуДляРаботыСПараметрами(СтрокаЗначениеПараметра);
			
			Если ПараметрыВСтроке.Количество() <> 1 Тогда
				ВызватьИсключение СтрШаблон(НСтр("ru = 'В строке %1 ожидался 1 параметр, а найдено %2.'"),
				      СтрТаблицаСтрок.СтрокаСокр,ПараметрыВСтроке.Количество());
			КонецЕсли;	 
			
			ДанныеПараметра = ПараметрыВСтроке[0];
			
			Если Не ОбрабатываласьЯвнаяПередачаПараметров Тогда
				ОбрабатываласьЯвнаяПередачаПараметров = Истина;
				//т.к. могли быть загружены данные из кеш, а при явной передаче параметров это не надо
				ТекущаяСтрокаВложенногоСценария.Параметры.Очистить();
				
				//т.к. идёт явная передача параметров не нужно делать подстановку значений в строку вызова подсценария
				ТекЭлемент.ОбработаннаяСтрокаПараметров = ТекЭлемент.ОписаниеЭлемента;
			КонецЕсли;	 
			
			
			СтруктураПараметра = СтруктураПараметра();
			СтруктураПараметра.Вставить("ЗначениеПараметра",ДанныеПараметра.ЗначениеПараметра);
			СтруктураПараметра.Вставить("Символ",ДанныеПараметра.Символ);
			СтруктураПараметра.Вставить("Тип",ДанныеПараметра.Тип);
			СтруктураПараметра.Вставить("Вид",ДанныеПараметра.Вид);
			СтруктураПараметра.Вставить("ПереданСнизу",Истина);
			СтруктураПараметра.Вставить("ИмяПараметра",ИмяПараметра);
			СтруктураПараметра.Вставить("ИзменяемыйПараметр",ДанныеПараметра.ИзменяемыйПараметр);
			СтруктураПараметра.Вставить("НомерПараметра",ТекущаяСтрокаВложенногоСценария.Параметры.Количество()+1);
			СтруктураПараметра.Вставить("НомерПараметраВСценарии",
			   НомерПараметраВСценарии(ИзменяемыеПараметрыСценария,ДанныеПараметра.ЗначениеПараметра));
			
			ТекущаяСтрокаВложенногоСценария.Параметры.Добавить(СтруктураПараметра);
			
			ТекЭлемент                      = ТекущаяСтрокаВложенногоСценария.Строки.Добавить();
			ТекЭлемент.ОписаниеЭлемента     = СтрТаблицаСтрок.СтрокаСокр;
			ТекЭлемент.ТипЭлемента          = ТипЭлементаПередачаПараметров();
			
			ПараметрыДаннойСтроки = Новый Массив;
			ПараметрыДаннойСтроки.Добавить(СтруктураПараметра);
			ТекЭлемент.Параметры = ПараметрыДаннойСтроки;
			ТекЭлемент.ОбработаннаяСтрокаПараметров = ИмяПараметра + " = " + СтрокаЗначениеПараметра;
			
			Если ТекущаяСтрокаВложенногоСценария <> Неопределено Тогда
				ВыровнятьТекстПриПередачеВПодсценарий(ТекущаяСтрокаВложенногоСценария);
			КонецЕсли;	 																			
		ИначеЕсли СтрТаблицаСтрок.Оператор = ОператорТег() Тогда
			Теги.Добавить(СтрТаблицаСтрок.СтрокаСокр);
		ИначеЕсли СтрТаблицаСтрок.Оператор = ОператорСтоп() Тогда
			ТекЭлемент                      = НовыйЭлементДерева(ТекРодитель,ДеревоСхемы,ИдСтрокиТаблицаСтрок,
																		СтрТаблицаСтрок,ПараметрыОбработки);
			ТекЭлемент.ОписаниеЭлемента     = СтрТаблицаСтрок.СтрокаСокр;
			ТекЭлемент.ТипЭлемента          = ТипЭлементаСтоп();
			
		ИначеЕсли СтрТаблицаСтрок.Оператор = ОператорМетка() Тогда
			ТекЭлемент                      = НовыйЭлементДерева(ТекРодитель,ДеревоСхемы,ИдСтрокиТаблицаСтрок,
																		СтрТаблицаСтрок,ПараметрыОбработки);
			ТекЭлемент.ОписаниеЭлемента     = СтрТаблицаСтрок.СтрокаСокр;
			ТекЭлемент.ТипЭлемента          = ТипЭлементаМетка();
			ТекЭлемент.ИмяМетки             = СтрТаблицаСтрок.ПараметрыОператора.ИмяМетки;
			
		ИначеЕсли СтрТаблицаСтрок.Оператор = ОператорПерейти() Тогда
			ТекЭлемент                      = НовыйЭлементДерева(ТекРодитель,ДеревоСхемы,ИдСтрокиТаблицаСтрок,
																		СтрТаблицаСтрок,ПараметрыОбработки);
			ТекЭлемент.ОписаниеЭлемента     = СтрТаблицаСтрок.СтрокаСокр;
			ТекЭлемент.ТипЭлемента          = ТипЭлементаПерейти();
			ТекЭлемент.ИмяМетки             = СтрТаблицаСтрок.ПараметрыОператора.ИмяМетки;
			
		Иначе
			СтрокаШаблона = Неопределено;
			ПередчаПараметраВПодсценарийТаблица = Ложь;
			
			Если НЕ ИдетЗаполнениеЗаголовка И ЭтоВложенныйСценарийШаблон(СтрТаблицаСтрок,ТаблицаШаблоновСценариев,СтрокаШаблона) Тогда
				//надо загрузить данные из шаблона
				ТекЭлемент = НовыйЭлементДерева(ТекРодитель,ДеревоСхемы,ИдСтрокиТаблицаСтрок,
																			СтрТаблицаСтрок,ПараметрыОбработки);
																			
				Если ТекущаяСтрокаВложенногоСценария <> Неопределено Тогда
					ВыровнятьТекстПриПередачеВПодсценарий(ТекущаяСтрокаВложенногоСценария);
				КонецЕсли;	 																			
				ТекущаяСтрокаВложенногоСценария = ТекЭлемент;
																			
				ТекЭлемент.ОписаниеЭлемента     = СтрТаблицаСтрок.СтрокаСокр;
				ТекЭлемент.ТипЭлемента          = ТипЭлементаШаблонСценария();
				
				ДопПараметры = Новый Структура;
				ДопПараметры.Вставить("ИспользоватьКеш",Ложь);
				ЗаполнитьПараметрыДляДанногоЭлемента(СтрТаблицаСтрок.СтрокаСокр,ТекЭлемент,ИзменяемыеПараметрыСценария,Ложь,
				                                КешПараметровСтроки,ДопПараметры);
												
				ОбрабатываласьЯвнаяПередачаПараметров = Ложь;
												
				Если НЕ СтруктураПараметров.ЧтениеИзТекста Тогда
					СценарийДляЗагрузкиШаблона = Сценарий.Ссылка;
				Иначе	
					СценарийДляЗагрузкиШаблона = Сценарий;
				КонецЕсли;	 												
												
				ЗагрузитьСценарийИзШаблона(СтрокаШаблона,ТекЭлемент,ТаблицаШаблоновСценариев,СценарийДляЗагрузкиШаблона,СтруктураПараметров);
			Иначе	
				ТекущаяСтрокаВложенногоСценария = Неопределено;
				ТекЭлемент                      = НовыйЭлементДерева(ТекРодитель,ДеревоСхемы,ИдСтрокиТаблицаСтрок,
																			СтрТаблицаСтрок,ПараметрыОбработки);
				ТекЭлемент.ОписаниеЭлемента     = СтрТаблицаСтрок.СтрокаСокр;
				
				Если ИдетЗаполнениеЗаголовка Тогда
					ТекЭлемент.ТипЭлемента                  = ТипЭлементаЭлементЗаголовка();
				Иначе	
					
					ТекЭлемент.ТипЭлемента                  = ТипЭлементаДействие();
					ЗаполнитьПараметрыДляДанногоЭлемента(СтрТаблицаСтрок.СтрокаСокр,ТекЭлемент,ИзменяемыеПараметрыСценария,Ложь,
					                                     КешПараметровСтроки);
														 
				КонецЕсли;
			КонецЕсли;
		КонецЕсли; 
	КонецЦикла;
	
	Если ОператорНачалоУсловия.Количество() <> ОператорОкончанияУсловия.Количество() Тогда
		ВызватьИсключение СтрШаблон(НСтр("ru = 'Сценарий <%1>.
				|Количество операторов открывающих условие <%2>
				|не совпадает с количеством операторов закрывающих условие <%3>.'"),
				  Сценарий, ОператорНачалоУсловия.Количество(), ОператорОкончанияУсловия.Количество());
	КонецЕсли;	 
	
	Если ОператорНачалоУсловияПрепроцессор.Количество() <> ОператорОкончанияУсловияПрепроцессор.Количество() Тогда
		ВызватьИсключение СтрШаблон(НСтр("ru = 'Сценарий <%1>.
				|Количество операторов открывающих условие препроцессора <%2>
				|не совпадает с количеством операторов закрывающих условие препроцессора <%3>.'"),
				  Сценарий, ОператорНачалоУсловияПрепроцессор.Количество(), ОператорОкончанияУсловияПрепроцессор.Количество());
	КонецЕсли;	 
	
	Если ОператорНачалоЦикла.Количество() <> ОператорОкончанияЦикла.Количество() Тогда
		ВызватьИсключение СтрШаблон(НСтр("ru = 'Сценарий <%1>.
				|Количество операторов открывающих цикл <%2>
				|не совпадает с количеством операторов закрывающих цикл <%3>.'"),
				  Сценарий, ОператорНачалоЦикла.Количество(), ОператорОкончанияЦикла.Количество());
	КонецЕсли;	 
	
	
	ТекЭлемент                      = ДеревоСхемы.Строки.Добавить();
	ТекЭлемент.ОписаниеЭлемента     = ОписаниеЭлементаСтоп();
	ТекЭлемент.ТипЭлемента          = ТипЭлементаОкончаниеСхемы();
	ИдСтрокиТаблицаСтрок            = ИдСтрокиТаблицаСтрок + 1;
	ТекЭлемент.ИдСтрокиТаблицаСтрок = ИдСтрокиТаблицаСтрок;
	Если Комментарии.Количество() > 0 Тогда
		ТекЭлемент.Комментарии = Комментарии;
	КонецЕсли;	 
	Если Теги.Количество() > 0 Тогда
		ТекЭлемент.Теги = Теги;
	КонецЕсли;	 
	
	ИдСтрокиДерева = 0;
	ДобавитьСлужебныеЭлементыДляГруппШагов(ДеревоСхемы);
	ПроставитьИдСтрокиИЗависимыеПараметрыДереваРекурсивно(ДеревоСхемы,ИдСтрокиДерева);
	
	Возврат ДеревоСхемы;
КонецФункции	

Процедура ВыровнятьТекстПриПередачеВПодсценарий(СтрокаШага)
	Если СтрокаШага.Параметры.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;	
	
	ПараметрыПереданыСнизу = Ложь;
	Если СтрокаШага.Параметры[0].Свойство("ПереданСнизу") Тогда
		Если СтрокаШага.Параметры[0].ПереданСнизу Тогда
			ПараметрыПереданыСнизу = Истина;
		КонецЕсли;	 
	КонецЕсли;	 
	
	Если Не ПараметрыПереданыСнизу  Тогда
		Возврат;
	КонецЕсли;	
	
	МассивСтрокОписаниеЭлемента = Новый Массив;
	МассивСтрокСтрокаПараметров = Новый Массив;
	Для Каждого ПодчиненнаяСтрока Из СтрокаШага.Строки Цикл
		Если ПодчиненнаяСтрока.ТипЭлемента <> ТипЭлементаПередачаПараметров() Тогда
			Продолжить;
		КонецЕсли;	 
		
		МассивСтрокОписаниеЭлемента.Добавить(ПодчиненнаяСтрока.ОписаниеЭлемента);
		МассивСтрокСтрокаПараметров.Добавить(ПодчиненнаяСтрока.ОбработаннаяСтрокаПараметров);
	КонецЦикла;	
	
	ВыровнятьМассивСтрокПоПервомуВхождениюСимвола(МассивСтрокОписаниеЭлемента,"=");
	ВыровнятьМассивСтрокПоПервомуВхождениюСимвола(МассивСтрокСтрокаПараметров,"=");
	
	Сч = -1;
	Для Каждого ПодчиненнаяСтрока Из СтрокаШага.Строки Цикл
		Если ПодчиненнаяСтрока.ТипЭлемента <> ТипЭлементаПередачаПараметров() Тогда
			Продолжить;
		КонецЕсли;	 
		
		Сч = Сч + 1;
		ПодчиненнаяСтрока.ОписаниеЭлемента = МассивСтрокОписаниеЭлемента[Сч];
		ПодчиненнаяСтрока.ОбработаннаяСтрокаПараметров = МассивСтрокСтрокаПараметров[Сч];
	КонецЦикла;	
	
КонецПроцедуры

Процедура НайтиИзменяемыеПараметры(Стр,Параметры,ИзменяемыеПараметрыСценария)
	Символ        = "[";
	Если Найти(Стр,Символ) = 0 Тогда
		Возврат;
	КонецЕсли;	
	
	ЭкранироватьКвадратнрыеСкобки(Стр);
	
	Вид           = "КвадратныеСкобки";
	МассивСлов    = СтрРазделить(Стр,Символ,Истина);
	КолПараметров = 0;
	
	Ном = -1;
	Для Каждого Слово Из МассивСлов Цикл
		Ном = Ном + 1;
		
		Если Ном = 0 Тогда
			Продолжить;
		КонецЕсли;	 
		
		ПозицияЗакрывающейСкобки = Найти(Слово,"]");
		Если ПозицияЗакрывающейСкобки = 0 Тогда
			ВызватьИсключение СтрШаблон(
			    НСтр("ru = 'Неверно используется квадратная скобка в строке: %1'"),УбратьСлужебныеСимволы(Стр));
		КонецЕсли;	 
		
		ЗначениеПараметра = Лев(Слово,ПозицияЗакрывающейСкобки-1);
		Если ЗначениеПараметра = "" Тогда
			ВызватьИсключение СтрШаблон(
			    НСтр("ru = 'Не указано значение параметра в квадратных скобках в строке : %1'"),УбратьСлужебныеСимволы(Стр));
		КонецЕсли;	 
		
		ВтораяЧастьСлова = Сред(Слово,ПозицияЗакрывающейСкобки+1);
		
		ЗначениеПараметра = УбратьСлужебныеСимволы(ЗначениеПараметра);
		
		КолПараметров   = КолПараметров + 1;
		
		СтруктураПараметра = СтруктураПараметра();
		СтруктураПараметра.Вставить("ЗначениеПараметра",ЗначениеПараметра);
		СтруктураПараметра.Вставить("Символ",Символ);
		СтруктураПараметра.Вставить("Тип",ТипПараметра("Строка"));
		СтруктураПараметра.Вставить("Вид",Вид);
		СтруктураПараметра.Вставить("ИзменяемыйПараметр",Истина);
		СтруктураПараметра.Вставить("НомерПараметра",КолПараметров);
		СтруктураПараметра.Вставить("НомерПараметраВСценарии",
		           НомерПараметраВСценарии(ИзменяемыеПараметрыСценария,ЗначениеПараметра));
		
		МассивСлов[Ном] = "~ПараметрСтрока" + Вид + XMLСтрока(КолПараметров) + "~" + ВтораяЧастьСлова;
		
		Параметры.Добавить(СтруктураПараметра);
	КонецЦикла;	
	
	Стр = СтрСоединить(МассивСлов,"");
	ВернутьКвадратнрыеСкобки(Стр);
КонецПроцедуры

Процедура ЭкранироватьКвадратнрыеСкобки(Стр)
	Стр = СтрЗаменить(Стр,"\[","~ПредставлениеЛеваяКвадратнаяСкобка~");
	Стр = СтрЗаменить(Стр,"\]","~ПредставлениеПраваяКвадратнаяСкобка~");
КонецПроцедуры

Процедура ВернутьКвадратнрыеСкобки(Стр)
	Стр = СтрЗаменить(Стр,"~ПредставлениеЛеваяКвадратнаяСкобка~","\[");
	Стр = СтрЗаменить(Стр,"~ПредставлениеПраваяКвадратнаяСкобка~","\]");
КонецПроцедуры 

Процедура НайтиИзменяемыеПараметрыВПараметрах(Стр,Параметры,ИзменяемыеПараметрыСценария)
	
	Для Каждого ПараметрСценария Из Параметры Цикл
		ЗначениеПараметра = ПараметрСценария.ЗначениеПараметра;
		ЭкранироватьКвадратнрыеСкобки(ЗначениеПараметра);
		
		Если Найти(ЗначениеПараметра,"[") = 0 Тогда
			Продолжить;
		КонецЕсли;	
		
		Нашли = Истина;
		
		Если ПараметрСценария.ВложенныеПараметры = Неопределено Тогда
			ПараметрСценария.ВложенныеПараметры = Новый Массив;
		КонецЕсли;	 
		
		ПараметрСценария.ЗначениеПараметраВложенное = ПараметрСценария.ЗначениеПараметра;
		
		ВложенныеПараметры = ПараметрСценария.ВложенныеПараметры;
		
		НайтиИзменяемыеПараметры(ПараметрСценария.ЗначениеПараметраВложенное,ВложенныеПараметры,ИзменяемыеПараметрыСценария);
		
	КонецЦикла;	
КонецПроцедуры

Процедура НайтиСтроковыеПараметрыПоСимволу(Стр,Параметры,Символ,Вид)
	МассивСлов    = СтрРазделить(Стр,Символ,Истина);
	КолПараметров = 0;
	
	ЭтоПараметр = Ложь;
	Ном = -1;
	Для Каждого Слово Из МассивСлов Цикл
		Ном = Ном + 1;
		Если ЭтоПараметр Тогда
			
			ЗначениеПараметра = УбратьСлужебныеСимволы(Слово);
			Если НЕ ЭтотПараметрЯвляетсяТолькоЗначением(ЗначениеПараметра) Тогда
				КолПараметров   = КолПараметров + 1;
				
				СтруктураПараметра = СтруктураПараметра();
				СтруктураПараметра.Вставить("ЗначениеПараметра",ЗначениеПараметра);
				СтруктураПараметра.Вставить("Символ",Символ);
				СтруктураПараметра.Вставить("Тип",ТипПараметра("Строка"));
				СтруктураПараметра.Вставить("Вид",Вид);
				СтруктураПараметра.Вставить("НомерПараметра",КолПараметров);
				
				МассивСлов[Ном] = "~Параметр" + "Строка" + Вид + XMLСтрока(КолПараметров) + "~";
				
				Параметры.Добавить(СтруктураПараметра);
			КонецЕсли;	 
		КонецЕсли;	 
		
		ЭтоПараметр = Не ЭтоПараметр;
	КонецЦикла;	
	
	Стр = СтрСоединить(МассивСлов,"");
КонецПроцедуры

Процедура НайтиПараметрыДаты(Стр,Параметры,КолПараметров = Неопределено)
	Массив = СтрРазделить(Стр,".");
	
	Если КолПараметров = Неопределено Тогда
		КолПараметров = 0;
	КонецЕсли;	 
	
	Для Ккк = 0 По Массив.Количество()-1-2 Цикл
		Элем1 = Прав(Массив[Ккк],2);
		Элем2 = Массив[Ккк+1];
		Элем3 = Лев(Массив[Ккк+2],4);
		Если СтрДлина(Элем3) < 4 Тогда
			Элем3 = Лев(Массив[Ккк+2],2);
		КонецЕсли;	 
		Если СтрДлина(Элем3) = 4 Тогда
			Если НЕ ТолькоЦифрыВСтроке(Элем3) Тогда
				Элем3 = Лев(Массив[Ккк+2],2);
			КонецЕсли;	 
		КонецЕсли;	 
		
		Если СтрДлина(Элем1) <>  2 Тогда
			Продолжить;
		КонецЕсли;	 
		Если СтрДлина(Элем2) <>  2 Тогда
			Продолжить;
		КонецЕсли;	 
		Если (СтрДлина(Элем3) = 2) или (СтрДлина(Элем3) = 4) Тогда
		Иначе
			Продолжить;
		КонецЕсли;	 
		
		Если НЕ ТолькоЦифрыВСтроке(Элем1) Тогда
			Продолжить;
		КонецЕсли;	 
		Если НЕ ТолькоЦифрыВСтроке(Элем2) Тогда
			Продолжить;
		КонецЕсли;	 
		Если НЕ ТолькоЦифрыВСтроке(Элем3) Тогда
			Продолжить;
		КонецЕсли;	 
		
		СтрДат = "" + Элем1 + "." + Элем2 + "." +Элем3;
		
		Поз = Найти(Стр,СтрДат);
		Если Поз > 0 Тогда
			КолПараметров   = КолПараметров + 1;
			
			СтруктураПараметра = СтруктураПараметра();
			СтруктураПараметра.Вставить("ЗначениеПараметра",СтрДат);
			СтруктураПараметра.Вставить("Тип",ТипПараметра("Дата"));
			СтруктураПараметра.Вставить("НомерПараметра",КолПараметров);			
			
			Стр = Лев(Стр,Поз-1) + "~Параметр" + "Дата" + XMLСтрока(КолПараметров) + "~" + Сред(Стр,Поз+СтрДлина(СтрДат));
			
			Параметры.Добавить(СтруктураПараметра);
			
			НайтиПараметрыДаты(Стр,Параметры,КолПараметров);
			
			Прервать;
		КонецЕсли;	 
		
	КонецЦикла;
	
КонецПроцедуры

Функция ЭтоКорректноеЧисло(Стр, ТипЧисло)
	Если ПустаяСтрока(Стр) или (Стр = "-") или (Стр = "+") Тогда
		Возврат Ложь;
	КонецЕсли;	 
	
	Если Стр = "0" Тогда
		Возврат Истина;
	КонецЕсли;
	
	Результат = ТипЧисло.ПривестиЗначение(Стр);
	Если Результат = 0 Тогда
		Возврат Ложь;
	КонецЕсли;	 
	
	Возврат Истина;
КонецФункции	

Процедура НайтиЧисловыеПараметры(Стр,Параметры, ТипЧисло)
	КолПараметров = 0;
	
	МассивСлов = СтрРазделить(Стр," ");
	Для Ккк = 0 По МассивСлов.Количество()-1 Цикл
		Слово = МассивСлов[Ккк];
		
		Если ЭтоКорректноеЧисло(Слово, ТипЧисло) Тогда
			КолПараметров   = КолПараметров + 1;
			
			СтруктураПараметра = СтруктураПараметра();
			СтруктураПараметра.Вставить("ЗначениеПараметра",Слово);
			СтруктураПараметра.Вставить("Тип",ТипПараметра("Число"));
			СтруктураПараметра.Вставить("НомерПараметра",КолПараметров);
			Параметры.Добавить(СтруктураПараметра);
			
			МассивСлов[Ккк] = "~Параметр" + "Число" + XMLСтрока(КолПараметров) + "~";
		КонецЕсли;	 
	КонецЦикла;	
	
	Стр = СтрСоединить(МассивСлов," ");
КонецПроцедуры

Процедура УпорядочитьМассивПараметров(Стр,Параметры)
	ТаблицаДляУпорядочивания = Новый ТаблицаЗначений;
	ТаблицаДляУпорядочивания.Колонки.Добавить("Параметр");
	ТаблицаДляУпорядочивания.Колонки.Добавить("ПозицияВСтроке",Новый ОписаниеТипов("Число"));
	
	Для Каждого Параметр Из Параметры Цикл
		СтрокаПоиска = Неопределено;
		
		Если Параметр.Тип = ТипПараметра("Число") Тогда
			СтрокаПоиска = "~Параметр" + "Число" + XMLСтрока(Параметр.НомерПараметра) + "~";
		ИначеЕсли Параметр.Тип = ТипПараметра("Строка") Тогда
			СтрокаПоиска = "~Параметр" + Параметр.Тип + Параметр.Вид + XMLСтрока(Параметр.НомерПараметра) + "~";
		ИначеЕсли Параметр.Тип = ТипПараметра("Дата") Тогда
			СтрокаПоиска = "~ПараметрДата" + XMLСтрока(Параметр.НомерПараметра) + "~";
		Иначе
			ВызватьИсключение СтрШаблон(НСтр("ru = 'Не известный тип параметра: %1'"),Параметр.Тип);
		КонецЕсли;
		
		ПозицияВСтроке = Найти(Стр,СтрокаПоиска);
		Если ПозицияВСтроке = 0 Тогда
			ВызватьИсключение СтрШаблон(НСтр("ru = 'Не найден параметр в строке: %1'"), Параметр.ЗначениеПараметра);
		КонецЕсли;	 
		
		СтрокаТаблицаДляУпорядочивания                = ТаблицаДляУпорядочивания.Добавить();
		СтрокаТаблицаДляУпорядочивания.Параметр       = Параметр;
		СтрокаТаблицаДляУпорядочивания.ПозицияВСтроке = ПозицияВСтроке;
	КонецЦикла;	
	
	ТаблицаДляУпорядочивания.Сортировать("ПозицияВСтроке");
	
	Массив = Новый Массив;
	Для Каждого СтрокаТаблицаДляУпорядочивания Из ТаблицаДляУпорядочивания Цикл
		Массив.Добавить(СтрокаТаблицаДляУпорядочивания.Параметр);
	КонецЦикла;	
	
	Параметры = Неопределено;
	Параметры = Массив;
КонецПроцедуры

Процедура ОператорыВСтроке(СтрТаблицаСтрок,НайденоНачалоБлочногоКомментария,НомерСтроки)
	МассивСловНРег = СтрРазделить(НРег(СтрТаблицаСтрок.СтрокаСокр)," ",Истина);
	Если МассивСловНРег.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;	 
	
	ПервоеСлово    = МассивСловНРег[0];
	ПоследнееСлово = МассивСловНРег[МассивСловНРег.Количество()-1];
	
	Если (Лев(ПервоеСлово,2) = "/*") и (Прав(СокрЛП(ПоследнееСлово),2) = "*/") Тогда
		СтрТаблицаСтрок.Оператор = ОператорКомментарий();
		
	ИначеЕсли Лев(ПервоеСлово,2) = "*/" Тогда
		СтрТаблицаСтрок.Оператор = ОператорБлочныйКомменатрийОкончание();
		
	ИначеЕсли Лев(ПервоеСлово,2) = "/*" Тогда
		СтрТаблицаСтрок.Оператор = ОператорБлочныйКомменатрийНачало();
		
	ИначеЕсли Прав(СокрЛП(ПоследнееСлово),2) = "*/" Тогда
		СтрТаблицаСтрок.Оператор = ОператорБлочныйКомменатрийОкончание();
		
	ИначеЕсли Лев(ПервоеСлово,2) = "//" Тогда
		СтрТаблицаСтрок.Оператор = ОператорКомментарий();
		
	ИначеЕсли НайденоНачалоБлочногоКомментария Тогда
		СтрТаблицаСтрок.Оператор = ОператорБлочныйКомментарий();
	ИначеЕсли ПервоеСлово = КлючевоеСловоЦикл() Тогда
		СтрТаблицаСтрок.Оператор = ОператорЦикл();
		
		СтрТаблицаСтрок.ПараметрыОператора = Новый Структура;
		
		МассивСлов = МассивСловШага(СтрТаблицаСтрок.СтрокаСокр);
		МассивСлов.Удалить(0);
		
		Шаг = СтрСоединить(МассивСлов," ");
		СтрТаблицаСтрок.ПараметрыОператора.Вставить("Шаг",Шаг);
	ИначеЕсли ПервоеСлово = КлючевоеСловоЦиклEng() Тогда
		СтрТаблицаСтрок.Оператор = ОператорЦиклEng();
		
		СтрТаблицаСтрок.ПараметрыОператора = Новый Структура;
		
		МассивСлов = МассивСловШага(СтрТаблицаСтрок.СтрокаСокр);
		МассивСлов.Удалить(0);
		
		Шаг = СтрСоединить(МассивСлов," ");
		СтрТаблицаСтрок.ПараметрыОператора.Вставить("Шаг",Шаг);
	ИначеЕсли ПервоеСлово = КлючевоеСловоКонецЦикла() Тогда
		СтрТаблицаСтрок.Оператор = ОператорКонецЦикла();
	ИначеЕсли ПервоеСлово = КлючевоеСловоКонецЦиклаEng() Тогда
		СтрТаблицаСтрок.Оператор = ОператорКонецЦиклаEng();
	ИначеЕсли (ПервоеСлово = КлючевоеСловоЕслиПрепроцессор()) и (ПоследнееСлово = КлючевоеСловоТогдаПрепроцессор()) Тогда
		// В Gherkin разрешено использовать ключевое слово "Если" в начале шага
		// поэтому если найдена конструкция "Если" без ключевого слова Тогда - это не ошибка
		СтрТаблицаСтрок.Оператор = ОператорУсловиеПрепроцессор();
		
		СтрТаблицаСтрок.ПараметрыОператора = Новый Структура;
		
		МассивСлов = МассивСловШага(СтрТаблицаСтрок.СтрокаСокр);
		МассивСлов.Удалить(МассивСлов.Количество()-1);
		МассивСлов.Удалить(0);
		
		ЗначениеУсловия = СтрСоединить(МассивСлов," ");
		СтрТаблицаСтрок.ПараметрыОператора.Вставить("ЗначениеУсловия",ЗначениеУсловия);
		
	ИначеЕсли ((ПервоеСлово = КлючевоеСловоЕсли()) и (ПоследнееСлово = КлючевоеСловоТогда()) )
		или ((ПервоеСлово = КлючевоеСловоЕслиEng()) и (ПоследнееСлово = КлючевоеСловоТогдаEng()) )
		Тогда
		// Это условие, которое будет выполняться в реальном времени
		
		Если ПервоеСлово = КлючевоеСловоЕсли() Тогда
			СтрТаблицаСтрок.Оператор = ОператорУсловие();
		ИначеЕсли ПервоеСлово = КлючевоеСловоЕслиEng() Тогда
			СтрТаблицаСтрок.Оператор = ОператорУсловиеEng();
		КонецЕсли;	 
		
		
		СтрТаблицаСтрок.ПараметрыОператора = Новый Структура;
		
		МассивСлов = МассивСловШага(СтрТаблицаСтрок.СтрокаСокр);
		МассивСлов.Удалить(МассивСлов.Количество()-1);
		МассивСлов.Удалить(0);
		
		ЗначениеУсловия = СтрСоединить(МассивСлов," ");
		СтрТаблицаСтрок.ПараметрыОператора.Вставить("ЗначениеУсловия",ЗначениеУсловия);
		
	ИначеЕсли ((ПервоеСлово = КлючевоеСловоИначеЕсли()) и (ПоследнееСлово = КлючевоеСловоТогда()) )
		или ((ПервоеСлово = КлючевоеСловоИначеЕслиEng()) и (ПоследнееСлово = КлючевоеСловоТогдаEng()) )
		Тогда
		// Это условие ИначеЕсли, которое будет выполняться в реальном времени
		
		Если ПервоеСлово = КлючевоеСловоИначеЕсли() Тогда
			СтрТаблицаСтрок.Оператор = ОператорИначеЕсли();
		ИначеЕсли ПервоеСлово = КлючевоеСловоИначеЕслиEng() Тогда
			СтрТаблицаСтрок.Оператор = ОператорИначеЕслиEng();
		КонецЕсли;	 
		
		
		СтрТаблицаСтрок.ПараметрыОператора = Новый Структура;
		
		МассивСлов = МассивСловШага(СтрТаблицаСтрок.СтрокаСокр);
		МассивСлов.Удалить(МассивСлов.Количество()-1);
		МассивСлов.Удалить(0);
		
		ЗначениеУсловия = СтрСоединить(МассивСлов," ");
		СтрТаблицаСтрок.ПараметрыОператора.Вставить("ЗначениеУсловия",ЗначениеУсловия);
		
	ИначеЕсли (ПервоеСлово = КлючевоеСловоИначеЕслиПрепроцессор()) и (ПоследнееСлово = КлючевоеСловоТогдаПрепроцессор()) Тогда
		СтрТаблицаСтрок.Оператор = ОператорИначеЕслиПрепроцессор();
		СтрТаблицаСтрок.ПараметрыОператора = Новый Структура;
		
		МассивСлов = МассивСловШага(СтрТаблицаСтрок.СтрокаСокр);
		МассивСлов.Удалить(МассивСлов.Количество()-1);
		МассивСлов.Удалить(0);
		
		ЗначениеУсловия = СтрСоединить(МассивСлов," ");
		СтрТаблицаСтрок.ПараметрыОператора.Вставить("ЗначениеУсловия",ЗначениеУсловия);
	ИначеЕсли (ПервоеСлово = КлючевоеСловоИначеЕслиПрепроцессор()) и (ПоследнееСлово <> КлючевоеСловоТогдаПрепроцессор()) Тогда
		ВызватьИсключение НСтр("ru = '<ИначеЕсли> нельзя использовать без ключевого слова <Тогда>'");
	ИначеЕсли (ПервоеСлово = КлючевоеСловоИначе()) Тогда
		СтрТаблицаСтрок.Оператор = ОператорИначе();
	ИначеЕсли (ПервоеСлово = КлючевоеСловоИначеEng()) Тогда
		СтрТаблицаСтрок.Оператор = ОператорИначеEng();
	ИначеЕсли (ПервоеСлово = КлючевоеСловоИначеПрепроцессор()) Тогда
		СтрТаблицаСтрок.Оператор = ОператорИначеПрепроцессор();
	ИначеЕсли (ПервоеСлово = КлючевоеСловоКонецЕслиПрепроцессор()) Тогда
		СтрТаблицаСтрок.Оператор = ОператорКонецЕслиПрепроцессор();
	ИначеЕсли (ПервоеСлово = КлючевоеСловоКонецЕсли()) Тогда
		СтрТаблицаСтрок.Оператор = ОператорКонецЕсли();
	ИначеЕсли (ПервоеСлово = КлючевоеСловоКонецЕслиEng()) Тогда
		СтрТаблицаСтрок.Оператор = ОператорКонецЕслиEng();
	ИначеЕсли (ПервоеСлово = КлючевоеСловоСтоп()) Тогда
		СтрТаблицаСтрок.Оператор = ОператорСтоп();
	ИначеЕсли Лев(ПервоеСлово,1) = "#" Тогда
		СтрТаблицаСтрок.Оператор = ОператорКомментарий();
	ИначеЕсли Лев(ПервоеСлово,1) = "~" Тогда
		СтрТаблицаСтрок.Оператор = ОператорМетка();
		СтрТаблицаСтрок.ПараметрыОператора = Новый Структура;
		СтрТаблицаСтрок.ПараметрыОператора.Вставить("ИмяМетки",Сред(СтрТаблицаСтрок.СтрокаСокр,2));
	ИначеЕсли (ПервоеСлово = КлючевоеСловоПерейти()) Тогда
		СтрТаблицаСтрок.Оператор = ОператорПерейти();
		
		МассивСлов = МассивСловШага(СтрТаблицаСтрок.СтрокаСокр);
		МассивСлов.Удалить(0);
		ИмяМетки   = СтрЗаменить(СтрСоединить(МассивСлов," "),"~","");
		
		СтрТаблицаСтрок.ПараметрыОператора = Новый Структура;
		СтрТаблицаСтрок.ПараметрыОператора.Вставить("ИмяМетки",ИмяМетки);
	ИначеЕсли (ПервоеСлово = КлючевоеФункционал()) или (ПервоеСлово = КлючевоеФункциональность()) Тогда
		СтрТаблицаСтрок.Оператор = ОператорЗаголовок();
	ИначеЕсли (ПервоеСлово = КлючевоеКонтекст()) Тогда
		СтрТаблицаСтрок.Оператор = ОператорКонтекст();
	ИначеЕсли (ПервоеСлово = КлючевоеСценарий()) Тогда
		СтрТаблицаСтрок.Оператор = ОператорСценарий();
	ИначеЕсли Лев(ПервоеСлово,1) = "@" Тогда
		СтрТаблицаСтрок.Оператор = ОператорТег();
	ИначеЕсли Лев(СтрТаблицаСтрок.СтрокаСокр,1) = "[" И Прав(СтрТаблицаСтрок.СтрокаСокр,1) = "]" Тогда
		СтрТаблицаСтрок.Оператор = ОператорПараметрТаблица();
	ИначеЕсли Лев(ПервоеСлово,1) = "|" Тогда
		СтрТаблицаСтрок.Оператор = ОператорТаблица();
		Если Прав(СтрТаблицаСтрок.СтрокаСокр,1) <> "|" Тогда
			ВызватьИсключение СтрШаблон(
				НСтр("ru = 'Строка №%1. <%2> Не найден символ | в завершении строки.'"),НомерСтроки,СтрТаблицаСтрок.СтрокаСокр);
		КонецЕсли;	 
	КонецЕсли;	 
КонецПроцедуры

Процедура ИдРодительскойГруппы(ТаблицаСтрок,СтрТаблицаСтрок,НомерСтроки,Отступ)
	Ид                     = НомерСтроки;
	Нашли                  = Ложь;
	НадоПропуститьУсловие  = Ложь;
	КешОператорСценарий    = ОператорСценарий();
	КешОператорКонтекст    = ОператорКонтекст();
	КешОператорКомментарий = ОператорКомментарий();
	КешОператорБлочныйКомменатрий = ОператорБлочныйКомментарий();
	КешОператорБлочныйКомменатрийНачало = ОператорБлочныйКомменатрийНачало();
	КешОператорБлочныйКомменатрийОкончание = ОператорБлочныйКомменатрийОкончание();
	
	Пока Истина Цикл
		Если ИД < 0 Тогда
			Прервать;
		КонецЕсли;	 
		
		ПервыйСимвол = Лев(ТаблицаСтрок[ИД].СтрокаСокр,1);
		Оператор     = ТаблицаСтрок[ИД].Оператор;
		
		Если ПервыйСимвол = "#" Тогда // это строка комментария
			Ид = Ид - 1;
			Продолжить;
		ИначеЕсли ПервыйСимвол = "@" Тогда // это строка тега
			Ид = Ид - 1;
			Продолжить;
		ИначеЕсли Оператор  = КешОператорКомментарий Тогда
			Ид = Ид - 1;
			Продолжить;
		ИначеЕсли Оператор  = КешОператорБлочныйКомменатрий Тогда
			Ид = Ид - 1;
			Продолжить;
		ИначеЕсли Оператор  = КешОператорСценарий Тогда
			СтрТаблицаСтрок.ИдРодительскойГруппы = ИД;
			Нашли = Истина;
			Прервать;
		ИначеЕсли Оператор  = КешОператорКонтекст Тогда
			СтрТаблицаСтрок.ИдРодительскойГруппы = ИД;
			Нашли = Истина;
			Прервать;
		ИначеЕсли (ПервыйСимвол <> "*") и (ТаблицаСтрок[ИД].СтрокаСокр <> "") 
			И (Отступ >= ТаблицаСтрок[ИД].Отступ)
			И Оператор <> КешОператорКомментарий 
			И Оператор <> КешОператорБлочныйКомменатрийНачало Тогда
			СтрТаблицаСтрок.ИдРодительскойГруппы = ТаблицаСтрок[ИД].ИдРодительскойГруппы;
			Нашли = Истина;
			Прервать;
		ИначеЕсли (ПервыйСимвол = "*") и (Отступ > ТаблицаСтрок[ИД].Отступ) 
			 и (Оператор <> КешОператорБлочныйКомменатрийОкончание) Тогда
			СтрТаблицаСтрок.ИдРодительскойГруппы = ИД;
			Нашли = Истина;
			Прервать;
		ИначеЕсли Отступ <= ТаблицаСтрок[ИД].Отступ Тогда
			Ид = Ид - 1;
			Продолжить;
		ИначеЕсли Не ТаблицаСтрок[ИД].ГруппаШаговНачалась Тогда
			Ид = Ид - 1;
			Продолжить;
		КонецЕсли;	 
		
		Нашли = Истина;
		СтрТаблицаСтрок.ИдРодительскойГруппы = ИД;
		Прервать;
	КонецЦикла;	
	
	Если Не Нашли Тогда
		СтрТаблицаСтрок.ИдРодительскойГруппы = -1;
	КонецЕсли;	 
КонецПроцедуры

Процедура ОтформатироватьТаблицы(ТаблицаСтрок)
	НайденныеТаблицы = Новый Массив;
	НашлиТаблицу = Ложь;
	Для Ккк = 0 По ТаблицаСтрок.Количество()-1 Цикл
		СтрокаТаблицаСтрок = ТаблицаСтрок[Ккк];
		
		
		Если СтрокаТаблицаСтрок.Оператор = ОператорБлочныйКомменатрийНачало() 
		Или  СтрокаТаблицаСтрок.Оператор = ОператорБлочныйКомменатрийОкончание() 
		Или  СтрокаТаблицаСтрок.Оператор = ОператорКомментарий() 
		Или  СокрЛП(СтрокаТаблицаСтрок.СтрокаСокр) = ""
		Тогда
			Продолжить;
		КонецЕсли;	 
		
		Если СтрокаТаблицаСтрок.Оператор = ОператорТаблица() Тогда
			Если Не НашлиТаблицу Тогда
				НомераСтрок = Новый Массив;
				НайденныеТаблицы.Добавить(НомераСтрок);
			КонецЕсли;	 
			
			НашлиТаблицу = Истина;
		Иначе	
			НашлиТаблицу = Ложь;
			Продолжить;
		КонецЕсли;	 
		
		НомераСтрок.Добавить(Новый Структура("НомерСтроки,СтрокаСокр",Ккк,СтрокаТаблицаСтрок.СтрокаСокр));
	КонецЦикла;	
	
	Для Каждого ТаблицаGherkin Из НайденныеТаблицы Цикл
		ВыровнятьТаблицуGherkin(ТаблицаGherkin,ТаблицаСтрок);
	КонецЦикла;	
	
КонецПроцедуры

Функция МассивСловШага(Знач Стр)
	Стр = СтрЗаменить(Стр,Символы.Таб," ");
	Возврат СтрРазделить(Стр," ",Истина);
КонецФункции	

Функция СтруктураПараметра()
	Структура = Новый Структура;
	Структура.Вставить("ЗначениеПараметра",Неопределено);
	Структура.Вставить("ЗначениеПараметраВложенное",Неопределено);//если в значение параметра есть параметры
	Структура.Вставить("Символ",Неопределено);
	Структура.Вставить("Тип",Неопределено);
	Структура.Вставить("Вид",Неопределено);
	Структура.Вставить("ПереданСнизу",Ложь);
	Структура.Вставить("ИмяПараметра",Неопределено);
	Структура.Вставить("ИзменяемыйПараметр",Ложь);
	Структура.Вставить("НомерПараметра",-1);//Номер параметра среди своего типа
	Структура.Вставить("ИдПараметраВСтроке",-1); //начинается с нуля
	Структура.Вставить("НомерПараметраВСценарии",-1); //начинается с нуля
	Структура.Вставить("ВложенныеПараметры",Неопределено);// массив вложенных параметров
	
	Возврат Структура;
КонецФункции	

Функция НовыйЭлементДерева(ТекРодитель,ДеревоСхемы,ИдСтрокиТаблицаСтрок,СтрТаблицаСтрок,ПараметрыОбработки,СоздатьУЭтогоРодителя = Ложь)
	Если СоздатьУЭтогоРодителя Тогда
		ТекЭлемент                      = ТекРодитель.Строки.Добавить();
		ТекЭлемент.ИдСтрокиТаблицаСтрок = ИдСтрокиТаблицаСтрок;
	ИначеЕсли ТипЗнч(ТекРодитель) = Тип("ДеревоЗначений") Тогда
		ТекЭлемент                      = ТекРодитель.Строки.Добавить();
		ТекЭлемент.ИдСтрокиТаблицаСтрок = ИдСтрокиТаблицаСтрок;
	ИначеЕсли СтрТаблицаСтрок.ГруппаШаговЗакончилась Тогда
		Если    (ТекРодитель.ТипЭлемента = ТипЭлементаИначеПрепроцессор())
			или (ТекРодитель.ТипЭлемента = ТипЭлементаИначеЕслиПрепроцессор()) 
			или (ТекРодитель.ТипЭлемента = ТипЭлементаНачалоУсловияПрепроцессор())
			или (ТекРодитель.ТипЭлемента = ТипЭлементаИначе())
			или (ТекРодитель.ТипЭлемента = ТипЭлементаИначеЕсли()) 
			или (ТекРодитель.ТипЭлемента = ТипЭлементаИначеEng())
			или (ТекРодитель.ТипЭлемента = ТипЭлементаИначеЕслиEng()) 
			или (ТекРодитель.ТипЭлемента = ТипЭлементаНачалоУсловия()) 
			или (ТекРодитель.ТипЭлемента = ТипЭлементаНачалоУсловияEng()) 
			или (ТекРодитель.ТипЭлемента = ТипЭлементаНачалоЦикла())
			или (ТекРодитель.ТипЭлемента = ТипЭлементаНачалоЦиклаEng())
			Тогда
			//оставляем того же родителя, т.к. нельзя просто так выйти за рамки условия
		Иначе	
			ТекРодитель = РодительЭлемента(ТекРодитель,ДеревоСхемы,СтрТаблицаСтрок,
			    ПараметрыОбработки.ТаблицаСтрок,ПараметрыОбработки.МассивУсловий);
		КонецЕсли;	 
		ТекЭлемент  = ТекРодитель.Строки.Добавить();
		ТекЭлемент.ИдСтрокиТаблицаСтрок = ИдСтрокиТаблицаСтрок;
	Иначе	
		ТекЭлемент                      = ТекРодитель.Строки.Добавить();
		ТекЭлемент.ИдСтрокиТаблицаСтрок = ИдСтрокиТаблицаСтрок;
	КонецЕсли;
	
	Если ПараметрыОбработки.Комментарии.Количество() > 0 Тогда
		ТекЭлемент.Комментарии = СкопироватьПростойМассив(ПараметрыОбработки.Комментарии);
		ПараметрыОбработки.Комментарии.Очистить();
	КонецЕсли;	 
	
	Если ПараметрыОбработки.Теги.Количество() > 0 Тогда
		ТекЭлемент.Теги = СкопироватьПростойМассив(ПараметрыОбработки.Теги);
		ПараметрыОбработки.Теги.Очистить();
	КонецЕсли;	 
	
	ТекЭлемент.НомерСтрокиТекстаСценария = СтрТаблицаСтрок.НомерСтрокиТекстаСценария;
	
	Возврат ТекЭлемент;
КонецФункции	

Функция РодительПоИдИдРодительскойГруппы(ТекРодитель,ДеревоСхемы,СтрТаблицаСтрок,МассивУсловий)
	ИдРодительскойГруппы = СтрТаблицаСтрок.ИдРодительскойГруппы;
	
	Если ИдРодительскойГруппы = -1 Тогда
		Если МассивУсловий.Количество() > 0 Тогда
			РодительУсловие = МассивУсловий[МассивУсловий.Количество()-1];
			Возврат РодительУсловие.Строки[РодительУсловие.Строки.Количество()-1];
		КонецЕсли;	 
		
		Возврат ДеревоСхемы;
	КонецЕсли;	 
	
	СтрокаДерева = ДеревоСхемы.Строки.Найти(ИдРодительскойГруппы,"ИдСтрокиТаблицаСтрок",Истина);
	Если СтрокаДерева = Неопределено Тогда
		ВызватьИсключение СтрШаблон(НСтр("ru = 'Не найдена группа сценария по ИД: %1 для строки сценария %2'"),
		                            ИдРодительскойГруппы,СтрТаблицаСтрок.СтрокаСокр);
	КонецЕсли;	 
	
	Если МассивУсловий.Количество() > 0 Тогда
		РодительУсловие = МассивУсловий[МассивУсловий.Количество()-1];
		Если РодительУсловие.ИдСтрокиТаблицаСтрок > СтрокаДерева.ИдСтрокиТаблицаСтрок Тогда
			Возврат РодительУсловие.Строки[РодительУсловие.Строки.Количество()-1];
		КонецЕсли;	 
	КонецЕсли;	 
	
	Возврат СтрокаДерева;
КонецФункции	

Процедура ЗаполнитьПараметрыДляДанногоЭлемента(Строка,ТекЭлемент,ИзменяемыеПараметрыСценария,ЭтоУсловие,
	   КешПараметровСтроки,ДопПараметры = Неопределено)
	ОбработаннаяСтрокаПараметров = Строка;
	Если ЭтоУсловие Тогда
		ТекЭлемент.Параметры = ПараметрыУсловия(ОбработаннаяСтрокаПараметров,ТекЭлемент,ИзменяемыеПараметрыСценария);
	Иначе	
		ТекЭлемент.Параметры = ПараметрыGherkinИзСтрокиИПреобразоватьСтрокуДляРаботыСПараметрами(
		                                  ОбработаннаяСтрокаПараметров,ИзменяемыеПараметрыСценария,КешПараметровСтроки,,ДопПараметры);
	КонецЕсли;	 
	ТекЭлемент.ОбработаннаяСтрокаПараметров = ОбработаннаяСтрокаПараметров;
КонецПроцедуры

Функция РодительЭлемента(ТекЭлемент,ДеревоСхемы,СтрТаблицаСтрок,ТаблицаСтрок,МассивУсловий)
	
	Если ТипЗнч(ТекЭлемент) = Тип("ДеревоЗначений") Тогда
		Возврат ТекЭлемент;
	КонецЕсли;	 
	
	Если СтрТаблицаСтрок.ИдРодительскойГруппы <> -1 Тогда
		СтрокаДерева = ДеревоСхемы.Строки.Найти(СтрТаблицаСтрок.ИдРодительскойГруппы,"ИдСтрокиТаблицаСтрок",Истина);
		Если СтрокаДерева <> Неопределено Тогда
			Если МассивУсловий.Количество() > 0 Тогда
				ТекУсловие = МассивУсловий[МассивУсловий.Количество()-1];
				Если ТекУсловие.НомерСтрокиТекстаСценария > СтрокаДерева.НомерСтрокиТекстаСценария Тогда
					СтрокаДерева = ТекУсловие.Строки[0];
				КонецЕсли;	 
			КонецЕсли;	 
			
			Возврат СтрокаДерева;
		КонецЕсли;	 
	ИначеЕсли МассивУсловий.Количество() > 0 Тогда
		ТекУсловие = МассивУсловий[МассивУсловий.Количество()-1];
		НижняяСекцияУсловия = ТекУсловие.Строки[ТекУсловие.Строки.Количество()-1];
		Возврат НижняяСекцияУсловия;
	Иначе
		Возврат ДеревоСхемы;
	КонецЕсли;	 
	
КонецФункции	

Функция ПервыйЭлементДереваПодчиненВторому(Элемент1,Элемент2)
	Если ТипЗнч(Элемент1) = Тип("ДеревоЗначений") Тогда
		ТекРодитель = Элемент1;
	Иначе	
		ТекРодитель = Элемент1.Родитель;
	КонецЕсли;	 
	
	Пока ТекРодитель <> Неопределено Цикл
		Если ТекРодитель = Элемент2 Тогда
			Возврат Истина;
		КонецЕсли;	 
		
		Если ТипЗнч(ТекРодитель) = Тип("ДеревоЗначений") Тогда
			Возврат Ложь;
		КонецЕсли;	
		
		ТекРодитель = ТекРодитель.Родитель;
	КонецЦикла;	
	
	Возврат Ложь;
КонецФункции	                             

Функция ЭтоВложенныйСценарийШаблон(СтрТаблицаСтрок,ТаблицаШаблоновСценариев,СтрокаШаблона)
	Если Не ЗначениеЗаполнено(СтрТаблицаСтрок.СнипетНРег) Тогда
		Возврат Ложь;
	КонецЕсли;	  
	
	СтрокиТаблицаШаблоновСценариев = ТаблицаШаблоновСценариев.НайтиСтроки(
	    Новый Структура("СнипетНРег",СтрТаблицаСтрок.СнипетНРег));
	Если СтрокиТаблицаШаблоновСценариев.Количество() = 0 Тогда
		Возврат Ложь;
	КонецЕсли;	 
	
	Если СтрокиТаблицаШаблоновСценариев.Количество() > 1 Тогда
		СтрокаСценариев = Символы.ПС;
		Для Каждого Элем Из СтрокиТаблицаШаблоновСценариев Цикл
			СтрокаСценариев = СтрокаСценариев + СтрШаблон(НСтр("ru = '%1, код %2'"), Элем.Наименование, Элем.Код) + Символы.ПС;
		КонецЦикла;	
		
		ВызватьИсключение СтрШаблон(
		   НСтр("ru = 'Найдено несколько сценариев подходящих для вызова: %1. %2'"),СтрокаСценариев, Символы.ПС + "Общая ошибка.");
	КонецЕсли;	 
	
	СтрокаШаблона = СтрокиТаблицаШаблоновСценариев[0];
	
	Возврат Истина;
КонецФункции	

Функция СтруктураПараметровДляПолученияВложенногоСценария(ИсходнаяСтруктураПараметров,UID,ИмяСценария)
	СтруктураПараметров = Новый Структура;
	
	Если ИсходнаяСтруктураПараметров.Свойство("ДанныеСценариев") Тогда
		ДанныеСценариев = ИсходнаяСтруктураПараметров.ДанныеСценариев;
	Иначе	
		ДанныеСценариев = ИсходнаяСтруктураПараметров.СтруктураПараметров.ДанныеСценариев;
	КонецЕсли;	 
	
	
	СтрокаДанныеСценариев = ДанныеСценариев.Найти(UID,"UID");
	Если СтрокаДанныеСценариев = Неопределено Тогда
		ВызватьИсключение СтрШаблон(
		   НСтр("ru = 'Не найдены данные сценария UID: %1. Имя %2'"),UID,ИмяСценария);
	КонецЕсли;
	   
	СтруктураПараметров.Вставить("ЧтениеИзТекста",ИсходнаяСтруктураПараметров.ЧтениеИзТекста);   
	СтруктураПараметров.Вставить("UIDСценария",СтрокаДанныеСценариев.UID);   
	СтруктураПараметров.Вставить("ДанныеСценариев",ДанныеСценариев);
	СтруктураПараметров.Вставить("Проект",СтрокаДанныеСценариев.Проект);   
	СтруктураПараметров.Вставить("UIDФункцияСистемы",СтрокаДанныеСценариев.UIDФункцияСистемы);   
	СтруктураПараметров.Вставить("ПараметрыСценария",СтрокаДанныеСценариев.ПараметрыСценария);   
	СтруктураПараметров.Вставить("ВложенныеСценарии",СтрокаДанныеСценариев.ВложенныеСценарии);   
	
	Возврат СтруктураПараметров;
КонецФункции	

Процедура ЗагрузитьСценарийИзШаблона(СтрокаШаблона,ДеревоСхемы,ТаблицаШаблоновСценариев,ИсходныйСценарий,СтруктураПараметров)
	Если СтрокаШаблона.ДанныеШаблона = Неопределено Тогда
		СтрокаШаблона.ДанныеШаблона = Новый Структура;
		СтрокаШаблона.ДанныеШаблона.Вставить("ДеревоШаблона",Неопределено);
		СтрокаШаблона.ДанныеШаблона.Вставить("ПараметрыВходящие",Неопределено);
	КонецЕсли;	 
	
	ДанныеШаблона = СтрокаШаблона.ДанныеШаблона;
	
	Если ДанныеШаблона.ДеревоШаблона = Неопределено Тогда
		Если СтруктураПараметров.ЧтениеИзТекста Тогда
			СтруктураПараметровДляПолученияВложенногоСценария = 
			     СтруктураПараметровДляПолученияВложенногоСценария(СтруктураПараметров,
				     СтрокаШаблона.UID,СтрокаШаблона.Наименование);
					 
					 
			Попытка
				ДанныеШаблона.ДеревоШаблона = ДеревоСценарияИзБазыДанных(СтрокаШаблона.Наименование,
				      СтруктураПараметровДляПолученияВложенногоСценария);
			Исключение
				ИнфоОбОшибке = ИнформацияОбОшибке();
				КраткоеПредставлениеОшибки = КраткоеПредставлениеОшибки(ИнфоОбОшибке);
				ВызватьИсключение СтрШаблон(
					НСтр("ru = 'Не получилось скомпилировать сценарий <%1>, код <%2>. Строка №%3. Подробная информация об ошибке: %4'"),
					ИсходныйСценарий,СтруктураПараметров.КодСценария,
					ДеревоСхемы.НомерСтрокиТекстаСценария,Символы.ПС + КраткоеПредставлениеОшибки);
			КонецПопытки;
			
				  
				  
			ДанныеСценариев = СтруктураПараметровДляПолученияВложенногоСценария.ДанныеСценариев;	  
			СтрокаДанныеСценариев = ДанныеСценариев.Найти(СтрокаШаблона.UID,"UID");
			ДанныеШаблона.ПараметрыВходящие = СтрокаДанныеСценариев.ПараметрыСценария;
		Иначе
			ДанныеШаблона.ДеревоШаблона     = ДеревоСценарияИзБазыДанных(СтрокаШаблона.Ссылка,СтруктураПараметров);
			ДанныеШаблона.ПараметрыВходящие = ПараметрыВходящиеСценария(СтрокаШаблона.Ссылка);
		КонецЕсли;	 
		
	КонецЕсли;	 
	
	СкопироватьДеревоШаблонаВДеревоСценария(СтрокаШаблона,СтрокаШаблона.ДанныеШаблона.ДеревоШаблона,
	                                        ДеревоСхемы,ИсходныйСценарий,Ложь);
	
КонецПроцедуры

Процедура ДобавитьСлужебныеЭлементыДляГруппШагов(Дерево)
	Для Каждого СтрокаДерева Из Дерево.Строки Цикл
		
		Если СтрокаДерева.ТипЭлемента = ТипЭлементаШаблонСценария()  Тогда
			Продолжить;
		КонецЕсли;	 
		
		Если СтрокаДерева.ТипЭлемента = ТипЭлементаГруппаШагов() Тогда
			СтрокаСтарт                      = СтрокаДерева.Строки.Вставить(0);
			СтрокаСтарт.ОписаниеЭлемента     = ОписаниеЭлементаСтарт();
			СтрокаСтарт.ТипЭлемента          = ТипЭлементаНачалоСхемы();
			
			СтрокаСтоп                       = СтрокаДерева.Строки.Вставить(СтрокаДерева.Строки.Количество());
			СтрокаСтоп.ОписаниеЭлемента      = ОписаниеЭлементаСтоп();
			СтрокаСтоп.ТипЭлемента           = ТипЭлементаОкончаниеСхемы();
		КонецЕсли;	 
		
		ДобавитьСлужебныеЭлементыДляГруппШагов(СтрокаДерева);
	КонецЦикла;	
КонецПроцедуры

Функция НомерПараметраВСценарии(ВсеПараметрыСценария,ПроверяемоеЗначениеПараметра)
	НомерПараметра = -1;
	Если ВсеПараметрыСценария = Неопределено Тогда
		Возврат НомерПараметра;
	КонецЕсли;	 
	
	
	Для Каждого ЗначениеПараметраСценария Из ВсеПараметрыСценария Цикл
		НомерПараметра = НомерПараметра + 1;
		
		Если НРег(ЗначениеПараметраСценария) = НРег(ПроверяемоеЗначениеПараметра) Тогда
			Возврат НомерПараметра;
		КонецЕсли;	 
	КонецЦикла;	
	
	ВсеПараметрыСценария.Добавить(ПроверяемоеЗначениеПараметра);
	
	Возврат НомерПараметра + 1;
КонецФункции	

Функция  ЭтотПараметрЯвляетсяТолькоЗначением(ЗначениеПараметра)
	Возврат Ложь;
КонецФункции

Процедура ВыровнятьТаблицуGherkin(ТаблицаGherkin,ТаблицаСтрок)
	Если ТаблицаGherkin.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;	
	
	КолЗначенийВТаблице = 0;
	ЗначенияТаблицы     = Новый Массив;
	Для Каждого СтрокаТаблицаGherkin Из ТаблицаGherkin Цикл
		ТекМассивДлин = Новый Массив;
		
		Стр = СтрокаТаблицаGherkin.СтрокаСокр;
		Стр = Сред(Стр,2);
		Стр = Лев(Стр,СтрДлина(Стр)-1);
		Стр = СокрЛП(Стр);
		
		
		Стр = СтрЗаменить(Стр,"\|","~ПредставлениеВертикальнойЧерты~");
		МассивЗначенийСтроки = СтрРазделить(Стр,"|");

		КолЗначенийВДаннойСтроке = МассивЗначенийСтроки.Количество();
		
		Если КолЗначенийВТаблице < КолЗначенийВДаннойСтроке Тогда
			КолЗначенийВТаблице = КолЗначенийВДаннойСтроке;
		КонецЕсли;	 
		
		
		Для Каждого ЗначениеТаблицы Из МассивЗначенийСтроки Цикл
			ТекЗначение = СокрЛП(ЗначениеТаблицы);
			ТекЗначение = СтрЗаменить(ТекЗначение,"~ПредставлениеВертикальнойЧерты~","\|");
			Если Лев(ТекЗначение,1) = "'" Тогда
				Если Прав(ТекЗначение,1) <> "'" Тогда
					ТекЗначение = ТекЗначение + "'";
				КонецЕсли;	 
			ИначеЕсли Лев(ТекЗначение,1) = """" Тогда
				Если Прав(ТекЗначение,1) <> """" Тогда
					ТекЗначение = ТекЗначение + """";
				КонецЕсли;	 
			КонецЕсли;	 
			
			
			ТекМассивДлин.Добавить(Новый Структура("Стр,Длина",ТекЗначение,СтрДлина(ТекЗначение)));
		КонецЦикла;
		
		ЗначенияТаблицы.Добавить(ТекМассивДлин);
	КонецЦикла;	
	
	//добавим пустые значения, если в строках таблицы разное количество строк
	Для Каждого СтрокаЗначенияТаблицы Из ЗначенияТаблицы Цикл
		Пока СтрокаЗначенияТаблицы.Количество() < КолЗначенийВТаблице Цикл
			СтрокаЗначенияТаблицы.Добавить(Новый Структура("Стр,Длина","",0));
		КонецЦикла;	
	КонецЦикла;	
	
	МаксДлинаКолонок = Новый Массив;
	Для Ккк = 1 По КолЗначенийВТаблице Цикл
		МаксДлина = 0;
		Для Каждого СтрокаЗначенияТаблицы Из ЗначенияТаблицы Цикл
			Если МаксДлина < СтрокаЗначенияТаблицы[Ккк-1].Длина Тогда
				МаксДлина = СтрокаЗначенияТаблицы[Ккк-1].Длина;
			КонецЕсли;	 
		КонецЦикла;	
		
		МаксДлинаКолонок.Добавить(МаксДлина);
	КонецЦикла;	
	
	
	//собираем таблицу
	Ид = -1;
	Для Каждого СтрокаЗначенияТаблицы Из ЗначенияТаблицы Цикл
		Ид = Ид + 1;
		
		Стр = "|";
		Для Ккк = 1 По КолЗначенийВТаблице Цикл
			НужнаяДлина = МаксДлинаКолонок[Ккк-1];
			
			ТекЗначение = СтрокаЗначенияТаблицы[Ккк-1].Стр;
			
			Пока СтрДлина(ТекЗначение) < НужнаяДлина Цикл
				ТекЗначение = ТекЗначение + " ";
			КонецЦикла;	
			
			Стр = Стр + " " + ТекЗначение + " |";
		КонецЦикла;
		
		ТаблицаGherkin[Ид].СтрокаСокр = Стр;
		НомерСтрокиТаблицаСтрок = ТаблицаGherkin[Ид].НомерСтроки;
		ТаблицаСтрок[НомерСтрокиТаблицаСтрок].СтрокаСокр = Стр;
	КонецЦикла;	
КонецПроцедуры

Функция МассивПервыхСловGherkin()
	Массив = Новый Массив;
	
	//русский язык
	Массив.Добавить("И");
	Массив.Добавить("Когда");
	Массив.Добавить("Тогда");
	Массив.Добавить("Затем");
	Массив.Добавить("Дано");
	Массив.Добавить("Допустим");
	Массив.Добавить("Если");
	Массив.Добавить("ИначеЕсли");
	Массив.Добавить("Иначе");
	Массив.Добавить("Пусть");
	Массив.Добавить("То");
	Массив.Добавить("К тому же");
	Массив.Добавить("Также");
	Массив.Добавить("Но");
	Массив.Добавить("А");
	
	//английский язык
	Массив.Добавить("Given");
	Массив.Добавить("When");
	Массив.Добавить("Then");
	Массив.Добавить("And");
	Массив.Добавить("But");
	Массив.Добавить("If");
	Массив.Добавить("ElseIf");
	Массив.Добавить("Else");
	
	Возврат Массив;
КонецФункции	

Функция СкопироватьПростойМассив(Оригинал)
	Массив = Новый Массив;
	Для Каждого Элем Из Оригинал Цикл
		Массив.Добавить(Элем);
	КонецЦикла;	
	
	Возврат Массив;
КонецФункции	

Функция ПараметрыУсловия(Стр,ТекЭлемент,ИзменяемыеПараметрыСценария = Неопределено)
	
	ЗначениеУсловия = Стр;
	ЭтоПрепроцессор = Ложь;
	Если ТекЭлемент.ТипЭлемента = ТипЭлементаНачалоУсловияПрепроцессор() Тогда
		ЭтоПрепроцессор = Истина;
	ИначеЕсли ТекЭлемент.ТипЭлемента = ТипЭлементаИначеЕслиПрепроцессор() Тогда
		ЭтоПрепроцессор = Истина;
	КонецЕсли;	 
	
	МассивСлов = СтрРазделить(ЗначениеУсловия," ",Ложь);
	Если ЭтоПрепроцессор И МассивСлов.Количество() = 1 Тогда
		МассивПараметров = ПараметрыGherkinИзСтрокиИПреобразоватьСтрокуДляРаботыСПараметрами(Стр,ИзменяемыеПараметрыСценария);
		Если МассивПараметров.Количество() = 0 Тогда
			ВызватьИсключение СтрШаблон(
				НСтр("ru = 'Неверно были получены параметры из строки ""%1"". Должен был быть получен 1 параметр, а получили %2'"),
				ЗначениеУсловия,МассивПараметров.Количество());
		ИначеЕсли МассивПараметров.Количество() <> 1 Тогда
			ВызватьИсключение СтрШаблон(
				НСтр("ru = 'Неверно были получены параметры из строки ""%1"". Должен был быть получен 1 параметр, а получили %2'"),
				ЗначениеУсловия,МассивПараметров.Количество());
		КонецЕсли;	 
		
		СтруктураПараметра = МассивПараметров[0];
		СтруктураПараметра.Вставить("СвязанСФО",Ложь);
		СтруктураПараметра.Вставить("ИмяПараметра",СтруктураПараметра.ЗначениеПараметра);
	ИначеЕсли ЭтоПрепроцессор И МассивСлов.Количество() = 2 Тогда
		Если НРег(МассивСлов[0]) = "фо" Тогда
			Стр              = МассивСлов[1];
			МассивПараметров = ПараметрыGherkinИзСтрокиИПреобразоватьСтрокуДляРаботыСПараметрами(Стр,ИзменяемыеПараметрыСценария);
			Если МассивПараметров.Количество() = 0 Тогда
				ВызватьИсключение СтрШаблон(
					НСтр("ru = 'Неверно были получены параметры из строки ""%1"". Должен был быть получен 1 параметр, а получили %2'"),
					ЗначениеУсловия,МассивПараметров.Количество());
			ИначеЕсли МассивПараметров.Количество() <> 1 Тогда
				ВызватьИсключение СтрШаблон(
					НСтр("ru = 'Неверно были получены параметры из строки ""%1"". Должен был быть получен 1 параметр, а получили %2'"),
					ЗначениеУсловия,МассивПараметров.Количество());
			КонецЕсли;
			
			СтруктураПараметра = МассивПараметров[0];
			СтруктураПараметра.Вставить("СвязанСФО",Истина);
			
			Стр = "ФО " + Стр;
		Иначе
			МассивПараметров = ПараметрыGherkinИзСтрокиИПреобразоватьСтрокуДляРаботыСПараметрами(Стр,ИзменяемыеПараметрыСценария);
		КонецЕсли;	 
	Иначе
		МассивПараметров = ПараметрыGherkinИзСтрокиИПреобразоватьСтрокуДляРаботыСПараметрами(Стр,ИзменяемыеПараметрыСценария);
	КонецЕсли;	 
	
	Для Каждого Элем Из МассивПараметров Цикл
		Элем.Вставить("ИмяПараметра",Элем.ЗначениеПараметра);
		Если Не Элем.Свойство("СвязанСФО") Тогда
			Элем.Вставить("СвязанСФО",Ложь);
		КонецЕсли;	 
	КонецЦикла;
	
	УпорядочитьМассивПараметров(Стр,МассивПараметров);
	
	ЭкранироватьУгловыеСкобки(Стр);
	
	Возврат МассивПараметров;
КонецФункции	

Функция ИменаВходящихПараметров(ТаблицаПараметров)
	Массив = Новый Массив;
	
	Для Каждого СтрокаТаблицаПараметров Из ТаблицаПараметров Цикл
		Массив.Добавить(НРег(СтрокаТаблицаПараметров.Имя));
	КонецЦикла;	
	
	Возврат Массив;
КонецФункции	 

Процедура СкопироватьДеревоШаблонаВДеревоСценария(СтрокаШаблона,ДеревоОткуда,ДеревоКуда,ИсходныйСценарий,КопироватьСтроки)
	Если ДеревоКуда.ДополнительныеСвойства = Неопределено Тогда
		ДеревоКуда.ДополнительныеСвойства = Новый Структура;
	КонецЕсли;	 
	
	Если Не ДеревоКуда.ДополнительныеСвойства.Свойство("Шаблон") Тогда
		Если СтрокаШаблона.ЧтениеИзТекста Тогда
			ДеревоКуда.ДополнительныеСвойства.Вставить("Шаблон",СтрокаШаблона.Наименование);
			ДеревоКуда.ДополнительныеСвойства.Вставить("Код",СтрокаШаблона.Код);
			ДеревоКуда.ДополнительныеСвойства.Вставить("UID",СтрокаШаблона.UID);
		Иначе	
			ДеревоКуда.ДополнительныеСвойства.Вставить("Шаблон",СтрокаШаблона.Ссылка);
			ДеревоКуда.ДополнительныеСвойства.Вставить("ИмяПодсценария",СтрокаШаблона.Наименование);
		КонецЕсли;	 
		ДеревоКуда.ДополнительныеСвойства.Вставить("ПараметрыВходящие",
		ИменаВходящихПараметров(СтрокаШаблона.ДанныеШаблона.ПараметрыВходящие));
		ДеревоКуда.ДополнительныеСвойства.Вставить("ЧтениеИзТекста",СтрокаШаблона.ЧтениеИзТекста);
	КонецЕсли;	 
	
	Для Каждого СтрокаОткуда Из ДеревоОткуда.Строки Цикл
		Если СтрокаОткуда.ТипЭлемента = ТипЭлементаКомментарий() Тогда
			Продолжить;
		ИначеЕсли СтрокаОткуда.ТипЭлемента = ТипЭлементаБлочныйКомментарий() Тогда
			Продолжить;
		КонецЕсли;	 
		
		Если СтрокаОткуда.ТипЭлемента = ТипЭлементаШаблонСценария() Тогда
			Если СтрокаОткуда.ДополнительныеСвойства.Шаблон = ИсходныйСценарий Тогда
					ВызватьИсключение СтрШаблон(
					       НСтр("ru = 'Зацикливание при вызове сценария: %1'"),ИсходныйСценарий);
			КонецЕсли;	 

		КонецЕсли;	 
	КонецЦикла;	
	
	Если НЕ КопироватьСтроки Тогда
		Возврат;
	КонецЕсли;	 
	
	Для Каждого СтрокаОткуда Из ДеревоОткуда.Строки Цикл
		Если СтрокаОткуда.ТипЭлемента = ТипЭлементаКомментарий() Тогда
			Продолжить;
		ИначеЕсли СтрокаОткуда.ТипЭлемента = ТипЭлементаБлочныйКомментарий() Тогда
			Продолжить;
		КонецЕсли;	 
		
		Если СтрокаОткуда.ТипЭлемента = ТипЭлементаШаблонСценария() Тогда
			Если СтрокаОткуда.ДополнительныеСвойства.Шаблон = ИсходныйСценарий Тогда
					ВызватьИсключение СтрШаблон(
					       НСтр("ru = 'Зацикливание при вызове сценария: %1'"),ИсходныйСценарий);
			КонецЕсли;	 
		КонецЕсли;	 
		
		Если ДеревоКуда.ДополнительныеСвойства = Неопределено Тогда
			ДеревоКуда.ДополнительныеСвойства = Новый Структура;
		КонецЕсли;	 
		
		Если Не ДеревоКуда.ДополнительныеСвойства.Свойство("Шаблон") Тогда
			Если СтрокаШаблона.ЧтениеИзТекста Тогда
				ДеревоКуда.ДополнительныеСвойства.Вставить("Шаблон",СтрокаШаблона.Наименование);
				ДеревоКуда.ДополнительныеСвойства.Вставить("Код",СтрокаШаблона.Код);
				ДеревоКуда.ДополнительныеСвойства.Вставить("UID",СтрокаШаблона.UID);
			Иначе	
				ДеревоКуда.ДополнительныеСвойства.Вставить("Шаблон",СтрокаШаблона.Ссылка);
				ДеревоКуда.ДополнительныеСвойства.Вставить("ИмяПодсценария",СтрокаШаблона.Наименование);
			КонецЕсли;	 
			ДеревоКуда.ДополнительныеСвойства.Вставить("ПараметрыВходящие",
				   ИменаВходящихПараметров(СтрокаШаблона.ДанныеШаблона.ПараметрыВходящие));
			ДеревоКуда.ДополнительныеСвойства.Вставить("ЧтениеИзТекста",СтрокаШаблона.ЧтениеИзТекста);
		КонецЕсли;	 
		
		СтрокаКуда = ДеревоКуда.Строки.Добавить();
		
		ЗаполнитьЗначенияСвойств(СтрокаКуда,СтрокаОткуда);
		Если СтрокаКуда.Комментарии <> Неопределено Тогда
			СтрокаКуда.Комментарии.Очистить();
		КонецЕсли;	 
		
		СтрокаКуда.ЭтаСтрокаВложенногоСценария = Истина;
		
		Если СтрокаКуда.ДополнительныеСвойства = Неопределено Тогда
			СтрокаКуда.ДополнительныеСвойства = Новый Структура;
		КонецЕсли;	 
		
		Если Не СтрокаКуда.ДополнительныеСвойства.Свойство("Шаблон") Тогда
			СтрокаКуда.ДополнительныеСвойства.Вставить("Шаблон",СтрокаШаблона.Ссылка);
			СтрокаКуда.ДополнительныеСвойства.Вставить("ИмяПодсценария",СтрокаШаблона.Наименование);
		КонецЕсли;	 
	КонецЦикла;	
КонецПроцедуры

Функция СоздатьТекстФорматированногоДокументаПоДеревуСхемы(ДеревоСхемы,ДополнительныеПараметры)
	ТаблицаСтрок = Новый ТаблицаЗначений;
	ТаблицаСтрок.Колонки.Добавить("Строка");
	ТаблицаСтрок.Колонки.Добавить("СтрокаСокр");
	ТаблицаСтрок.Колонки.Добавить("ТипЭлемента");
	ТаблицаСтрок.Колонки.Добавить("Отступ");
	ТаблицаСтрок.Колонки.Добавить("ОбработаннаяСтрокаПараметров");
	ТаблицаСтрок.Колонки.Добавить("Параметры");
	ТаблицаСтрок.Колонки.Добавить("Комментарии");
	ТаблицаСтрок.Колонки.Добавить("Теги");
	
	ДелатьРаскраску = ДополнительныеПараметры.ДелатьРаскраску;
	
	Уровень = -1;
	РекурсивныйОбходДереваСхемыДляПолученияТекста(ДеревоСхемы,ТаблицаСтрок,Уровень);
	
	Если ДелатьРаскраску Тогда
		ОбработатьВыделениеЦветомУсловияЕслиИЦиклы(ТаблицаСтрок);
		ОбработатьВыделениеЦветомМеток(ТаблицаСтрок);
		ОтформатироватьГруппыШагов(ТаблицаСтрок);
		ОтформатироватьТекстGherkin(ТаблицаСтрок);
		ОбработатьВыделениеЦветомШаблонСценария(ТаблицаСтрок);
	КонецЕсли;	 
	
	ОтступУСтрокИСпецСимволы(ТаблицаСтрок,ДополнительныеПараметры);
	
	МассивСтрок  = ТаблицаСтрок.ВыгрузитьКолонку("Строка");
	
	Возврат СтрСоединить(МассивСтрок,Символы.ПС);
КонецФункции	

Процедура РекурсивныйОбходДереваСхемыДляПолученияТекста(Дерево,ТаблицаСтрок,Уровень)
	Для Каждого СтрокаДерева Из Дерево.Строки Цикл
		
		Если СтрокаДерева.Теги <> Неопределено Тогда
			Отступ = (Уровень+1)*8;
			ВставитьТегиВТаблицуСтрок(ТаблицаСтрок,СтрокаДерева.Теги,СтрокаДерева,Отступ);
		КонецЕсли;	 
		
		Если СтрокаДерева.Комментарии <> Неопределено Тогда
			ВставитьКомментарииВТаблицуСтрок(ТаблицаСтрок,СтрокаДерева.Комментарии,СтрокаДерева);
		КонецЕсли;	 
		
		Если СтрокаДерева.ТипЭлемента = ТипЭлементаНачалоСхемы() Тогда
			Продолжить;
		ИначеЕсли СтрокаДерева.ТипЭлемента = ТипЭлементаОкончаниеСхемы() Тогда
			Продолжить;
		ИначеЕсли СтрокаДерева.Родитель <> Неопределено И СтрокаДерева.Родитель.ТипЭлемента = ТипЭлементаШаблонСценария() Тогда
			Если СтрокаДерева.ТипЭлемента = ТипЭлементаПередачаПараметров() Тогда
				СтрТаблицаСтрок                              = ТаблицаСтрок.Добавить();
				Отступ                                       = (Уровень+1)*8;
				СтрТаблицаСтрок.Отступ                       = Отступ;
				СтрТаблицаСтрок.ТипЭлемента                  = СтрокаДерева.ТипЭлемента;
				
				СтрТаблицаСтрок.СтрокаСокр = СтрокаДерева.ОписаниеЭлемента;
				СтрТаблицаСтрок.Строка     = СтрокаДерева.ОписаниеЭлемента;
				СтрТаблицаСтрок.Параметры  = СтрокаДерева.Параметры;
				Поз1 = Найти(СтрокаДерева.ОписаниеЭлемента,"=");
				Поз2 = Найти(СтрокаДерева.ОбработаннаяСтрокаПараметров,"=");
				
				СтрТаблицаСтрок.ОбработаннаяСтрокаПараметров = Лев(СтрокаДерева.ОписаниеЭлемента,Поз1-1) + Сред(СтрокаДерева.ОбработаннаяСтрокаПараметров,Поз2);
				
				ПередачаТаблицы = Ложь;
				Если ТипЗнч(СтрокаДерева.ДополнительныеСвойства) = Тип("Структура") Тогда
					Если СтрокаДерева.ДополнительныеСвойства.Свойство("ПередачаТаблицы") Тогда
						ПередачаТаблицы = СтрокаДерева.ДополнительныеСвойства.ПередачаТаблицы;
					КонецЕсли;	 
				КонецЕсли;	 
				
				Если ПередачаТаблицы Тогда
					СтрТаблицаСтрок.ОбработаннаяСтрокаПараметров = СтрокаДерева.ОписаниеЭлемента;
					Для Каждого СтрокаПараметры Из СтрокаДерева.Родитель.Параметры Цикл
						Если СтрокаПараметры.Тип = ТипПараметра("Таблица") Тогда
							Если СтрокаПараметры.ИмяПараметра = СтрокаДерева.ДополнительныеСвойства.ИмяПараметра Тогда
								Ид = -1;
								Для Каждого Стр Из СтрокаПараметры.ЗначениеПараметра Цикл
									Ид = Ид + 1;
									
									СтрТаблицаСтрок                              = ТаблицаСтрок.Добавить();
									Отступ                                       = (Уровень+2)*8;
									СтрТаблицаСтрок.Отступ                       = Отступ;
									СтрТаблицаСтрок.ТипЭлемента                  = ТипЭлементаТаблица();
									
									ЗначениеПараметраПодстановкаЗначений = СтрокаПараметры.ЗначениеПараметраПодстановкаЗначений[Ид];
									
									СтрТаблицаСтрок.ОбработаннаяСтрокаПараметров = ЗначениеПараметраПодстановкаЗначений.ОбработаннаяСтрокаПараметров;
									СтрТаблицаСтрок.Параметры = ЗначениеПараметраПодстановкаЗначений.Параметры;
									
									СтрТаблицаСтрок.СтрокаСокр = Стр;
									СтрТаблицаСтрок.Строка     = Стр;
								КонецЦикла;	 
							КонецЕсли;	 
						КонецЕсли;	 
					КонецЦикла;	 
				КонецЕсли;	 
				
			КонецЕсли;	 

			Продолжить;
		КонецЕсли;	 
		
		Если СтрокаДерева.ТипЭлемента = ТипЭлементаУсловиеПрепроцессор()
			или СтрокаДерева.ТипЭлемента = ТипЭлементаУсловие()
			или СтрокаДерева.ТипЭлемента = ТипЭлементаУсловиеEng()
			или СтрокаДерева.ТипЭлемента = ТипЭлементаЦикл()
			или СтрокаДерева.ТипЭлемента = ТипЭлементаЦиклEng()
			Тогда
			
		Иначе	
			Уровень = Уровень + 1;
			
			СтрТаблицаСтрок                              = ТаблицаСтрок.Добавить();
			Отступ                                       = Уровень*8;
			Если СтрокаДерева.ТипЭлемента = ТипЭлементаТаблица() Тогда
				Отступ = (Уровень+1)*8;
			КонецЕсли;	 
			
			СтрТаблицаСтрок.Отступ                       = Отступ;
			
			Если (СтрокаДерева.ТипЭлемента = ТипЭлементаКомментарий())
				или (СтрокаДерева.ТипЭлемента = ТипЭлементаБлочныйКомментарий())
				или (СтрокаДерева.ТипЭлемента = ТипЭлементаБлочныйКомментарийНачало())
				или (СтрокаДерева.ТипЭлемента = ТипЭлементаБлочныйКомментарийОкончание()) Тогда
				
				Если Найти(СтрокаДерева.ОписаниеЭлемента,"=") = 0 или Уровень = 0 Тогда
					СтрТаблицаСтрок.Строка = СтрокаДерева.ОписаниеЭлемента; //комментарии не трогаем
					СтрТаблицаСтрок.Отступ = 0;
				Иначе	
					//значит это передача параметра снизу вызова подсценария
					Родитель = СтрокаДерева.Родитель;
					Если Родитель <> Неопределено И Родитель.ТипЭлемента <> ТипЭлементаГруппаШагов() Тогда
						СтрТаблицаСтрок.Строка = СтрокаДерева.ОписаниеЭлемента; //комментарии не трогаем
						СтрТаблицаСтрок.Отступ = 0;
					Иначе	
						Отступ = (Уровень+1)*8;
						СтрТаблицаСтрок.Строка = ПолучитьСтрокуОтступПоЧислуПробелов(Отступ) + СокрЛП(СтрокаДерева.ОписаниеЭлемента);
						СтрТаблицаСтрок.Отступ = Отступ;
					КонецЕсли;	 
				КонецЕсли;	 
				
			Иначе	
				СтрТаблицаСтрок.Строка = ПолучитьСтрокуОтступПоЧислуПробелов(Отступ) + СокрЛП(СтрокаДерева.ОписаниеЭлемента);
			КонецЕсли;	 
			
			СтрТаблицаСтрок.СтрокаСокр                   = СтрокаДерева.ОписаниеЭлемента;
			СтрТаблицаСтрок.ОбработаннаяСтрокаПараметров = СтрокаДерева.ОбработаннаяСтрокаПараметров;
			СтрТаблицаСтрок.Параметры                    = СтрокаДерева.Параметры;
			СтрТаблицаСтрок.ТипЭлемента                  = СтрокаДерева.ТипЭлемента;
			
			Если ТипЗнч(СтрокаДерева.ДополнительныеСвойства) = Тип("Структура") Тогда
				Если СтрокаДерева.ДополнительныеСвойства.Свойство("ПараметрыСТипомТаблицы") Тогда
					Для Каждого Элем Из СтрокаДерева.ДополнительныеСвойства.ПараметрыСТипомТаблицы Цикл
						СтрТаблицаСтрок = ТаблицаСтрок.Добавить();
						ПредставлениеПараметра = "[" + Элем + "]";
						СтрТаблицаСтрок.СтрокаСокр = ПредставлениеПараметра;
						СтрТаблицаСтрок.ТипЭлемента = ТипЭлементаПараметрТаблица();
						СтрТаблицаСтрок.Отступ = Отступ+8;
					КонецЦикла;	 
				КонецЕсли;	 
			КонецЕсли;	 
			
		КонецЕсли;	 
		
		РекурсивныйОбходДереваСхемыДляПолученияТекста(СтрокаДерева,ТаблицаСтрок,Уровень);
		
		Если СтрокаДерева.ТипЭлемента = ТипЭлементаУсловиеПрепроцессор()
			 или СтрокаДерева.ТипЭлемента = ТипЭлементаУсловие()
			 или СтрокаДерева.ТипЭлемента = ТипЭлементаУсловиеEng()
			 или СтрокаДерева.ТипЭлемента = ТипЭлементаЦикл()
			 или СтрокаДерева.ТипЭлемента = ТипЭлементаЦиклEng()
			 Тогда
			 
		Иначе	
			Уровень = Уровень - 1;
		КонецЕсли;	 
		
	КонецЦикла;	
КонецПроцедуры

Процедура ОбработатьВыделениеЦветомУсловияЕслиИЦиклы(ТаблицаСтрок)
	
	КодHTMLДляПодсветки             = "<span style=""color: #ff0000;"">";
	КодHTMLДляПодсветкиПрепроцессор = "<span style=""color: #ff0000;font-weight: bold;"">";
	
	Для Каждого СтрТаблицаСтрок Из ТаблицаСтрок Цикл
		Если СтрТаблицаСтрок.ТипЭлемента = ТипЭлементаКомментарий() Тогда
			Продолжить;
		ИначеЕсли СтрТаблицаСтрок.ТипЭлемента = ТипЭлементаБлочныйКомментарий() Тогда
			Продолжить;
		КонецЕсли;	 
		
		Стр = СтрТаблицаСтрок.СтрокаСокр;
		
		Если Стр = "" Тогда
			Продолжить;
		КонецЕсли;	 
		
		МассивСлов = СтрРазделить(Стр," ",Истина);
		
		ИДПервоеСлово = 0;
		ПервоеСлово   = МассивСлов[0];
		
		Если ПервоеСлово = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		НРег_ПервоеСлово = НРег(ПервоеСлово);
		
		ПоследнееСлово = МассивСлов[МассивСлов.Количество()-1];
		
		Если (НРег_ПервоеСлово = КлючевоеСловоЕслиПрепроцессор()) и (НРег(ПоследнееСлово) = КлючевоеСловоТогдаПрепроцессор()) Тогда
			МассивСлов = Новый Массив(3);
			МассивСлов[0] = КодHTMLДляПодсветкиПрепроцессор + "#Если</span>";
			МассивСлов[1] = СтрТаблицаСтрок.ОбработаннаяСтрокаПараметров;
			МассивСлов[2] = КодHTMLДляПодсветкиПрепроцессор + "Тогда</span>";
		ИначеЕсли НРег_ПервоеСлово = КлючевоеСловоЕсли() Тогда
			МассивСлов = Новый Массив(3);
			МассивСлов[0] = КодHTMLДляПодсветки + "Если</span>";
			МассивСлов[1] = СтрТаблицаСтрок.ОбработаннаяСтрокаПараметров;
			МассивСлов[2] = КодHTMLДляПодсветки + "Тогда</span>";
		ИначеЕсли НРег_ПервоеСлово = КлючевоеСловоЕслиEng() Тогда
			МассивСлов = Новый Массив(3);
			МассивСлов[0] = КодHTMLДляПодсветки + "If</span>";
			МассивСлов[1] = СтрТаблицаСтрок.ОбработаннаяСтрокаПараметров;
			МассивСлов[2] = КодHTMLДляПодсветки + "Then</span>";
		ИначеЕсли НРег_ПервоеСлово = КлючевоеСловоИначеЕсли() Тогда
			МассивСлов = Новый Массив(3);
			МассивСлов[0] = КодHTMLДляПодсветки + "ИначеЕсли</span>";
			МассивСлов[1] = СтрТаблицаСтрок.ОбработаннаяСтрокаПараметров;
			МассивСлов[2] = КодHTMLДляПодсветки + "Тогда</span>";
		ИначеЕсли НРег_ПервоеСлово = КлючевоеСловоИначеЕслиEng() Тогда
			МассивСлов = Новый Массив(3);
			МассивСлов[0] = КодHTMLДляПодсветки + "ElseIf</span>";
			МассивСлов[1] = СтрТаблицаСтрок.ОбработаннаяСтрокаПараметров;
			МассивСлов[2] = КодHTMLДляПодсветки + "Then</span>";
		ИначеЕсли НРег_ПервоеСлово = КлючевоеСловоИначеПрепроцессор() Тогда
			МассивСлов[ИДПервоеСлово] = КодHTMLДляПодсветкиПрепроцессор + "#Иначе</span>";
		ИначеЕсли НРег_ПервоеСлово = КлючевоеСловоИначе() Тогда
			МассивСлов[ИДПервоеСлово] = КодHTMLДляПодсветки + "Иначе</span>";
		ИначеЕсли НРег_ПервоеСлово = КлючевоеСловоИначеEng() Тогда
			МассивСлов[ИДПервоеСлово] = КодHTMLДляПодсветки + "Else</span>";
		ИначеЕсли НРег_ПервоеСлово = КлючевоеСловоИначеЕслиПрепроцессор() Тогда
			МассивСлов = Новый Массив(3);
			МассивСлов[0] = КодHTMLДляПодсветкиПрепроцессор + "#ИначеЕсли</span>";
			МассивСлов[1] = СтрТаблицаСтрок.ОбработаннаяСтрокаПараметров;
			МассивСлов[2] = КодHTMLДляПодсветкиПрепроцессор + "Тогда</span>";
		ИначеЕсли НРег_ПервоеСлово = КлючевоеСловоКонецЕслиПрепроцессор() Тогда
			МассивСлов[ИДПервоеСлово] = КодHTMLДляПодсветкиПрепроцессор + "#КонецЕсли</span>";
		ИначеЕсли НРег_ПервоеСлово = КлючевоеСловоКонецЕсли() Тогда
			МассивСлов[ИДПервоеСлово] = КодHTMLДляПодсветки + "КонецЕсли</span>";
		ИначеЕсли НРег_ПервоеСлово = КлючевоеСловоКонецЕслиEng() Тогда
			МассивСлов[ИДПервоеСлово] = КодHTMLДляПодсветки + "EndIf</span>";
		ИначеЕсли НРег_ПервоеСлово = КлючевоеСловоЦикл() Тогда
			МассивСлов = Новый Массив(2);
			МассивСлов[0] = КодHTMLДляПодсветки + "Цикл</span>";
			МассивСлов[1] = СтрТаблицаСтрок.ОбработаннаяСтрокаПараметров;
		ИначеЕсли НРег_ПервоеСлово = КлючевоеСловоЦиклEng() Тогда
			МассивСлов = Новый Массив(2);
			МассивСлов[0] = КодHTMLДляПодсветки + "Do</span>";
			МассивСлов[1] = СтрТаблицаСтрок.ОбработаннаяСтрокаПараметров;
		ИначеЕсли НРег_ПервоеСлово = КлючевоеСловоКонецЦикла() Тогда
			МассивСлов[ИДПервоеСлово] = КодHTMLДляПодсветки + "КонецЦикла</span>";
		ИначеЕсли НРег_ПервоеСлово = КлючевоеСловоКонецЦиклаEng() Тогда
			МассивСлов[ИДПервоеСлово] = КодHTMLДляПодсветки + "EndDo</span>";
		Иначе 
			Продолжить;
		КонецЕсли;	 
		
		СтрТаблицаСтрок.СтрокаСокр = СтрСоединить(МассивСлов," ");
	КонецЦикла;	
КонецПроцедуры

Процедура ОбработатьВыделениеЦветомМеток(ТаблицаСтрок)
	КодHTMLДляПодсветки = "<span style=""font-style: italic;"">";
	
	Для Каждого СтрТаблицаСтрок Из ТаблицаСтрок Цикл
		Если СтрТаблицаСтрок.ТипЭлемента = ТипЭлементаКомментарий() Тогда
			Продолжить;
		ИначеЕсли СтрТаблицаСтрок.ТипЭлемента = ТипЭлементаБлочныйКомментарий() Тогда
			Продолжить;
		КонецЕсли;	 
		
		Стр = СтрТаблицаСтрок.СтрокаСокр;
		
		Если Стр = "" Тогда
			Продолжить;
		КонецЕсли;	 
		
		Если (СтрТаблицаСтрок.ТипЭлемента = ТипЭлементаМетка()) или (СтрТаблицаСтрок.ТипЭлемента = ТипЭлементаПерейти()) Тогда
			СтрТаблицаСтрок.СтрокаСокр = КодHTMLДляПодсветки + СтрТаблицаСтрок.СтрокаСокр + "</span>";
		КонецЕсли;	 
	КонецЦикла;	
	
КонецПроцедуры

Процедура ОтформатироватьГруппыШагов(ТаблицаСтрок)
	КодHTMLДляПодсветки = "<span style=""font-style: normal;font-weight: bold;"">";
	
	Для Каждого СтрТаблицаСтрок Из ТаблицаСтрок Цикл
		Если СтрТаблицаСтрок.ТипЭлемента = ТипЭлементаКомментарий() Тогда
			Продолжить;
		ИначеЕсли СтрТаблицаСтрок.ТипЭлемента = ТипЭлементаБлочныйКомментарий() Тогда
			Продолжить;
		КонецЕсли;	 
		
		Стр = СтрТаблицаСтрок.СтрокаСокр;
		
		Если Стр = "" Тогда
			Продолжить;
		КонецЕсли;	 
		
		Если (СтрТаблицаСтрок.ТипЭлемента = ТипЭлементаГруппаШагов()) Тогда
			ЭкранироватьУгловыеСкобки(СтрТаблицаСтрок.СтрокаСокр);
			СтрТаблицаСтрок.СтрокаСокр          = КодHTMLДляПодсветки + СтрТаблицаСтрок.СтрокаСокр  + "</span>";
		КонецЕсли;	 
	КонецЦикла;	
	
КонецПроцедуры

Функция ЭтотТипЭлементаСхемыНеНадоФорматироватьПоСтандартуGherkin(ТипЭлемента)
	Если ТипЭлемента = ТипЭлементаГруппаШагов() Тогда
		Возврат Истина;
	ИначеЕсли ТипЭлемента = ТипЭлементаНачалоУсловияПрепроцессор() Тогда
		Возврат Истина;
	ИначеЕсли ТипЭлемента = ТипЭлементаИначеЕслиПрепроцессор() Тогда
		Возврат Истина;
	ИначеЕсли ТипЭлемента = ТипЭлементаИначеПрепроцессор() Тогда
		Возврат Истина;
	ИначеЕсли ТипЭлемента = ТипЭлементаКонецЕслиПрепроцессор() Тогда
		Возврат Истина;
	ИначеЕсли ТипЭлемента = ТипЭлементаНачалоУсловия() Тогда
		Возврат Истина;
	ИначеЕсли ТипЭлемента = ТипЭлементаНачалоУсловияEng() Тогда
		Возврат Истина;
	ИначеЕсли ТипЭлемента = ТипЭлементаИначеЕсли() Тогда
		Возврат Истина;
	ИначеЕсли ТипЭлемента = ТипЭлементаИначеЕслиEng() Тогда
		Возврат Истина;
	ИначеЕсли ТипЭлемента = ТипЭлементаИначе() Тогда
		Возврат Истина;
	ИначеЕсли ТипЭлемента = ТипЭлементаИначеEng() Тогда
		Возврат Истина;
	ИначеЕсли ТипЭлемента = ТипЭлементаКонецЕсли() Тогда
		Возврат Истина;
	ИначеЕсли ТипЭлемента = ТипЭлементаКонецЕслиEng() Тогда
		Возврат Истина;
	ИначеЕсли ТипЭлемента = ТипЭлементаНачалоЦикла() Тогда
		Возврат Истина;
	ИначеЕсли ТипЭлемента = ТипЭлементаНачалоЦиклаEng() Тогда
		Возврат Истина;
	ИначеЕсли ТипЭлемента = ТипЭлементаКонецЦикла() Тогда
		Возврат Истина;
	ИначеЕсли ТипЭлемента = ТипЭлементаКонецЦиклаEng() Тогда
		Возврат Истина;
	ИначеЕсли ТипЭлемента = ТипЭлементаМетка() Тогда
		Возврат Истина;
	ИначеЕсли ТипЭлемента = ТипЭлементаПерейти() Тогда
		Возврат Истина;
	ИначеЕсли ТипЭлемента = ТипЭлементаПустаяСтрока() Тогда
		Возврат Истина;
	КонецЕсли;	 
	
	Возврат Ложь;
КонецФункции	

Функция УЭтогоЭлементаСхемыНеНадоФорматироватьПараметры(ТипЭлемента)
	Если ТипЭлемента = ТипЭлементаГруппаШагов() Тогда
		Возврат Истина;
	ИначеЕсли ТипЭлемента = ТипЭлементаИначеПрепроцессор() Тогда
		Возврат Истина;
	ИначеЕсли ТипЭлемента = ТипЭлементаКонецЕслиПрепроцессор() Тогда
		Возврат Истина;
	ИначеЕсли ТипЭлемента = ТипЭлементаИначе() Тогда
		Возврат Истина;
	ИначеЕсли ТипЭлемента = ТипЭлементаИначеEng() Тогда
		Возврат Истина;
	ИначеЕсли ТипЭлемента = ТипЭлементаКонецЕсли() Тогда
		Возврат Истина;
	ИначеЕсли ТипЭлемента = ТипЭлементаКонецЕслиEng() Тогда
		Возврат Истина;
	ИначеЕсли ТипЭлемента = ТипЭлементаКонецЦикла() Тогда
		Возврат Истина;
	ИначеЕсли ТипЭлемента = ТипЭлементаКонецЦиклаEng() Тогда
		Возврат Истина;
	ИначеЕсли ТипЭлемента = ТипЭлементаМетка() Тогда
		Возврат Истина;
	ИначеЕсли ТипЭлемента = ТипЭлементаПерейти() Тогда
		Возврат Истина;
	ИначеЕсли ТипЭлемента = ТипЭлементаПустаяСтрока() Тогда
		Возврат Истина;
	КонецЕсли;	 
	
	Возврат Ложь;
КонецФункции	

Процедура ОтформатироватьПервыеСловаGherkin(ТаблицаСтрок)
	МассивПервыхСлов     = МассивПервыхСловGherkin();
	МассивПервыхСловНРег = Новый Массив;
	Для Каждого Стр Из МассивПервыхСлов Цикл
		МассивПервыхСловНРег.Добавить(НРег(Стр));
	КонецЦикла;	
	
	КодHTMLДляПодсветки = "<span style=""color: #0000ff;"">";
	КешТипЭлементаКомментарий = ТипЭлементаКомментарий();
	КешТипЭлементаБлочныйКомментарий = ТипЭлементаБлочныйКомментарий();
	
	Для Каждого СтрТаблицаСтрок Из ТаблицаСтрок Цикл
		Если СтрТаблицаСтрок.ТипЭлемента = КешТипЭлементаКомментарий Тогда
			Продолжить;
		ИначеЕсли СтрТаблицаСтрок.ТипЭлемента = КешТипЭлементаБлочныйКомментарий Тогда
			Продолжить;
		КонецЕсли;	 
		
		Если ЭтотТипЭлементаСхемыНеНадоФорматироватьПоСтандартуGherkin(СтрТаблицаСтрок.ТипЭлемента) Тогда
			Продолжить;
		КонецЕсли;	 
		
		Стр = СтрТаблицаСтрок.СтрокаСокр;
		
		Если Стр = "" Тогда
			Продолжить;
		КонецЕсли;	 
		
		МассивСлов  = СтрРазделить(Стр," ",Истина);
		
		ПервоеСлово   = МассивСлов[0];
		
		Если ПервоеСлово = Неопределено Тогда
			Продолжить;
		КонецЕсли;	 
		
		НРегСтр = НРег(Стр);
		НомКлючевоеСлово = -1;
		Для Каждого КлючевоеСлово Из МассивПервыхСловНРег Цикл
			НомКлючевоеСлово = НомКлючевоеСлово + 1;
			Позиция = Найти(НРегСтр, КлючевоеСлово);	
			Если (Позиция = 1) и Сред(НРегСтр,СтрДлина(КлючевоеСлово)+1,1) = " " Тогда 
				СтрТаблицаСтрок.СтрокаСокр = КодHTMLДляПодсветки + МассивПервыхСлов[НомКлючевоеСлово]  + "</span>"
				                             + Сред(Стр,СтрДлина(КлючевоеСлово)+1);
				Прервать;
			КонецЕсли;	 
		КонецЦикла;	
		
	КонецЦикла;	
	
КонецПроцедуры

Процедура ОтформатироватьТекстGherkin(ТаблицаСтрок)
	ОтформатироватьПередачуПараметраТипаТаблица(ТаблицаСтрок);
	ОтформатироватьЗаголовокGherkin(ТаблицаСтрок);
	ОтформатироватьСценарийGherkin(ТаблицаСтрок);
	ОтформатироватьКомментарииGherkin(ТаблицаСтрок);
	ОтформатироватьТегиGherkin(ТаблицаСтрок);
	ОтформатироватьПараметрыGherkin(ТаблицаСтрок);
	ОтформатироватьПервыеСловаGherkin(ТаблицаСтрок);
КонецПроцедуры

Процедура ОбработатьВыделениеЦветомШаблонСценария(ТаблицаСтрок)
	КодHTMLДляПодсветки = "<span style=""text-decoration: underline;"">";
	
	Для Каждого СтрТаблицаСтрок Из ТаблицаСтрок Цикл
		Если СтрТаблицаСтрок.ТипЭлемента = ТипЭлементаКомментарий() Тогда
			Продолжить;
		ИначеЕсли СтрТаблицаСтрок.ТипЭлемента = ТипЭлементаБлочныйКомментарий() Тогда
			Продолжить;
		КонецЕсли;	 
		
		Стр = СтрТаблицаСтрок.СтрокаСокр;
		
		Если Стр = "" Тогда
			Продолжить;
		КонецЕсли;	 
		
		Если (СтрТаблицаСтрок.ТипЭлемента = ТипЭлементаШаблонСценария()) Тогда
			СтрТаблицаСтрок.СтрокаСокр = КодHTMLДляПодсветки + СокрЛ(Стр) + "</span>";
		КонецЕсли;	 
	КонецЦикла;	
	
КонецПроцедуры

Процедура ОтступУСтрокИСпецСимволы(ТаблицаСтрок,ДополнительныеПараметры)
	КодHTMLДляПодсветкиБезВыделенияТекста = "<span style=""text-decoration: none;"">";
	
	ОбрабатыватьЭкранированныеСпецсимволы = Ложь;
	Если ДополнительныеПараметры.Свойство("ОбрабатыватьЭкранированныеСпецсимволы") Тогда
		Если ДополнительныеПараметры.ОбрабатыватьЭкранированныеСпецсимволы Тогда
			ОбрабатыватьЭкранированныеСпецсимволы = Истина;
		КонецЕсли;	 
	КонецЕсли;	 
	
	Для Каждого СтрТаблицаСтрок Из ТаблицаСтрок Цикл
		Если ОбрабатыватьЭкранированныеСпецсимволы Тогда
			СтрТаблицаСтрок.СтрокаСокр = СтрЗаменить(СтрТаблицаСтрок.СтрокаСокр,"\[","[");
			СтрТаблицаСтрок.СтрокаСокр = СтрЗаменить(СтрТаблицаСтрок.СтрокаСокр,"\]","]");
		КонецЕсли;	 
		
		Стр = СтрТаблицаСтрок.СтрокаСокр;
		
		Если Стр = "" Тогда
			Продолжить;
		КонецЕсли;
		
		СтрокаОтступ = ПолучитьСтрокуОтступПоЧислуПробелов(СтрТаблицаСтрок.Отступ);
		Если ДополнительныеПараметры.ДелатьРаскраску Тогда
			СтрТаблицаСтрок.Строка = КодHTMLДляПодсветкиБезВыделенияТекста + СтрокаОтступ + "</span>" + СтрТаблицаСтрок.СтрокаСокр;
		Иначе	
			СтрТаблицаСтрок.Строка = СтрокаОтступ + СтрТаблицаСтрок.СтрокаСокр;
		КонецЕсли;	 
	КонецЦикла;	
КонецПроцедуры

Процедура ВставитьТегиВТаблицуСтрок(ТаблицаСтрок,МассивТеги,СтрокаДерева,Отступ)
	Для Каждого ТекстТега Из МассивТеги Цикл
		СтрТаблицаСтрок                              = ТаблицаСтрок.Добавить();
		СтрТаблицаСтрок.Строка                       = ТекстТега;
		СтрТаблицаСтрок.СтрокаСокр                   = ТекстТега;
		СтрТаблицаСтрок.ТипЭлемента                  = ТипЭлементаТег();
		СтрТаблицаСтрок.Отступ                       = Отступ;
	КонецЦикла;	
КонецПроцедуры

Процедура ВставитьКомментарииВТаблицуСтрок(ТаблицаСтрок,МассивКомментарии,СтрокаДерева)
	Для Каждого ТекстКомментария Из МассивКомментарии Цикл
		СтрТаблицаСтрок                              = ТаблицаСтрок.Добавить();
		СтрТаблицаСтрок.Строка                       = ТекстКомментария;
		СтрТаблицаСтрок.СтрокаСокр                   = ТекстКомментария;
		СтрТаблицаСтрок.ТипЭлемента                  = ТипЭлементаКомментарий();
		СтрТаблицаСтрок.Отступ                       = 0;
	КонецЦикла;	
КонецПроцедуры

Процедура ОтформатироватьПараметрыGherkin(ТаблицаСтрок)
	МассивПервыхСловНРег = МассивПервыхСловGherkinНРег();
	
	КодHTMLДляПодсветки = "<span style=""color: #0000ff;"">";
	
	КешТипЭлементаКомментарий = ТипЭлементаКомментарий();
	КешТипЭлементаБлочныйКомментарий = ТипЭлементаБлочныйКомментарий();
	
	КешТипЭлементаНачалоУсловияПрепроцессор = ТипЭлементаНачалоУсловияПрепроцессор();
	КешТипЭлементаНачалоУсловия = ТипЭлементаНачалоУсловия();
	КешТипЭлементаНачалоУсловияEng = ТипЭлементаНачалоУсловияEng();
	КешТипЭлементаНачалоЦикла = ТипЭлементаНачалоЦикла();
	КешТипЭлементаНачалоЦиклаEng = ТипЭлементаНачалоЦиклаEng();
	
	КешТипЭлементаИначеЕслиПрепроцессор = ТипЭлементаИначеЕслиПрепроцессор();
	КешТипЭлементаИначеЕсли = ТипЭлементаИначеЕсли();
	КешТипЭлементаИначеЕслиEng = ТипЭлементаИначеЕслиEng();
	
	КешТипЭлементаПередачаПараметров = ТипЭлементаПередачаПараметров();
	
	
	Для Каждого СтрТаблицаСтрок Из ТаблицаСтрок Цикл
		Если СтрТаблицаСтрок.ТипЭлемента = КешТипЭлементаКомментарий Тогда
			Продолжить;
		ИначеЕсли СтрТаблицаСтрок.ТипЭлемента = КешТипЭлементаБлочныйКомментарий Тогда
			Продолжить;
		КонецЕсли;	 
		
		Если УЭтогоЭлементаСхемыНеНадоФорматироватьПараметры(СтрТаблицаСтрок.ТипЭлемента) Тогда
			Продолжить;
		КонецЕсли;	 
		
		Если СтрТаблицаСтрок.ТипЭлемента = ТипЭлементаПараметрТаблица() Тогда
			КодHTMLДляПодсветки = "<span style=""color: #008000;"">";
			СтрТаблицаСтрок.СтрокаСокр = КодHTMLДляПодсветки + СтрТаблицаСтрок.СтрокаСокр + "</span>";
			Продолжить;
		КонецЕсли;	 
		
		Если СтрТаблицаСтрок.Параметры = Неопределено Тогда
			Продолжить;
		КонецЕсли;	 
		
		Если СтрТаблицаСтрок.Параметры.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;	 
		
		ТипВыделенияПараметра = Неопределено;
		
		Если СтрТаблицаСтрок.ТипЭлемента = КешТипЭлементаНачалоУсловияПрепроцессор
			 или СтрТаблицаСтрок.ТипЭлемента = КешТипЭлементаНачалоУсловия
			 или СтрТаблицаСтрок.ТипЭлемента = КешТипЭлементаНачалоУсловияEng
			 или СтрТаблицаСтрок.ТипЭлемента = КешТипЭлементаНачалоЦикла
			 или СтрТаблицаСтрок.ТипЭлемента = КешТипЭлементаНачалоЦиклаEng
			 Тогда
			Стр = СтрТаблицаСтрок.СтрокаСокр;
			ТипВыделенияПараметра = ТипВыделенияПараметраУсловие();
		ИначеЕсли СтрТаблицаСтрок.ТипЭлемента = КешТипЭлементаИначеЕслиПрепроцессор
			 или СтрТаблицаСтрок.ТипЭлемента = КешТипЭлементаИначеЕсли 
			 или СтрТаблицаСтрок.ТипЭлемента = КешТипЭлементаИначеЕслиEng 
			 Тогда
			Стр = СтрТаблицаСтрок.СтрокаСокр;
			ТипВыделенияПараметра = ТипВыделенияПараметраУсловие();
		ИначеЕсли СтрТаблицаСтрок.ТипЭлемента = КешТипЭлементаПередачаПараметров Тогда
			Стр = СтрТаблицаСтрок.ОбработаннаяСтрокаПараметров;
			ТипВыделенияПараметра = ТипВыделенияПередачаПараметров();
		Иначе	
			Стр = СтрТаблицаСтрок.ОбработаннаяСтрокаПараметров;
		КонецЕсли;	 
		
		ПодсветкаСтрокиПоПараметрам(Стр,СтрТаблицаСтрок.Параметры,ТипВыделенияПараметра);
		
		Если СтрТаблицаСтрок.ТипЭлемента = КешТипЭлементаНачалоУсловияПрепроцессор Тогда
			СтрТаблицаСтрок.СтрокаСокр = Стр;
		ИначеЕсли СтрТаблицаСтрок.ТипЭлемента = КешТипЭлементаИначеЕслиПрепроцессор Тогда
			СтрТаблицаСтрок.СтрокаСокр = Стр;
		Иначе	
			СтрТаблицаСтрок.СтрокаСокр = Стр;
		КонецЕсли;	 
		
	КонецЦикла;	
КонецПроцедуры

Процедура ОтформатироватьКомментарииGherkin(ТаблицаСтрок)
	МассивПервыхСловНРег     = МассивПервыхСловGherkinНРег();
	
	КодHTMLДляПодсветки = "<span style=""color: #009646;"">";
	
	КешТипЭлементаКомментарий = ТипЭлементаКомментарий();
	КешТипЭлементаБлочныйКомментарий = ТипЭлементаБлочныйКомментарий();
	КешТипЭлементаБлочныйКомментарийНачало = ТипЭлементаБлочныйКомментарийНачало();
	КешТипЭлементаБлочныйКомментарийОкончание = ТипЭлементаБлочныйКомментарийОкончание();
	
	Для Каждого СтрТаблицаСтрок Из ТаблицаСтрок Цикл
		Если СтрТаблицаСтрок.ТипЭлемента <> КешТипЭлементаКомментарий
			И СтрТаблицаСтрок.ТипЭлемента <> КешТипЭлементаБлочныйКомментарий
			И СтрТаблицаСтрок.ТипЭлемента <> КешТипЭлементаБлочныйКомментарийНачало
		    И СтрТаблицаСтрок.ТипЭлемента <> КешТипЭлементаБлочныйКомментарийОкончание Тогда
			Продолжить;
		КонецЕсли;	 
		
		Стр = СтрТаблицаСтрок.СтрокаСокр;
		
		Если Стр = "" Тогда
			Продолжить;
		КонецЕсли;	 
		
		ЭкранироватьУгловыеСкобки(Стр);
		СтрТаблицаСтрок.СтрокаСокр = КодHTMLДляПодсветки + Стр  + "</span>";
	КонецЦикла;	
	
КонецПроцедуры

Процедура ОтформатироватьТегиGherkin(ТаблицаСтрок)
	МассивПервыхСловНРег     = МассивПервыхСловGherkinНРег();
	
	КодHTMLДляПодсветки = "<span style=""color: #0000ff;"">";
	
	КешТипЭлементаТег = ТипЭлементаТег();
	
	Для Каждого СтрТаблицаСтрок Из ТаблицаСтрок Цикл
		Если СтрТаблицаСтрок.ТипЭлемента <> КешТипЭлементаТег Тогда
			Продолжить;
		КонецЕсли;	 
		
		Стр = СтрТаблицаСтрок.СтрокаСокр;
		
		Если Стр = "" Тогда
			Продолжить;
		КонецЕсли;	 
		
		СтрТаблицаСтрок.СтрокаСокр = КодHTMLДляПодсветки + Стр  + "</span>";
	КонецЦикла;	
	
КонецПроцедуры

Процедура ОтформатироватьЗаголовокGherkin(ТаблицаСтрок)
	МассивПервыхСловНРег     = МассивПервыхСловGherkinНРег();
	
	КодHTMLПервоеСлово   = "<span style=""color: #2C75B3;"">";
	КодHTMLОсновнойТекст = "<span style=""color: #CE743A;"">";
	
	КешТипЭлементаЗаголовок = ТипЭлементаЗаголовок();
	
	Для Каждого СтрТаблицаСтрок Из ТаблицаСтрок Цикл
		Если (СтрТаблицаСтрок.ТипЭлемента <> КешТипЭлементаЗаголовок) Тогда
			Продолжить;
		КонецЕсли;	 
		
		Стр = СтрТаблицаСтрок.СтрокаСокр;
		
		Если Стр = "" Тогда
			Продолжить;
		КонецЕсли;	 
		
		ЭкранироватьУгловыеСкобки(Стр);
		
		МассивСлов  = СтрРазделить(Стр," ",Истина);
		ПервоеСлово = МассивСлов[0];
		ПервоеСлово = КодHTMLПервоеСлово + ПервоеСлово  + "</span>";
		
		МассивСлов.Удалить(0);
		ВтораяЧастьСтроки = СтрСоединить(МассивСлов," ");
		ВтораяЧастьСтроки = КодHTMLОсновнойТекст + ВтораяЧастьСтроки  + "</span>";
		
		СтрТаблицаСтрок.СтрокаСокр = ПервоеСлово + " " + ВтораяЧастьСтроки;
	КонецЦикла;	
	
КонецПроцедуры

Процедура ОтформатироватьСценарийGherkin(ТаблицаСтрок)
	МассивПервыхСловНРег     = МассивПервыхСловGherkinНРег();
	
	КодHTMLПервоеСлово   = "<span style=""color: #2C75B3;"">";
	КодHTMLОсновнойТекст = "<span style=""color: #CE743A;"">";
	
	КешТипЭлементаСценарий = ТипЭлементаСценарий();
	КешТипЭлементаКонтекст = ТипЭлементаКонтекст();
	
	Для Каждого СтрТаблицаСтрок Из ТаблицаСтрок Цикл
		Если (СтрТаблицаСтрок.ТипЭлемента <> КешТипЭлементаСценарий)
			и (СтрТаблицаСтрок.ТипЭлемента <> КешТипЭлементаКонтекст) Тогда
			Продолжить;
		КонецЕсли;	 
		
		Стр = СтрТаблицаСтрок.СтрокаСокр;
		
		Если Стр = "" Тогда
			Продолжить;
		КонецЕсли;	 
		
		ЭкранироватьУгловыеСкобки(Стр);
		
		МассивСлов  = СтрРазделить(Стр," ",Истина);
		ПервоеСлово = МассивСлов[0];
		ПервоеСлово = КодHTMLПервоеСлово + ПервоеСлово  + "</span>";
		
		МассивСлов.Удалить(0);
		ВтораяЧастьСтроки = СтрСоединить(МассивСлов," ");
		ВтораяЧастьСтроки = КодHTMLОсновнойТекст + ВтораяЧастьСтроки  + "</span>";
		
		СтрТаблицаСтрок.СтрокаСокр = ПервоеСлово + " " + ВтораяЧастьСтроки;
	КонецЦикла;	
	
КонецПроцедуры

Процедура ПодсветкаСтрокиПоПараметрам(Стр,Параметры,ТипВыделенияПараметра)
	ПодсветкаСтрокиПараметровИзменяемыеПараметры(Стр,Параметры,"КвадратныеСкобки",ТипВыделенияПараметра);
	ПодсветкаСтрокиПараметровСтрока(Стр,Параметры,"Кавычки",ТипВыделенияПараметра);
	ПодсветкаСтрокиПараметровСтрока(Стр,Параметры,"Апострофы",ТипВыделенияПараметра);
	ПодсветкаСтрокиПараметровДата(Стр,Параметры,ТипВыделенияПараметра);
	ПодсветкаСтрокиПараметровЧисло(Стр,Параметры,ТипВыделенияПараметра);
КонецПроцедуры

Процедура ПодсветкаСтрокиПараметровСтрока(Стр,Параметры,Вид,ТипВыделенияПараметра)
	КодHTMLДляПодсветкиИзменяемыйПараметр = "<span style=""color: #008000;"">";
	КодHTMLДляПодсветки = "<span style=""color: #b8860b;"">";
	
	Если ТипВыделенияПараметра = ТипВыделенияПередачаПараметров() Тогда
		КодHTMLДляПодсветкиИзменяемыйПараметр = "<span style=""color: #008000;font-style: italic;"">";
		КодHTMLДляПодсветки = "<span style=""color: #b8860b;font-style: italic;"">";
	КонецЕсли;	 
	
	
	Тип = ТипПараметра("Строка");
	КолПараметров = 0;
	Для Каждого Параметр Из Параметры Цикл
		Если Параметр.Тип <> Тип Тогда
			Продолжить;
		КонецЕсли;	 
		Если Параметр.Вид <> Вид Тогда
			Продолжить;
		КонецЕсли;	 
		
		КолПараметров = КолПараметров + 1;
		
		ЗначениеПараметра = Параметр.ЗначениеПараметра;
		
		Если ТипЗнч(Параметр.ВложенныеПараметры) = Тип("Массив") Тогда
			КолПараметровВложенные = 0;
			ЗначениеПараметра = Параметр.ЗначениеПараметраВложенное;
			ЭкранироватьСпецСимволы(ЗначениеПараметра);
			Для Каждого ВложенныйПараметр Из Параметр.ВложенныеПараметры Цикл
				КолПараметровВложенные = КолПараметровВложенные + 1;
				НовоеИмяПараметра = ВложенныйПараметр.НовоеИмяПараметра;
				ЗначениеВложенногоПараметра = КодHTMLДляПодсветкиИзменяемыйПараметр 
				     + "[" + НовоеИмяПараметра + "]" + "</span>";
					 
				ЗначениеПараметра = СтрЗаменить(ЗначениеПараметра,"~ПараметрСтрока" + ВложенныйПараметр.Вид 
				   + XMLСтрока(КолПараметровВложенные) + "~",ЗначениеВложенногоПараметра);
					 
			КонецЦикла;	
			
			ЗначениеПараметра = КодHTMLДляПодсветки + ЗначениеПараметра + "</span>";
			
			Стр = СтрЗаменить(Стр,"~Параметр" + Тип + Вид + XMLСтрока(КолПараметров) + "~",
										Параметр.Символ + ЗначениеПараметра + Параметр.Символ);
			
			Продолжить;
		КонецЕсли;	 
		
		ЭтотПараметрЯвляетсяТолькоЗначением = ЭтотПараметрЯвляетсяТолькоЗначением(ЗначениеПараметра);
		
		ЭкранироватьСпецСимволы(ЗначениеПараметра);
		
		Если НЕ ЭтотПараметрЯвляетсяТолькоЗначением Тогда
			ЗначениеПараметра = КодHTMLДляПодсветки + ЗначениеПараметра + "</span>";
		КонецЕсли;	 
		
		Стр               = СтрЗаменить(Стр,"~Параметр" + Тип + Вид + XMLСтрока(КолПараметров) + "~",
										Параметр.Символ + ЗначениеПараметра + Параметр.Символ);
	КонецЦикла;	
КонецПроцедуры

Процедура ПодсветкаСтрокиПараметровИзменяемыеПараметры(Стр,Параметры,Вид,ТипВыделенияПараметра)
	КодHTMLДляПодсветки = "<span style=""color: #008000;"">";
	Если ТипВыделенияПараметра = ТипВыделенияПередачаПараметров() Тогда
		КодHTMLДляПодсветки = "<span style=""color: #008000;font-style: italic;"">";
	КонецЕсли;	 
	
	Тип = ТипПараметра("Строка");
	КолПараметров = 0;
	Для Каждого Параметр Из Параметры Цикл
		Если Параметр.Тип <> Тип Тогда
			Продолжить;
		КонецЕсли;	 
		Если Параметр.Вид <> Вид Тогда
			Продолжить;
		КонецЕсли;	 
		
		КолПараметров = КолПараметров + 1;
		
		ЗначениеПараметра = Параметр.ЗначениеПараметра;
		
		ЭкранироватьСпецСимволы(ЗначениеПараметра);
		
		ЗначениеПараметра = КодHTMLДляПодсветки + ЗначениеПараметра + "</span>";
		
		Стр               = СтрЗаменить(Стр,"~ПараметрСтрока" + Вид + XMLСтрока(КолПараметров) + "~",
										"[" + ЗначениеПараметра + "]");
	КонецЦикла;	
КонецПроцедуры

Процедура ПодсветкаСтрокиПараметровДата(Стр,Параметры,ТипВыделенияПараметра)
	КодHTMLДляПодсветки = "<span style=""color: #CE9164;"">";
	Если ТипВыделенияПараметра = ТипВыделенияПередачаПараметров() Тогда
		КодHTMLДляПодсветки = "<span style=""color: #CE9164;font-style: italic;"">";
	КонецЕсли;	 
	
	Тип = ТипПараметра("Дата");
	КолПараметров = 0;
	Для Каждого Параметр Из Параметры Цикл
		Если Параметр.Тип <> Тип Тогда
			Продолжить;
		КонецЕсли;	 
		КолПараметров = КолПараметров + 1;
		
		ЗначениеПараметра = Параметр.ЗначениеПараметра;
		
		ЭкранироватьСпецСимволы(ЗначениеПараметра);
		
		ЗначениеПараметра = КодHTMLДляПодсветки + ЗначениеПараметра + "</span>";
		Стр               = СтрЗаменить(Стр,"~Параметр" + Тип + XMLСтрока(КолПараметров) + "~",ЗначениеПараметра);
	КонецЦикла;	
КонецПроцедуры

Процедура ПодсветкаСтрокиПараметровЧисло(Стр,Параметры,ТипВыделенияПараметра)
	КодHTMLДляПодсветки = "<span style=""color: #CE9164;"">";
	Если ТипВыделенияПараметра = ТипВыделенияПередачаПараметров() Тогда
		КодHTMLДляПодсветки = "<span style=""color: #CE9164;font-style: italic;"">";
	КонецЕсли;	 
	
	Тип = ТипПараметра("Число");
	КолПараметров = 0;
	Для Каждого Параметр Из Параметры Цикл
		Если Параметр.Тип <> Тип Тогда
			Продолжить;
		КонецЕсли;	 
		КолПараметров = КолПараметров + 1;
		
		ЗначениеПараметра = Параметр.ЗначениеПараметра;
		
		ЭкранироватьСпецСимволы(ЗначениеПараметра);
		
		ЗначениеПараметра = КодHTMLДляПодсветки + ЗначениеПараметра + "</span>";
		Стр               = СтрЗаменить(Стр,"~Параметр" + Тип + XMLСтрока(КолПараметров) + "~",ЗначениеПараметра);
	КонецЦикла;	
КонецПроцедуры

Процедура ЗаполнитьВерсииШаблоновРекурсивно(Дерево,ВерсииШаблонов,ТаблицаШаблоновСценариев,МассивПервыхСловНРег,Уровень)
	Для Каждого СтрокаДерева Из Дерево.Строки Цикл
		
		Если СтрокаДерева.ТипЭлемента = ТипЭлементаШаблонСценария() Тогда
			Снипет = СтрокаДерева.ОписаниеЭлемента;
			СнипетПоСтроке(Снипет,МассивПервыхСловНРег);
			СнипетНРег = НРег(Снипет);
			
			СтрокаТаблицаШаблоновСценариев = ТаблицаШаблоновСценариев.Найти(СнипетНРег,"СнипетНРег");
			Если СтрокаТаблицаШаблоновСценариев = Неопределено Тогда
				ВызватьИсключение СтрШаблон(НСтр("ru = 'Не найден подсценарий, соответствующий строке: %1'"),
																				  СнипетНРег);
			КонецЕсли;	 
			
			СтрокаВерсииШаблонов = ВерсииШаблонов.Найти(СтрокаТаблицаШаблоновСценариев.Ссылка,"Шаблон");
			Если СтрокаВерсииШаблонов = Неопределено Тогда
				СтрокаВерсииШаблонов = ВерсииШаблонов.Добавить();
				СтрокаВерсииШаблонов.Шаблон = СтрокаТаблицаШаблоновСценариев.Ссылка;
			КонецЕсли;	 
			СтрокаВерсииШаблонов.Версия = СтрокаТаблицаШаблоновСценариев.ВерсияСценария;
			
			Если Уровень = 1 Тогда
				СтрокаВерсииШаблонов.ВЭтомСценарии = Истина;
			Иначе	
				СтрокаВерсииШаблонов.ВЭтомСценарии = Ложь;
			КонецЕсли;	 
			
			Уровень = Уровень + 1;
			ЗаполнитьВерсииШаблоновРекурсивно(СтрокаДерева,ВерсииШаблонов,ТаблицаШаблоновСценариев,МассивПервыхСловНРег,Уровень);
			Уровень = Уровень - 1;
			
		Иначе	
			ЗаполнитьВерсииШаблоновРекурсивно(СтрокаДерева,ВерсииШаблонов,ТаблицаШаблоновСценариев,МассивПервыхСловНРег,Уровень);
		КонецЕсли;
		
	КонецЦикла;	
	
КонецПроцедуры

Процедура ВставитьВКаждыйСценарийПроверкуЧтоФОСНесколькимиЗначениямиИмеютНужноеЗначение(ТекстыСценариев,
	                                                                         ФОИмеющиеНесколькоЗначений)
	ИменаФО = Новый Массив;
	Для Каждого ФО Из ФОИмеющиеНесколькоЗначений Цикл
		ИменаФО.Добавить(ФО.Имя);
	КонецЦикла;	
	
	Для Каждого ОбъектСценария Из ТекстыСценариев Цикл
		ПараметрыСНесколькимиЗначениями = ОбъектСценария.ПараметрыСНесколькимиЗначениями;
		Если ПараметрыСНесколькимиЗначениями.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;	 
		
		ТекстСценария = СтрРазделить(ОбъектСценария.ТекстСценария,Символы.ПС);
		
		Ид = 0;
		Для Каждого ДанныеПараметра Из ПараметрыСНесколькимиЗначениями Цикл
			
			Если ИменаФО.Найти(ДанныеПараметра.ИмяПараметра) = Неопределено Тогда
				Продолжить;
			КонецЕсли;	 
			
			Ид = Ид + 1 + 1;
			ТекстСценария.Вставить(Ид,Символы.Таб + "И я проверяю что ФО """ + ДанныеПараметра.ИмяПараметра 
			                                + """ имеет значение """ + ДанныеПараметра.ЗначениеПараметра + """");
		КонецЦикла;	
		
		ОбъектСценария.ТекстСценария = СтрСоединить(ТекстСценария,Символы.ПС);
		
	КонецЦикла;	
КонецПроцедуры

Функция ОсновнойТекст(ТекстЗаголовок,ТекстОбщийКонтекст,ТекстыСценариев,
	             ЗаголовокЗначенияПараметровСценария,ДанныеНомеровСтрок)
				 
	КоличествоПереводовСтроки = 3;
	
	ПерваяЧасть     = СокрЛ(ЗаголовокЗначенияПараметровСценария + Символы.ПС + Символы.ПС)
	                           + ТекстЗаголовок + Символы.ПС + Символы.ПС + ТекстОбщийКонтекст;
							   
	Если НЕ ПустаяСтрока(ТекстОбщийКонтекст) Тогда
		ПерваяЧасть = ПерваяЧасть + Символы.ПС + Символы.ПС;
	КонецЕсли;	 							   
							   
	МассивСтрок = СтрРазделить(ПерваяЧасть,Символы.ПС);
	ВысотаЗаголовка = МассивСтрок.Количество()-1;
	
	Смещение = ВысотаЗаголовка;
	МассивТекстов = Новый Массив;
	Для Каждого ОбъектСценария Из ТекстыСценариев Цикл
		МассивТекстов.Добавить(ОбъектСценария.ТекстСценария);
		
		Если ДанныеНомеровСтрок <> Неопределено Тогда
			Для Каждого ТекДанные Из ОбъектСценария.ДанныеНомеровСтрок Цикл
				НомерСтрокиЛинейногоСценария = ТекДанные.НомерСтрокиЛинейногоСценария;
				ТекДанные.Вставить("НомерСтрокиЛинейногоСценария",
				   НомерСтрокиЛинейногоСценария + Смещение);
				ДанныеНомеровСтрок.Добавить(ТекДанные);
			КонецЦикла;	
			
			МассивСтрок = СтрРазделить(ОбъектСценария.ТекстСценария,Символы.ПС);
			
			Смещение = Смещение + МассивСтрок. Количество() - 1 + КоличествоПереводовСтроки;
		КонецЕсли;	 
	КонецЦикла;	
	
	ТекстыСценариев = ТекстСценарияИзМассива(МассивТекстов,КоличествоПереводовСтроки);
	
	Возврат ПерваяЧасть + ТекстыСценариев;
КонецФункции	

Функция ТекстОбщийКонтекст(ФОИмеющиеОдноЗначение,ШагиДоТеста,ПараметрыПодключения)
	Массив = Новый Массив;
	
	
	Если ПараметрыПодключения <> Неопределено Тогда
		Стр = СтрШаблон(НСтр(
		   "ru = '	И я подключаю TestClient ""%1"" логин ""%2"" пароль ""%3""'; en = 'And I connect ""%1"" TestClient using ""%2"" login and ""%3"" password'"),
		  ПараметрыПодключения.ИмяПрофиля, ПараметрыПодключения.Логин, ПараметрыПодключения.Пароль);
		  
		Массив.Добавить(Стр);  
	КонецЕсли;	 
	
	Массив.Добавить(НСтр("ru = '	И Я закрыл все окна клиентского приложения'; en = 'And I close all client application windows'"));
	
	Для Каждого ФО Из ФОИмеющиеОдноЗначение Цикл
		Стр = СтрШаблон(НСтр(
		"ru = '	И я проверяю что ФО ""%1"" имеет значение ""%2""'"),
     	  ФО.Имя, ФО.Значение);

		Массив.Добавить(Стр);  
	КонецЦикла;	
	
	Для Каждого Шаг Из ШагиДоТеста Цикл
		Массив.Добавить(" " + Шаг);
	КонецЦикла;	
	
	Если Массив.Количество() > 0 Тогда
		Массив.Вставить(0,"Контекст:");
	КонецЕсли;	 
	
	Возврат СтрСоединить(Массив,Символы.ПС);
КонецФункции	

Функция ТекстЗаголовок(Параметры,ТекстыСценариев)
	Процесс = Параметры.Процесс;
	СтрокаУровеньОтчета = "";
	Если Параметры.ЧтениеИзТекста Тогда
		СтрокаУровеньОтчета = "#report.feature=" + Параметры.УровеньОтчета1 
		+ Символы.ПС + "#report.story=" + Параметры.УровеньОтчета2 + Символы.ПС;
	КонецЕсли;	 
	Часть1 = "#language: ru" + Символы.ПС + "@tree" + Символы.ПС + СтрокаУровеньОтчета + "Функциональность: ";
	
	Если Параметры.ЧтениеИзТекста Тогда
		Возврат Часть1 + Параметры.ИмяНастройки; 
	Иначе	
		Если ТипЗнч(Процесс) = Тип("Строка") Тогда
			Возврат Часть1 + Процесс; 
		Иначе	
			Возврат Часть1 + СокрЛП(Процесс.Наименование);
		КонецЕсли;	 
	КонецЕсли;	 
КонецФункции	

Функция ВставитьТекстОкончаниеВСценарии(ШагиПроверка,ШагиПослеТеста,ТекстыСценариев)
	
	Для Каждого ОбъектСценарий Из ТекстыСценариев Цикл
		Массив = СтрРазделить(ОбъектСценарий.ТекстСценария,Символы.ПС);
		
		Если ШагиПроверка.Количество() > 0 Тогда
			Массив.Добавить("# секция проверки");
		КонецЕсли;	 
		Для Каждого Шаг Из ШагиПроверка Цикл
			Массив.Добавить(Шаг);
		КонецЦикла;	
		
		Если ШагиПослеТеста.Количество() > 0 Тогда
			Массив.Добавить("# секция после теста");
		КонецЕсли;	 
		Для Каждого Шаг Из ШагиПослеТеста Цикл
			Массив.Добавить(Шаг);
		КонецЦикла;	
		
		ОбъектСценарий.ТекстСценария = СтрСоединить(Массив,Символы.ПС); 
	КонецЦикла;	
	
КонецФункции	

Функция ТекстСценарияИзМассива(МассивТекстов,КоличествоПереводовСтроки)
	ПереводовСтроки = "";
	Для Ккк = 1 По КоличествоПереводовСтроки Цикл
		ПереводовСтроки = ПереводовСтроки + Символы.ПС;
	КонецЦикла;	
	
	Возврат СтрСоединить(МассивТекстов,ПереводовСтроки);
КонецФункции	

Процедура ЗаполнитьПараметрТипаТаблица(СтрокаДерева,ТаблицаПараметров)
	Если СтрокаДерева.ДополнительныеСвойства <> Неопределено Тогда
		Если СтрокаДерева.ДополнительныеСвойства.Свойство("ПараметрыСТипомТаблицы") Тогда
			Для Каждого Элем Из СтрокаДерева.ДополнительныеСвойства.ПараметрыСТипомТаблицы Цикл
				СтрокаПараметрыВходящие = ТаблицаПараметров.Найти(Элем,"Имя");
				Если СтрокаПараметрыВходящие = Неопределено Тогда
					СтрПараметрыВходящие              = ТаблицаПараметров.Добавить();
					СтрПараметрыВходящие.Имя          = Элем;
					СтрПараметрыВходящие.Значение     = "";
					СтрПараметрыВходящие.ТипПараметра = ТипПараметра("Таблица");
				КонецЕсли;	 
			КонецЦикла;	 
		КонецЕсли;	 
	КонецЕсли;	 
КонецПроцедуры 

// Рекурсивно обходит дерево значений и заполняет таблицу параметров
Процедура ЗаполнитьПараметрыВходящиеИзДереваРекурсивно(Дерево,ТаблицаПараметров,ИДОбщий)
	Для Каждого СтрокаДерева Из Дерево.Строки Цикл
		Если СтрокаДерева.Параметры <> Неопределено и СтрокаДерева.Параметры.Количество() > 0 Тогда
			Если (СтрокаДерева.ТипЭлемента = ТипЭлементаНачалоУсловияПрепроцессор())
				или (СтрокаДерева.ТипЭлемента = ТипЭлементаИначеЕслиПрепроцессор()) Тогда
				
				НомерПараметра = 0;
				Для Каждого ПараметрУсловия Из СтрокаДерева.Параметры Цикл
					
					НомерПараметра         = НомерПараметра + 1;
					ЭтоСтандартныйПараметр = Истина;
					ИДОбщий                = ИДОбщий + 1;
					НовоеИмяПараметра      = ПараметрУсловия.ЗначениеПараметра;
					ПараметрУсловия.Вставить("НовоеИмяПараметра",ПараметрУсловия.ИмяПараметра);
					
					СтрокаПараметрыВходящие = ТаблицаПараметров.Найти(ПараметрУсловия.ИмяПараметра,"Имя");
					
					
					Если ПараметрУсловия.ИзменяемыйПараметр И СтрокаПараметрыВходящие = Неопределено Тогда
						СтрПараметрыВходящие          = ТаблицаПараметров.Добавить();
						СтрПараметрыВходящие.Имя      = ПараметрУсловия.ИмяПараметра;
						Если ПараметрУсловия.СвязанСФО Тогда
							СтрПараметрыВходящие.ФО       = ПараметрУсловия.ИмяПараметра
						КонецЕсли;	 
						
						СтрПараметрыВходящие.Значение     = "";
						СтрПараметрыВходящие.ТипПараметра = ПараметрУсловия.Тип;
					ИначеЕсли ПараметрУсловия.ВложенныеПараметры <> Неопределено	Тогда
						Для Каждого ВложенныйПараметр Из ПараметрУсловия.ВложенныеПараметры Цикл
							НовоеИмяПараметра      = ВложенныйПараметр.ЗначениеПараметра;
							ВложенныйПараметр.Вставить("НовоеИмяПараметра",НовоеИмяПараметра);
							
							СтрокаПараметрыВходящие = ТаблицаПараметров.Найти(НовоеИмяПараметра,"Имя");
							Если ВложенныйПараметр.ИзменяемыйПараметр И СтрокаПараметрыВходящие = Неопределено Тогда
								СтрПараметрыВходящие              = ТаблицаПараметров.Добавить();
								СтрПараметрыВходящие.Имя          = НовоеИмяПараметра;
								СтрПараметрыВходящие.Значение     = ВложенныйПараметр.ЗначениеПараметра;
								СтрПараметрыВходящие.ТипПараметра = ВложенныйПараметр.Тип;
							КонецЕсли;	 
						КонецЦикла;	
					КонецЕсли;	 
					
				КонецЦикла;	
				
			Иначе	
				НомерПараметра = 0;
				Для Каждого ПараметрСтроки Из СтрокаДерева.Параметры Цикл
					ОбработатьПараметрСценария(ПараметрСтроки, НомерПараметра, ТаблицаПараметров, ИДОбщий);
					
					Если ПараметрСтроки.Свойство("ЗначениеПараметраПодстановкаЗначений") Тогда
						Для Каждого ЗначениеПараметраПодстановкаЗначений Из ПараметрСтроки.ЗначениеПараметраПодстановкаЗначений Цикл
							Для Каждого ПараметрСтрокиПодстановка Из ЗначениеПараметраПодстановкаЗначений.Параметры Цикл
								ОбработатьПараметрСценария(ПараметрСтрокиПодстановка, НомерПараметра, ТаблицаПараметров, ИДОбщий);
							КонецЦикла;	 
						КонецЦикла;	 
					КонецЕсли;	 
					
				КонецЦикла;	
				
				ЗаполнитьПараметрТипаТаблица(СтрокаДерева,ТаблицаПараметров);
				
			КонецЕсли;	 
		Иначе
			ЗаполнитьПараметрТипаТаблица(СтрокаДерева,ТаблицаПараметров);
		КонецЕсли;
		
		Если СтрокаДерева.ТипЭлемента = ТипЭлементаШаблонСценария() Тогда
			Продолжить;
		КонецЕсли;	 
		
		ЗаполнитьПараметрыВходящиеИзДереваРекурсивно(СтрокаДерева,ТаблицаПараметров,ИДОбщий);
	КонецЦикла;	
КонецПроцедуры

Процедура ОбработатьПараметрСценария(ПараметрСтроки, НомерПараметра, ТаблицаПараметров, ИДОбщий)
	НомерПараметра         = НомерПараметра + 1;
	ЭтоСтандартныйПараметр = Истина;
	ИДОбщий                = ИДОбщий + 1;
	НовоеИмяПараметра      = ПараметрСтроки.ЗначениеПараметра;
	ПараметрСтроки.Вставить("НовоеИмяПараметра",НовоеИмяПараметра);
	
	СтрокаПараметрыВходящие = ТаблицаПараметров.Найти(НовоеИмяПараметра,"Имя");
	
	Если ПараметрСтроки.ИзменяемыйПараметр И СтрокаПараметрыВходящие = Неопределено Тогда
		СтрПараметрыВходящие              = ТаблицаПараметров.Добавить();
		СтрПараметрыВходящие.Имя          = НовоеИмяПараметра;
		СтрПараметрыВходящие.Значение     = "";
		СтрПараметрыВходящие.ТипПараметра = ПараметрСтроки.Тип;
	ИначеЕсли ПараметрСтроки.Свойство("ВложенныеПараметры") и ПараметрСтроки.ВложенныеПараметры <> Неопределено	Тогда
		Для Каждого ВложенныйПараметр Из ПараметрСтроки.ВложенныеПараметры Цикл
			НовоеИмяПараметра      = ВложенныйПараметр.ЗначениеПараметра;
			ВложенныйПараметр.Вставить("НовоеИмяПараметра",НовоеИмяПараметра);
			
			СтрокаПараметрыВходящие = ТаблицаПараметров.Найти(НовоеИмяПараметра,"Имя");
			Если ВложенныйПараметр.ИзменяемыйПараметр И СтрокаПараметрыВходящие = Неопределено Тогда
				СтрПараметрыВходящие              = ТаблицаПараметров.Добавить();
				СтрПараметрыВходящие.Имя          = НовоеИмяПараметра;
				СтрПараметрыВходящие.Значение     = "";
				СтрПараметрыВходящие.ТипПараметра = ВложенныйПараметр.Тип;
			КонецЕсли;	 
		КонецЦикла;	
	КонецЕсли;	 
КонецПроцедуры 

// Проверяет, содержит ли строка только цифры.
//
// Параметры:
//  Значение                - Строка - проверяемая строка.
//  УчитыватьЛидирующиеНули - Булево - флаг учета лидирующих нулей, если Истина, то ведущие нули пропускаются.
//  УчитыватьПробелы        - Булево - флаг учета пробелов, если Истина, то пробелы при проверке игнорируются.
//
// Возвращаемое значение:
//   Булево - Истина - строка содержит только цифры или пустая, Ложь - строка содержит иные символы.
//
Функция ТолькоЦифрыВСтроке(Знач Значение, Знач УчитыватьЛидирующиеНули = Истина, Знач УчитыватьПробелы = Истина)
	
	Если ТипЗнч(Значение) <> Тип("Строка") Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если Не УчитыватьПробелы Тогда
		Значение = СтрЗаменить(Значение, " ", "");
	КонецЕсли;
		
	Если СтрДлина(Значение) = 0 Тогда
		Возврат Истина;
	КонецЕсли;
	
	Если Не УчитыватьЛидирующиеНули Тогда
		Позиция = 1;
		// Взятие символа за границей строки возвращает пустую строку.
		Пока Сред(Значение, Позиция, 1) = "0" Цикл
			Позиция = Позиция + 1;
		КонецЦикла;
		Значение = Сред(Значение, Позиция);
	КонецЕсли;
	
	// Если содержит только цифры, то в результате замен должна быть получена пустая строка.
	// Проверять при помощи ПустаяСтрока нельзя, так как в исходной строке могут быть пробельные символы.
	Возврат СтрДлина(
		СтрЗаменить( СтрЗаменить( СтрЗаменить( СтрЗаменить( СтрЗаменить(
		СтрЗаменить( СтрЗаменить( СтрЗаменить( СтрЗаменить( СтрЗаменить( 
			Значение, "0", ""), "1", ""), "2", ""), "3", ""), "4", ""), "5", ""), "6", ""), "7", ""), "8", ""), "9", "")) = 0;
	
КонецФункции

Функция ТипПараметра(Тип)
	Если РаботаВРежимеВнешнейОбработки Тогда
		Возврат Тип;
	КонецЕсли;	
	
	Возврат Обработки.СборкаТекстовСценариев.ТипПараметра(Тип); 
КонецФункции	 

Функция ПолучитьДеревоСценария(Сценарий,СтруктураПараметров)
	Если СтруктураПараметров.ЧтениеИзТекста Тогда
		Возврат ПолучитьДеревоСценарияИзФайла(Сценарий,СтруктураПараметров);
	Иначе
		
		КомпилироватьСценарий = Ложь;
		Если СтруктураПараметров.Свойство("КомпилироватьСценарий") Тогда
			КомпилироватьСценарий = СтруктураПараметров.КомпилироватьСценарий;
		КонецЕсли;	 
		
		Если КомпилироватьСценарий Тогда
			ДеревоСхемы = СоздатьДеревоСхемы();
			ДополнительныеПараметры = Новый Структура;
			ДополнительныеПараметры.Вставить("ДелатьРаскраску",Ложь);
			ДополнительныеПараметры.Вставить("ЧтениеИзТекста",Ложь);
			ДополнительныеПараметры.Вставить("СтруктураПараметров",СтруктураПараметров);
			
			ДанныеСенария = ДанныеСценарияДляПолученияСхемыСценария(Сценарий);
			ДеревоСценарияИзТекста(ДанныеСенария.Текст,ДеревоСхемы,ДанныеСенария.Проект,Сценарий,ДополнительныеПараметры);
			
			ПараметрыВходящие = ДанныеСенария.ПараметрыВходящие;
			ПараметрыВходящиеИзДерева(ДеревоСхемы,ПараметрыВходящие);
			
			Возврат ДеревоСхемы; 
		Иначе	
			Возврат Сценарий.ХранилищеСтруктурыСхемы.Получить();
		КонецЕсли;	 
		
	КонецЕсли;	 
КонецФункции	

Функция ДанныеВложенныхСценариев()
	ДанныеВложенныхСценариев = Новый ТаблицаЗначений;
	ДанныеВложенныхСценариев.Колонки.Добавить("Сценарий");
	ДанныеВложенныхСценариев.Колонки.Добавить("UID");
	ДанныеВложенныхСценариев.Колонки.Добавить("ДеревоСхемы");
	ДанныеВложенныхСценариев.Колонки.Добавить("ПараметрыВходящие");
	
	Возврат ДанныеВложенныхСценариев;
КонецФункции	 

Процедура ОбновитьОписаниеЭлементаСУчетомЯзыка(СтрокаДерева)
	СтрокаДерева.ОписаниеЭлемента = СтрЗаменить(СтрокаДерева.ОписаниеЭлемента,"#language: en","");
	СтрокаДерева.ОписаниеЭлемента = СтрЗаменить(СтрокаДерева.ОписаниеЭлемента,"#language: ru","#language: en");
КонецПроцедуры

Процедура МногоязыковаяПоддержка(Текст,ДеревоСхемы)
	Если Найти(Текст,"#language: en") > 0 Тогда
		Текст = СтрЗаменить(Текст,"#language: en","");
		Текст = СтрЗаменить(Текст,"#language: ru","#language: en");
		Текст = СтрЗаменить(Текст,"Сценарий:","Scenario:");
		Текст = СтрЗаменить(Текст,"Контекст:","Background:");
		Текст = СтрЗаменить(Текст,"Функциональность:","Feature:");
		Текст = СтрЗаменить(Текст,"И Я закрыл все окна клиентского приложения"
		     ,"And I close all client application windows");
		
		Если ДеревоСхемы <> Неопределено Тогда
			Для Каждого СтрокаДерева Из ДеревоСхемы.Строки Цикл
				Если СтрокаДерева.ТипЭлемента = ТипЭлементаКонтекст() Тогда
					СтрокаДерева.ОписаниеЭлемента = СтрЗаменить(СтрокаДерева.ОписаниеЭлемента,"Контекст:","Background:");
					Для Каждого СтрокаДействий Из СтрокаДерева.Строки Цикл
						Если СтрокаДействий.ТипЭлемента = ТипЭлементаДействие() Тогда
							СтрокаДействий.ОписаниеЭлемента = СтрЗаменить(СтрокаДействий.ОписаниеЭлемента
							,"И Я закрыл все окна клиентского приложения"
							,"And I close all client application windows");
						КонецЕсли;	 
					КонецЦикла;	
				ИначеЕсли СтрокаДерева.ТипЭлемента = ТипЭлементаСценарий() Тогда
					СтрокаДерева.ОписаниеЭлемента = СтрЗаменить(СтрокаДерева.ОписаниеЭлемента,"Сценарий:","Scenario:");
					СтрокаДерева.ОписаниеЭлемента = СтрЗаменить(СтрокаДерева.ОписаниеЭлемента,"Сценарий ","Scenario ");
					Для Каждого СтрокаСценария Из СтрокаДерева.Строки Цикл
						ОбновитьОписаниеЭлементаСУчетомЯзыка(СтрокаСценария);
					КонецЦикла;	
				ИначеЕсли СтрокаДерева.ТипЭлемента = ТипЭлементаЗаголовок() Тогда
					СтрокаДерева.ОписаниеЭлемента = СтрЗаменить(СтрокаДерева.ОписаниеЭлемента,"Функциональность:","Feature:");
				ИначеЕсли СтрокаДерева.ТипЭлемента = ТипЭлементаКомментарий() Тогда
					ОбновитьОписаниеЭлементаСУчетомЯзыка(СтрокаДерева);
				КонецЕсли;	 
			КонецЦикла;	
		КонецЕсли;	 
	КонецЕсли;	 
	
КонецПроцедуры

Функция ПолучитьСтрокуЦикла(Знач Стр,Позиция,Отступ,КлючевоеСлово)
	ВтораяЧасть = СокрЛ(Сред(Стр,Позиция+Отступ+1));
	
	МассивСтрокВтораяЧасть = СтрРазделить(ВтораяЧасть," ");
	
	МассивПервыхСловНРег = МассивПервыхСловGherkinНРег();
	ПервоеСловоВторойЧасти = МассивСтрокВтораяЧасть[0];
	Если МассивПервыхСловНРег.Найти(НРег(ПервоеСловоВторойЧасти)) = Неопределено Тогда
		ВтораяЧасть = КлючевоеСлово + " " + ВтораяЧасть;
	КонецЕсли;	 
	
	Стр = Лев(Стр,Позиция-1) + ВтораяЧасть;
	
	Возврат Стр;
КонецФункции	 

Функция ПользователиЭталоннойБазы(ЭталоннаяБазаТестирования)
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ЭталонныеБазыТестированияПользователи.Ссылка КАК Ссылка,
		|	ЭталонныеБазыТестированияПользователи.НомерСтроки КАК НомерСтроки,
		|	ЭталонныеБазыТестированияПользователи.ПрофильПользователя КАК ПрофильПользователя,
		|	ЭталонныеБазыТестированияПользователи.Логин КАК Логин,
		|	ЭталонныеБазыТестированияПользователи.Пароль КАК Пароль
		|ИЗ
		|	Справочник.ЭталонныеБазыТестирования.Пользователи КАК ЭталонныеБазыТестированияПользователи
		|ГДЕ
		|	ЭталонныеБазыТестированияПользователи.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", ЭталоннаяБазаТестирования);
	
	Возврат Запрос.Выполнить().Выгрузить();
КонецФункции	 

Функция СценарииПроцесса(Процесс)
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ШагиПроцесса.СценарийРаботыПользователя КАК Сценарий,
		|	ШагиПроцесса.Ссылка КАК ШагПроцесса,
		|	ШагиПроцесса.Наименование КАК Наименование,
		|	ШагиПроцесса.Исполнитель КАК Исполнитель,
		|	ШагиПроцесса.ТипШага КАК ТипШага,
		|	ШагиПроцесса.ВложенныйПроцесс КАК ВложенныйПроцесс,
		|	ШагиПроцесса.ПараметрыСценария.(
		|		Ссылка КАК Ссылка,
		|		НомерСтроки КАК НомерСтроки,
		|		Имя КАК Имя,
		|		ЗначениеПоСсылке КАК ЗначениеПоСсылке,
		|		ТипПараметра КАК ТипПараметра,
		|		ИсходящийПараметр КАК ИсходящийПараметр,
		|		ШагПроцессаЗначениеПоСсылке КАК ШагПроцессаЗначениеПоСсылке,
		|		ИмяПараметраПоСсылке КАК ИмяПараметраПоСсылке,
		|		ЗначениеПроизвольное КАК ЗначениеПроизвольное,
		|		"""" КАК Значение
		|	) КАК ПараметрыСценария
		|ИЗ
		|	Справочник.ШагиПроцесса КАК ШагиПроцесса
		|ГДЕ
		|	НЕ ШагиПроцесса.ПометкаУдаления
		|	И ШагиПроцесса.Владелец = &Процесс
		|	И НЕ ШагиПроцесса.ЭтоГруппа
		|	И (ШагиПроцесса.СценарийРаботыПользователя <> ЗНАЧЕНИЕ(Справочник.СценарииРаботыПользователей.ПустаяСсылка)
		|			ИЛИ ШагиПроцесса.ТипШага = 1)
		|
		|УПОРЯДОЧИТЬ ПО
		|	ШагиПроцесса.ПолныйКод";
	
	Запрос.УстановитьПараметр("Процесс", Процесс);
	
	ШагиПроцесса = Запрос.Выполнить().Выгрузить();
	
	Возврат ШагиПроцесса; 
КонецФункции	 

Функция ПараметрыПроцесса(Процесс)
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПроцессыПараметрыБизнесПроцесса.Ссылка КАК Ссылка,
		|	ПроцессыПараметрыБизнесПроцесса.НомерСтроки КАК НомерСтроки,
		|	ПроцессыПараметрыБизнесПроцесса.Имя КАК Имя,
		|	ПроцессыПараметрыБизнесПроцесса.Значение КАК Значение,
		|	ПроцессыПараметрыБизнесПроцесса.ТипПараметра КАК ТипПараметра,
		|	ПроцессыПараметрыБизнесПроцесса.ИсходящийПараметр КАК ИсходящийПараметр
		|ИЗ
		|	Справочник.Процессы.ПараметрыПроцесса КАК ПроцессыПараметрыБизнесПроцесса
		|ГДЕ
		|	ПроцессыПараметрыБизнесПроцесса.Ссылка = &Процесс
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки";
	
	Запрос.УстановитьПараметр("Процесс", Процесс);
	
	Возврат Запрос.Выполнить().Выгрузить(); 
КонецФункции	 

Функция ПараметрыШагаПроцессаИзТекста(СтрокаСценария,ПараметрыБизнесПроцесса,ПараметрыФормированияТекстаСценария)
	UIDПроцесса = ПараметрыФормированияТекстаСценария.UIDПроцесса;
	ДанныеПроцессов = ПараметрыФормированияТекстаСценария.ДанныеПроцессов;
	ШагПроцессаUID = СтрокаСценария.ШагПроцессаUID;
	
	СтрокаДанныеПроцессов = ДанныеПроцессов.Найти(UIDПроцесса,"UID");
	
	ШагиПроцесса = СтрокаДанныеПроцессов.ШагиПроцесса;
	СтрокаШагиПроцесса = ШагиПроцесса.Найти(ШагПроцессаUID,"UID");
	
	
	Тзн = СтрокаШагиПроцесса.ПараметрыШага;
	Если Тзн.Колонки.Найти("Значение") = Неопределено Тогда
		Тзн.Колонки.Добавить("Значение");
	КонецЕсли;	 
	ОбработкаТаблицыПараметровШагаПроцесса(Тзн);
	
	
	ТаблицаПараметров = Новый ТаблицаЗначений;
	ТаблицаПараметров.Колонки.Добавить("Имя");
	ТаблицаПараметров.Колонки.Добавить("ТипПараметра");
	ТаблицаПараметров.Колонки.Добавить("Значение");
	
	Для Каждого СтрокаПараметра Из Тзн Цикл
		СтрокаТаблицаПараметров                   = ТаблицаПараметров.Добавить();
		СтрокаТаблицаПараметров.ТипПараметра      = СтрокаПараметра.ТипПараметра;
		СтрокаТаблицаПараметров.Значение          = ЗначениеПараметраПроцесса(СтрокаПараметра.Значение,
			ПараметрыБизнесПроцесса,СтрокаПараметра,СтрокаСценария,СтрокаСценария.Сценарий,ПараметрыФормированияТекстаСценария);
			
		СтрокаТаблицаПараметров.Имя               = СтрокаПараметра.Имя;
	КонецЦикла;	
	
	Возврат ТаблицаПараметров;
КонецФункции	 

Функция ПараметрыШагаПроцесса(ШагПроцесса,ПараметрыБизнесПроцесса,СтрокаСценария,СценарииБизнесПроцесса,ПараметрыФормированияТекстаСценария)
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ШагиПроцессаПараметрыСценария.Ссылка КАК Ссылка,
		|	ШагиПроцессаПараметрыСценария.НомерСтроки КАК НомерСтроки,
		|	ШагиПроцессаПараметрыСценария.Имя КАК Имя,
		|	ШагиПроцессаПараметрыСценария.ЗначениеПоСсылке КАК ЗначениеПоСсылке,
		|	ШагиПроцессаПараметрыСценария.ТипПараметра КАК ТипПараметра,
		|	ШагиПроцессаПараметрыСценария.ИсходящийПараметр КАК ИсходящийПараметр,
		|	ШагиПроцессаПараметрыСценария.ШагПроцессаЗначениеПоСсылке КАК ШагПроцессаЗначениеПоСсылке,
		|	ШагиПроцессаПараметрыСценария.ИмяПараметраПоСсылке КАК ИмяПараметраПоСсылке,
		|	ШагиПроцессаПараметрыСценария.ЗначениеПроизвольное КАК ЗначениеПроизвольное
		|ИЗ
		|	Справочник.ШагиПроцесса.ПараметрыСценария КАК ШагиПроцессаПараметрыСценария
		|ГДЕ
		|	ШагиПроцессаПараметрыСценария.Ссылка = &ШагПроцесса
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки";
	
	Запрос.УстановитьПараметр("ШагПроцесса", ШагПроцесса);
	
	Тзн = Запрос.Выполнить().Выгрузить(); 
	
	Тзн.Колонки.Добавить("Значение");
	ОбработкаТаблицыПараметровШагаПроцесса(Тзн);
	
	
	ТаблицаПараметров = Новый ТаблицаЗначений;
	ТаблицаПараметров.Колонки.Добавить("Имя");
	ТаблицаПараметров.Колонки.Добавить("ТипПараметра");
	ТаблицаПараметров.Колонки.Добавить("Значение");
	
	Для Каждого СтрокаПараметра Из Тзн Цикл
		СтрокаТаблицаПараметров                   = ТаблицаПараметров.Добавить();
		СтрокаТаблицаПараметров.ТипПараметра      = СтрокаПараметра.ТипПараметра;
		СтрокаТаблицаПараметров.Значение          = ЗначениеПараметраПроцесса(СтрокаПараметра.Значение,
			ПараметрыБизнесПроцесса,СтрокаПараметра,СтрокаСценария,СценарииБизнесПроцесса,ПараметрыФормированияТекстаСценария);
			
		СтрокаТаблицаПараметров.Имя               = СтрокаПараметра.Имя;
	КонецЦикла;	
	
	Возврат ТаблицаПараметров;
КонецФункции	 

Процедура ОбработкаТаблицыПараметровШагаПроцесса(Тзн)
	Для Каждого СтрокаТзн Из Тзн Цикл
		Если ЗначениеЗаполнено(СтрокаТзн.ЗначениеПоСсылке) Тогда
			СтрокаТзн.Значение = СтрокаТзн.ЗначениеПоСсылке;
		Иначе	
			СтрокаТзн.Значение = СтрокаТзн.ЗначениеПроизвольное;
		КонецЕсли;	 
	КонецЦикла;	 
КонецПроцедуры 

Процедура ДополнитьПередаваемыеПараметрыПараметрамиПоУмолчанию(ЗначенияВходящихПараметров,ПараметрыСценария)
	Для Каждого СтрокаПараметрыСценария Из ПараметрыСценария Цикл
		СтрокаЗначенияВходящихПараметров = ЗначенияВходящихПараметров.Найти(СтрокаПараметрыСценария.Имя,"Имя");
		Если СтрокаЗначенияВходящихПараметров = Неопределено Тогда
			СтрокаЗначенияВходящихПараметров = ЗначенияВходящихПараметров.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаЗначенияВходящихПараметров,СтрокаПараметрыСценария);
		КонецЕсли;	 
	КонецЦикла;	 
	
КонецПроцедуры 

Процедура СкопироватьДеревоСхемы(ДеревоКуда,ДеревоОткуда)
	Для Каждого СтрокаДеревоОткуда Из ДеревоОткуда.Строки Цикл
		СтрокаДеревоКуда = ДеревоКуда.Строки.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаДеревоКуда,СтрокаДеревоОткуда);
		Если СтрокаДеревоОткуда.Строки.Количество() > 0 Тогда
			СкопироватьДеревоСхемы(СтрокаДеревоКуда,СтрокаДеревоОткуда);
		КонецЕсли;	 
	КонецЦикла;	 
КонецПроцедуры 

Функция ПолучитьДеревоВложенногоПроцесса(ДанныеПроцесса,СтруктураПараметров,ПараметрыРодительскогоПроцесса,ПараметрыФормированияТекстаСценария)
	Если СтруктураПараметров.ЧтениеИзТекста Тогда
		ВызватьИсключение "Надо сделать!!!!!!!";
	Иначе	
		СценарииПроцесса = СценарииПроцесса(ДанныеПроцесса.ВложенныйПроцесс);
		СхемаДерево      = СоздатьДеревоСхемы();
		
		СтрокаВложенногоПроцесса = СхемаДерево.Строки.Добавить();
		СтрокаВложенногоПроцесса.ТипЭлемента = ТипЭлементаВложенныйПроцесс();
		СтрокаВложенногоПроцесса.ИмяЭлемента = ДанныеПроцесса.Наименование;
		
		Для Каждого СтрокаСценарииПроцесса Из СценарииПроцесса Цикл
			СтрокаСхемаДерево = СтрокаВложенногоПроцесса.Строки.Добавить();
			СтрокаСхемаДерево.ТипЭлемента = ТипЭлементаШагВложенногоПроцесса();
			СтрокаСхемаДерево.ИмяЭлемента = СтрокаСценарииПроцесса.Наименование;
			Если СтрокаСхемаДерево.ДополнительныеСвойства = Неопределено Тогда
				СтрокаСхемаДерево.ДополнительныеСвойства = Новый Структура;
			КонецЕсли;	 
			
			ПараметрыБизнесПроцесса = ПараметрыПроцесса(ДанныеПроцесса.ВложенныйПроцесс);
			Для Каждого СтрокаПараметрыШага Из ДанныеПроцесса.ПараметрыСценария Цикл
				СтрокаПараметра = ПараметрыБизнесПроцесса.Найти(СтрокаПараметрыШага.Имя,"Имя");
				Если СтрокаПараметра <> Неопределено Тогда
					Если ЗначениеЗаполнено(СтрокаПараметрыШага.ЗначениеПроизвольное) Тогда
						СтрокаПараметра.Значение = СтрокаПараметрыШага.ЗначениеПроизвольное;
					ИначеЕсли ЗначениеЗаполнено(СтрокаПараметрыШага.ЗначениеПоСсылке) Тогда
						ИмяПараметраПоСсылке = СтрокаПараметрыШага.ИмяПараметраПоСсылке;
						СтрокаПараметрыРодительскогоПроцесса = ПараметрыРодительскогоПроцесса.Найти(ИмяПараметраПоСсылке,"Имя");
						Если СтрокаПараметрыРодительскогоПроцесса <> Неопределено Тогда
							СтрокаПараметра.Значение = СтрокаПараметрыРодительскогоПроцесса.Значение;
						КонецЕсли;	 
					КонецЕсли;	 
				КонецЕсли;	 
			КонецЦикла;	 
			
			
			ЗначенияВходящихПараметров = ПараметрыШагаПроцесса(СтрокаСценарииПроцесса.ШагПроцесса,ПараметрыБизнесПроцесса,
			   СтрокаСценарииПроцесса,СценарииПроцесса, ПараметрыФормированияТекстаСценария);
			   
			ПараметрыВходящиеСценария = ПараметрыВходящиеСценария(СтрокаСценарииПроцесса.Сценарий);
			ДополнитьПередаваемыеПараметрыПараметрамиПоУмолчанию(ЗначенияВходящихПараметров,ПараметрыВходящиеСценария);
			
			СтрокаСхемаДерево.ДополнительныеСвойства.Вставить("ПараметрыШагаВложенногоПроцесса",ЗначенияВходящихПараметров);
			
			Если СтрокаСценарииПроцесса.ТипШага = 0 Тогда //обычный сценарий
				ВложенноеДеревоСхемы = СтрокаСценарииПроцесса.Сценарий.ХранилищеСтруктурыСхемы.Получить();
			Иначе	
				ПараметрыВложенногоПроцесса = ПараметрыПроцесса(СтрокаСценарииПроцесса.ВложенныйПроцесс);
				ВложенноеДеревоСхемы = ПолучитьДеревоВложенногоПроцесса(СтрокаСценарииПроцесса,СтруктураПараметров,
				ПараметрыВложенногоПроцесса,ПараметрыФормированияТекстаСценария);
			КонецЕсли;	 
			СкопироватьДеревоСхемы(СтрокаСхемаДерево,ВложенноеДеревоСхемы);
		КонецЦикла;	 
		
		Возврат СхемаДерево;
	КонецЕсли;	 
КонецФункции	 

Функция ПерваяСтрока(Стр)
	Если Найти(Стр,Символы.ПС) = 0 Тогда
		Возврат Стр;
	КонецЕсли;	 
	
	МассивСтрок = СтрРазделить(Стр,Символы.ПС);
	Возврат МассивСтрок[0];
КонецФункции	 

Процедура ОтформатироватьПередачуПараметраТипаТаблица(ТаблицаСтрок)
	Для Каждого СтрТаблицаСтрок Из ТаблицаСтрок Цикл
		Если СтрТаблицаСтрок.ТипЭлемента <> ТипЭлементаТаблица() Тогда
			Продолжить;
		КонецЕсли;	 
		
		//ЭкранироватьУгловыеСкобки(СтрТаблицаСтрок.СтрокаСокр);
		
		Стр = СтрТаблицаСтрок.ОбработаннаяСтрокаПараметров;
		ПодсветкаСтрокиПоПараметрам(Стр,СтрТаблицаСтрок.Параметры,Неопределено);
		СтрТаблицаСтрок.СтрокаСокр = Стр;
		
	КонецЦикла;	
КонецПроцедуры 

#КонецОбласти

# Область ОпределениеОператора

Функция ОператорПерейти()
	Возврат "Перейти";
КонецФункции	 

Функция ОператорЗаголовок()
	Возврат "заголовок";
КонецФункции	 

Функция ОператорЧастьЗаголовока()
	Возврат "частьзаголовка";
КонецФункции	 

Функция ОператорКонтекст()
	Возврат "контекст";
КонецФункции	 

Функция ОператорСценарий()
	Возврат "сценарий";
КонецФункции	 

Функция ОператорУсловиеПрепроцессор()
	Возврат "УсловиеПрепроцессор";
КонецФункции	 

Функция ОператорУсловие()
	Возврат "Условие";
КонецФункции	 

Функция ОператорУсловиеEng()
	Возврат "Condition";
КонецФункции	 

Функция ОператорИначеЕслиПрепроцессор()
	Возврат "ИначеЕслиПрепроцессор";
КонецФункции	 

Функция ОператорИначеПрепроцессор()
	Возврат "ИначеПрепроцессор";
КонецФункции	 

Функция ОператорКонецЕслиПрепроцессор()
	Возврат "КонецЕслиПрепроцессор";
КонецФункции	 

Функция ОператорКонецЕсли()
	Возврат "КонецЕсли";
КонецФункции	 

Функция ОператорКонецЕслиEng()
	Возврат "EndIf";
КонецФункции	 

Функция ОператорКомментарий()
	Возврат "Комментарий";
КонецФункции	 

Функция ОператорПараметрТаблица()
	Возврат "ПараметрТаблица";
КонецФункции	 

Функция ОператорБлочныйКомментарий()
	Возврат "БлочныйКомментарий";
КонецФункции	 

Функция ОператорТег()
	Возврат "Тег";
КонецФункции	 

Функция ОператорСтоп()
	Возврат "Стоп";
КонецФункции	 

Функция ОператорМетка()
	Возврат "Метка";
КонецФункции	 

Функция ОператорТаблица()
	Возврат "Таблица";
КонецФункции	 

Функция ОператорБлочныйКомменатрийНачало()
	Возврат "БлочныйКомменатрийНачало";
КонецФункции	 

Функция ОператорБлочныйКомменатрийОкончание()
	Возврат "БлочныйКомменатрийОкончание";
КонецФункции	 

Функция ОператорЦикл()
	Возврат "Цикл";
КонецФункции	 

Функция ОператорЦиклEng()
	Возврат "Do";
КонецФункции	 

Функция ОператорКонецЦикла()
	Возврат "КонецЦикла";
КонецФункции	 

Функция ОператорКонецЦиклаEng()
	Возврат "EndDo";
КонецФункции	 

Функция ОператорПередачаПараметров()
	Возврат "ПередачаПараметров";
КонецФункции

Функция ОператорИначеЕсли()
	Возврат "ИначеЕсли";
КонецФункции	 

Функция ОператорИначеЕслиEng()
	Возврат "ElseIf";
КонецФункции	 

Функция ОператорИначе()
	Возврат "Иначе";
КонецФункции	 

Функция ОператорИначеEng()
	Возврат "Else";
КонецФункции	 

#КонецОбласти

# Область ОпределениеИмяЭлемента

Функция ИмяЭлементаСтарт()
	Возврат "Старт";
КонецФункции	 

Функция ИмяЭлементаСтоп()
	Возврат "Стоп";
КонецФункции	 

Функция ИмяЭлементаВложенныйПроцесс()
	Возврат "ВложенныйПроцесс";
КонецФункции	 

Функция ИмяЭлементаШаблонСценария()
	Возврат "ШаблонСценария";
КонецФункции	 

Функция ИмяЭлементаМетка()
	Возврат "Метка";
КонецФункции	 

Функция ИмяЭлементаДействие()
	Возврат "Действие";
КонецФункции	 

#КонецОбласти

# Область ОпределениеОписанияЭлемента

Функция ОписаниеЭлементаСтоп()
	Возврат "Стоп";
КонецФункции	 

Функция ОписаниеЭлементаКонецЕслиПрепроцессор()
	Возврат "#КонецЕсли";
КонецФункции	 

Функция ОписаниеЭлементаКонецЕсли()
	Возврат "КонецЕсли";
КонецФункции	 

Функция ОписаниеЭлементаКонецЕслиEng()
	Возврат "EndIf";
КонецФункции	 

Функция ОписаниеЭлементаКонецЦикла()
	Возврат "КонецЦикла";
КонецФункции	 

Функция ОписаниеЭлементаКонецЦиклаEng()
	Возврат "EndDo";
КонецФункции	 

Функция ОписаниеЭлементаИначеПрепроцессор()
	Возврат "#Иначе";
КонецФункции	 

Функция ОписаниеЭлементаУсловиеПрепроцессор()
	Возврат "#Если";
КонецФункции	

Функция ОписаниеЭлементаЦикл()
	Возврат "Цикл";
КонецФункции	

Функция ОписаниеЭлементаЦиклEng()
	Возврат "Do";
КонецФункции	

Функция ОписаниеЭлементаУсловие()
	Возврат "Если";
КонецФункции	

Функция ОписаниеЭлементаУсловиеEng()
	Возврат "If";
КонецФункции	

Функция ОписаниеЭлементаСтарт()
	Возврат "Старт";
КонецФункции	 

Функция ОписаниеЭлементаИначе()
	Возврат "Иначе";
КонецФункции	

Функция ОписаниеЭлементаИначеEng()
	Возврат "Else";
КонецФункции	

#КонецОбласти

# Область ОпределениеТипаЭлемента

Функция ТипЭлементаТег()
	Возврат "Тег";
КонецФункции	 

Функция ТипЭлементаКомментарий()
	Возврат "Комментарий";
КонецФункции	 

Функция ТипЭлементаБлочныйКомментарий()
	Возврат "БлочныйКомментарий";
КонецФункции	 

Функция ТипЭлементаБлочныйКомментарийНачало()
	Возврат "БлочныйКомментарийНачало";
КонецФункции	 

Функция ТипЭлементаБлочныйКомментарийОкончание()
	Возврат "БлочныйКомментарийОкончание";
КонецФункции	 

Функция ТипЭлементаДействие()
	Возврат "Действие";
КонецФункции	 

Функция ТипЭлементаГруппаШагов()
	Возврат "ГруппаШагов";
КонецФункции	 

Функция ТипЭлементаНачалоСхемы()
	Возврат "НачалоСхемы";
КонецФункции	 

Функция ТипЭлементаСтоп()
	Возврат "Стоп";
КонецФункции	 

Функция ТипЭлементаОкончаниеСхемы()
	Возврат "ОкончаниеСхемы";
КонецФункции	 

Функция ТипЭлементаПередачаПараметров()
	Возврат "ПередачаПараметров";
КонецФункции	 

//условия
Функция ТипЭлементаУсловиеПрепроцессор()
	Возврат "УсловиеПрепроцессор";
КонецФункции	 

Функция ТипЭлементаЦикл()
	Возврат "Цикл";
КонецФункции	 

Функция ТипЭлементаЦиклEng()
	Возврат "Do";
КонецФункции	 

Функция ТипЭлементаУсловие()
	Возврат "Условие";
КонецФункции	 

Функция ТипЭлементаУсловиеEng()
	Возврат "Condition";
КонецФункции	 

Функция ТипЭлементаИначеПрепроцессор()
	Возврат "ИначеПрепроцессор";
КонецФункции	 

Функция ТипЭлементаИначе()
	Возврат "Иначе";
КонецФункции	 

Функция ТипЭлементаИначеEng()
	Возврат "Else";
КонецФункции	 

Функция ТипЭлементаИначеЕслиПрепроцессор()
	Возврат "ИначеЕслиПрепроцессор";
КонецФункции	 

Функция ТипЭлементаИначеЕсли()
	Возврат "ИначеЕсли";
КонецФункции	 

Функция ТипЭлементаИначеЕслиEng()
	Возврат "ElseIf";
КонецФункции	 

Функция ТипЭлементаНачалоУсловияПрепроцессор()
	Возврат "НачалоУсловияПрепроцессор";
КонецФункции	 

Функция ТипЭлементаНачалоУсловия()
	Возврат "НачалоУсловия";
КонецФункции	 

Функция ТипЭлементаНачалоУсловияEng()
	Возврат "BeginCondition";
КонецФункции	 

Функция ТипЭлементаНачалоЦикла()
	Возврат "НачалоЦикла";
КонецФункции	 

Функция ТипЭлементаНачалоЦиклаEng()
	Возврат "BeginDo";
КонецФункции	 

Функция ТипЭлементаКонецЕслиПрепроцессор()
	Возврат "КонецЕслиПрепроцессор";
КонецФункции	 

Функция ТипЭлементаКонецЕсли()
	Возврат "КонецЕсли";
КонецФункции	 

Функция ТипЭлементаКонецЕслиEng()
	Возврат "EndIf";
КонецФункции	 

Функция ТипЭлементаКонецЦикла()
	Возврат "КонецЦикла";
КонецФункции	 

Функция ТипЭлементаКонецЦиклаEng()
	Возврат "EndDo";
КонецФункции	 

Функция ТипЭлементаЗаголовок()
	Возврат "Заголовок";
КонецФункции	 

Функция ТипЭлементаПустаяСтрока()
	Возврат "ПустаяСтрока";
КонецФункции	 

Функция ТипЭлементаСценарий()
	Возврат "Сценарий";
КонецФункции	 

Функция ТипЭлементаЭлементЗаголовка()
	Возврат "ЭлементЗаголовка";
КонецФункции	 

Функция ТипЭлементаКонтекст()
	Возврат "Контекст";
КонецФункции	 

Функция ТипЭлементаМетка()
	Возврат "Метка";
КонецФункции	 

Функция ТипЭлементаПерейти()
	Возврат "Перейти";
КонецФункции	 

Функция ТипЭлементаТаблица()
	Возврат "Таблица";
КонецФункции	 

Функция ТипЭлементаПараметрТаблица()
	Возврат "ПараметрТаблица";
КонецФункции	 

Функция ТипЭлементаВложенныйПроцесс()
	Возврат "ВложенныйПроцесс";
КонецФункции	 

Функция ТипЭлементаШагВложенногоПроцесса()
	Возврат "ШагВложенногоПроцесса";
КонецФункции	 

#КонецОбласти

# Область ОпределениеКлючевыхСловТекста

Функция КлючевоеСловоЕсли()
	Возврат "если";
КонецФункции	 

Функция КлючевоеСловоТогда()
	Возврат "тогда";
КонецФункции	 

Функция КлючевоеСловоКонецЕсли()
	Возврат "конецесли"; 
КонецФункции	 

Функция КлючевоеСловоЕслиEng()
	Возврат "if";
КонецФункции	 

Функция КлючевоеСловоТогдаEng()
	Возврат "then";
КонецФункции	 

Функция КлючевоеСловоКонецЕслиEng()
	Возврат "endif"; 
КонецФункции	 

Функция КлючевоеСловоЕслиПрепроцессор()
	Возврат "#если";
КонецФункции	 

Функция КлючевоеСловоЦикл()
	Возврат "цикл";
КонецФункции	 

Функция КлючевоеСловоЦиклEng()
	Возврат "do";
КонецФункции	 

Функция КлючевоеСловоКонецЦикла()
	Возврат "конеццикла";
КонецФункции	 

Функция КлючевоеСловоКонецЦиклаEng()
	Возврат "enddo";
КонецФункции	 

Функция КлючевоеСловоИначеПрепроцессор()
	Возврат "#иначе";
КонецФункции	 

Функция КлючевоеСловоТогдаПрепроцессор()
	Возврат "тогда";
КонецФункции	 

Функция КлючевоеСловоИначеЕслиПрепроцессор()
	Возврат "#иначеесли";
КонецФункции	 

Функция КлючевоеСловоКонецЕслиПрепроцессор()
	Возврат "#конецесли"; 
КонецФункции	 

Функция КлючевоеСловоСтоп()
	Возврат "стоп"; 
КонецФункции	 

Функция КлючевоеСловоПерейти()
	Возврат "перейти";
КонецФункции	 

Функция КлючевоеФункционал()
	Возврат "функционал:";
КонецФункции	 

Функция КлючевоеФункциональность()
	Возврат "функциональность:";
КонецФункции	 

Функция КлючевоеКонтекст()
	Возврат "контекст:";
КонецФункции	 

Функция КлючевоеСценарий()
	Возврат "сценарий:";
КонецФункции	 

Функция КлючевоеСловоИначеЕсли()
	Возврат "иначеесли";
КонецФункции	 

Функция КлючевоеСловоИначе()
	Возврат "иначе";
КонецФункции	 

Функция КлючевоеСловоИначеЕслиEng()
	Возврат "elseif";
КонецФункции	 

Функция КлючевоеСловоИначеEng()
	Возврат "else";
КонецФункции	 

#КонецОбласти

# Область ОпределениеТипВыделенияПараметраВТексте

Функция ТипВыделенияПараметраУсловие()
	Возврат "Условие";
КонецФункции	 

Функция ТипВыделенияПередачаПараметров()
	Возврат "ПередачаПараметров";
КонецФункции	 

#КонецОбласти

#КонецЕсли